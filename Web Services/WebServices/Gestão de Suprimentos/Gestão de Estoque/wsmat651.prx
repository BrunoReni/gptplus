#INCLUDE "WSMAT651.CH"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT651  ³ Autor ³Erike Yuri da Silva    ³ Data ³26.06.2006  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades de Manutencao de Ops prevista³±±
±±³          ³ s                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Portais                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±     	
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtForeseenOrdersOfProduction DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtforeseenordersofproduction.apw" //"Serviço de consulta e atualização de ordens de produção previstas"
	WSDATA UserCode               			As String
	WSDATA FromProduct						As String 
	WSDATA ToProduct						As String 
	WSDATA FromPOID							As String
	WSDATA ToPOID							As String
	WSDATA FromDeliveryDate					As Date
	WSDATA ToDeliveryDate					As Date 
	WSDATA FromInitialDate					As Date
	WSDATA ToInitialDate					As Date	
	WSDATA ListForeseenOfProductionOrder 	As Array OF ProductionOrderView OPTIONAL
	WSDATA QueryAddWhere					As String OPTIONAL
	WSDATA IndexKey							As String OPTIONAL
	WSDATA WsNull      		           		As String 

	WSMETHOD BrwForeseenOrdersOfProduction  DESCRIPTION STR0002 //"Método de listagem das ordens de produção previstas"
	WSMETHOD FirmForeseenOrdersOfProduction	DESCRIPTION STR0003 //"Método para firmar ordens de produção previstas"
	WSMETHOD DelForeseenOrdersOfProduction	DESCRIPTION STR0004 //"Método de exclusão de ordens de produção previstas"
ENDWSSERVICE



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwPurchasesRequest³Autor ³Erike Yuri     ³ Data ³22.06.2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao das ordens de producao previstas       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Parametros para filtro                                ³±±
±±³          ³ExpC3: Filtro adicional                                      ³±±
±±³          ³ExpC4: Indice para visualizacao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve as ordens de producoes previstas conforme³±±
±±³          ³parametrizacao.                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Portais                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwForeseenOrdersOfProduction WSRECEIVE UserCode,FromProduct,ToProduct,FromPOID,ToPOID,FromDeliveryDate,ToDeliveryDate,FromInitialDate,ToInitialDate,QueryAddWhere,IndexKey WSSEND ListForeseenOfProductionOrder WSSERVICE MtForeseenOrdersOfProduction

Local aArea		:= GetArea()
Local cQuery	:= ""
Local cArqInd	:= ""
Local cAliasSC2	:= "SC2"    
Local lRetorno	:= .T.
Local lQuery	:= .F.
Local nY		:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao de parametros para filtro³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local cDoProduto	:=	::FromProduct
Local cAteProduto	:=	If(Empty(::ToProduct),"ZZZZZZZZZZZZZZZ",::ToProduct)
Local cDaOp			:=	::FromPOID
Local cAteOp		:=	If(Empty(::ToPOID),"ZZZZZZZZZZZZZ",::ToPOID)
Local dDaDtEntr		:=	::FromDeliveryDate
Local dAteDtEntr	:=	If(Empty(::ToDeliveryDate),dDataBase,::ToDeliveryDate)
Local dDaDtIni		:=	::FromInitialDate
Local dAteDtIni		:=	If(Empty(::ToInitialDate),dDataBase,::ToInitialDate)

If PrtChkUser(::UserCode,"MtForeseenOrdersOfProduction","BrwForeseenOrdersOfProduction")  
	DbSelectArea("SC2")
	DbSetOrder(1)
	#IFDEF TOP
		If (TcSrvType()#'AS/400')
			lQuery		:= .T.
			cAliasSC2	:= GetNextAlias()

			cQuery := "SELECT * "
			cQuery += "FROM "	    + RetSqlName( 'SC2' ) +" SC2 "
			cQuery += " WHERE "
			cQuery += "C2_FILIAL='" + xFilial( 'SC2' )    	+ "' AND "
			cQuery += "C2_TPOP='P' AND " 
			cQuery += "C2_PRODUTO >='"+cDoProduto+"' AND "
			cQuery += "C2_PRODUTO <='"+cAteProduto+"' AND "
			cQuery += "C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD>='"   + cDaOp 	+ "' AND "
			cQuery += "C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD<='"   + cAteOp	+ "' AND "
			cQuery += "C2_DATPRF>='"  + DTOS(dDaDtEntr)   	+ "' AND "
			cQuery += "C2_DATPRF<='"  + DTOS(dAteDtEntr)   	+ "' AND "
			cQuery += "C2_DATPRI>='"  + DTOS(dDaDtIni)   	+ "' AND "
			cQuery += "C2_DATPRI<='"  + DTOS(dAteDtIni)   	+ "' AND "
			cQuery += "SC2.D_E_L_E_T_<>'*' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(IIf(Empty(::IndexKey),SC2->(IndexKey()),::IndexKey))

			cQuery := ChangeQuery(cQuery)
			
			dbUseArea( .T., 'TOPCONN', TCGENQRY(,,cQuery), cAliasSC2, .F., .T.)
			aEval(SC2->(dbStruct()),{|x| If(x[2]<>"C" .And. x[2]<>"M",TcSetField(cAliasSC2,AllTrim(x[1]),x[2],x[3],x[4]),Nil)})
		EndIf
	#ENDIF
	If !lQuery
		cArqInd   := CriaTrab( , .F. )
		cQuery += 'C2_FILIAL=="'+xFilial("SC2")+'".And.C2_TPOP=="'+"P"+'".And.'
		cQuery += 'C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD>="'+cDaOp+'".And.C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD<="'+cAteOp+'".And.'
		cQuery += 'dTos(C2_DATPRF)>="'+dTos(dDaDtEntr)+'".And.dTos(C2_DATPRF)<="'+dTos(dAteDtEntr)+'".And.'
		cQuery += 'C2_PRODUTO>="'+cDoProduto+'".And.C2_PRODUTO<="'+cAteProduto+'".And.'
		cQuery += 'dTos(C2_DATPRI)>="'+dTos(dDaDtIni)+'".And.dTos(C2_DATPRI)<="'+dTos(dAteDtIni)+'"'
		
		IndRegua("SC2",cArqInd,IIf(Empty(::IndexKey),SC2->(IndexKey()),::IndexKey),,cQuery)
		dbGotop()		
	EndIf              
	
	While (cAliasSC2)->( !Eof() )
		aadd(::ListForeseenOfProductionOrder,WSClassNew("ProductionOrderView"))
		nY++
		GetOPPR(@::ListForeseenOfProductionOrder[nY],cAliasSC2)
		(cAliasSC2)->( DbSkip() )
	EndDo	

	If lQuery
		dbSelectArea(cAliasSC2)		
		dbCloseArea()	
		dbSelectArea("SC2")
	Else
		dbSelectArea("SC2")
		RetIndex("SC2")
		FErase(cArqInd+OrdBagExt())
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FirmForeseenO³Autor  ³Erike Yuri da Silva ³Data  ³26.06.2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de firma as ops previstas definidas no range do filtro³±±
±±³          ³conforme parametrizacao.                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Estrutura de parametros utilizados para filtro na roti³±±
±±³          ³       na automatica                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Portais   		                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD FirmForeseenOrdersOfProduction WSRECEIVE UserCode, FromProduct,ToProduct,FromPOID,ToPOID,FromDeliveryDate,ToDeliveryDate,FromInitialDate,ToInitialDate WSSEND WSNULL WSSERVICE MtForeseenOrdersOfProduction

Local aArea     := GetArea()
Local lRetorno  := .T.
Local nX        := 0 
Local aCab		:= {}
Local aErro		:= {}
Local cErro		:= ""

Private lMsErroAuto 	:= .F.
Private lAutoErrNoFile 	:= .T.

If PrtChkUser(::UserCode,"MtForeseenOrdersOfProduction","FirmForeseenOrdersOfProduction")
    If Empty(::ToProduct)
    	cErro += chr(13)+chr(10)+STR0005 //"- O parâmetro ToProduct deve ser informado" 
    	lRetorno := .F.
    EndIf

    If Empty(::ToPOID)
    	cErro += chr(13)+chr(10)+STR0006 //"- O parâmetro ToPOID deve ser informado"
    	lRetorno := .F.
    EndIf

    If Empty(::FromDeliveryDate)
    	cErro += chr(13)+chr(10)+STR0007 //"- O parâmetro FromDeliveryDate deve ser informado"
    	lRetorno := .F.
    EndIf  
    
    If Empty(::ToDeliveryDate)
    	cErro += chr(13)+chr(10)+STR0008 //"- O parâmetro ToDeliveryDate deve ser informado"
    	lRetorno := .F.
    EndIf  

    If Empty(::FromInitialDate)
    	cErro += chr(13)+chr(10)+STR0009 //"- O parâmetro FromInitialDate deve ser informado"
    	lRetorno := .F.
    EndIf  

    If Empty(::ToInitialDate)
    	cErro += chr(13)+chr(10)+STR0010 //"- O parâmetro ToInitialDate deve ser informado"
    	lRetorno := .F.
    EndIf  

    If lRetorno 
		aadd(aCab,{"MV_PAR01",::FromProduct			,}	)
		aadd(aCab,{"MV_PAR02",::ToProduct			,}	)
		aadd(aCab,{"MV_PAR03",::FromPOID			,}	)
		aadd(aCab,{"MV_PAR04",::ToPOID				,}	)
		aadd(aCab,{"MV_PAR05",::FromDeliveryDate	,}	)
		aadd(aCab,{"MV_PAR06",::ToDeliveryDate		,}	)
		aadd(aCab,{"MV_PAR07",::FromInitialDate		,}	)
		aadd(aCab,{"MV_PAR08",::ToInitialDate		,}	)
	    
		MATA651(aCab,4)
		
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("FirmForeseenOrdersOfProduction",cErro)
			lRetorno := .F.
		EndIf	
	Else
		SetSoapFault("FirmForeseenOrdersOfProduction",cErro)	
	EndIf
Else
	lRetorno := .F.
	SetSoapFault("FirmForeseenOrdersOfProduction",STR0011) //"Usuario invalido"	
Endif

RestArea(aArea)
Return(lRetorno)



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelForeseenOr³Autor  ³Erike Yuri da Silva ³Data  ³26.06.2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo que exclui as ops previstas definidas no range do fil-³±±
±±³          ³tro conforme parametrizacao.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Estrutura de parametros utilizados para filtro na roti³±±
±±³          ³       na automatica                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Portais   		                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelForeseenOrdersOfProduction WSRECEIVE UserCode, FromProduct,ToProduct,FromPOID,ToPOID,FromDeliveryDate,ToDeliveryDate,FromInitialDate,ToInitialDate WSSEND WSNULL WSSERVICE MtForeseenOrdersOfProduction

Local aArea     := GetArea()
Local lRetorno  := .T.
Local nX        := 0 
Local aCab		:= {}
Local aErro		:= {}
Local cErro		:= ""

Private lMsErroAuto 	:= .F.
Private lAutoErrNoFile 	:= .T.

If PrtChkUser(::UserCode,"MtForeseenOrdersOfProduction","DelForeseenOrdersOfProduction")
    If Empty(::ToProduct)
    	cErro += chr(13)+chr(10)+STR0005 //"- O parâmetro ToProduct deve ser informado" 
    	lRetorno := .F.
    EndIf

    If Empty(::ToPOID)
    	cErro += chr(13)+chr(10)+STR0006 //"- O parâmetro ToPOID deve ser informado"
    	lRetorno := .F.
    EndIf

    If Empty(::FromDeliveryDate)
    	cErro += chr(13)+chr(10)+STR0007 //"- O parâmetro FromDeliveryDate deve ser informado"
    	lRetorno := .F.
    EndIf  
    
    If Empty(::ToDeliveryDate)
    	cErro += chr(13)+chr(10)+STR0008 //"- O parâmetro ToDeliveryDate deve ser informado"
    	lRetorno := .F.
    EndIf  

    If Empty(::FromInitialDate)
    	cErro += chr(13)+chr(10)+STR0009 //"- O parâmetro FromInitialDate deve ser informado"
    	lRetorno := .F.
    EndIf  

    If Empty(::ToInitialDate)
    	cErro += chr(13)+chr(10)+STR0010 //"- O parâmetro ToInitialDate deve ser informado"
    	lRetorno := .F.
    EndIf  
    
	If lRetorno
		aadd(aCab,{"MV_PAR01",::FromProduct		,}	)
		aadd(aCab,{"MV_PAR02",::ToProduct			,}	)
		aadd(aCab,{"MV_PAR03",::FromPOID			,}	)
		aadd(aCab,{"MV_PAR04",::ToPOID			,}	)
		aadd(aCab,{"MV_PAR05",::FromDeliveryDate	,}	)
		aadd(aCab,{"MV_PAR06",::ToDeliveryDate	,}	)
		aadd(aCab,{"MV_PAR07",::FromInitialDate	,}	)
		aadd(aCab,{"MV_PAR08",::ToInitialDate		,}	)
	    
		MATA651(aCab,5)
		
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("DelForeseenOrdersOfProduction",cErro)
			lRetorno := .F.
		EndIf	
	Else
		SetSoapFault("FirmForeseenOrdersOfProduction",cErro)	
	EndIf
Else
	lRetorno := .F.
	SetSoapFault("DelForeseenOrdersOfProduction",STR0011) //"Usuario invalido"	
Endif

RestArea(aArea)
Return(lRetorno)



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetOPPR   ³Autor  ³ Eduardo Riera         ³ Data ³30.08.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento da estrutura da Ordem de Producao    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto da Ordem de Producao                           ³±±
±±³          ³ExpC2: Alias do SC2                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SC2 posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GetOPPR(oObjeto,cAliasSC2)

Local aArea    := GetArea()

oObjeto:POID	          := (cAliasSC2)->C2_NUM
oObjeto:POItem	          := (cAliasSC2)->C2_ITEM
oObjeto:POSequence        := (cAliasSC2)->C2_SEQUEN
oObjeto:ProductCode       := (cAliasSC2)->C2_PRODUTO
oObjeto:Quantity          := (cAliasSC2)->C2_QUANT
oObjeto:WareHouse         := (cAliasSC2)->C2_LOCAL
oObjeto:StartForecastDate := (cAliasSC2)->C2_DATPRI
oObjeto:FinishForecastDate:= (cAliasSC2)->C2_DATPRF
oObjeto:Note              := (cAliasSC2)->C2_OBS
oObjeto:PODate            := (cAliasSC2)->C2_EMISSAO
oObjeto:Priority          := (cAliasSC2)->C2_PRIOR
oObjeto:Situation         := (cAliasSC2)->C2_STATUS
oObjeto:SecondMeasureUnitQuantity := (cAliasSC2)->C2_QTSEGUM
oObjeto:TypeOfPO          := (cAliasSC2)->C2_TPOP
oObjeto:StructureRevision := (cAliasSC2)->C2_REVISAO
oObjeto:QuantityProduced  := (cAliasSC2)->C2_QUJE
oObjeto:FinishRealDate    := (cAliasSC2)->C2_DATRF
oObjeto:QuantityLoss      := (cAliasSC2)->C2_PERDA
oObjeto:StartAdjustedDate := (cAliasSC2)->C2_DATAJI
oObjeto:FinishAdjustedDate:= (cAliasSC2)->C2_DATAJF
oObjeto:StartAdjustedHour := (cAliasSC2)->C2_HORAJI
oObjeto:FinishAdjustedHour:= (cAliasSC2)->C2_HORAJF
oObjeto:OrderID           := (cAliasSC2)->C2_PEDIDO
oObjeto:OrderItem         := (cAliasSC2)->C2_ITEMPV
oObjeto:CostCenterCode    := (cAliasSC2)->C2_CC
oObjeto:AccountingItemCode:= (cAliasSC2)->C2_ITEMCTA
oObjeto:ValueCategoryCode := (cAliasSC2)->C2_CLVL
oOBjeto:Status            := Iif(Empty((cAliasSC2)->C2_DATRF),"OPEN","CLOSE")

UserFields("SC2",@oObjeto:UserFields,cAliasSC2)

RestArea(aArea)
Return(.T.)


Function wsmat651()
Return