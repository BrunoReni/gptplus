#INCLUDE "WSMAT173.CH"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT173  ³ Autor ³Erike Yuri da Silva    ³ Data ³11.07.2006  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades de Gera Autorizacao Entrega  ³±±
±±³          ³ baseado nos Contrato de Parceria                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Portais                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±     	
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtDeliveryAuthorizationPartnershipContract DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtdeliveryauthorizationpartnershipcontract.apw" //"Serviço de Geração de Autorização de Entrega  baseado nos Contrato de Parceria"
	WSDATA UserCode               		As String
	WSDATA FromProduct					As String 
	WSDATA ToProduct					As String 
	WSDATA FromGroup					As String
	WSDATA ToGroup						As String
	WSDATA FromDeliveryDate				As Date
	WSDATA ToDeliveryDate				As Date 
	WSDATA EvaluateStock				As Integer
	WSDATA FromWarehouse				As String
	WSDATA ToWarehouse					As String
	WSDATA ConsidersRudeNecessity		As Integer
	WSDATA ConsidersNegativeBalance		As Integer
	WSDATA FromContract					As String
	WSDATA ToContract					As String	
	WSDATA WsNull      		        	As String 

	WSMETHOD GeneratesDeliveryAuthorization	DESCRIPTION STR0002 //"Método de geracao da autorização de entrega baseado no contrato de parceria"
ENDWSSERVICE



/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GeneratesDeli³Autor  ³Erike Yuri da Silva ³Data  ³11.07.2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de gerar autorizacao de entrega a partir de contrato  ³±±
±±³          ³de parceria.                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Estrutura de parametros utilizados para filtro na roti³±±
±±³          ³       na automatica                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Portais   		                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GeneratesDeliveryAuthorization WSRECEIVE UserCode, FromProduct,ToProduct,FromGroup,ToGroup,FromDeliveryDate,ToDeliveryDate,EvaluateStock,FromWarehouse,ToWarehouse,ConsidersRudeNecessity,ConsidersNegativeBalance,FromContract,ToContract WSSEND WSNULL WSSERVICE MtDeliveryAuthorizationPartnershipContract

Local aArea     := GetArea()
Local lRetorno  := .T.
Local nX        := 0 
Local aCab		:= {}
Local aErro		:= {}
Local cErro		:= ""

Private lMsErroAuto 	:= .F.
Private lAutoErrNoFile 	:= .T.

If PrtChkUser(::UserCode,"MtDeliveryAuthorizationPartnershipContract","GeneratesDeliveryAuthorization")

    If Empty(::ToProduct)
    	cErro += chr(13)+chr(10)+STR0003 //"- O parâmetro ToProduct deve ser informado" 
    	lRetorno := .F.
    EndIf
    If Empty(::ToGroup)
    	cErro += chr(13)+chr(10)+STR0004 //"- O parâmetro ToGroup deve ser informado" 
    	lRetorno := .F.
    EndIf       
    If Empty(::ToDeliveryDate)
    	cErro += chr(13)+chr(10)+STR0005 //"- O parâmetro ToDeliveryDate deve ser informado" 
    	lRetorno := .F.
    EndIf   
    If Empty(::ToWarehouse)
    	cErro += chr(13)+chr(10)+STR0006 //"- O parâmetro ToWarehouse deve ser informado" 
    	lRetorno := .F.
    EndIf      
    If Empty(::ConsidersRudeNecessity)
    	cErro += chr(13)+chr(10)+STR0007 //"- O parâmetro ConsidersRudeNecessity deve ser informado" 
    	lRetorno := .F.
    EndIf  
    If Empty(::ConsidersNegativeBalance)
    	cErro += chr(13)+chr(10)+STR0008 //"- O parâmetro ConsidersNegativeBalance deve ser informado" 
    	lRetorno := .F.
    EndIf  
    If Empty(::ToContract)
    	cErro += chr(13)+chr(10)+STR0009 //"- O parâmetro ToContract deve ser informado" 
    	lRetorno := .F.
    EndIf    

    If lRetorno 
		aadd(aCab,{"MV_PAR01",::FromProduct				,}	)
		aadd(aCab,{"MV_PAR02",::ToProduct				,}	)
		aadd(aCab,{"MV_PAR03",::FromGroup				,}	)
		aadd(aCab,{"MV_PAR04",::ToGroup					,}	)
		aadd(aCab,{"MV_PAR05",::FromDeliveryDate		,}	)
		aadd(aCab,{"MV_PAR06",::ToDeliveryDate			,}	)
		aadd(aCab,{"MV_PAR07",::EvaluateStock			,}	)
		aadd(aCab,{"MV_PAR08",::FromWarehouse			,}	)
		aadd(aCab,{"MV_PAR09",::ToWarehouse				,}	)
		aadd(aCab,{"MV_PAR10",::ConsidersRudeNecessity	,}	)
		aadd(aCab,{"MV_PAR11",::ConsidersNegativeBalance	,}	)
		aadd(aCab,{"MV_PAR12",::FromContract	  		,}	)
		aadd(aCab,{"MV_PAR13",::ToContract				,}	)
	    
		MATA173(aCab)
		
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("GeneratesDeliveryAuthorization",cErro)
			lRetorno := .F.
		EndIf	
	Else
		SetSoapFault("GeneratesDeliveryAuthorization",cErro)	
	EndIf
Else
	lRetorno := .F.
	SetSoapFault("GeneratesDeliveryAuthorization",STR0010)	//"Usuario invalido"
Endif

RestArea(aArea)
Return(lRetorno)

Function wsmat173()
Return