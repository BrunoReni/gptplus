#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "WSMAT410.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT410  ³ Autor ³Kleber Dias Gomes      ³ Data ³19.05.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades do Pedido de Venda           ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³06/08/07³129977³-Inclusao do UserFields na estrutura      ³±±
±±³              ³        ³      ³SalesOrderRelease.                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao da estruturas utilizadas                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSTRUCT SalesOrderView
	WSDATA SoHeader					AS SalesOrderHeaderView
	WSDATA SoItem					AS ARRAY OF SalesOrderItemView
ENDWSSTRUCT

WSSTRUCT SalesOrderRelease
	WSDATA OrderID					AS String  	//	Numero do Pedido
	WSDATA OrderItem				AS String  	//	Numero do Item no Pedido
	WSDATA CustomerCode				AS String  	//	Codigo do Cliente
	WSDATA CustomerUnit				AS String  	//	Loja do Cliente
	WSDATA ProductID				AS String  	//	Codigo do Produto
	WSDATA QuantityApproved			AS Float   	//	Quantidade Liberada
	WSDATA Status					AS String  	//	Status
	WSDATA UserFields            	AS Array Of UserField OPTIONAL	// Campos de Usuario
ENDWSSTRUCT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtCustomerSalesOrder DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtcustomersalesorder.apw" //"Serviço de consulta e atualização dos pedidos de venda ( <b>Restrição de cliente</b> )"
	WSDATA Header                 	AS Array OF BrwHeader
	WSDATA HeaderType             	AS String
	WSDATA UserCode               	AS String
	WSDATA CustomerID			  	AS String
	WSDATA OrderID				  	AS String
	WSDATA SalesOrderStatus       	AS String
	WSDATA RegisterDateFrom       	AS Date OPTIONAL
	WSDATA RegisterDateTo   	  	AS Date OPTIONAL
	WSDATA ListOfSalesOrder		  	AS Array OF SalesOrderHeaderView OPTIONAL
	WSDATA SalesOrder	  		  	AS SalesOrderView
	WSDATA ListOfRelease		  	AS Array OF SalesOrderRelease
	WSDATA WsNull                 	AS String
	WSDATA QueryAddWhere          	AS String OPTIONAL
	WSDATA IndexKey               	AS String OPTIONAL
	WSDATA PageLen                	AS Integer OPTIONAL
	WSDATA PageFirst              	AS Integer OPTIONAL
	WSDATA BidNumberFrom          	As String OPTIONAL
	WSDATA BidNumberTo            	As String OPTIONAL
	WSDATA TotalSO					AS Integer OPTIONAL
	
	WSMETHOD GetHeader        		DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
	WSMETHOD GetSalesOrder    		DESCRIPTION STR0003 //"Método de consulta das informacoes do pedido de venda"
	WSMETHOD BrwSalesOrder    		DESCRIPTION STR0004 //"Método de listagem dos pedidos de venda"
	WSMETHOD BrwTotalSalesOrder    	DESCRIPTION STR0026 //"Método de retorno do total de pedidos de venda do BrwSalesOrder"
	WSMETHOD PutSalesOrder    		DESCRIPTION STR0005 //"Método de atualização das informacoes do pedido de venda"
	WSMETHOD DelSalesOrder    		DESCRIPTION STR0006 //"Método de exclusão do pedido de venda"
	WSMETHOD BrwRelease       		DESCRIPTION STR0007 //"Método de listagem das liberações do pedido de venda ( <i>Período</i> )"
	WSMETHOD GetRelease       		DESCRIPTION STR0008 //"Método de consulta as liberações do pedido de venda ( <i>Cliente</i> )"
	WSMETHOD GetStatusRelease 		DESCRIPTION STR0009 //"Método de consulta a liberaçao do pedido de venda"
	WSMETHOD BrwIdOrder  			DESCRIPTION STR0004 //"Método de listagem dos pedidos de venda"	
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Kleber Dias Gomes     ³ Data ³21.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos headers deste WS                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da Estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtCustomerSalesOrder

Local nX := 0

::Header := MtHeader(::HeaderType)
If Empty(::Header)
	Do Case 
		Case ::HeaderType == "COMISSION"
			
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_VEND1")
			::Header[nX]:HeaderField   := "CODE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3Cbox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := STR0010 //"Descrição"
			::Header[nX]:HeaderField   := "DESCRIPTION"
			::Header[nX]:HeaderPicture := "@!"
			::Header[nX]:HeaderSize    := 20
			::Header[nX]:HeaderDec     := 0
			::Header[nX]:HeaderType    := "C"
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := .F.
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++			
			::Header[nX]:HeaderTitle   := RetTitle("C5_COMIS1")
			::Header[nX]:HeaderField   := "VALUE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
		Case ::HeaderType == "PACKAGESVIEW"
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_ESPECI1")
			::Header[nX]:HeaderField   := "CODE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3Cbox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := STR0010 //"Descrição"
			::Header[nX]:HeaderField   := "DESCRIPTION"
			::Header[nX]:HeaderPicture := "@!"
			::Header[nX]:HeaderSize    := 20
			::Header[nX]:HeaderDec     := 0
			::Header[nX]:HeaderType    := "C"
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := .F.
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_VOLUME1")
			::Header[nX]:HeaderField   := "VALUE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
		Case ::HeaderType == "SALESORDERRELEASE"
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_PEDIDO")
			::Header[nX]:HeaderField := "ORDERID"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_ITEM")
			::Header[nX]:HeaderField := "ORDERITEM"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_CLIENTE")
			::Header[nX]:HeaderField := "CUSTOMERCODE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_LOJA")
			::Header[nX]:HeaderField := "CUSTOMERUNIT"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_PRODUTO")
			::Header[nX]:HeaderField := "PRODUCTID"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_QTDLIB")
			::Header[nX]:HeaderField := "QUANTITYAPPROVED"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := STR0011 //"Status"
			::Header[nX]:HeaderField := "STATUS"
			::Header[nX]:HeaderPicture := "@!"
			::Header[nX]:HeaderSize    := 20
			::Header[nX]:HeaderDec     := 0
			::Header[nX]:HeaderType    := "C"
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := .F.
	EndCase
Endif

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSalesOrder³Autor  ³Kleber Dias Gomes   ³Data  ³20.05.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos pedidos de venda de um cliente     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Pedido de Venda                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos pedidos de venda de um cliente     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetSalesOrder WSRECEIVE UserCode,CustomerID,OrderID,QueryAddWhere WSSEND SalesOrder WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSC5 := "SC5"
Local cAliasSC6 := "SC6"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local aStruSC6  := {}
Local cQuery    := ""
Local nX        := 0

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","GetSalesOrder","SA1",::CustomerID)
	If !Empty(cCliente) .AND. !Empty(cLoja)

		DbSelectArea("SC5")
		DbSetOrder(1)
		If MsSeek(xFilial("SC5")+::OrderID)               

			::SalesOrder:SOHeader	:= WsClassNew("SalesOrderHeaderView")
			GetPVHeader(@::SalesOrder:SOHeader,cAliasSC5)

			DbSelectArea("SC6")
			DbSetOrder(1)
			lQuery	:= .T.
			aStruSC6	:= SC6->(dbStruct())
			cAliasSC6:= GetNextAlias()

			cQuery	:= "SELECT * "
			cQuery	+= "FROM "+RetSqlName("SC6")+" SC6 "
			cQuery	+= "WHERE "
			cQuery	+= "SC6.C6_FILIAL = '"+xFilial("SC6")+"' AND "
			cQuery	+= "SC6.C6_NUM='"+::OrderID+"' AND "
			cQuery	+= "SC6.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery	+= "ORDER BY "+SqlOrder(SC6->(IndexKey()))

			cQuery	:= ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6,.T.,.T.)

			For nX := 1 To Len(aStruSC6)
				If aStruSC6[nX][2]<>"C" .AND. aStruSC6[nX][2]<>"M"
					TcSetField(cAliasSC6,aStruSC6[nX][1],aStruSC6[nX][2],aStruSC6[nX][3],aStruSC6[nX][4])
				Endif
			Next nX

			DbSelectArea(cAliasSC6)
			While !Eof() .AND. xFilial("SC6") == (cAliasSC6)->C6_FILIAL .AND.;
					::OrderID == (cAliasSC6)->C6_NUM

				AAdd(::SalesOrder:SOItem,WSClassNew("SalesOrderItemView"))
				nY++
				GetPVItem(@::SalesOrder:SOItem[nY],cAliasSC6,cAliasSC5)

				DbSelectArea(cAliasSC6)
				dbSkip()
			EndDo
			If lQuery
				DbSelectArea(cAliasSC6)
				dbCloseArea()
				DbSelectArea("SC6")
			Endif
		Else
			lRetorno := .F.
			SetSoapFault("GETSALESORDER",STR0012) //"Pedido nao encontrado"
		Endif
	Else
		lRetorno := .F.
		SetSoapFault("GETSALESORDER",STR0013) //"Cliente invalido"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwSalesOrder³Autor  ³Kleber Dias Gomes   ³Data  ³16.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos pedidos de venda de um cliente em  ³±±
±±³          ³Browse                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC3: Data Emissao ate                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos pedidos de venda de um cliente em  ³±±
±±³          ³Browse                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwSalesOrder WSRECEIVE UserCode,CustomerID,RegisterDateFrom,RegisterDateTo,BidNumberFrom,BidNumberTo,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfSalesOrder WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()

Local lRetorno  := .T.
Local lQuery    := .F.
Local nX        := 0
Local nY        := 0
Local cAliasSC5 := "SC5"
Local cQuery    := ""
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cArquivo  := ""       
Local aStruSC5  := {}

SC5->(DbSetOrder(2))

DEFAULT ::RegisterDateFrom := dDataBase
DEFAULT ::RegisterDateTo   := dDataBase
DEFAULT ::IndexKey         := SC5->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","BrwSalesOrder","SA1",::CustomerID)
	If !Empty(cCliente) .AND. !Empty(cLoja)

		DbSelectArea("SC5")
		DbSetOrder(2)

		lQuery	:= .T.
		aStruSC5	:= SC5->(dbStruct())
		cAliasSC5:= GetNextAlias()

		cQuery	:= "SELECT * "
		cQuery	+= "FROM "+RetSqlName("SC5")+" SC5 "
		cQuery	+= "WHERE "
		cQuery	+= "SC5.C5_FILIAL = '"+xFilial("SC5")+"' AND "
		cQuery	+= "SC5.C5_CLIENTE='"+cCliente+"' AND "
		cQuery	+= "SC5.C5_LOJACLI='"+cLoja+"' AND "
		cQuery	+= "SC5.C5_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
		cQuery	+= "SC5.C5_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
		If !Empty(::BidNumberFrom)
			cQuery	+= "SC5.C5_COTACAO>='"+::BidNumberFrom+"' AND "
		Endif
		If !Empty(::BidNumberTo)
			cQuery	+= "SC5.C5_COTACAO<='"+::BidNumberTo+"' AND "
		Endif
		cQuery	+= "SC5.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+WsSqlOrder(::IndexKey)

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

	
		For nX := 1 To Len(aStruSC5)
			If aStruSC5[nX][2]<>"C" .AND. aStruSC5[nX][2]<>"M"
				TcSetField(cAliasSC5,aStruSC5[nX][1],aStruSC5[nX][2],aStruSC5[nX][3],aStruSC5[nX][4])
			Endif
		Next nX

		nX := 0
		DbSelectArea(cAliasSC5)		
		While !Eof() .AND. xFilial("SC5") == (cAliasSC5)->C5_FILIAL
			If (cAliasSC5)->C5_CLIENTE == cCliente .AND.;
					(cAliasSC5)->C5_LOJACLI == cLoja
				nX++					
				If ::PageFirst==0 .OR. nX >= ::PageFirst
					If nY == 0
						::ListOfSalesOrder := {}
					Endif
					AAdd(::ListOfSalesOrder,WsClassNew("SalesOrderHeaderView"))
					nY++
					GetPVHeader(@::ListOfSalesOrder[nY],cAliasSC5)
				Endif
			Endif
			If nY >= ::PageLen .AND. ::PageLen <> 0
				Exit
			Endif
			DbSelectArea(cAliasSC5)
			dbSkip()
		EndDo
		If lQuery
			DbSelectArea(cAliasSC5)
			dbCloseArea()
			DbSelectArea("SC5")
		Else
			RetIndex(cAliasSC5)
			FErase(cArquivo+OrdBagExt())
		Endif
	Else
		lRetorno := .F.
		SetSoapFault("BRWSALESORDER",STR0013) //"Cliente invalido"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwTotalSales³Autor  ³Luiz Couto          ³Data  ³01.09.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao do total de pedidos de venda           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC4: Data Emissao ate                                      ³±±
±±³          ³ExpC5: Seu Pedido de                                         ³±±
±±³          ³ExpC6: Seu pedido ate                                        ³±±
±±³          ³ExpC7: Add Where                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao do total de pedidos de venda           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwTotalSalesOrder WSRECEIVE UserCode,CustomerID,RegisterDateFrom,RegisterDateTo,BidNumberFrom,BidNumberTo,QueryAddWhere WSSEND TotalSO WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local cAliasSC5 := ""
Local cQuery    := ""
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)

DEFAULT ::RegisterDateFrom := FirstDay(dDataBase)
DEFAULT ::RegisterDateTo   := LastDay(dDataBase)

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","BrwSalesOrder","SA1",::CustomerID)
	If !Empty(cCliente) .AND. !Empty(cLoja)
		::TotalSO := 0
		cAliasSC5 := GetNextAlias()

		cQuery	:= "SELECT COUNT(*) TOTALSO "
		cQuery	+= "FROM "+RetSqlName("SC5")+" SC5 "
		cQuery	+= "WHERE "
		cQuery	+= "SC5.C5_FILIAL = '"+xFilial("SC5")+"' AND "
		cQuery	+= "SC5.C5_CLIENTE='"+cCliente+"' AND "
		cQuery	+= "SC5.C5_LOJACLI='"+cLoja+"' AND "
		cQuery	+= "SC5.C5_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
		cQuery	+= "SC5.C5_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
		If !Empty(::BidNumberFrom)
			cQuery	+= "SC5.C5_COTACAO>='"+::BidNumberFrom+"' AND "
		Endif
		If !Empty(::BidNumberTo)
			cQuery	+= "SC5.C5_COTACAO<='"+::BidNumberTo+"' AND "
    		Endif
		cQuery	+= "SC5.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

		::TotalSO := (cAliasSC5)->TOTALSO
	Else
		lRetorno := .F.
		SetSoapFault("BRWSALESORDER",STR0013) //"Cliente invalido"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSalesOrder³Autor  ³Kleber Dias Gomes   ³Data  ³24.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de inclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Dados do Pedido de Venda                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de inclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³14/11/06³112931³-Incluido PadR() na comparacao do codigo ³±±
±±³              ³        ³      ³do cliente.                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutSalesOrder WSRECEIVE UserCode,CustomerID,SalesOrder WSSEND OrderID WSSERVICE MtCustomerSalesOrder

Local aArea		:= GetArea()	//Guarda a area atual
Local aCab		:= {}			//Array com os dados do cabecalho do PV
Local aItens	:= {}			//Array com os dados dos itens do PV
Local aErro     := {}			//Array com o retorno do erro
Local cErro     := ""			//String com o erro
Local lRetorno	:= .T.			//Indica retorno da funcao
Local nX        := 0			//Usada em lacos For...Next

PRIVATE lMsErroAuto    := .F.	//Indica retorno da MsExecAuto()
PRIVATE lAutoErrNoFile := .T.	//Usada dentro da MsExecAuto()

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","PutSalesOrder","SA1",::CustomerID)

	If PadR(::SalesOrder:SOHeader:CustomerCode, Len(SA1->A1_COD))  == SubStr(::CustomerID,1,Len(SA1->A1_COD))
	
		DbSelectArea("SC5")
		DbSetOrder(1)
		If ::SalesOrder:SOHeader:OrderID <> Nil
			MsSeek(xFilial("SC5")+::SalesOrder:SOHeader:OrderID)
		Else
			MsGoto(0)
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiacao do cabecalho do pedido de venda                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lRetorno := PutPvHead(::SalesOrder:SOHeader,@aCab)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiacao dos itens do pedido de venda                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRetorno 
			lRetorno := PutPvItem(::SalesOrder:SOHeader,::SalesOrder:SOItem,@aItens)
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se eh alteracao ou inclusao                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRetorno
			DbSelectArea("SC5")
			DbSetOrder(1)
			If !Empty(::SalesOrder:SOHeader:OrderID) .AND. MsSeek(xFilial("SC5")+::SalesOrder:SOHeader:OrderID)
				MATA410(aCab,aItens,4)
				::OrderId := SC5->C5_NUM
			Else
				MATA410(aCab,aItens,3)
				::OrderId := SC5->C5_NUM			
			Endif
	
			If lMsErroAuto
				aErro := GetAutoGRLog()
				For nX := 1 To Len(aErro)
					cErro += aErro[nX] + Chr(13)+Chr(10)
				Next nX
				SetSoapFault("PUTSALESORDER",cErro)
				lRetorno := .F.
			Endif
		Endif
	Else
		lRetorno := .F.
		SetSoapFault("PUTSALESORDER",STR0013) //"Cliente invalido"
	Endif	
Else
	lRetorno := .F.
Endif
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelSalesOrder³Autor  ³Kleber Dias Gomes   ³Data  ³30.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de exclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Pedido de Venda                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de exclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelSalesOrder WSRECEIVE UserCode,CustomerID,OrderID WSSEND WsNull  WSSERVICE MtCustomerSalesOrder

Local aCab		:= {}
Local aItens	:= {}
Local aErro     := {}
Local lRetorno  := .T.
Local cErro     := ""
Local nX        := 0
PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","DelSalesOrder","SA1",::CustomerID)
	AAdd(aCab,{"C5_NUM"		,::OrderID,Nil})

	MATA410(aCab,aItens,5)
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("DELSALESORDER",cErro)
		lRetorno := .F.
	Endif
Else
	lRetorno := .F.
Endif

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwRelease   ³Autor  ³Kleber Dias Gomes   ³Data  ³25.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos pedidos de venda de um cliente em  ³±±
±±³          ³Browse                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC3: Data Emissao ate	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos pedidos de venda de um cliente em  ³±±
±±³          ³Browse                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwRelease WSRECEIVE UserCode,CustomerID,RegisterDateFrom,RegisterDateTo,QueryAddWhere WSSEND ListOfRelease WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nX        := 0
Local cAliasSC5 := "SC5"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cStatus   := ""
Local cQuery    := ""

DEFAULT RegisterDateFrom := dDataBase
DEFAULT RegisterDateTo   := dDataBase

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","BrwRelease","SA1",::CustomerID)
	If !Empty(cCliente) .AND. !Empty(cLoja)

		DbSelectArea("SC9")
		DbSetOrder(1)
		DbSelectArea("SC5")
		DbSetOrder(2)
		lQuery	:= .T.
		cAliasSC5:= GetNextAlias()

		cQuery := "SELECT C5_FILIAL, C5_EMISSAO, C9_PEDIDO, C9_ITEM, C9_CLIENTE, C9_LOJA, "
		cQuery += "C9_PRODUTO, C9_QTDLIB, C9_BLEST, C9_BLCRED, C9_BLWMS "
		If SC9->(FieldPos("C9_BLTMS"))>0
			cQuery += ",C9_BLTMS "
		Endif
		cQuery += GetUserFields("SC9") 
		cQuery += "FROM "+RetSqlName("SC5")+" SC5, "+RetSqlName("SC9")+" SC9 "
		cQuery += "WHERE "
		cQuery += "SC5.C5_FILIAL  = '"+xFilial("SC5")+"' AND "
		cQuery += "SC5.C5_CLIENTE ='"+cCliente+"' AND "
		cQuery += "SC5.C5_LOJACLI ='"+cLoja+"' AND "
		cQuery += "SC5.C5_EMISSAO >='"+DtoS(::RegisterDateFrom)+"' AND "
		cQuery += "SC5.C5_EMISSAO <='"+DtoS(::RegisterDateTo)+"' AND "
		cQuery += "SC5.D_E_L_E_T_ =' ' AND "
		cQuery += "SC9.C9_FILIAL  ='" + xFilial("SC9") + "' AND "
		cQuery += "SC9.C9_PEDIDO  = SC5.C5_NUM AND "
		cQuery += "SC9.C9_CLIENTE = SC5.C5_CLIENTE AND "
		cQuery += "SC9.C9_LOJA    = SC5.C5_LOJACLI AND "
		cQuery += "SC9.D_E_L_E_T_ =' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+SqlOrder(SC5->(IndexKey())+","+SC9->(IndexKey()))

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

		TcSetField(cAliasSC5,"C5_EMISSAO","D",TamSX3("C5_EMISSAO")[1],TamSX3("C5_EMISSAO")[2])
		TcSetField(cAliasSC5,"C9_QTDLIB" ,"N",TamSX3("C9_QTDLIB")[1] ,TamSX3("C9_QTDLIB")[2])					

		DbSelectArea(cAliasSC5)

		::ListOfRelease := {}
		While !Eof() .AND. xFilial("SC5") == (cAliasSC5)->C5_FILIAL .AND.;
				::RegisterDateFrom <= (cAliasSC5)->C5_EMISSAO .AND.;
				::RegisterDateTo >= (cAliasSC5)->C5_EMISSAO

				AAdd(::ListOfRelease,WsClassNew('SalesOrderRelease'))
				nX++

				::ListOfRelease[nX]:OrderID					:= (cAliasSC5)->C9_PEDIDO
				::ListOfRelease[nX]:OrderItem				:= (cAliasSC5)->C9_ITEM
				::ListOfRelease[nX]:CustomerCode			:= (cAliasSC5)->C9_CLIENTE
				::ListOfRelease[nX]:CustomerUnit			:= (cAliasSC5)->C9_LOJA
				::ListOfRelease[nX]:ProductID				:= (cAliasSC5)->C9_PRODUTO
				::ListOfRelease[nX]:QuantityApproved		:= (cAliasSC5)->C9_QTDLIB
				UserFields("SC9", @::ListOfRelease[nX]:UserFields, cAliasSC5)
				Do Case

				Case Empty(C9_BLEST) .AND. Empty(C9_BLCRED) .AND.;
						C9_BLWMS $ '  ,05,06,07' .AND.;
						Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.)

					cStatus := PadR(STR0014,20) //"Liberado"

				Case C9_BLCRED == '10' .AND. C9_BLEST == '10'

					cStatus := PadR(STR0015,20) //"Faturado"

				Case !Empty(C9_BLCRED) .AND. C9_BLCRED <> '10'

					cStatus := PadR(STR0016,20) //"Blq. de Credito"

				Case !Empty(C9_BLEST) .AND. C9_BLEST<>'10'

					cStatus := PadR(STR0017,20) //"Blq. de Estoque"

				Case !C9_BLWMS$'  ,05,06,07'

					cStatus := PadR(STR0018,20) //"Blq. de WMS"

				Case !(Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.))

					cStatus := PadR(STR0019,20) //"Blq. de TMS"

				EndCase
				::ListOfRelease[nX]:Status						:= cStatus

			DbSelectArea(cAliasSC5)
			dbSkip()
		EndDo

		If lQuery
			DbSelectArea(cAliasSC5)
			dbCloseArea()
			DbSelectArea("SC5")
		Endif
		nX := 0
	Else
		lRetorno := .F.
		SetSoapFault("BRWRELEASE",STR0013) //"Cliente invalido"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetRelease   ³Autor  ³Kleber dias Gomes   ³Data  ³03.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao das liberacoes de um pedidos de venda  ³±±
±±³          ³de um cliente                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente                                               ³±±
±±³          ³ExpC3: Numero do Pedido  	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao das liberacoes de um pedidos de venda  ³±±
±±³          ³de um cliente                                                ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetRelease WSRECEIVE UserCode,CustomerID,OrderID,QueryAddWhere WSSEND ListOfRelease WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local nX        := 0
Local lQuery    := .F.
Local cAliasSC9 := "SC9"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cStatus   := ""
Local cQuery    := ""

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","GetRelease","SA1",::CustomerID)
	If !Empty(cCliente) .AND. !Empty(cLoja)

		DbSelectArea("SC9")
		DbSetOrder(1)

		lQuery	:= .T.
		cAliasSC9:= GetNextAlias()

		cQuery	:= "SELECT C9_FILIAL, C9_PEDIDO, C9_ITEM, C9_CLIENTE, C9_LOJA, C9_PRODUTO, C9_QTDLIB, "
		cQuery  += "C9_BLEST, C9_BLCRED, C9_BLWMS "
		If SC9->(FieldPos("C9_BLTMS"))>0
			cQuery += ",C9_BLTMS "
		Endif
		cQuery	+= GetUserFields("SC9") 
		cQuery	+= "FROM "+RetSqlName("SC9")+" SC9 "
		cQuery	+= "WHERE "
		cQuery	+= "SC9.C9_FILIAL = '"+xFilial("SC9")+"' AND "
		cQuery	+= "SC9.C9_PEDIDO='"+::OrderID+"' AND "
		cQuery	+= "SC9.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+SqlOrder(SC9->(IndexKey()))

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9,.T.,.T.)

		TcSetField(cAliasSC9,"C9_QTDLIB" ,"N",TamSX3("C9_QTDLIB")[1] ,TamSX3("C9_QTDLIB")[2])					

		DbSelectArea(cAliasSC9)

		::ListOfRelease := {}
		While !Eof() .AND. xFilial("SC9") == (cAliasSC9)->C9_FILIAL .AND.;
				::OrderID == (cAliasSC9)->C9_PEDIDO

			AAdd(::ListOfRelease,WsClassNew('SalesOrderRelease'))
			nX++

			::ListOfRelease[nX]:OrderID					:= (cAliasSC9)->C9_PEDIDO
			::ListOfRelease[nX]:OrderItem				:= (cAliasSC9)->C9_ITEM
			::ListOfRelease[nX]:CustomerCode			:= (cAliasSC9)->C9_CLIENTE
			::ListOfRelease[nX]:CustomerUnit			:= (cAliasSC9)->C9_LOJA
			::ListOfRelease[nX]:ProductID				:= (cAliasSC9)->C9_PRODUTO
			::ListOfRelease[nX]:QuantityApproved		:= (cAliasSC9)->C9_QTDLIB
			UserFields("SC9", @::ListOfRelease[nX]:UserFields, cAliasSC9)
			Do Case

			Case Empty(C9_BLEST) .AND. Empty(C9_BLCRED) .AND.;
					C9_BLWMS $ '  ,05,06,07' .AND.;
					Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.)

				cStatus := PadR(STR0014,20) //"Liberado"

			Case C9_BLCRED == '10' .AND. C9_BLEST == '10'

				cStatus := PadR(STR0015,20) //"Faturado"

			Case !Empty(C9_BLCRED) .AND. C9_BLCRED <> '10'

				cStatus := PadR(STR0016,20) //"Blq. de Credito"

			Case !Empty(C9_BLEST) .AND. C9_BLEST<>'10'

				cStatus := PadR(STR0017,20) //"Blq. de Estoque"

			Case !C9_BLWMS$'  ,05,06,07'

				cStatus := PadR(STR0018,20) //"Blq. de WMS"

			Case !(Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.))

				cStatus := PadR(STR0019,20) //"Blq. de TMS"

			EndCase

			::ListOfRelease[nX]:Status	:= cStatus


			DbSelectArea(cAliasSC9)
			dbSkip()
		EndDo
		If lQuery
			DbSelectArea(cAliasSC9)
			dbCloseArea()
			DbSelectArea("SC9")
		Endif
		nX := 0
	Else
		lRetorno := .F.
		SetSoapFault("BRWRELEASE",STR0013) //"Cliente invalido"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetStatusRele³Autor  ³Eduardo Riera       ³Data  ³17.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos status de um pedido de venda       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente                                               ³±±
±±³          ³ExpC3: Numero do Pedido  	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos status de um pedido de venda       ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetStatusRelease WSRECEIVE UserCode,CustomerID,OrderID,QueryAddWhere WSSEND SalesOrderStatus WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local lBloqueio := .F.
Local cAliasSC9 := "SC9"
Local cStatus   := ""
Local cQuery    := ""

DEFAULT ::SalesOrderStatus := ""

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","GetStatusRelease","SA1",::CustomerID)
	DbSelectArea("SC9")
	DbSetOrder(1)

	lQuery	:= .T.
	cAliasSC9:= GetNextAlias()

	cQuery	:= "SELECT C9_FILIAL, C9_PEDIDO, C9_ITEM, C9_CLIENTE, C9_LOJA, "
	cQuery  += "C9_PRODUTO, C9_QTDLIB, C9_BLEST, C9_BLCRED, C9_BLWMS "
	If SC9->(FieldPos("C9_BLTMS"))>0
		cQuery += ",C9_BLTMS "
	Endif
	cQuery	+= "FROM "+RetSqlName("SC9")+" SC9 "
	cQuery	+= "WHERE "
	cQuery	+= "SC9.C9_FILIAL = '"+xFilial("SC9")+"' AND "
	cQuery	+= "SC9.C9_PEDIDO='"+::OrderID+"' AND "
	cQuery	+= "SC9.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery	+= "ORDER BY "+SqlOrder(SC9->(IndexKey()))

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9,.T.,.T.)

	TcSetField(cAliasSC9,"C9_QTDLIB" ,"N",TamSX3("C9_QTDLIB")[1] ,TamSX3("C9_QTDLIB")[2])					

	DbSelectArea(cAliasSC9)

	While !Eof() .AND. xFilial("SC9") == (cAliasSC9)->C9_FILIAL .AND.;
			::OrderID == (cAliasSC9)->C9_PEDIDO

		Do Case

		Case Empty(C9_BLEST) .AND. Empty(C9_BLCRED) .AND.;
				C9_BLWMS $ '  ,05,06,07' .AND.;
				Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.)

			cStatus := PadR(STR0014,20) //"Liberado"

		Case C9_BLCRED == '10' .AND. C9_BLEST == '10'

			cStatus := PadR(STR0015,20) //"Faturado"

		Case !Empty(C9_BLCRED) .AND. C9_BLCRED <> '10'

			cStatus := PadR(STR0016,20) //"Blq. de Credito"

		Case !Empty(C9_BLEST) .AND. C9_BLEST<>'10'

			cStatus := PadR(STR0017,20) //"Blq. de Estoque"

		Case !C9_BLWMS$'  ,05,06,07'

			cStatus := PadR(STR0018,20) //"Blq. de WMS"

		Case !(Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.))

			cStatus := PadR(STR0019,20) //"Blq. de TMS"

		EndCase

		cStatus := AllTrim(cStatus)
		If !AllTrim(cStatus) $ AllTrim(::SalesOrderStatus)
			If STR0014$cStatus //"Liberado"
				::SalesOrderStatus := ","+AllTrim(cStatus)+STR0020+SubStr(::SalesOrderStatus,2) //" com "
			Else
				::SalesOrderStatus += ","+AllTrim(cStatus)
				lBloqueio := .T.
			Endif
		Endif

		DbSelectArea(cAliasSC9)
		dbSkip()
	EndDo
	If lQuery
		DbSelectArea(cAliasSC9)
		dbCloseArea()
		DbSelectArea("SC9")
	Endif
	If Empty(::SalesOrderStatus)
		::SalesOrderStatus := STR0021 //"Carteira"
	Else
		::SalesOrderStatus := SubStr(::SalesOrderStatus,2)
	Endif
	If !lBloqueio .AND. STR0014$::SalesOrderStatus //"Liberado"
		::SalesOrderStatus := STR0014 //"Liberado"
	Endif		
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ BrwIdOrder  ³Autor  ³ Cleber Martinez    ³Data  ³11.07.2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Metodo de recuperacao dos numeros de licitacoes dos Pedidos ³±±
±±³          ³ de Venda (C5_COTACAO) para consulta F3                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente/Loja                                          ³±±
±±³          ³ExpC3: Numero Pedido   	                                   ³±±
±±³          ³ExpC4: Exp. query adicional                                  ³±±
±±³          ³ExpC5: Chave/Indice p/ ordenacao                             ³±±
±±³          ³ExpN6: Nr. de itens para paginacao                           ³±±
±±³          ³ExpN7: Primeiro item a paginar                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Portal do Cliente                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwIdOrder WSRECEIVE UserCode,CustomerID,OrderID,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfSalesOrder WSSERVICE MtCustomerSalesOrder

Local aArea     := GetArea()			//Guarda a area atual
Local lRetorno  := .T.					//Indica o retorno do metodo 
Local lQuery    := .F.					//Indica se executou query
Local nX        := 0					//Usada em lacos For...Next
Local nY        := 0					//Usada em lacos For...Next
Local cAliasSC5 := "SC5"				//Alias da tabela SC5
Local cQuery    := ""					//Query a ser executada 
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))		//Cod. cliente Portal
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)		//Loja cliente Portal
Local cArquivo  := ""       			//Nome do arquivo temporario (CodeBase)
Local aStruSC5  := {}					//Estrutura da tabela SC5

SC5->(DbSetOrder(2))

DEFAULT ::IndexKey	:= SC5->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","BrwIdOrder","SA1",::CustomerID)
	If !Empty(cCliente) .AND. !Empty(cLoja)

		DbSelectArea("SC5")
		DbSetOrder(2)
		lQuery	:= .T.
		aStruSC5	:= SC5->(dbStruct())
		cAliasSC5:= GetNextAlias()

		cQuery	:= "SELECT * "
		cQuery	+= "FROM "+RetSqlName("SC5")+" SC5 "
		cQuery	+= "WHERE "
		cQuery	+= "SC5.C5_FILIAL = '"+xFilial("SC5")+"' AND "
		cQuery	+= "SC5.C5_CLIENTE='"+cCliente+"' AND "
		cQuery	+= "SC5.C5_LOJACLI='"+cLoja+"' AND "
		cQuery	+= "SC5.C5_COTACAO <>'"+Space(Len(SC5->C5_COTACAO))+"' AND "
		If !Empty(::OrderID)
			cQuery	+= "SC5.C5_COTACAO LIKE '%"+::OrderID+"%' AND "
		EndIf
		cQuery	+= "SC5.D_E_L_E_T_=' ' "
		cQuery	:= WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+SqlOrder(SC5->(IndexKey()))

		cQuery	:= ChangeQuery(cQuery)
		
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)
	
		For nX := 1 To Len(aStruSC5)
			If aStruSC5[nX][2]<>"C" .AND. aStruSC5[nX][2]<>"M"
				TcSetField(cAliasSC5,aStruSC5[nX][1],aStruSC5[nX][2],aStruSC5[nX][3],aStruSC5[nX][4])
			EndIf
		Next nX

		nX := 0
		DbSelectArea(cAliasSC5)		
		While !Eof() .AND. xFilial("SC5") == (cAliasSC5)->C5_FILIAL
			If (cAliasSC5)->C5_CLIENTE == cCliente .AND. (cAliasSC5)->C5_LOJACLI == cLoja
				nX++					
				If ::PageFirst==0 .OR. nX >= ::PageFirst
					If nY == 0
						::ListOfSalesOrder := {}
					EndIf
					AAdd(::ListOfSalesOrder,WsClassNew("SalesOrderHeaderView"))
					nY++
					GetPVHeader(@::ListOfSalesOrder[nY],cAliasSC5)
				EndIf
			EndIf
			If nY >= ::PageLen .AND. ::PageLen <> 0
				Exit
			EndIf
			DbSelectArea(cAliasSC5)
			DbSkip()
		EndDo
		If lQuery
			DbSelectArea(cAliasSC5)
			dbCloseArea()
			DbSelectArea("SC5")
		Else
			RetIndex(cAliasSC5)
			FErase(cArquivo+OrdBagExt())
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWIDORDER",STR0013) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPVHead ³Autor  ³ Eduardo Riera         ³ Data ³29.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do Header do pedido de venda         ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do item do pedido de venda                     ³±±
±±³          ³ExpC2: Alias do SC5                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SC5 posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tatiana C.    ³12/02/07³118356³Tratamento do tipo de frete para exibir a³±±
±±³              ³        ³      ³a descricao completa no HTML.            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetPVHeader(oObjeto,cAliasSC5,aCab)

Local aArea    := GetArea()
Local nX       := 0
Local nVend    := 0
Local nMaxVend := Fa440CntVen()
Local cEspec   := ""

If aCab == Nil
	oObjeto:OrderID				:= (cAliasSC5)->C5_NUM
	oObjeto:OrderTypeCode		:= (cAliasSC5)->C5_TIPO
	oObjeto:CustomerCode		:= (cAliasSC5)->C5_CLIENTE
	oObjeto:CustomerUnit		:= (cAliasSC5)->C5_LOJACLI
	oObjeto:DeliveryCustomer	:= (cAliasSC5)->C5_CLIENT
	oObjeto:DeliveryUnitCode	:= (cAliasSC5)->C5_LOJAENT
	oObjeto:CarrierCode			:= (cAliasSC5)->C5_TRANSP
	oObjeto:CustomerType		:= (cAliasSC5)->C5_TIPOCLI
	oObjeto:PaymentPlanCode		:= (cAliasSC5)->C5_CONDPAG
	oObjeto:PriceListCode		:= (cAliasSC5)->C5_TABELA
	
	oObjeto:Discount1			   := (cAliasSC5)->C5_DESC1
	oObjeto:Discount2			   := (cAliasSC5)->C5_DESC2
	oObjeto:Discount3			   := (cAliasSC5)->C5_DESC3
	oObjeto:Discount4			   := (cAliasSC5)->C5_DESC4
	oObjeto:BankCode			   := (cAliasSC5)->C5_BANCO
	oObjeto:FinancialDiscount	   := (cAliasSC5)->C5_DESCFI
	oObjeto:RegisterDate		   := (cAliasSC5)->C5_EMISSAO
	oObjeto:BidNumber			   := (cAliasSC5)->C5_COTACAO
	oObjeto:FreightType			   := (cAliasSC5)->C5_TPFRETE 
	If !Empty((cAliasSC5)->C5_TPFRETE)
		If (cAliasSC5)->C5_TPFRETE == "C"
		   oObjeto:FreightType := "CIF"
		Else
		   oObjeto:FreightType := "FOB"
		Endif
	Endif       

	oObjeto:FreightValue		   := (cAliasSC5)->C5_FRETE
	oObjeto:InsuranceValue		   := (cAliasSC5)->C5_SEGURO
	oObjeto:AdditionalExpenseValue := (cAliasSC5)->C5_DESPESA
	oObjeto:IndependentFreight	   := (cAliasSC5)->C5_FRETAUT
	oObjeto:AdjustmentType		   := (cAliasSC5)->C5_REAJUST
	oObjeto:SalesOrderCurrency	   := (cAliasSC5)->C5_MOEDA
	oObjeto:NetWeight			   := (cAliasSC5)->C5_PESOL
	oObjeto:GrossWeight			   := (cAliasSC5)->C5_PBRUTO
	oObjeto:RedeliveryCarrierCode  := (cAliasSC5)->C5_REDESP
	oObjeto:FinancialIncrease	   := (cAliasSC5)->C5_ACRSFIN
	oObjeto:InvoiceMessage		   := (cAliasSC5)->C5_MENNOTA
	oObjeto:StandardMessage1	   := (cAliasSC5)->C5_MENPAD
	oObjeto:PriceWithServiceTax	   := (cAliasSC5)->C5_INCISS
	oObjeto:AprobationType		   := (cAliasSC5)->C5_TIPLIB
	oObjeto:IndemnityValue	   	   := (cAliasSC5)->C5_DESCONT
	oObjeto:IndemnityPercentage	   := (cAliasSC5)->C5_PDESCAB
	UserFields("SC5",@oObjeto:UserFields,cAliasSC5)          
	
	nX := 0
	For nVend := 1 To nMaxVend
		If nX == 0
			oObjeto:Sellers := {}
		Endif	
		If (cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_COMIS"+AllTrim(Str(nVend)))))) > 0
			AAdd(oObjeto:Sellers,WsClassNew('GenericStruct'))
			nX++
			oObjeto:Sellers[nX]:Code 		:= (cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_VEND"+AllTrim(Str(nVend))))))
			oObjeto:Sellers[nX]:Description	:= PadR(STR0027,40) // "Comissão do vendedor"
			oObjeto:Sellers[nX]:Value		:= (cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_COMIS"+AllTrim(Str(nVend))))))
		EndIf
	Next nVend
	nX:=0
	cEspec := "1"
	While ( !Empty(cEspec) )
		If !Empty((cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_ESPECI"+cEspec)))))
			If nX == 0
				oObjeto:PackagesVolumes := {}
			Endif
			AAdd(oObjeto:PackagesVolumes,WsClassNew('GenericStruct'))
			nX++
			oObjeto:PackagesVolumes[nX]:Code			:= (cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_ESPECI"+cEspec))))
			oObjeto:PackagesVolumes[nX]:Description	:= PadR("PackageVolumeType"+cEspec,20)
			oObjeto:PackagesVolumes[nX]:Value			:= (cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_VOLUME"+cEspec))))
		Endif
		cEspec := Soma1(cEspec,1)
		If ( FieldPos("C5_ESPECI"+cEspec) == 0 )
			cEspec := ""
		Endif
	EndDo
Else
	oObjeto:OrderID               := WsProcH(aCab,"C5_NUM")
	oObjeto:RegisterDate          := WsProcH(aCab,"C5_EMISSAO")
	oObjeto:OrderTypeCode         := WsProcH(aCab,"C5_TIPO")
	oObjeto:CustomerCode          := WsProcH(aCab,"C5_CLIENTE")
	oObjeto:CustomerUnit          := WsProcH(aCab,"C5_LOJACLI")
	oObjeto:DeliveryCustomer      := WsProcH(aCab,"C5_CLIENT")
	oObjeto:DeliveryUnitCode      := WsProcH(aCab,"C5_LOJAENT")
	oObjeto:CarrierCode           := WsProcH(aCab,"C5_TRANSP")
	oObjeto:CustomerType          := WsProcH(aCab,"C5_TIPOCLI")
	oObjeto:PaymentPlanCode       := WsProcH(aCab,"C5_CONDPAG")
	oObjeto:PriceListCode         := WsProcH(aCab,"C5_TABELA")

	oObjeto:Discount1			   := WsProcH(aCab,"C5_DESC1")
	oObjeto:Discount2			   := WsProcH(aCab,"C5_DESC2")
	oObjeto:Discount3			   := WsProcH(aCab,"C5_DESC3")
	oObjeto:Discount4			   := WsProcH(aCab,"C5_DESC4")
	oObjeto:BankCode			   := WsProcH(aCab,"C5_BANCO")
	oObjeto:FinancialDiscount	   := WsProcH(aCab,"C5_DESCFI")
	oObjeto:RegisterDate		   := WsProcH(aCab,"C5_EMISSAO")
	oObjeto:BidNumber			   := WsProcH(aCab,"C5_COTACAO")
	oObjeto:FreightType			   := WsProcH(aCab,"C5_TPFRETE")
	oObjeto:FreightValue		   := WsProcH(aCab,"C5_FRETE")
	oObjeto:InsuranceValue		   := WsProcH(aCab,"C5_SEGURO")
	oObjeto:AdditionalExpenseValue := WsProcH(aCab,"C5_DESPESA")
	oObjeto:IndependentFreight	   := WsProcH(aCab,"C5_FRETAUT")
	oObjeto:AdjustmentType		   := WsProcH(aCab,"C5_REAJUST")
	oObjeto:SalesOrderCurrency	   := WsProcH(aCab,"C5_MOEDA")
	oObjeto:NetWeight			   := WsProcH(aCab,"C5_PESOL")
	oObjeto:GrossWeight			   := WsProcH(aCab,"C5_PBRUTO")
	oObjeto:RedeliveryCarrierCode  := WsProcH(aCab,"C5_REDESP")
	oObjeto:FinancialIncrease	   := WsProcH(aCab,"C5_ACRSFIN")
	oObjeto:InvoiceMessage		   := WsProcH(aCab,"C5_MENNOTA")
	oObjeto:StandardMessage1	   := WsProcH(aCab,"C5_MENPAD")
	oObjeto:PriceWithServiceTax	   := WsProcH(aCab,"C5_INCISS")
	oObjeto:AprobationType		   := WsProcH(aCab,"C5_TIPLIB")
	oObjeto:IndemnityValue	   	   := WsProcH(aCab,"C5_DESCONT")
	oObjeto:IndemnityPercentage	   := WsProcH(aCab,"C5_PDESCAB")
	UserFields("SC5",@oObjeto:UserFields,cAliasSC5,@aCab)
	
	nX := 0
	For nVend := 1 To nMaxVend
		If nX == 0
			oObjeto:Sellers := {}
		Endif
		AAdd(oObjeto:Sellers,WsClassNew("GENERICSTRUCT"))
		nX++
		If aScan(aCab,{|x| x[1] == "C5_VEND"+AllTrim(Str(nVend))})<>0
			oObjeto:Sellers[nX]:Code 			:= WsProcH(aCab,"C5_VEND"+AllTrim(Str(nVend)))
			oObjeto:Sellers[nX]:Description	    := PadR(STR0027,40) // "Comissão do vendedor"
			oObjeto:Sellers[nX]:Value			:= WsProcH(aCab,"C5_COMIS"+AllTrim(Str(nVend)))		
		Else
			oObjeto:Sellers[nX]:Code 			:= ""
			oObjeto:Sellers[nX]:Description	    := ""
			oObjeto:Sellers[nX]:Value			:= 0
		EndIf
	Next nVend
	nX := 0
	cEspec := "1"
	While ( !Empty(cEspec) )
		If !Empty(WsProcH(aCab,"C5_ESPECI"+cEspec))
			If nX == 0
				oObjeto:PackagesVolumes := {}
			Endif
			AAdd(oObjeto:PackagesVolumes,WsClassNew("GenericStruct"))
			nX++
			oObjeto:PackagesVolumes[nX]:Code			:= WsProcH(aCab,"C5_ESPECI"+cEspec)
			oObjeto:PackagesVolumes[nX]:Description	    := PadR("PackageVolumeType"+cEspec,20)
			oObjeto:PackagesVolumes[nX]:Value			:= WsProcH(aCab,"C5_VOLUME"+cEspec)
		Endif
		cEspec := Soma1(cEspec,1)
		If ( FieldPos("C5_ESPECI"+cEspec) == 0 )
			cEspec := ""
		Endif
	EndDo
Endif                                          

			
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPVItem ³Autor  ³ Eduardo Riera         ³ Data ³29.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do item do pedido de venda           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do item do pedido de venda                     ³±±
±±³          ³ExpC2: Alias do SC6                                          ³±±
±±³          ³ExpC3: Alias do SC5                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SC6 posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³20/10/06³107980³-Incluido o campo OperationType na estru-³±±
±±³              ³        ³      ³tura do Item do PV.                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetPVItem(oObjeto,cAliasSC6,cAliasSC5,aCab,aItem)

Local aArea    := GetArea()		//Guarda a area atual
Local nX       := 0				//Variavel usada em lacos For...Next
Local nVend    := 0				//Usada no laco para comissao de vendedores
Local nMaxVend := Fa440CntVen()	//Nr. maximo de vendedores 

If aCab == Nil

	oObjeto:OrderItem             := (cAliasSC6)->C6_ITEM
	oObjeto:OrderID				  := (cAliasSC6)->C6_NUM
	oObjeto:ProductID	          := (cAliasSC6)->C6_PRODUTO
	oObjeto:UnitOfMeasure         := (cAliasSC6)->C6_UM
	oObjeto:Quantity			  := (cAliasSC6)->C6_QTDVEN
	oObjeto:QuantityDelivered     := (cAliasSC6)->C6_QTDENT
	oObjeto:NetUnitPrice          := (cAliasSC6)->C6_PRCVEN
	oObjeto:NetTotal			  := (cAliasSC6)->C6_VALOR
	oObjeto:QuantityApproved	  := (cAliasSC6)->C6_QTDEMP
	oObjeto:QuantityReleasedSecondUnit:= (cAliasSC6)->C6_QTDEMP2
	oObjeto:SecondUnitOfMeasure	  := (cAliasSC6)->C6_SEGUM 
	oObjeto:ItemOutflowType		  := (cAliasSC6)->C6_TES
	oObjeto:SecondMeasureUnitQuantity := (cAliasSC6)->C6_UNSVEN
	oObjeto:Warehouse			  := (cAliasSC6)->C6_LOCAL
	oObjeto:FiscalOperation	      := (cAliasSC6)->C6_CF
	oObjeto:DiscountPercentage	  := (cAliasSC6)->C6_DESCONT
	oObjeto:ItemDiscountValue	  := (cAliasSC6)->C6_VALDESC
	oObjeto:DeliveryDate		  := (cAliasSC6)->C6_ENTREG
	oObjeto:CustomerOrderNumber	  := (cAliasSC6)->C6_PEDCLI
	oObjeto:ProductDescription	  := (cAliasSC6)->C6_DESCRI
	oObjeto:UnitListPrice		  := (cAliasSC6)->C6_PRUNIT
	oObjeto:OriginalInvoice		  := (cAliasSC6)->C6_NFORI
	oObjeto:OriginalInvoiceSerialId  := (cAliasSC6)->C6_SERIORI
	oObjeto:OriginalInvoiceItem	  := (cAliasSC6)->C6_ITEMORI
	oObjeto:LotNumber		      := (cAliasSC6)->C6_LOTECTL
	oObjeto:SubLot				  := (cAliasSC6)->C6_NUMLOTE
	oObjeto:Location			  := (cAliasSC6)->C6_LOCALIZ
	oObjeto:ProductSerialNumber	  := (cAliasSC6)->C6_NUMSERI
	oObjeto:ManufacturerCode	  := (cAliasSC6)->C6_CODFAB
	oObjeto:ManufacturerUnit	  := (cAliasSC6)->C6_LOJAFA	
	oObjeto:POID                  := (cAliasSC6)->C6_NUMOP
	oObjeto:POItem                := (cAliasSC6)->C6_ITEMOP
	oObjeto:POSequence            := "001"
	oObjeto:POGradeItem           := (cAliasSC6)->C6_ITEMGRD
	
	UserFields("SC6",@oObjeto:UserFields,cAliasSC6)
	
	nX := 0
	For nVend := 1 To nMaxVend
		If nX == 0
			oObjeto:CommissionsPercentage := {}
		Endif
		If (cAliasSC6)->(FieldGet((cAliasSC6)->(FieldPos("C6_COMIS"+AllTrim(Str(nVend)))))) > 0
			AAdd(oObjeto:CommissionsPercentage,WsClassNew('GenericStruct'))
			nX++
			oObjeto:CommissionsPercentage[nX]:Code			:= (cAliasSC5)->(FieldGet((cAliasSC5)->(FieldPos("C5_VEND"+AllTrim(Str(nVend))))))
			oObjeto:CommissionsPercentage[nX]:Description	:= PadR(STR0028,40)	// "Percentual de comissão"
			oObjeto:CommissionsPercentage[nX]:Value			:= (cAliasSC6)->(FieldGet((cAliasSC6)->(FieldPos("C6_COMIS"+AllTrim(Str(nVend))))))
		EndIf
	Next nVend

Else

	oObjeto:OrderItem             := WsProcH(aItem,"C6_ITEM")
	oObjeto:OrderID				  := WsProcH(aItem,"C6_NUM")
	oObjeto:ProductID	          := WsProcH(aItem,"C6_PRODUTO")
	oObjeto:UnitOfMeasure         := WsProcH(aItem,"C6_UM")
	oObjeto:Quantity			  := WsProcH(aItem,"C6_QTDVEN")
	oObjeto:QuantityDelivered     := WsProcH(aItem,"C6_QTDENT")
	oObjeto:NetUnitPrice          := WsProcH(aItem,"C6_PRCVEN")
	oObjeto:NetTotal			  := WsProcH(aItem,"C6_VALOR")
	oObjeto:QuantityApproved	  := WsProcH(aItem,"C6_QTDEMP")
	oObjeto:QuantityReleasedSecondUnit:= WsProcH(aItem,"C6_QTDEMP2")
	oObjeto:SecondUnitOfMeasure	  := WsProcH(aItem,"C6_SEGUM")
	oObjeto:OperationType		  := WsProcH(aItem,"C6_OPER")	 
	oObjeto:ItemOutflowType		  := WsProcH(aItem,"C6_TES")
	oObjeto:SecondMeasureUnitQuantity := WsProcH(aItem,"C6_UNSVEN")
	oObjeto:Warehouse			  := WsProcH(aItem,"C6_LOCAL")
	oObjeto:FiscalOperation	      := WsProcH(aItem,"C6_CF")
	oObjeto:DiscountPercentage	  := WsProcH(aItem,"C6_DESCONT")
	oObjeto:ItemDiscountValue	  := WsProcH(aItem,"C6_VALDESC")
	oObjeto:DeliveryDate		  := WsProcH(aItem,"C6_ENTREG")
	oObjeto:CustomerOrderNumber	  := WsProcH(aItem,"C6_PEDCLI")
	oObjeto:ProductDescription	  := WsProcH(aItem,"C6_DESCRI")
	oObjeto:UnitListPrice		  := WsProcH(aItem,"C6_PRUNIT")
	oObjeto:OriginalInvoice		  := WsProcH(aItem,"C6_NFORI")
	oObjeto:OriginalInvoiceSerialId  := WsProcH(aItem,"C6_SERIORI")
	oObjeto:OriginalInvoiceItem	  := WsProcH(aItem,"C6_ITEMORI")
	oObjeto:LotNumber		      := WsProcH(aItem,"C6_LOTECTL")
	oObjeto:SubLot				  := WsProcH(aItem,"C6_NUMLOTE")
	oObjeto:Location			  := WsProcH(aItem,"C6_LOCALIZ")
	oObjeto:ProductSerialNumber	  := WsProcH(aItem,"C6_NUMSERI")
	oObjeto:ManufacturerCode	  := WsProcH(aItem,"C6_CODFAB")
	oObjeto:ManufacturerUnit	  := WsProcH(aItem,"C6_LOJAFA")	

	UserFields("SC6",@oObjeto:UserFields,cAliasSC6,@aItem)
	
	nX := 0
	For nVend := 1 To nMaxVend
		If nX == 0
			oObjeto:CommissionsPercentage := {}
		Endif
		If WsProcH(aItem,"C6_COMIS"+AllTrim(Str(nVend))) > 0
			AAdd(oObjeto:CommissionsPercentage,WsClassNew('GenericStruct'))
			nX++
			oObjeto:CommissionsPercentage[nX]:Code			:= WsProcH(aCab,"C5_VEND"+AllTrim(Str(nVend)))
			oObjeto:CommissionsPercentage[nX]:Description	:= PadR(STR0028,40) // "Percentual de comissão"
			oObjeto:CommissionsPercentage[nX]:Value			:= WsProcH(aItem,"C6_COMIS"+AllTrim(Str(nVend)))
		EndIf
	Next nVend
Endif
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutPVHead ³Autor  ³ Eduardo Riera         ³ Data ³25.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do Header do pedido de venda para a  ³±±
±±³          ³rotina automatica                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do Header do Pedido de Venda                   ³±±
±±³          ³ExpA2: Array com os dados do Cabecalho                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1:Indica que nao houve erro nos dados enviados           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SC5 posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³17/10/06³107100³-Inicializacao do campo C5_TABELA confor-³±±
±±³              ³        ³      ³me a tabela do cadastro de clientes.     ³±±
±±³ Cleber M.    ³26/04/07³124950³-Alteracao para buscar sempre a Tabela de³±±
±±³              ³        ³      ³Preco do cadastro de clientes.           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutPvHead(oObjeto,aCab)

Local aArea    := GetArea()		//Guarda a area atual 
Local nX       := 0				//Variavel usada em lacos For...Next
Local lRetorno := .T.			//Indica o retorno da funcao

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiacao do cabecalho do pedido de venda                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(oObjeto:OrderID)
	AAdd(aCab,{"C5_NUM"		,oObjeto:OrderID,})
EndIf
AAdd(aCab,{"C5_EMISSAO"	,oObjeto:RegisterDate,})
AAdd(aCab,{"C5_TIPO"    ,oObjeto:OrderTypeCode,})
AAdd(aCab,{"C5_CLIENTE"	,PADR(oObjeto:CustomerCode,TAMSX3('C5_CLIENTE')[1]),})
AAdd(aCab,{"C5_LOJACLI"	,PADR(oObjeto:CustomerUnit,TAMSX3('C5_LOJACLI')[1]),})
AAdd(aCab,{"C5_CLIENT"	,PADR(oObjeto:DeliveryCustomer,TAMSX3('C5_CLIENT')[1]),})
AAdd(aCab,{"C5_LOJAENT"	,PADR(oObjeto:DeliveryUnitCode,TAMSX3('C5_LOJAENT')[1]),})
AAdd(aCab,{"C5_TRANSP"	,PADR(oObjeto:CarrierCode,TAMSX3('C5_TRANSP')[1]),})
AAdd(aCab,{"C5_TIPOCLI"	,oObjeto:CustomerType,})
AAdd(aCab,{"C5_CONDPAG"	,PADR(oObjeto:PaymentPlanCode,TAMSX3('C5_CONDPAG')[1]),}) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Preenche a tab. de preco de acordo com o cad. de cliente ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA1")
DbSetOrder(1)
If DbSeek( xFilial("SA1") + PADR(oObjeto:CustomerCode,TAMSX3('C5_CLIENTE')[1]) + oObjeto:CustomerUnit )
	If !Empty(SA1->A1_TABELA)
		oObjeto:PriceListCode := SA1->A1_TABELA
	EndIf
	If !Empty(SA1->A1_DESC)
		oObjeto:Discount1 := SA1->A1_DESC
	EndIf	
Else
	lRetorno := .F.
   	SetSoapFault("PutSalesOrder",STR0029) //"Cliente e/ou Loja não cadastrados."
Endif	
AAdd(aCab,{"C5_TABELA"	,oObjeto:PriceListCode,})

If (oObjeto:Sellers <> Nil)
	For nX := 1 To Len(oObjeto:Sellers)
		If ( SC5->(FieldPos("C5_VEND"+AllTrim(Str(nX)))) <> 0 )
			AAdd(aCab,{"C5_VEND"+AllTrim(Str(nX)) 	,oObjeto:Sellers[nX]:Code,})
			AAdd(aCab,{"C5_COMIS"+AllTrim(Str(nX))	,oObjeto:Sellers[nX]:Value,})
		Else
			lRetorno := .F.
			SetSoapFault("PutSalesOrder",STR0022+oObjeto:Sellers[nX]:Description) //"O sistema não suporta "
		Endif
	Next nX
Endif
nX:=0	
AAdd(aCab,{"C5_DESC1"	,oObjeto:Discount1,})
AAdd(aCab,{"C5_DESC2"	,oObjeto:Discount2,})
AAdd(aCab,{"C5_DESC3"	,oObjeto:Discount3,})
AAdd(aCab,{"C5_DESC4"	,oObjeto:Discount4,})
AAdd(aCab,{"C5_DESCONT"	,oObjeto:IndemnityValue,})
AAdd(aCab,{"C5_PDESCAB"	,oObjeto:IndemnityPercentage,})		
AAdd(aCab,{"C5_BANCO"	,oObjeto:BankCode,})
AAdd(aCab,{"C5_DESCFI"	,oObjeto:FinancialDiscount,})
AAdd(aCab,{"C5_ACRSFIN"	,oObjeto:FinancialIncrease,})		
AAdd(aCab,{"C5_COTACAO"	,oObjeto:BidNumber,})
AAdd(aCab,{"C5_TPFRETE"	,oObjeto:FreightType,})	
AAdd(aCab,{"C5_FRETE"	,oObjeto:FreightValue,})	
AAdd(aCab,{"C5_SEGURO"	,oObjeto:InsuranceValue,})
AAdd(aCab,{"C5_DESPESA"	,oObjeto:AdditionalExpenseValue,})
AAdd(aCab,{"C5_FRETAUT"	,oObjeto:IndependentFreight,})
AAdd(aCab,{"C5_REAJUST"	,oObjeto:AdjustmentType,})
AAdd(aCab,{"C5_MOEDA"	,oObjeto:SalesOrderCurrency,})
AAdd(aCab,{"C5_PESOL"	,oObjeto:NetWeight,})
AAdd(aCab,{"C5_PBRUTO"	,oObjeto:GrossWeight,})
AAdd(aCab,{"C5_REDESP"	,oObjeto:RedeliveryCarrierCode,})
If (oObjeto:PackagesVolumes <> Nil)
	For nX := 1 To Len(oObjeto:PackagesVolumes)
		If ( SC5->(FieldPos("C5_ESPECI"+AllTrim(Str(nX)))) <> 0 )
			AAdd(aCab,{"C5_ESPECI"+AllTrim(Str(nX)) 	,oObjeto:PackagesVolumes[nX]:Code,})
			AAdd(aCab,{"C5_VOLUME"+AllTrim(Str(nX))	,oObjeto:PackagesVolumes[nX]:Value,})
		Else
			lRetorno := .F.
			SetSoapFault("PutSalesOrder",STR0022+oObjeto:PackagesVolumes[nX]:Description) //"O sistema não suporta "
		Endif
	Next nX
Endif
nX:=0	
AAdd(aCab,{"C5_MENNOTA"	,oObjeto:InvoiceMessage,})
AAdd(aCab,{"C5_MENPAD"	,oObjeto:StandardMessage1,})
AAdd(aCab,{"C5_INCISS"	,oObjeto:PriceWithServiceTax,})
AAdd(aCab,{"C5_TIPLIB"	,oObjeto:AprobationType,})
PutUserFields("SC5",@oObjeto:UserFields,@aCab)
aCab := WsAutoOpc(aCab)

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutPVItem ³Autor  ³ Eduardo Riera         ³ Data ³25.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento dos itens do pedido de venda para a  ³±±
±±³          ³rotina automatica                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do Header do Pedido de Venda                   ³±±
±±³          ³ExpO2: Objeto dos itens do Pedido de Venda                   ³±±
±±³          ³ExpA3: Array com os dados dos itens do pedido de venda       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1:Indica que nao houve erro nos dados enviados           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SC5 posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Analista     ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³19/06/07³127615³-Alteracao p/ recalcular o preco do item ³±±
±±³              ³        ³  9.12³quando houver desconto no cabec. do PV.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutPvItem( oObjSC5, oObjSC6, aItens, PortCli )

Local cAliasSC6  	:= "SC6"			//Alias do arquivo SC6
Local nX         	:= 0				//Usada em lacos For...Next
Local nY         	:= 0				//Usada em lacos For...Next
Local aArea      	:= GetArea()		//Guarda a area atual
Local aItem      	:= {}				//Array com os dados do Item corrente
Local aItensTmp		:= {}				//Array temporario dos Itens
Local aNoDeleted 	:= {}				//Array dos registros nao-deletados
Local lRetorno   	:= .T.				//Indica retorno da funcao
Local lQuery     	:= .F.				//Indica se executou query
Local lInclui    	:= .T.				//Indica se eh inclusao ou alteracao do PV
Local cQuery     := ""					//Expressao/query a executar no Banco
Local aStruSC6   := {}					//Estrutura do arquivo SC6

DbSelectArea( "SC5" )
DbSetOrder( 1 )

If !Empty( oObjSC5:OrderID ) .AND. MsSeek( xFilial( "SC5" ) + oObjSC5:OrderID )
	lInclui := .F.
Endif

DbSelectArea( "SC6" )
DbSetOrder( 1 )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiacao dos itens do pedido de venda³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea( "SC6" )

For nY := 1 To Len( oObjSC6 ) 
	oObjSC6[nY]:ProductID := PADR( oObjSC6[nY]:ProductID, TAMSX3('C6_PRODUTO')[1] )
	AAdd( aNoDeleted , oObjSC6[nY]:OrderItem+oObjSC6[nY]:ProductID)

	If !lInclui .AND. SC6->( MsSeek( xFilial( "SC6" ) + oObjSC5:OrderID + oObjSC6[nY]:OrderItem ) )
		AAdd( aItem, { "LINPOS", "C6_ITEM+C6_PRODUTO", oObjSC6[nY]:OrderItem, oObjSC6[nY]:ProductID } )
	Endif
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Busca o Preco de Lista do Item    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If oObjSC6[nY]:UnitListPrice == 0 .OR. PortCli
	oObjSC6[nY]:UnitListPrice := MaTabPrVen( 	IIF( oObjSC5:PriceListCode <> NIL, oObjSC5:PriceListCode, "" )			,;
												oObjSC6[nY]:ProductID													,;
												oObjSC6[nY]:Quantity													,;
												oObjSC5:CustomerCode													,;
												oObjSC5:CustomerUnit													,;
												IIF( oObjSC5:SalesOrderCurrency <> NIL, oObjSC5:SalesOrderCurrency, 1 ),;
												IIF( oObjSC5:RegisterDate <> NIL, oObjSC5:RegisterDate, dDataBase ) )
	EndIf 

	 


	AAdd( aItem, { "C6_ITEM"	, oObjSC6[nY]:OrderItem						, } )
	AAdd( aItem, { "C6_PRODUTO"	, oObjSC6[nY]:ProductID						, } )
	AAdd( aItem, { "C6_UM"		, oObjSC6[nY]:UnitOfMeasure					, } )
	AAdd( aItem, { "C6_SEGUM"	, oObjSC6[nY]:SecondUnitOfMeasure			, } )
	AAdd( aItem, { "C6_QTDVEN"	, oObjSC6[nY]:Quantity						, } )
	AAdd( aItem, { "C6_UNSVEN"	, oObjSC6[nY]:SecondMeasureUnitQuantity		, } )
	AAdd( aItem, { "C6_PRUNIT"	, oObjSC6[nY]:UnitListPrice				    , } )	

    Iif( oObjSC5:Discount1 == NIl , oObjSC5:Discount1:=0 , )
    Iif( oObjSC5:Discount2 == NIl , oObjSC5:Discount2:=0 , )
    Iif( oObjSC5:Discount3 == NIl , oObjSC5:Discount3:=0 , )
    Iif( oObjSC5:Discount4 == NIl , oObjSC5:Discount4:=0 , ) 

      

    If!( PortCli )
		AAdd( aItem, { "C6_PRCVEN"	, oObjSC6[nY]:NetUnitPrice					, } )
	EndIf
	AAdd( aItem, { "C6_VALOR"	, oObjSC6[nY]:NetTotal						, } )
	AAdd( aItem, { "C6_QTDLIB"	, oObjSC6[nY]:QuantityApproved				, } )
	AAdd( aItem, { "C6_QTDLIB2"	, oObjSC6[nY]:QuantityReleasedSecondUnit	, } )
	AAdd( aItem, { "C6_OPER"	, oObjSC6[nY]:OperationType					, } )

	If !Empty( oObjSC6[nY]:ItemOutflowType )
		AAdd( aItem, { "C6_TES", oObjSC6[nY]:ItemOutflowType, } )
	Endif

	AAdd( aItem, { "C6_CF"		, oObjSC6[nY]:FiscalOperation		, } )
	AAdd( aItem, { "C6_LOCAL"	, oObjSC6[nY]:Warehouse				, } )
	AAdd( aItem, { "C6_DESCONT"	, oObjSC6[nY]:DiscountPercentage	, } )
	AAdd( aItem, { "C6_VALDESC"	, oObjSC6[nY]:ItemDiscountValue		, } )
	AAdd( aItem, { "C6_ENTREG"	, oObjSC6[nY]:DeliveryDate			, } )

	If ( oObjSC6[nY]:CommissionsPercentage <> NIL )
		For nX := 1 To Len( oObjSC6[nY]:CommissionsPercentage )
			If ( SC6->( FieldPos( "C6_COMIS" + AllTrim( Str( nX ) ) ) ) <> 0 )
				AAdd( aItem, { "C6_COMIS" + AllTrim( Str( nX ) ), oObjSC6[nY]:CommissionsPercentage[nX]:Value, } )
			Else
				lRetorno := .F.
				SetSoapFault( "PutSalesOrder", STR0022 + oObjSC6[nY]:CommissionsPercentage[nX]:Description ) //"O sistema não suporta "
			Endif
		Next nX
	Endif

	nX:=0	                                          

	AAdd( aItem, { "C6_PEDCLI"	, oObjSC6[nY]:CustomerOrderNumber		, } )
   	
	AAdd( aItem, { "C6_NFORI"	, oObjSC6[nY]:OriginalInvoice			, } )
	AAdd( aItem, { "C6_SERIORI"	, oObjSC6[nY]:OriginalInvoiceSerialId	, } )
	AAdd( aItem, { "C6_ITEMORI"	, oObjSC6[nY]:OriginalInvoiceItem		, } )
	AAdd( aItem, { "C6_LOTECTL"	, oObjSC6[nY]:LotNumber					, } )
	AAdd( aItem, { "C6_NUMLOTE"	, oObjSC6[nY]:SubLot					, } )
	AAdd( aItem, { "C6_LOCALIZ"	, oObjSC6[nY]:Location					, } )
	AAdd( aItem, { "C6_NUMSERI"	, oObjSC6[nY]:ProductSerialNumber		, } )
	AAdd( aItem, { "C6_CODFAB"	, oObjSC6[nY]:ManufacturerCode			, } )
	AAdd( aItem, { "C6_LOJAFA"	, oObjSC6[nY]:ManufacturerUnit			, } )

	PutUserFields( "SC6", @oObjSC6[nY]:UserFields, @aItem )

	aItem := WsAutoOpc( aItem, .T. )
	
	AAdd( aItens, aItem )

	aItem := {}
Next nY

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se eh alteracao ou inclusao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lInclui
	DbSelectArea("SC6")
	DbSetOrder(1)

	lQuery		:= .T.
	aStruSC6	:= SC6->(DbStruct() )
	cAliasSC6	:= GetNextAlias()

	cQuery	:= "SELECT * "
	cQuery	+= "FROM " + RetSqlName( "SC6" ) + " SC6 "
	cQuery	+= "WHERE "
	cQuery	+= "SC6.C6_FILIAL = '" + xFilial( "SC6" ) + "' AND "
	cQuery	+= "SC6.C6_NUM='" + oObjSC5:OrderID + "' AND "
	cQuery	+= "SC6.D_E_L_E_T_=' ' "
	cQuery	+= "ORDER BY " + SqlOrder( SC6->( IndexKey() ) )

	cQuery	:= ChangeQuery( cQuery )

	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasSC6 )

	For nX := 1 To Len( aStruSC6 )
		If aStruSC6[nX][2] <> "C" .AND. aStruSC6[nX][2] <> "M"
			TcSetField( cAliasSC6, aStruSC6[nX][1], aStruSC6[nX][2], aStruSC6[nX][3], aStruSC6[nX][4] )
		Endif
	Next nX

	DbSelectArea( cAliasSC6 )

	While !EOF() .AND. xFilial( "SC6" ) == ( cAliasSC6 )->C6_FILIAL .AND. oObjSC5:OrderID == ( cAliasSC6 )->C6_NUM
		If aScan( aNoDeleted, ( cAliasSC6 )->C6_ITEM + ( cAliasSC6 )->C6_PRODUTO ) == 0
			AAdd( aItens, { 	{ "LINPOS"		, "C6_ITEM+C6_PRODUTO", ( cAliasSC6 )->C6_ITEM, ( cAliasSC6 )->C6_PRODUTO},;
								{ "AUTDELETA"	, "S" } } )
		Endif	

		DbSelectArea( cAliasSC6 )
		DbSkip()
	EndDo
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Passa primeiro no array de itens os itens que possuem na primeira posicao LINPOS³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len( aItens )
		If aItens[nX][1][1] == "LINPOS"
			AAdd( aItensTmp, aItens[nX] )
		Endif
	Next nX
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Passa por ultimo no array de itens os itens que nao possuem na primeira posicao LINPOS³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len( aItens )
		If aItens[nX][1][1] <> "LINPOS"
			AAdd( aItensTmp, aItens[nX] )
		Endif
	Next nX
	
	aItens := AClone( aItensTmp )

	If lQuery
		DbSelectArea( cAliasSC6 )
		DbCloseArea()
		DbSelectArea( "SC6" )
	Endif			
Endif

RestArea( aArea )

Return ( lRetorno )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT410  ³ Autor ³Eduardo Riera          ³ Data ³03.09.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades do Pedido de Venda.          ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Norbert Waage ³10/08/07³126152³Inclusao do metodo BrwIdOrder             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSellerSalesOrder  DESCRIPTION STR0023 NAMESPACE "http://webservices.microsiga.com.br/mtsellersalesorder.apw" //"Serviço de consulta e atualização dos pedidos de venda ( <b>Restrição de representante comercial</b> )"
WSDATA Header                 AS Array OF BrwHeader
WSDATA HeaderType             AS String
WSDATA UserCode               AS String
WSDATA SellerCode			  AS String
WSDATA OrderID				  AS String
WSDATA SalesOrderStatus       AS String	
WSDATA RegisterDateFrom       AS Date OPTIONAL
WSDATA RegisterDateTo   	  AS Date OPTIONAL
WSDATA ListOfSalesOrder		  AS Array OF SalesOrderHeaderView OPTIONAL
WSDATA SalesOrder	  		  AS SalesOrderView
WSDATA ListOfRelease		  AS Array OF SalesOrderRelease
WSDATA WsNull                 AS String
WSDATA QueryAddWhere          AS String OPTIONAL
WSDATA IndexKey               AS String OPTIONAL
WSDATA PageLen                AS Integer OPTIONAL
WSDATA PageFirst              AS Integer OPTIONAL
WSDATA BidNumberFrom          As String OPTIONAL
WSDATA BidNumberTo            As String OPTIONAL
WSDATA TotalSO				AS Integer OPTIONAL

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
WSMETHOD GetSalesOrder    DESCRIPTION STR0003 //"Método de consulta das informacoes do pedido de venda"
WSMETHOD BrwTotalSalesOrder
WSMETHOD BrwSalesOrder    DESCRIPTION STR0004 //"Método de listagem dos pedidos de venda"
WSMETHOD PutSalesOrder    DESCRIPTION STR0005 //"Método de atualização das informacoes do pedido de venda"
WSMETHOD DelSalesOrder    DESCRIPTION STR0006 //"Método de exclusão do pedido de venda"
WSMETHOD BrwRelease       DESCRIPTION STR0007 //"Método de listagem das liberações do pedido de venda ( <i>Período</i> )"
WSMETHOD GetRelease       DESCRIPTION STR0008 //"Método de consulta as liberações do pedido de venda ( <i>Cliente</i> )"
WSMETHOD GetStatusRelease DESCRIPTION STR0009 //"Método de consulta a liberaçao do pedido de venda"
WSMETHOD BrwIdOrder       DESCRIPTION STR0004 //"Método de listagem dos pedidos de venda"	
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Kleber Dias Gomes     ³ Data ³21.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos headers deste WS                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSellerSalesOrder

Local nX := 0

::Header := MtHeader(::HeaderType)
::Header := MtHeader(::HeaderType)
If Empty(::Header)
	Do Case 
		Case ::HeaderType == "COMISSION"
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_VEND1")
			::Header[nX]:HeaderField   := "CODE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := STR0010 //"Descrição"
			::Header[nX]:HeaderField   := "DESCRIPTION"
			::Header[nX]:HeaderPicture := "@!"
			::Header[nX]:HeaderSize    := 20
			::Header[nX]:HeaderDec     := 0
			::Header[nX]:HeaderType    := "C"
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := .F.
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_COMIS1")
			::Header[nX]:HeaderField   := "VALUE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
		Case ::HeaderType == "PACKAGESVIEW"
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_ESPECI1")
			::Header[nX]:HeaderField   := "CODE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := STR0010 //"Descrição"
			::Header[nX]:HeaderField   := "DESCRIPTION"
			::Header[nX]:HeaderPicture := "@!"
			::Header[nX]:HeaderSize    := 20
			::Header[nX]:HeaderDec     := 0
			::Header[nX]:HeaderType    := "C"
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := .F.
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle   := RetTitle("C5_VOLUME1")
			::Header[nX]:HeaderField   := "VALUE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
		Case ::HeaderType == "SALESORDERRELEASE"
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_PEDIDO")
			::Header[nX]:HeaderField := "ORDERID"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_ITEM")
			::Header[nX]:HeaderField := "ORDERITEM"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_CLIENTE")
			::Header[nX]:HeaderField := "CUSTOMERCODE"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_LOJA")
			::Header[nX]:HeaderField := "CUSTOMERUNIT"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_PRODUTO")
			::Header[nX]:HeaderField := "PRODUCTID"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := RetTitle("C9_QTDLIB")
			::Header[nX]:HeaderField := "QUANTITYAPPROVED"
			::Header[nX]:HeaderPicture := SX3->X3_PICTURE
			::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
			::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
			::Header[nX]:HeaderType    := SX3->X3_TIPO
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
		
			AAdd(::Header,WSClassNew("BrwHeader"))
			nX++
			::Header[nX]:HeaderTitle := "Status"
			::Header[nX]:HeaderField := "STATUS"
			::Header[nX]:HeaderPicture := "@!"
			::Header[nX]:HeaderSize    := 20
			::Header[nX]:HeaderDec     := 0
			::Header[nX]:HeaderType    := "C"
			::Header[nX]:HeaderComboBox:= X3CBox()
			::Header[nX]:HeaderOblig   := .F.
	EndCase
Endif

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwTotalSales³Autor  ³Luiz Couto          ³Data  ³01.09.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao do total de pedidos de venda           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC4: Data Emissao ate                                      ³±±
±±³          ³ExpC5: Seu Pedido de                                         ³±±
±±³          ³ExpC6: Seu pedido ate                                        ³±±
±±³          ³ExpC7: Add Where                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao do total de pedidos de venda           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwTotalSalesOrder WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,BidNumberFrom,BidNumberTo,QueryAddWhere WSSEND TotalSO WSSERVICE MtSellerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local cAliasSC5 := ""
Local cQuery    := ""

DEFAULT ::RegisterDateFrom := FirstDay(dDataBase)
DEFAULT ::RegisterDateTo   := LastDay(dDataBase)

If PrtChkUser(::UserCode,"MtSellerSalesOrder","BrwSalesOrder","SA3",::SellerCode)
	::TotalSO := 0
	cAliasSC5 := GetNextAlias()

	cQuery	:= "SELECT COUNT(*) TOTALSO "
	cQuery	+= "FROM "+RetSqlName("SC5")+" SC5 "
	cQuery	+= "WHERE "
	cQuery	+= "SC5.C5_FILIAL = '"+xFilial("SC5")+"' AND "
	cQuery	+= "SC5.C5_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
	cQuery	+= "SC5.C5_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
	If !Empty(::BidNumberFrom)
		cQuery	+= "SC5.C5_COTACAO>='"+::BidNumberFrom+"' AND "
	Endif
	If !Empty(::BidNumberTo)
		cQuery	+= "SC5.C5_COTACAO<='"+::BidNumberTo+"' AND "
   		Endif		
	cQuery  += "SC5.C5_VEND1='"+::SellerCode+"' AND "
	cQuery	+= "SC5.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

	::TotalSO := (cAliasSC5)->TOTALSO
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSalesOrder³Autor  ³Eduardo Riera       ³Data  ³03.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos pedidos de venda de um vendedor    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Codigo do Vendedor                                    ³±±
±±³          ³ExpC3: Pedido de Venda                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos pedidos de venda de um vendedor    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetSalesOrder WSRECEIVE UserCode,SellerCode,OrderID,QueryAddWhere WSSEND SalesOrder WSSERVICE MtSellerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSC5 := "SC5"
Local cAliasSC6 := "SC6"
Local aStruSC6  := {}
Local cQuery    := ""
Local nX        := 0

If PrtChkUser(::UserCode,"MtSellerSalesOrder","GetSalesOrder","SA3",::SellerCode)
	DbSelectArea("SC5")
	DbSetOrder(1)
	If MsSeek(xFilial("SC5")+::OrderID)

		::SalesOrder:SOHeader	:= WsClassNew("SalesOrderHeaderView")
		GetPVHeader(@::SalesOrder:SOHeader,cAliasSC5)

		DbSelectArea("SC6")
		DbSetOrder(1)
		lQuery	:= .T.
		aStruSC6	:= SC6->(dbStruct())
		cAliasSC6:= GetNextAlias()

		cQuery	:= "SELECT * "
		cQuery	+= "FROM "+RetSqlName("SC6")+" SC6 "
		cQuery	+= "WHERE "
		cQuery	+= "SC6.C6_FILIAL = '"+xFilial("SC6")+"' AND "
		cQuery	+= "SC6.C6_NUM='"+::OrderID+"' AND "
		cQuery	+= "SC6.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+SqlOrder(SC6->(IndexKey()))

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6,.T.,.T.)

		For nX := 1 To Len(aStruSC6)
			If aStruSC6[nX][2]<>"C" .AND. aStruSC6[nX][2]<>"M"
				TcSetField(cAliasSC6,aStruSC6[nX][1],aStruSC6[nX][2],aStruSC6[nX][3],aStruSC6[nX][4])
			Endif
		Next nX

		DbSelectArea(cAliasSC6)
		While !Eof() .AND. xFilial("SC6") == (cAliasSC6)->C6_FILIAL .AND.;
				::OrderID == (cAliasSC6)->C6_NUM

			AAdd(::SalesOrder:SOItem,WSClassNew("SalesOrderItemView"))
			nY++
			GetPVItem(@::SalesOrder:SOItem[nY],cAliasSC6,cAliasSC5)

			DbSelectArea(cAliasSC6)
			dbSkip()
		EndDo
		If lQuery
			DbSelectArea(cAliasSC6)
			dbCloseArea()
			DbSelectArea("SC6")
		Endif
	Else
		lRetorno := .F.
		SetSoapFault("GETSALESORDER",STR0012) //"Pedido nao encontrado"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwSalesOrder³Autor  ³Eduardo Riera       ³Data  ³03.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos pedidos de venda de um vendedor em ³±±
±±³          ³Browse                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC3: Data Emissao ate                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos pedidos de venda de um vendedor em ³±±
±±³          ³Browse                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwSalesOrder WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,BidNumberFrom,BidNumberTo,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfSalesOrder WSSERVICE MtSellerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nX        := 0
Local nY        := 0
Local cAliasSC5 := "SC5"
Local cQuery    := ""
Local cArquivo  := ""
Local aStruSC5  := {}

SC5->(DbSetOrder(2))

DEFAULT ::RegisterDateFrom := dDataBase
DEFAULT ::RegisterDateTo   := dDataBase
DEFAULT ::IndexKey         := SC5->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

If PrtChkUser(::UserCode,"MtSellerSalesOrder","BrwSalesOrder","SA3",::SellerCode)
	DbSelectArea("SC5")
	DbSetOrder(2)
	lQuery	:= .T.
	aStruSC5	:= SC5->(dbStruct())
	cAliasSC5:= GetNextAlias()

	cQuery	:= "SELECT * "
	cQuery	+= "FROM "+RetSqlName("SC5")+" SC5 "
	cQuery	+= "WHERE "
	cQuery	+= "SC5.C5_FILIAL = '"+xFilial("SC5")+"' AND "
	cQuery	+= "SC5.C5_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
	cQuery	+= "SC5.C5_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
	If !Empty(::BidNumberFrom)
		cQuery	+= "SC5.C5_COTACAO>='"+::BidNumberFrom+"' AND "
	Endif
	If !Empty(::BidNumberTo)
		cQuery	+= "SC5.C5_COTACAO<='"+::BidNumberTo+"' AND "
	Endif		
	cQuery  += "SC5.C5_VEND1='"+::SellerCode+"' AND "
	cQuery	+= "SC5.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery	+= "ORDER BY "+WsSqlOrder(::IndexKey)

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

	For nX := 1 To Len(aStruSC5)
		If aStruSC5[nX][2]<>"C" .AND. aStruSC5[nX][2]<>"M"
			TcSetField(cAliasSC5,aStruSC5[nX][1],aStruSC5[nX][2],aStruSC5[nX][3],aStruSC5[nX][4])
		Endif
	Next nX

	nX := 0
	DbSelectArea(cAliasSC5)		
	While !Eof() .AND. xFilial("SC5") == (cAliasSC5)->C5_FILIAL
		If (cAliasSC5)->C5_VEND1 == ::SellerCode
			nX++
			If ::PageFirst==0 .OR. nX >= ::PageFirst
				If nY == 0
					::ListOfSalesOrder := {}
				Endif
				AAdd(::ListOfSalesOrder,WsClassNew("SalesOrderHeaderView"))
				nY++
				GetPVHeader(@::ListOfSalesOrder[nY],cAliasSC5)
			Endif
		Endif
		If nY >= ::PageLen .AND. ::PageLen <> 0
			Exit
		Endif		
		DbSelectArea(cAliasSC5)
		dbSkip()
	EndDo
	If lQuery
		DbSelectArea(cAliasSC5)
		dbCloseArea()
		DbSelectArea("SC5")
	Else
		RetIndex("SC5")
		FErase(cArquivo+OrdBagExt())
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSalesOrder³Autor  ³Eduardo Riera       ³Data  ³03.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de inclusao dos pedidos de venda de um vendedor       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Dados do Pedido de Venda                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de inclusao dos pedidos de venda de um vendedor       ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutSalesOrder WSRECEIVE UserCode,SellerCode,SalesOrder WSSEND OrderID  WSSERVICE MtSellerSalesOrder

Local aArea		:= GetArea()
Local aCab		:= {}
Local aItens	:= {}
Local aErro     := {}
Local cErro     := ""
Local lRetorno	:= .T.
Local nX        := 0

PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MTSELLERSALESORDER","PUTSALESORDER","SA3",::SellerCode)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiacao do cabecalho do pedido de venda                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SC5")
	DbSetOrder(1)
	If ::SalesOrder:SOHeader:OrderID <> Nil
		MsSeek(xFilial("SC5")+::SalesOrder:SOHeader:OrderID)
	Else
		MsGoto(0)		
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiacao do cabecalho do pedido de venda                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRetorno := PutPvHead(::SalesOrder:SOHeader,@aCab)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiacao dos itens do pedido de venda                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno 
		lRetorno := PutPvItem(::SalesOrder:SOHeader,::SalesOrder:SOItem,@aItens)
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se eh alteracao ou inclusaoda                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno
		DbSelectArea("SC5")
		DbSetOrder(1)
		If !Empty(::SalesOrder:SOHeader:OrderID) .AND. MsSeek(xFilial("SC5")+::SalesOrder:SOHeader:OrderID)
			MATA410(aCab,aItens,4)
			::OrderId := SC5->C5_NUM		
		Else
			MATA410(aCab,aItens,3)
			::OrderId := SC5->C5_NUM
		Endif
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTSALESORDER",cErro)
			lRetorno := .F.
		Endif
	Endif
Else
	lRetorno := .F.
Endif
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelSalesOrder³Autor  ³Kleber Dias Gomes   ³Data  ³30.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de exclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Pedido de Venda                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de exclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelSalesOrder WSRECEIVE UserCode,SellerCode,OrderID WSSEND WsNull  WSSERVICE MtSellerSalesOrder

Local aCab		:= {}
Local aItens	:= {}
Local aErro     := {}
Local lRetorno  := .T.
Local cErro     := ""
Local nX        := 0
PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MTSELLERSALESORDER","DelSalesOrder","SA3",::SellerCode)
	AAdd(aCab,{"C5_NUM"		,::OrderID,})

	MATA410(aCab,aItens,5)
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("DELSALESORDER",cErro)
		lRetorno := .F.
	Endif
Else
	lRetorno := .F.
Endif

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwRelease   ³Autor  ³Eduardo Riera       ³Data  ³03.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos pedidos de venda de um periodo em  ³±±
±±³          ³Browse                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC3: Data Emissao ate	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos pedidos de venda de um periodo em  ³±±
±±³          ³Browse                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwRelease WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,QueryAddWhere WSSEND ListOfRelease WSSERVICE MtSellerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nX        := 0
Local cAliasSC5 := "SC5"
Local cStatus   := ""
Local cQuery    := ""

DEFAULT RegisterDateFrom := dDataBase
DEFAULT RegisterDateTo   := dDataBase

If PrtChkUser(::UserCode,"MtCustomerSalesOrder","BrwRelease","SA3",::SellerCode)
	DbSelectArea("SC9")
	DbSetOrder(1)
	DbSelectArea("SC5")
	DbSetOrder(2)
	lQuery	:= .T.
	cAliasSC5:= GetNextAlias()

	cQuery := "SELECT C5_FILIAL, C5_EMISSAO, C9_PEDIDO, C9_ITEM, C9_CLIENTE, C9_LOJA, "
	cQuery += "C9_PRODUTO, C9_QTDLIB, C9_BLEST, C9_BLCRED, C9_BLWMS "
	If SC9->(FieldPos("C9_BLTMS"))>0
		cQuery += ",C9_BLTMS "
	EndIf
	cQuery += GetUserFields("SC9") 
	cQuery += "FROM "+RetSqlName("SC5")+" SC5, "+RetSqlName("SC9")+" SC9 "
	cQuery += "WHERE "
	cQuery += "SC5.C5_FILIAL  = '"+xFilial("SC5")+"' AND "
	cQuery += "SC5.C5_EMISSAO >='"+DtoS(::RegisterDateFrom)+"' AND "
	cQuery += "SC5.C5_EMISSAO <='"+DtoS(::RegisterDateTo)+"' AND "
	cQuery += "SC5.D_E_L_E_T_ =' ' AND "
	cQuery += "SC9.C9_FILIAL  ='" + xFilial("SC9") + "' AND "
	cQuery += "SC9.C9_PEDIDO  = SC5.C5_NUM AND "
	cQuery += "SC9.C9_CLIENTE = SC5.C5_CLIENTE AND "
	cQuery += "SC9.C9_LOJA    = SC5.C5_LOJACLI AND "
	cQuery += "SC9.D_E_L_E_T_ =' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery	+= "ORDER BY "+SqlOrder(SC5->(IndexKey())+","+SC9->(IndexKey()))

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

	TcSetField(cAliasSC5,"C5_EMISSAO","D",TamSX3("C5_EMISSAO")[1],TamSX3("C5_EMISSAO")[2])
	TcSetField(cAliasSC5,"C9_QTDLIB" ,"N",TamSX3("C9_QTDLIB")[1] ,TamSX3("C9_QTDLIB")[2])					

	DbSelectArea(cAliasSC5)

	::ListOfRelease := {}
	While !Eof() .AND. xFilial("SC5") == (cAliasSC5)->C5_FILIAL .AND.;
			::RegisterDateFrom <= (cAliasSC5)->C5_EMISSAO .AND.;
			::RegisterDateTo >= (cAliasSC5)->C5_EMISSAO

		AAdd(::ListOfRelease,WsClassNew('SalesOrderRelease'))
		nX++

		::ListOfRelease[nX]:OrderID					:= (cAliasSC5)->C9_PEDIDO
		::ListOfRelease[nX]:OrderItem				:= (cAliasSC5)->C9_ITEM
		::ListOfRelease[nX]:CustomerCode			:= (cAliasSC5)->C9_CLIENTE
		::ListOfRelease[nX]:CustomerUnit			:= (cAliasSC5)->C9_LOJA
		::ListOfRelease[nX]:ProductID				:= (cAliasSC5)->C9_PRODUTO
		::ListOfRelease[nX]:QuantityApproved		:= (cAliasSC5)->C9_QTDLIB
		UserFields("SC9", @::ListOfRelease[nX]:UserFields, cAliasSC5)
		
		Do Case

		Case Empty(C9_BLEST) .AND. Empty(C9_BLCRED) .AND.;
				C9_BLWMS $ '  ,05,06,07' .AND.;
				Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.)

			cStatus := PadR(STR0014,20) //"Liberado"

		Case C9_BLCRED == '10' .AND. C9_BLEST == '10'

			cStatus := PadR(STR0015,20) //"Faturado"

		Case !Empty(C9_BLCRED) .AND. C9_BLCRED <> '10'

			cStatus := PadR(STR0016,20) //"Blq. de Credito"

		Case !Empty(C9_BLEST) .AND. C9_BLEST<>'10'

			cStatus := PadR(STR0017,20) //"Blq. de Estoque"

		Case !C9_BLWMS$'  ,05,06,07'

			cStatus := PadR(STR0018,20) //"Blq. de WMS"

		Case !(Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.))

			cStatus := PadR(STR0019,20) //"Blq. de TMS"

		EndCase
		::ListOfRelease[nX]:Status						:= cStatus

		DbSelectArea(cAliasSC5)
		dbSkip()
	EndDo

	If lQuery
		DbSelectArea(cAliasSC5)
		dbCloseArea()
		DbSelectArea("SC5")
	Endif
	nX := 0
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetRelease   ³Autor  ³Eduardo Riera       ³Data  ³03.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao das liberacoes de um pedidos de venda  ³±±
±±³          ³de um vendedor                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Numero do Pedido  	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao das liberacoes de um pedidos de venda  ³±±
±±³          ³de um vendedor                                               ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetRelease WSRECEIVE UserCode,SellerCode,OrderID,QueryAddWhere WSSEND ListOfRelease WSSERVICE MtSellerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local nX        := 0
Local lQuery    := .F.
Local cAliasSC9 := "SC9"
Local cStatus   := ""
Local cQuery    := ""

If PrtChkUser(::UserCode,"MtSellerSalesOrder","BrwRelease","SA3",::SellerCode)
	DbSelectArea("SC9")
	DbSetOrder(1)

	lQuery	:= .T.
	cAliasSC9:= GetNextAlias()

	cQuery	:= "SELECT C9_FILIAL, C9_PEDIDO, C9_ITEM, C9_CLIENTE, C9_LOJA, C9_PRODUTO,"
	cQuery  += "C9_QTDLIB, C9_BLEST, C9_BLCRED, C9_BLWMS "
	If SC9->(FieldPos("C9_BLTMS"))>0
		cQuery += ",C9_BLTMS "
	EndIf
	cQuery	+= GetUserFields("SC9") 
	cQuery	+= "FROM "+RetSqlName("SC9")+" SC9 "
	cQuery	+= "WHERE "
	cQuery	+= "SC9.C9_FILIAL = '"+xFilial("SC9")+"' AND "
	cQuery	+= "SC9.C9_PEDIDO='"+::OrderID+"' AND "
	cQuery	+= "SC9.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery	+= "ORDER BY "+SqlOrder(SC9->(IndexKey()))

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9,.T.,.T.)

	TcSetField(cAliasSC9,"C9_QTDLIB" ,"N",TamSX3("C9_QTDLIB")[1] ,TamSX3("C9_QTDLIB")[2])					

	DbSelectArea(cAliasSC9)

	::ListOfRelease := {}
	While !Eof() .AND. xFilial("SC9") == (cAliasSC9)->C9_FILIAL .AND.;
			::OrderID == (cAliasSC9)->C9_PEDIDO

		AAdd(::ListOfRelease,WsClassNew('SalesOrderRelease'))
		nX++

		::ListOfRelease[nX]:OrderID					:= (cAliasSC9)->C9_PEDIDO
		::ListOfRelease[nX]:OrderItem				:= (cAliasSC9)->C9_ITEM
		::ListOfRelease[nX]:CustomerCode			:= (cAliasSC9)->C9_CLIENTE
		::ListOfRelease[nX]:CustomerUnit			:= (cAliasSC9)->C9_LOJA
		::ListOfRelease[nX]:ProductID				:= (cAliasSC9)->C9_PRODUTO
		::ListOfRelease[nX]:QuantityApproved		:= (cAliasSC9)->C9_QTDLIB
		UserFields("SC9", @::ListOfRelease[nX]:UserFields, cAliasSC9)

		Do Case

		Case Empty(C9_BLEST) .AND. Empty(C9_BLCRED) .AND.;
				C9_BLWMS $ '  ,05,06,07' .AND.;
				Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.)

			cStatus := PadR(STR0014,20) //"Liberado"

		Case C9_BLCRED == '10' .AND. C9_BLEST == '10'

			cStatus := PadR(STR0015,20) //"Faturado"

		Case !Empty(C9_BLCRED) .AND. C9_BLCRED <> '10'

			cStatus := PadR(STR0016,20) //"Blq. de Credito"

		Case !Empty(C9_BLEST) .AND. C9_BLEST<>'10'

			cStatus := PadR(STR0017,20) //"Blq. de Estoque"

		Case !C9_BLWMS$'  ,05,06,07'

			cStatus := PadR(STR0018,20) //"Blq. de WMS"

		Case !(Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.))

			cStatus := PadR(STR0019,20) //"Blq. de TMS"

		EndCase

		::ListOfRelease[nX]:Status	:= cStatus
		cStatus := ""

		DbSelectArea(cAliasSC9)
		dbSkip()
	EndDo
	If lQuery
		DbSelectArea(cAliasSC9)
		dbCloseArea()
		DbSelectArea("SC9")
	Endif
	nX := 0
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetStatusRele³Autor  ³Eduardo Riera       ³Data  ³17.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos status de um pedido de venda       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Numero do Pedido  	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos status de um pedido de venda       ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetStatusRelease WSRECEIVE UserCode,SellerCode,OrderID,QueryAddWhere WSSEND SalesOrderStatus WSSERVICE MtSellerSalesOrder

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local lBloqueio := .F.
Local cAliasSC9 := "SC9"
Local cStatus   := ""
Local cQuery    := ""

DEFAULT ::SalesOrderStatus := ""

If PrtChkUser(::UserCode,"MtSellerSalesOrder","GetStatusRelease","SA3",::SellerCode)
	DbSelectArea("SC9")
	DbSetOrder(1)

	lQuery	:= .T.
	cAliasSC9:= GetNextAlias()

	cQuery	:= "SELECT C9_FILIAL, C9_PEDIDO, C9_ITEM, C9_CLIENTE, C9_LOJA, C9_PRODUTO,"
	cQuery  += "C9_QTDLIB, C9_BLEST, C9_BLCRED, C9_BLWMS "
	If SC9->(FieldPos("C9_BLTMS"))>0
		cQuery += ",C9_BLTMS "
	Endif
	cQuery	+= "FROM "+RetSqlName("SC9")+" SC9 "
	cQuery	+= "WHERE "
	cQuery	+= "SC9.C9_FILIAL = '"+xFilial("SC9")+"' AND "
	cQuery	+= "SC9.C9_PEDIDO='"+::OrderID+"' AND "
	cQuery	+= "SC9.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery	+= "ORDER BY "+SqlOrder(SC9->(IndexKey()))

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9,.T.,.T.)

	TcSetField(cAliasSC9,"C9_QTDLIB" ,"N",TamSX3("C9_QTDLIB")[1] ,TamSX3("C9_QTDLIB")[2])					

	DbSelectArea(cAliasSC9)

	While !Eof() .AND. xFilial("SC9") == (cAliasSC9)->C9_FILIAL .AND.;
			::OrderID == (cAliasSC9)->C9_PEDIDO

		Do Case

		Case Empty(C9_BLEST) .AND. Empty(C9_BLCRED) .AND.;
				C9_BLWMS $ '  ,05,06,07' .AND.;
				Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.)

			cStatus := PadR(STR0014,20) //"Liberado"

		Case C9_BLCRED == '10' .AND. C9_BLEST == '10'

			cStatus := PadR(STR0015,20) //"Faturado"

		Case !Empty(C9_BLCRED) .AND. C9_BLCRED <> '10'

			cStatus := PadR(STR0016,20) //"Blq. de Credito"

		Case !Empty(C9_BLEST) .AND. C9_BLEST<>'10'

			cStatus := PadR(STR0017,20) //"Blq. de Estoque"

		Case !C9_BLWMS$'  ,05,06,07'

			cStatus := PadR(STR0018,20) //"Blq. de WMS"

		Case !(Iif(SC9->((FieldPos('C9_BLTMS') > 0)),Empty(C9_BLTMS),.T.))

			cStatus := PadR(STR0019,20) //"Blq. de TMS"

		EndCase
            
		If !Alltrim( cStatus ) $ AllTrim( ::SalesOrderStatus )
			If STR0014$cStatus //"Liberado"
				::SalesOrderStatus := ","+AllTrim(cStatus)+STR0020+SubStr(::SalesOrderStatus,2) //" com "
			Else
				::SalesOrderStatus += ","+AllTrim(cStatus)
				lBloqueio := .T.
			Endif
		Endif

		DbSelectArea(cAliasSC9)
		dbSkip()
	EndDo
	If lQuery
		DbSelectArea(cAliasSC9)
		dbCloseArea()
		DbSelectArea("SC9")
	Endif
	If Empty(::SalesOrderStatus)
		::SalesOrderStatus := STR0021 //"Carteira"
	Else
		::SalesOrderStatus := SubStr(::SalesOrderStatus,2)
	Endif
	If !lBloqueio .AND. STR0014$::SalesOrderStatus //"Liberado"
		::SalesOrderStatus := STR0014 //"Liberado"
	Endif
Else
	lRetorno := .F.
Endif

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ BrwIdOrder  ³Autor  ³ Cleber Martinez    ³Data  ³11.07.2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Metodo de recuperacao dos numeros de licitacoes dos Pedidos ³±±
±±³          ³ de Venda (C5_COTACAO) para consulta F3                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Codigo do vendedor                                    ³±±
±±³          ³ExpC3: Numero Pedido   	                                   ³±±
±±³          ³ExpC4: Exp. query adicional                                  ³±±
±±³          ³ExpC5: Chave/Indice p/ ordenacao                             ³±±
±±³          ³ExpN6: Nr. de itens para paginacao                           ³±±
±±³          ³ExpN7: Primeiro item a paginar                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Portal do Vendedor                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwIdOrder WSRECEIVE UserCode,SellerCode,OrderID,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfSalesOrder WSSERVICE MtSellerSalesOrder
Local aArea     := GetArea()			//Guarda a area atual
Local lRetorno  := .T.					//Indica o retorno do metodo 
Local lQuery    := .F.					//Indica se executou query
Local nX        := 0					//Usada em lacos For...Next
Local nY        := 0					//Usada em lacos For...Next
Local cAliasSC5 := "SC5"				//Alias da tabela SC5
Local cQuery    := ""					//Query a ser executada 
Local cVendedor := SubStr(::SellerCode,1,Len(SA3->A3_COD))		//Cod. vendedor Portal
Local cArquivo  := ""       			//Nome do arquivo temporario (CodeBase)
Local aStruSC5  := {}					//Estrutura da tabela SC5

SC5->(DbSetOrder(2))

DEFAULT ::IndexKey	:= SC5->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0
 
If PrtChkUser(::UserCode,"MtSellerSalesOrder","BrwIdOrder","SA3",::SellerCode)
	If !Empty(cVendedor)

		DbSelectArea("SC5")
		DbSetOrder(2)

		lQuery	:= .T.
		aStruSC5	:= SC5->(dbStruct())
		cAliasSC5:= GetNextAlias()

		cQuery	:= "SELECT * "
		cQuery	+= "FROM "+RetSqlName("SC5")+" SC5 "
		cQuery	+= "WHERE "
		cQuery	+= "SC5.C5_FILIAL = '"+xFilial("SC5")+"' AND "
		cQuery	+= "SC5.C5_VEND1='"+cVendedor+"' AND "
		cQuery	+= "SC5.C5_COTACAO <>'"+Space(Len(SC5->C5_COTACAO))+"' AND "
		If !Empty(::OrderID)
			cQuery	+= "SC5.C5_COTACAO LIKE '%"+::OrderID+"%' AND "
		EndIf
		cQuery	+= "SC5.D_E_L_E_T_=' ' "
		cQuery	:= WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+SqlOrder(SC5->(IndexKey()))

		cQuery	:= ChangeQuery(cQuery)
		
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)
	
		For nX := 1 To Len(aStruSC5)
			If aStruSC5[nX][2]<>"C" .AND. aStruSC5[nX][2]<>"M"
				TcSetField(cAliasSC5,aStruSC5[nX][1],aStruSC5[nX][2],aStruSC5[nX][3],aStruSC5[nX][4])
			EndIf
		Next nX

		nX := 0
		DbSelectArea(cAliasSC5)		
		While !Eof() .AND. xFilial("SC5") == (cAliasSC5)->C5_FILIAL
			If (cAliasSC5)->C5_VEND1 == cVendedor
				nX++					
				If ::PageFirst==0 .OR. nX >= ::PageFirst
					If nY == 0
						::ListOfSalesOrder := {}
					EndIf
					AAdd(::ListOfSalesOrder,WsClassNew("SalesOrderHeaderView"))
					nY++
					GetPVHeader(@::ListOfSalesOrder[nY],cAliasSC5)
				EndIf
			EndIf
			If nY >= ::PageLen .AND. ::PageLen <> 0
				Exit
			EndIf
			DbSelectArea(cAliasSC5)
			DbSkip()
		EndDo
		If lQuery
			DbSelectArea(cAliasSC5)
			dbCloseArea()
			DbSelectArea("SC5")
		Else
			RetIndex(cAliasSC5)
			FErase(cArquivo+OrdBagExt())
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWIDORDER",STR0013) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT410  ³ Autor ³Eduardo Riera          ³ Data ³25.11.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service de simulacao do pedido de venda.                 ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSimulationSalesOrder DESCRIPTION STR0024 NAMESPACE "http://webservices.microsiga.com.br/mtsimulationsalesorder.apw" //"Serviço de simulação da atualização do pedido de venda. <br><br><i>Este serviço pode ser utilizado para recuperar o preenchimento automático de algumas informações do pedido de venda, como por exemplo: as regras de desconto ou bonificação. Salientamos que este serviço executa as customização presentes no sistema, possibilitando seu uso.</i>"
WSDATA UserCode                  AS String
WSDATA OrderID				     AS String
WSDATA PortCli					 AS Boolean OPTIONAL
WSDATA OldSalesOrder	         AS SalesOrderView
WSDATA NewSalesOrder	  	     AS SalesOrderView

WSMETHOD PutSalesOrder DESCRIPTION STR0025 //"Método de simulação da atualização do pedido de venda"

ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSalesOrder³Autor  ³Eduardo Riera       ³Data  ³25.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de simulacao de atualizacao de um pedido de venda     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Dados do Pedido de Venda                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo simula a atualizacao de orcamento de venda       ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutSalesOrder WSRECEIVE UserCode,OldSalesOrder,PortCli WSSEND NewSalesOrder WSSERVICE MtSimulationSalesOrder

Local aArea		:= GetArea()
Local aCab		:= {}
Local aItens	:= {}
Local aErro     := {}
Local cErro     := ""
Local lRetorno	:= .T.
Local nX        := 0
Local aItensAux := {} 	
Local cCodigoTES:= 0		 
Local nIndiceAux:= 0
Local nPosTes	:= 0

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSimulationSalesOrder","PutSalesOrder")

	DbSelectArea("SC5")
	DbSetOrder(1)
	If ::OldSalesOrder:SOHeader:OrderID <> Nil
		MsSeek(xFilial("SC5")+::OldSalesOrder:SOHeader:OrderID)
	Else
		MsGoto(0)		
	Endif
	lRetorno := PutPvHead(::OldSalesOrder:SOHeader,@aCab)
	aCab := WsAutoOpc(@aCab,.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiacao dos itens do pedido de venda                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno 
		lRetorno := PutPvItem(::OldSalesOrder:SOHeader,::OldSalesOrder:SOItem,@aItens,::PortCli)
		For nX := 1 To Len(aItens)
			aItens[nX] := WsAutoOpc(@aItens[nX],.T.)
		Next nX
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se eh alteracao ou inclusao                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If lRetorno
		DbSelectArea("SC5")
		DbSetOrder(1)
		If !Empty(::OldSalesOrder:SOHeader:OrderID) .AND. MsSeek(xFilial("SC5")+::OldSalesOrder:SOHeader:OrderID)
			MATA410(@aCab,@aItens,4,.T.)
		Else
			MATA410(@aCab,@aItens,3,.T.)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Retira os produtos bonificados.³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cCodigoTES:=GETDTOVAL(SUPERGETMV("MV_BONUSTS", .F., 0))		  
			For nIndiceAux:=1 To Len(aItens)
				nPosTes := aScan( aItens[nIndiceAux], {|x| AllTrim(x[1]) == "C6_TES"} )
				If nPosTes > 0
				    If (Val(aItens[nIndiceAux][nPosTes][2]) != cCodigoTES)
			    		aAdd(aItensAux,aItens[nIndiceAux])
				    EndIf
				Else
					aAdd(aItensAux,aItens[nIndiceAux])
				EndIf
			Next nIndiceAux
			aItens:=aItensAux
			
		Endif
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PutSalesOrder",cErro)
			lRetorno := .F.
		Endif
		If lRetorno
			aCab := WsAutoOpc(@aCab,.T.)
			For nX := 1 To Len(aItens)
				aItens[nX] := WsAutoOpc(@aItens[nX],.T.)
			Next nX
			GetPVHeader(::NewSalesOrder:SOHeader,"SC5",aCab)
			For nX := 1 To Len(aItens)
				AAdd(::NewSalesOrder:SOItem,WSClassNew("SalesOrderItemView"))
				GetPVItem(::NewSalesOrder:SOItem[nX],"SC6","SC5",aCab,aItens[nX])
			Next nX
		Endif
	Endif
Else
	lRetorno := .F.
Endif
RestArea(aArea)
Return(lRetorno)
