#INCLUDE "wsmat040.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT040  ³ Autor ³Eduardo Riera          ³ Data ³01.09.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pela manutencao das informacoes      ³±±
±±³          ³de vendedores                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Web Service                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSeller              DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtseller.apw" //"Serviço de consulta e atualização da tabela de representantes comerciais"
WSDATA UserCode                 AS String
WSDATA SellerCode               AS String
WSDATA Commission               As Array Of CommissionView OPTIONAL
WSDATA RegisterDateFrom         As Date OPTIONAL
WSDATA RegisterDateTo           As Date OPTIONAL
WSDATA ReceivableDateFrom       As Date OPTIONAL
WSDATA ReceivableDateTo         As Date OPTIONAL
WSDATA Seller                   As SellerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA SellerId                 As String
WSDATA TypeOfPhone              As Array Of GenericStruct
WSDATA QueryAddWhere            As String OPTIONAL
WSDATA SellerPassword           As String
WSDATA Branchs                  As Array Of GenericStruct

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0003 //"Método que descreve os tipo de telefones"
WSMETHOD GetSeller        DESCRIPTION STR0004 //"Método de consulta as informações cadastrais do representante comercial"
WSMETHOD GetCommission    DESCRIPTION STR0005 //"Método de consulta a relação de comissões de um período. "
WSMETHOD PutSeller        DESCRIPTION STR0006 //"Método de atualização das informações cadastrais do representante comercial"
WSMETHOD GetLogin         DESCRIPTION STR0007 //"Método de Login do representante comercial. Este método verifica a senha na tabela de informações cadastrais do representante comercial, retornando a lista de empresas que o representante possui acesso. "
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³01.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos headers deste WS                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSeller

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSeller ³Autor  ³ Eduardo Riera         ³ Data ³01.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de obtencao dos dados de vendedores                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Codigo de identificacao do vendedor                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve os dados de um vendedor                  ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetSeller WSRECEIVE UserCode,SellerCode WSSEND Seller WSSERVICE MtSeller

Local aArea    := GetArea()
Local lRetorno := .T.
Local nX       := 0

If PrtChkUser(::UserCode,"MTSELLER","GetSeller","SA3",::SellerCode)
	dbSelectArea("SA3")
	dbSetOrder(1)
	If MsSeek(xFilial("SA3")+::SellerCode)	
		::Seller:SellerCode  := SA3->A3_COD
		::Seller:Name        := SA3->A3_NOME
		::Seller:NickName    := SA3->A3_NREDUZ
		::Seller:FederalID   := SA3->A3_CGC
		::Seller:StateID     := SA3->A3_INSCR
		::Seller:DistrictID  := SA3->A3_INSCRM
		::Seller:HomePage    := SA3->A3_HPAGE
		::Seller:Email       := SA3->A3_EMAIL
		::Seller:SalesSupervisorCode := SA3->A3_SUPER
		::Seller:SalesManagerCode    := SA3->A3_GEREN
		UserFields("SA3",@::Seller:UserFields)

		::Seller:Addresses := WsClassNew("AddressView")
		::Seller:Addresses:TypeOfAddress := "1"
		::Seller:Addresses:Address       := FisGetEnd(SA3->A3_END)[1]
		::Seller:Addresses:AddressNumber := AllTrim(Str(FisGetEnd(SA3->A3_END)[2],8))
		::Seller:Addresses:District      := SA3->A3_MUN
		::Seller:Addresses:State         := SA3->A3_EST
		::Seller:Addresses:ZipCode       := SA3->A3_CEP
		::Seller:Addresses:Zone          := SA3->A3_BAIRRO

		::Seller:Phones := {}
		nX := 0
		If !Empty(SA3->A3_TEL)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "1"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_TEL)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_TEL)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_TEL)[1],15))
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a FisGetTel retornar zero pega o DDD e DDI direto ³
			//³dos seus respectivos campos.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf						
			
		EndIf

		If !Empty(SA3->A3_FAX)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "2"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_FAX)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_FAX)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_FAX)[1],15))			
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a FisGetTel retornar zero pega o DDD e DDI direto ³
			//³dos seus respectivos campos.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf						
			
		EndIf	
	Else
		lRetorno := .F.
		SetSoapFault("GETSELLER",STR0008) //"Vendedor nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSeller ³Autor  ³ Eduardo Riera         ³ Data ³01.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de atualizacao dos dados de vendedor                  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Codigo de identificacao do vendedor                   ³±±
±±³          ³ExpO3: Estrutura a ser gravada                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo atualiza os dados de um vendedor                 ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutSeller WSRECEIVE UserCode,SellerCode,Seller WSSEND SellerId WSSERVICE MtSeller

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

DEFAULT ::SellerId  := ""

If PrtChkUser(::UserCode,"MTSELLER","PutSeller","SA3",::SellerCode)
	If !Empty(::Seller:SellerCode)
		aadd(aDados,{"A3_COD",::Seller:SellerCode,})
	EndIf
	aadd(aDados,{"A3_NOME",::Seller:Name,})
	aadd(aDados,{"A3_NREDUZ",::Seller:NickName,})
	aadd(aDados,{"A3_CGC",::Seller:FederalID,})
	aadd(aDados,{"A3_INSCR",::Seller:StateID,})
	aadd(aDados,{"A3_INSCRM",::Seller:DistrictID,})
	aadd(aDados,{"A3_HPAGE",::Seller:HomePage,})
	aadd(aDados,{"A3_EMAIL",::Seller:Email,})
	aadd(aDados,{"A3_END",AllTrim(::Seller:Addresses:Address)+", "+AllTrim(::Seller:Addresses:AddressNumber),})
	aadd(aDados,{"A3_MUN",::Seller:Addresses:District,})
	aadd(aDados,{"A3_EST",::Seller:Addresses:State,})
	aadd(aDados,{"A3_CEP",::Seller:Addresses:ZipCode,})
	aadd(aDados,{"A3_BAIRRO",::Seller:Addresses:Zone,})		
	For nX := 1 To Len(::Seller:Phones)
		Do Case
		Case ::Seller:Phones[nX]:TypeOfPhone == "1"
			If !Empty( ::Seller:Phones[nX]:PhoneNumber )
				aadd(aDados,{"A3_TEL",::Seller:Phones[nX]:PhoneNumber,})
			EndIf
		Case ::Seller:Phones[nX]:TypeOfPhone == "2"
			If !Empty( ::Seller:Phones[nX]:PhoneNumber )
				aadd(aDados,{"A3_FAX",::Seller:Phones[nX]:PhoneNumber,})
			EndIf
		OtherWise
			lRetorno := .F.
			SetSoapFault("PUTSELLER",STR0009) //"TypeOfPhone invalido"
		EndCase
	Next nX
	If lRetorno
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Variaveis de usuario                                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		PutUserFields("SA3",@::Seller:UserFields,@aDados)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualizacao do cadastro de vendedores                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SA3")
		dbSetOrder(1)
		If MsSeek(xFilial("SA3")+::SellerCode) .And. !Empty(::SellerCode)
			Mata040(aDados,4)
		Else
			Mata040(aDados,3)
		EndIf
		::SellerId := SA3->A3_COD
		If lMsErroAuto
			aErro := GetAutoGRLog()
			cErro := ""
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTSELLER",cErro)
			lRetorno := .F.
		EndIf
	EndIf

Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetTypeOfA³Autor  ³ Eduardo Riera         ³ Data ³04.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de demonstracao do TypeOfAddress                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtSeller

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0011 //"FAX"

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetCommiss³Autor  ³ Eduardo Riera         ³ Data ³01.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de retorno dos dados da comissao de um vendedor       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Codigo de identificacao do vendedor                   ³±±
±±³          ³ExpD3: Emissao de                                            ³±±
±±³          ³ExpD4: Emissao Ate                                           ³±±
±±³          ³ExpD5: Vencto  de                                            ³±±
±±³          ³ExpD6: Vencto  Ate                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo retorna a comissao do vendedor em um periodo     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³23/10/06³111610³-Inclusao do P.E. WSMT040A para filtro de³±±
±±³              ³        ³      ³comissoes.                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetCommission WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,ReceivableDateFrom,ReceivableDateTo,QueryAddWhere WSSEND Commission WSSERVICE MtSeller

Local aArea     := GetArea()	//Guarda a area atual
Local cQuery    := ""			//Conteudo da query a ser executada
Local cAliasSE3 := "SE3"		//Alias da tabela SE3
Local cAliasSA1 := "SA1"		//Alias da tabela SA1
Local cArquivo  := ""			//Arquivo de trabalho
Local lQuery    := .F.			//Indica se executou a query
Local lRetorno  := .T.			//Indica retorno da funcao
Local nX        := 0			//Variavel usada em lacos For...Next
Local aStruSE3  := {}			//Estrutura do arq. SE3

If Empty(::ReceivableDateFrom) .Or. Empty(::ReceivableDateTo)
	DEFAULT ::ReceivableDateFrom := FirstDay(dDataBase-30)
	DEFAULT ::ReceivableDateTo   := LastDay(dDataBase-30)
EndIf

If PrtChkUser(::UserCode,"MTSELLER","GetCommission","SA3",::SellerCode)
	dbSelectArea("SA1")
	dbSelectArea("SE3")
	dbSetOrder(1)

	aStruSE3  := SE3->(dbStruct())
	cAliasSE3 := "Getcommission"
	cAliasSA1 := cAliasSE3
	lQuery    := .T.

	cQuery := "SELECT SE3.*,A1_NOME,A1_NREDUZ "
	cQuery += "FROM "+RetSqlName("SE3")+ " SE3, "
	cQuery += RetSqlName("SA1")+ " SA1 "
	cQuery += "WHERE SE3.E3_FILIAL='"+xFilial("SE3")+"' AND "
	cQuery += "SE3.E3_VEND='"+::SellerCode+"' AND "
	If !Empty(::RegisterDateFrom)
		cQuery += "SE3.E3_EMISSAO>='"+Dtos(::RegisterDateFrom)+"' AND "
		cQuery += "SE3.E3_EMISSAO<='"+Dtos(::RegisterDateTo)+"' AND "
	EndIf
	If !Empty(::ReceivableDateFrom)
		cQuery += "SE3.E3_VENCTO>='"+Dtos(::ReceivableDateFrom)+"' AND "
		cQuery += "SE3.E3_VENCTO<='"+Dtos(::ReceivableDateTo)+"' AND "
	EndIf
	cQuery += "SE3.D_E_L_E_T_=' ' AND "
	cQuery += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
	cQuery += "SA1.A1_COD = SE3.E3_CODCLI AND "
	cQuery += "SA1.A1_LOJA = SE3.E3_LOJA AND "
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para filtrar comissoes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("WSMT040A") 
		cQuery += ExecBlock( "WSMT040A", .F., .F., { 1 } )
	EndIf 
	
	cQuery += "SA1.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery += "ORDER BY "+SqlOrder(SE3->(IndexKey()))

	cQuery := ChangeQuery(cQuery)
	MemoWrite("WSMAT040.SQL", cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE3)

	For nX := 1 To Len(aStruSE3)
		If aStruSE3[nX][2] <> "C"
			TcSetField(cAliasSE3,aStruSE3[nX][1],aStruSE3[nX][2],aStruSE3[nX][3],aStruSE3[nX][4])
		EndIf
	Next nX

	dbSelectArea(cAliasSE3)
	nX := 0
	While !Eof() .And. (cAliasSE3)->E3_FILIAL == xFilial("SE3")
		If nX == 0
			::Commission := {}
		EndIf
		aadd(::Commission,WsClassNew("CommissionView"))
		nX++
		If !lQuery
			dbSelectArea("SA1")
			dbSetOrder(1)
			MsSeek(xFilial("SA1")+(cAliasSE3)->E3_CODCLI+(cAliasSE3)->E3_LOJA)
		EndIf
		::Commission[nX]:SellerCode            := (cAliasSE3)->E3_VEND
		::Commission[nX]:CustomerCode          := (cAliasSE3)->E3_CODCLI+(cAliasSE3)->E3_LOJA
		::Commission[nX]:Name                  := (cAliasSA1)->A1_NOME
		::Commission[nX]:NickName              := (cAliasSA1)->A1_NREDUZ
		::Commission[nX]:Document              := (cAliasSE3)->E3_PREFIXO+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_PARCELA+(cAliasSE3)->E3_TIPO+(cAliasSE3)->E3_SEQ
		::Commission[nX]:SalesOrder            := (cAliasSE3)->E3_PEDIDO
		::Commission[nX]:RegisterDate          := (cAliasSE3)->E3_EMISSAO
		::Commission[nX]:ReceivableDate        := (cAliasSE3)->E3_VENCTO
		::Commission[nX]:PaymentDate           := (cAliasSE3)->E3_DATA
		::Commission[nX]:CommissionBase        := (cAliasSE3)->E3_BASE
		::Commission[nX]:CommissionPercentage  := (cAliasSE3)->E3_PORC
		::Commission[nX]:CommissionValue       := (cAliasSE3)->E3_COMIS
		::Commission[nX]:GeneratedBy           := IIF((cAliasSE3)->E3_BAIEMI=="B",STR0012,STR0013) //"PAGAMENTO"###"EMISSAO"
		
		dbSelectArea("SE1")
		dbSetOrder(1)
		If MsSeek(xFilial("SE1")+(cAliasSE3)->E3_PREFIXO+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_PARCELA+(cAliasSE3)->E3_TIPO)
			::Commission[nX]:BillRealDueDate := SE1->E1_VENCREA
		EndIf
		If (cAliasSE3)->E3_BAIEMI=="B"
			dbSelectArea("SE5")
			dbSetOrder(7)
			If MsSeek(xFilial("SE5")+(cAliasSE3)->E3_PREFIXO+(cAliasSE3)->E3_NUM+(cAliasSE3)->E3_PARCELA+(cAliasSE3)->E3_TIPO+(cAliasSE3)->E3_CODCLI+(cAliasSE3)->E3_LOJA+(cAliasSE3)->E3_SEQ)
				While !Eof() .And. xFilial("SE5") == SE5->E5_FILIAL .And.;
					(cAliasSE3)->E3_PREFIXO == SE5->E5_PREFIXO .And.;
					(cAliasSE3)->E3_NUM == SE5->E5_NUMERO .And.;
					(cAliasSE3)->E3_PARCELA == SE5->E5_PARCELA .And.;
					(cAliasSE3)->E3_TIPO == SE5->E5_TIPO .And.;
					(cAliasSE3)->E3_CODCLI == SE5->E5_CLIFOR .And.;
					(cAliasSE3)->E3_LOJA == SE5->E5_LOJA .And.;
					(cAliasSE3)->E3_SEQ == SE5->E5_SEQ
					
					If (SE5->E5_RECPAG=="R" .And. SE5->E5_TIPODOC<>"ES") .Or. (SE5->E5_RECPAG=="P" .And. SE5->E5_TIPODOC=="ES")
						::Commission[nX]:BillPaidDate := SE5->E5_DATA
					EndIf
									
					dbSelectArea("SE5")
					dbSkip()
				EndDo
			EndIf
		EndIf
		
		UserFields("SE3",@::Commission[nX]:UserFields)

		dbSelectArea(cAliasSE3)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSE3)
		dbCloseArea()
		dbSelectArea("SE3")
	Else
		RetIndex("SE3")
		dbClearFilter()
		FErase(cArquivo+OrdBagExt())
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetLogin  ³Autor  ³ Eduardo Riera         ³ Data ³16.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do login do vendedor pelo portal         ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do vendedor                                    ³±±
±±³          ³ExpC2: Senha do vendedor                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo retorna a comissao do vendedor em um periodo     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetLogin WSRECEIVE SellerCode,SellerPassWord WSSEND Branchs WSSERVICE MtSeller

Local aArea		:= GetArea()
Local lRetorno	:= .T.
Local cBkpAnt	:= cFilAnt
Local nX		:= 0
Local nY		:= 0
Local aSM0		:= FWLoadSM0()

DBSelectArea("SA3")
DBSetOrder(1)

For nY := 1 To Len(aSM0)
	If cEmpAnt == aSM0[nY,1]
		cFilAnt := aSM0[nY,2]
		If MsSeek(xFilial("SA3")+::SellerCode)
			If SA3->A3_SENHA == ::SellerPassWord
				If nX == 0
					::Branchs := {}
				EndIf
				nX++
				aadd(::Branchs,WsClassNew("GenericStruct"))
				::Branchs[nX]:Code := aSM0[nY,2]
				::Branchs[nX]:Description := aSM0[nY,7] 
			EndIf
		EndIf
	EndIf
Next nY

If nX == 0 
	lRetorno := .F.
EndIf

cFilAnt := cBkpAnt

RestArea(aArea)
Return(lRetorno)



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT040  ³ Autor ³Eduardo Riera          ³ Data ³01.09.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pela manutencao das informacoes      ³±±
±±³          ³de vendedores                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Web Service                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtCustomerSeller      DESCRIPTION STR0014 NAMESPACE "http://webservices.microsiga.com.br/mtcustomerseller.apw" //"Serviço de consulta da tabela de representantes comerciais"
WSDATA UserCode                 AS String
WSDATA SellerCode               AS String
WSDATA CustomerID               AS String
WSDATA Seller                   As SellerView
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA TypeOfPhone              As Array Of GenericStruct

WSMETHOD GetHeader        DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
WSMETHOD GetTypeOfPhone   DESCRIPTION STR0003 //"Método que descreve os tipo de telefones"
WSMETHOD GetSeller        DESCRIPTION STR0004 //"Método de consulta as informações cadastrais do representante comercial"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³01.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos headers deste WS                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtCustomerSeller

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSeller ³Autor  ³ Eduardo Riera         ³ Data ³01.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de obtencao dos dados de vendedores                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente                                               ³±±
±±³          ³ExpC3: Codigo de identificacao do vendedor                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve os dados de um vendedor                  ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetSeller WSRECEIVE UserCode,CustomerID,SellerCode WSSEND Seller WSSERVICE MtCustomerSeller

Local aArea    := GetArea()
Local lRetorno := .T.
Local nX       := 0

If PrtChkUser(::UserCode,"MtCustomerSeller","GetSeller","SA1",::CustomerID)
	dbSelectArea("SA3")
	dbSetOrder(1)
	If MsSeek(xFilial("SA3")+::SellerCode)	
		::Seller:SellerCode  := SA3->A3_COD
		::Seller:Name        := SA3->A3_NOME
		::Seller:NickName    := SA3->A3_NREDUZ
		::Seller:FederalID   := SA3->A3_CGC
		::Seller:StateID     := SA3->A3_INSCR
		::Seller:DistrictID  := SA3->A3_INSCRM
		::Seller:HomePage    := SA3->A3_HPAGE
		::Seller:Email       := SA3->A3_EMAIL
		::Seller:SalesSupervisorCode := SA3->A3_SUPER
		::Seller:SalesManagerCode    := SA3->A3_GEREN
		UserFields("SA3",@::Seller:UserFields)

		::Seller:Addresses := WsClassNew("AddressView")
		::Seller:Addresses:TypeOfAddress := "1"
		::Seller:Addresses:Address       := FisGetEnd(SA3->A3_END)[1]
		::Seller:Addresses:AddressNumber := AllTrim(Str(FisGetEnd(SA3->A3_END)[2],8))
		::Seller:Addresses:District      := SA3->A3_MUN
		::Seller:Addresses:State         := SA3->A3_EST
		::Seller:Addresses:ZipCode       := SA3->A3_CEP
		::Seller:Addresses:Zone          := SA3->A3_BAIRRO

		::Seller:Phones := {}
		nX := 0
		If !Empty(SA3->A3_TEL)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "1"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_TEL)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_TEL)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_TEL)[1],15))	

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a FisGetTel retornar zero pega o DDD e DDI direto ³
			//³dos seus respectivos campos.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf			
			
		EndIf

		If !Empty(SA3->A3_FAX)
			aadd(::Seller:Phones,WsClassNew("PhoneView"))
			nX++
			::Seller:Phones[nX]:TypeOfPhone     := "2"
			::Seller:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA3->A3_FAX)[3],15))
			::Seller:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA3->A3_FAX)[2],15))
			::Seller:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA3->A3_FAX)[1],15))

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se a FisGetTel retornar zero pega o DDD e DDI direto ³
			//³dos seus respectivos campos.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ::Seller:Phones[nX]:LocalAreaCode == "0"
				::Seller:Phones[nX]:LocalAreaCode := Alltrim(SA3->A3_DDDTEL)
			EndIf			
			If ::Seller:Phones[nX]:CountryAreaCode == "0"
				::Seller:Phones[nX]:CountryAreaCode := Alltrim(SA3->A3_DDI)
			EndIf						
			
		EndIf
			
	Else
		lRetorno := .F.
		SetSoapFault("GETSELLER",STR0008) //"Vendedor nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetTypeOfA³Autor  ³ Eduardo Riera         ³ Data ³04.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de demonstracao do TypeOfAddress                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtCustomerSeller

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0011 //"FAX"

Return(.T.)

Function wsmat040()
Return