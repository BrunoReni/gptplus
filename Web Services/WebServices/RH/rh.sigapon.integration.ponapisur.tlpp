#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "TOTVS.CH"  

NameSpace totvs.protheus.rh.sigapon.integration

/*/{Protheus.doc} recordValidatedVersion
@type method
@description Serviço para retornar Se o ambiente está atualizado com os novos campos para os leiautes da portaria 671
@author Cícero Alves
@since 25/11/2022
/*/
@GET("api/rh/v1/recordValidatedVersion")
Function recordValidatedVersion()
	Local oResp			:= JsonObject():New()	as Json
	Local cResponse 	:= "no"					as Character
	Local lIntRR1Sur	:= .F. 					as Logical
	
	lIntRR1Sur := SuperGetMv("MV_APISUR", .F., .F.) 
	
	If lIntRR1Sur
		If RR1->(	  ColumnPos( "RR1_CODCPF") > 0;
				.And. ColumnPos( "RR1_FUSO"  ) > 0;
				.And. ColumnPos( "RR1_VRSLAY") > 0;
				.And. ColumnPos( "RR1_INSCR" ) > 0)
				
			cResponse:= "yes"
		EndIf
	Else
		If TCSQLExec("SELECT COD_FUSO, COD_CPF_FUNC, NUM_VERS_LAYOUT_AFD, COD_INSCR_EMP FROM MSA_CONTROL_MARCAC") >= 0
			cResponse:= "yes"
		EndIf
	EndIf
	
	oResp["version671"] := cResponse
	oResp := oResp:ToJson()
	
Return oRest:setResponse(oResp)
