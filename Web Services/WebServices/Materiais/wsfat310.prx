#INCLUDE "wsfat310.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSFAT310  ³ Autor ³Eduardo Riera          ³ Data ³22.09.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pelos apontamentos do vendedor       ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE FtSellerAnnotations      DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/ftsellerannotations.apw" //"Serviço de consulta e atualização dos apontamentos do representante comercial"
   WSDATA UserCode                 As String
   WSDATA SellerCode               As String
   WSDATA DateFrom                 As Date OPTIONAL
   WSDATA DateTo                   As Date OPTIONAL
   WSDATA IndexKey				   AS String OPTIONAL
   WSDATA RegisterDate             As Date 
   WSDATA ContactSequence          As String
   WSDATA Header                   As Array Of BrwHeader
   WSDATA HeaderType               As String
   WSDATA ListOfAnnotations        As Array Of SellerAnnotationHeader
   WSDATA Annotation               As SellerAnnotationView
   WSDATA WsError                  As Integer 

   WSMETHOD GetHeader              DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
   WSMETHOD BrwAnnotations         DESCRIPTION STR0003 //"Método de listagem dos apontamentos do representante comercial"
   WSMETHOD GetAnnotations         DESCRIPTION STR0004 //"Método de consulta as informações do apontamento do representante comercial"
   WSMETHOD PutAnnotations         DESCRIPTION STR0005 //"Método de atualização as informações do apontamento do representante comercial"
   WSMETHOD DelAnnotations         DESCRIPTION STR0006 //"Método de exclusão das informações do apontamento do representante comercial"
      
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³24.07.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao do header do catalogo de produtos      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da Estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE FtSellerAnnotations

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwAnnotat³Autor  ³ Eduardo Riera         ³ Data ³22.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos apontamentos do vendedor           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpD3: Data Inicial                                          ³±±
±±³          ³ExpD4: Data Final                                            ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve a lista de apontamentos de um periodo    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwAnnotations WSRECEIVE UserCode,SellerCode,DateFrom,DateTo,IndexKey WSSEND ListOfAnnotations WSSERVICE FtSellerAnnotations
Local aArea    := GetArea()
Local cAliasAD5:= "AD5"
Local lQuery   := .F.
Local lRetorno := .F.
Local nX       := 0


Local aStruct  := {}
Local cQuery   := ""


AD5->( DbSetOrder( 1 ) )

DEFAULT ::DateFrom := dDataBase
DEFAULT ::DateTo   := dDataBase
DEFAULT ::IndexKey	:= AD5->( IndexKey() )

If PrtChkUser(::UserCode,"FtSellerAnnotations","BrwAnnotations","SA3",::SellerCode)
	dbSelectArea("AD5")
	dbSetOrder(1)
	
	lQuery    := .T.
	cAliasAD5 := GetNextAlias()
	aStruct   := AD5->(dbStruct())
	
	cQuery := "SELECT * "
	cQuery += "FROM "+RetSqlName("AD5")+" AD5 "
	cQuery += "WHERE AD5.AD5_FILIAL='"+xFilial("AD5")+"' AND "
	cQuery += "AD5.AD5_VEND='"+::SellerCode+"' AND "
	cQuery += "AD5.AD5_DATA>='"+Dtos(::DateFrom)+"' AND "
	cQuery += "AD5.AD5_DATA<='"+Dtos(::DateTo)+"' AND "
	cQuery += "AD5.D_E_L_E_T_=' ' "
	cQuery += "ORDER BY " + SqlOrder(::IndexKey)
	
	cQuery := ChangeQuery(cQuery)
	
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD5)
	
	For nX := 1 To Len(aStruct)
		If aStruct[nX][2] <> "C" .And. aStruct[nX][2] <> "M"
			TcSetField(cAliasAD5,aStruct[nX][1],aStruct[nX][2],aStruct[nX][3],aStruct[nX][4])
		EndIf
	Next nX
	
	nX := 0
	While !Eof() .And. (cAliasAD5)->AD5_FILIAL == xFilial("AD5") .And.;
		(cAliasAD5)->AD5_VEND == ::SellerCode .And.;
		(cAliasAD5)->AD5_DATA >= ::DateFrom .And.;
		(cAliasAD5)->AD5_DATA <= ::DateTo 
		
		nX++
		If nX == 1
			::ListOfAnnotations := {}
		EndIf
		aadd(::ListOfAnnotations,WsClassNew("SellerAnnotationHeader"))
		::ListOfAnnotations[nX]:SellerCode      := (cAliasAD5)->AD5_VEND
		::ListOfAnnotations[nX]:RegisterDate    := (cAliasAD5)->AD5_DATA
		::ListOfAnnotations[nX]:ContactSequence := (cAliasAD5)->AD5_SEQUEN
		::ListOfAnnotations[nX]:CustomerCode    := (cAliasAD5)->AD5_CODCLI
		::ListOfAnnotations[nX]:CustomerUnit    := (cAliasAD5)->AD5_LOJA
		::ListOfAnnotations[nX]:EventCode       := (cAliasAD5)->AD5_EVENTO
		::ListOfAnnotations[nX]:OpportunityID   := (cAliasAD5)->AD5_NROPOR
		
		dbSelectArea(cAliasAD5)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasAD5)
		dbCloseArea()
		dbSelectArea("AD5")
	EndIf
	If !Empty(::ListOfAnnotations)
		lRetorno := .T.	
	Else
		lRetorno := .F.	
		SetSoapFault("BRWANNOTATIONS",STR0007) //"Não há apontamentos no período"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetAnnotat³Autor  ³ Eduardo Riera         ³ Data ³22.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos apontamentos do vendedor           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpD3: Data                                                  ³±±
±±³          ³ExpC4: Sequencia                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve a lista de apontamentos de um periodo    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetAnnotations WSRECEIVE UserCode,SellerCode,RegisterDate,ContactSequence WSSEND Annotation WSSERVICE FtSellerAnnotations

Local aArea    := GetArea()
Local lRetorno := .T.
Local nX       := 0
Local cAliasAD5 := ""

If PrtChkUser(::UserCode,"FtSellerAnnotations","GetAnnotations","SA3",::SellerCode)
	dbSelectArea("AD5")
	dbSetOrder(1)
	If MsSeek(xFilial("AD5")+::SellerCode+Dtos(::RegisterDate)+::ContactSequence)
		cAliasAD5 := "AD5"
		
		::Annotation:Header                 := WsClassNew("SellerAnnotationHeader")
		::Annotation:Header:SellerCode      := (cAliasAD5)->AD5_VEND
		::Annotation:Header:RegisterDate    := (cAliasAD5)->AD5_DATA
		::Annotation:Header:ContactSequence := (cAliasAD5)->AD5_SEQUEN
		::Annotation:Header:CustomerCode    := (cAliasAD5)->AD5_CODCLI
		::Annotation:Header:CustomerUnit    := (cAliasAD5)->AD5_LOJA
		::Annotation:Header:EventCode       := (cAliasAD5)->AD5_EVENTO
		::Annotation:Header:OpportunityID   := (cAliasAD5)->AD5_NROPOR
		UserFields("AD5", @::Annotation:Header:UserFields, cAliasAD5)
		
		nX := 0		
		dbSelectArea("AD6")
		dbSetOrder(1)
		If MsSeek(xFilial("AD6")+::SellerCode+Dtos(::Annotation:Header:RegisterDate)+::Annotation:Header:ContactSequence)
			While !Eof() .And. xFilial("AD6") == AD6->AD6_FILIAL .And.;
				::Annotation:Header:SellerCode == AD6->AD6_VEND .AND. ;
				Dtos(::Annotation:Header:RegisterDate) == DToS(AD6->AD6_DATA) .And.;
				::Annotation:Header:ContactSequence == AD6->AD6_SEQUEN
				
				nX++
				If nX == 1
					::Annotation:Item := {}
				EndIf
				aadd(::Annotation:Item,WsClassNew("SellerAnnotationItem"))
				
				::Annotation:Item[nX]:Item        := AD6->AD6_ITEM
				::Annotation:Item[nX]:ProductCode := AD6->AD6_CODPRO
				::Annotation:Item[nX]:Quantity    := AD6->AD6_QUANT
				::Annotation:Item[nX]:UnitValue   := AD6->AD6_VLUNIT
				::Annotation:Item[nX]:TotalValue  := AD6->AD6_TOTAL
				UserFields("AD6", @::Annotation:Item[nX]:UserFields, "AD6")
				dbSelectArea("AD6")
				dbSkip()
		   	EndDo
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETANNOTATIONS",STR0008)	 //"Apontamento nao localizado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutAnnotat³Autor  ³ Eduardo Riera         ³ Data ³22.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de gravacao dos apontamentos do vendedor              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpO3: Apontamento                                           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve a lista de apontamentos de um periodo    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutAnnotations WSRECEIVE UserCode,SellerCode,Annotation WSSEND WsError  WSSERVICE FtSellerAnnotations

Local aArea      := GetArea()
Local aCabec     := {}
Local aItens     := {}
Local aLinha     := {}
Local aNoDeleted := {}
Local aErro     := {}
Local cErro     := ""
Local lRetorno   := .T.
Local nX         := 0

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"FtSellerAnnotations","PutAnnotations","SA3",::SellerCode)

	aadd(aCabec,{"AD5_VEND",::Annotation:Header:SellerCode,Nil})
	aadd(aCabec,{"AD5_DATA",::Annotation:Header:RegisterDate,Nil})
	aadd(aCabec,{"AD5_SEQUEN",::Annotation:Header:ContactSequence,Nil})
	aadd(aCabec,{"AD5_EVENTO",::Annotation:Header:EventCode,Nil})
	
	If Empty( ::Annotation:Header:OpportunityID )
		aadd(aCabec,{"AD5_CODCLI",::Annotation:Header:CustomerCode,Nil})
		aadd(aCabec,{"AD5_LOJA",::Annotation:Header:CustomerUnit,Nil})
	Else
		aadd(aCabec,{"AD5_NROPOR",::Annotation:Header:OpportunityID,Nil})
	EndIf
	PutUserFields("AD5",@::Annotation:Header:UserFields,@aCabec)
	aCabec := WsAutoOpc(aCabec)
				
		For nX := 1 To Len(::Annotation:Item)
			
			aadd(aNoDeleted,::Annotation:Item[nX]:Item)
			
			aLinha := {}			
			aadd(aLinha,{"AD6_ITEM",::Annotation:Item[nX]:Item,Nil})
			aadd(aLinha,{"AD6_CODPRO",::Annotation:Item[nX]:ProductCode,Nil})
			aadd(aLinha,{"AD6_QUANT",::Annotation:Item[nX]:Quantity,Nil})
			aadd(aLinha,{"AD6_VLUNIT",::Annotation:Item[nX]:UnitValue,Nil})
			aadd(aLinha,{"AD6_TOTAL",::Annotation:Item[nX]:TotalValue,Nil})
			
			PutUserFields("AD6",@::Annotation:Item[nX]:UserFields,@aLinha)
			aLinha := WsAutoOpc(aLinha)
			
			aadd(aItens,aLinha)
		
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica os deletados                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("AD6")
		dbSetOrder(1)
		If MsSeek(xFilial("AD6")+::SellerCode+Dtos(::Annotation:Header:RegisterDate)+::Annotation:Header:ContactSequence)
			While !Eof() .And. xFilial("AD6") == AD6->AD6_FILIAL .And.;
				::SellerCode == AD6_VEND .And.;
				Dtos(::Annotation:Header:RegisterDate) == Dtos(AD6->AD6_DATA) .And.;
				::Annotation:Header:ContactSequence == AD6->AD6_SEQUEN

				If aScan(aNoDeleted,AD6->AD6_ITEM) == 0
					aadd(aItens,{{"AD6_ITEM",AD6->AD6_ITEM},{"AUTDELETA","S"}})
				EndIf
				
				dbSelectArea("AD6")
				dbSkip()
			EndDo
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica a operacao                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("AD5")
		dbSetOrder(1)
		If MsSeek(xFilial("AD5")+::Annotation:Header:SellerCode+Dtos(::Annotation:Header:RegisterDate)+::Annotation:Header:ContactSequence)
			Fata310(aCabec,aItens,4)
		Else
			Fata310(aCabec,aItens,3)
		EndIf
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTANNOTATIONS",cErro)
			lRetorno := .F.
		EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelAnnotat³Autor  ³ Eduardo Riera         ³ Data ³22.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de gravacao dos apontamentos do vendedor              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpO3: Apontamento                                           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve a lista de apontamentos de um periodo    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelAnnotations WSRECEIVE UserCode,SellerCode,RegisterDate,ContactSequence WSSEND Annotation WSSERVICE FtSellerAnnotations

Local aArea    := GetArea()
Local aCabec   := {}
Local aErro     := {}
Local cErro     := ""
Local lRetorno := .T.
Local nX       := 0

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"FtSellerAnnotations","DelAnnotations","SA3",::SellerCode)
	dbSelectArea("AD5")
	dbSetOrder(1)
	If MsSeek(xFilial("AD5")+::SellerCode+Dtos(::RegisterDate)+::ContactSequence)
		aadd(aCabec,{"AD5_VEND",::SellerCode,Nil})
		aadd(aCabec,{"AD5_DATA",::RegisterDate,Nil})
		aadd(aCabec,{"AD5_SEQUEN",::ContactSequence,Nil})
		
		Fata310(aCabec,{},5)
		
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTANNOTATIONS",cErro)
			lRetorno := .F.
		EndIf
						
	Else
		lRetorno := .F.
		SetSoapFault("DELANNOTATIONS",STR0008)	 //"Apontamento nao localizado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

Function wsfat310()
Return
