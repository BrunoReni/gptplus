#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWEBSRV.CH"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tipos de Questao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#DEFINE UNICA_ESCOLHA 		"1"		//Unica Escolha
#DEFINE MULTIPLA_ESCOLHA 	"2"		//Multipla Escolha
#DEFINE DISSERTATIVA 		"3"		//Dissertativa

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tipos de exibicao da alternativa³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#DEFINE SEQUENCIAL 			"1"		//Sequencial
#DEFINE COLUNAS 			"2"		//Colunas
#DEFINE LINHAS 				"3"		//Linhas

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³WSGPRA060 ºAutor  ³Luiz Felipe Couto    º Data ³  02/03/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Este fonte possui as funcoes utilizadas para inclusao,      º±±
±±º          ³ alteracao, exclusao e busca de SubGrupos.                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPR     						                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±ºAnalista  ³ Data/Bops/Ver ³Manutencao Efetuada                      	   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do WebServices de SubGrupo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE GPRSubGroup DESCRIPTION "" //NAMESPACE "http://.apw"
	WSDATA UserCode			AS String									//Codigo do Usuario (seguranca WS)
	WSDATA QueryAddWhere	AS String OPTIONAL							//Filtro
	WSDATA PageLen			AS Integer OPTIONAL							//Limite de registros por Pagina (paginacao)
	WSDATA PageFirst		AS Integer OPTIONAL							//Primeira Pagina (paginacao)
	WSDATA IndexKey 		AS String OPTIONAL							//Ordenacao
	WSDATA GroupCode		AS String									//Codigo do Grupo
	WSDATA SubGroupCode		AS String									//Codigo do SubGrupo
	WSDATA ListOfSubGroup 	AS Array Of SubGroupView					//Lista de SubGrupos
	WSDATA SubGroup			AS SubGroupView								//SubGrupo

	WSMETHOD BrwSubGroup DESCRIPTION ""
	WSMETHOD GetSubGroup DESCRIPTION ""
ENDWSSERVICE

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³BrwSubGrouºAutor  ³Luiz Felipe Couto    º Data ³  02/03/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna os SubGrupos cadastrados no sistema.                º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpC1 : Codigo do Usuario WS                                º±±
±±º          ³ ExpC2 : Filtro                                              º±±
±±º          ³ ExpN3 : Limite de registros por pagina (paginacao)          º±±
±±º          ³ ExpN4 : Primeira pagina (paginacao)                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPR     						                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±ºAnalista  ³ Data/Bops/Ver ³Manutencao Efetuada                      	   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD BrwSubGroup WSRECEIVE UserCode, QueryAddWhere, PageLen, PageFirst, IndexKey WSSEND ListOfSubGroup WSSERVICE GPRSubGroup

Local lRetorno 				:= .T.					//Retorno da Funcao
Local aArea					:= GetArea()			//Area atual
Local cAliasSKF 			:= "SKF"				//Alias da tabela SKF
Local cQuery 				:= ""					//String para armazenagem da Query
Local aStruSKF 				:= {}					//Estrutura da tabela SKF
Local nI					:= 0					//Variavel de apoio
Local nJ					:= 0					//Variavel de apoio

SKF->( DbSetOrder( 1 ) )

DEFAULT ::QueryAddWhere 	:= ""					//Filtro
DEFAULT ::PageLen 			:= 0					//Limite de registros por pagina (paginacao)
DEFAULT ::PageFirst 		:= 0					//Primeira pagina (paginacao)
DEFAULT ::IndexKey 			:= SKF->( IndexKey() )	//Ordenacao

If ::UserCode == "MSALPHA"
	aStruSKF 	:= SKF->( DbStruct() )
	cAliasSKF 	:= GetNextAlias()
	
	cQuery 	:= "SELECT * "
	cQuery 	+= "FROM " + RetSqlName( "SKF" ) + " SKF "
	cQuery 	+= "WHERE "
	cQuery 	+= "SKF.KF_FILIAL = '" + xFilial( "SKF" ) + "' AND "
	cQuery 	+= "SKF.D_E_L_E_T_ = ' ' "
	cQuery 	:= WsQueryAdd( cQuery, ::QueryAddWhere )
	cQuery 	+= "ORDER BY " + WsSqlOrder( ::IndexKey )
	
	cQuery 	:= ChangeQuery( cQuery )
	
	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasSKF, .T., .T. )
	
	For nI := 1 To Len( aStruSKF )
		If aStruSKF[nI][2] <> "C" .AND. aStruSKF[nI][2] <> "M"
			TcSetField( cAliasSKF, aStruSKF[nI][1], aStruSKF[nI][2], aStruSKF[nI][3], aStruSKF[nI][4] )
		EndIf
	Next nI
	
	nI := 0
	
	While !( cAliasSKF )->( EOF() )
		nI++
	
		If ::PageFirst == 0 .OR. nI >= ::PageFirst
			If nJ == 0
				::ListOfSubGroup := {}
			EndIf
			
			aAdd( ::ListOfSubGroup, WsClassNew( "SubGroupView" ) )

			nJ++

			GetSubGroup( @::ListOfSubGroup[nJ], cAliasSKF )
		EndIf
	
		If nJ >= ::PageLen .And. ::PageLen <> 0
			Exit
		EndIf
		
		( cAliasSKF )->( DbSkip() )
	EndDo
	
	( cAliasSKF )->( DbCloseArea() )
Else
	lRetorno := .F.
	SetSoapFault( "BRWSUBGROUP", "UserCode invalido." )
EndIf

RestArea( aArea )

Return lRetorno

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetSubGrouºAutor  ³Luiz Felipe Couto    º Data ³  02/03/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Retorna o SubGrupo do sistema.                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ ExpC1 : Codigo do Usuario WS                                º±±
±±º          ³ ExpC2 : Codigo do SubGrupo                                  º±±
±±º          ³ ExpC3 : Filtro                                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SIGAGPR     						                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±ºAnalista  ³ Data/Bops/Ver ³Manutencao Efetuada                      	   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
WSMETHOD GetSubGroup WSRECEIVE UserCode, GroupCode, SubGroupCode, QueryAddWhere WSSEND SubGroup WSSERVICE GPRSubGroup

Local lRetorno 				:= .T.			//Retorno da Funcao
Local aArea					:= GetArea()	//Area atual
Local cAliasSKE				:= "SKE"		//Alias da tabela SKE
Local cAliasSKF				:= "SKF"		//Alias da tabela SKF
Local cAliasSKG				:= "SKG"		//Alias da tabela SKG
Local cAliasSKH				:= "SKH"		//Alias da tabela SKH
Local cAliasSKI				:= "SKI"		//Alias da tabela SKI
Local nLenQst				:= 0			//Ultima posicao do array de Questao
Local nLenOption			:= 0			//Ultima posicao do array de Opcao
Local nLenAlt				:= 0			//Ultima posicao do array de Alternativa

DEFAULT ::QueryAddWhere 	:= ""			//Filtro

If ::UserCode == "MSALPHA"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tabelas:                     ³
	//³SKE - Grupo                  ³
	//³SKF - Grupo x SubGrupo       ³
	//³SKG - Questao                ³
	//³SKH - Alternativa            ³
	//³SKI - Linha e Coluna         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Busca dados do SubGrupo³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	( cAliasSKF )->( DbSetOrder( 1 ) )
	
	If ( cAliasSKF )->( MsSeek( xFilial( "SKF" ) + ::GroupCode + ::SubGroupCode ) )
		::SubGroup := WsClassNew( "SubGroupView" )
		
		GetSubGroup( @::SubGroup, cAliasSKF )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Busca dados da questao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		( cAliasSKG )->( DbSetOrder( 2 ) )
		
		If ( cAliasSKG )->( MsSeek( xFilial( "SKG" ) + ( cAliasSKF )->KF_CODGRP + ( cAliasSKF )->KF_CODSUBG ) )
			::SubGroup:ListOfQuestion := {}
			
			While !( cAliasSKG )->( EOF() ) .AND. ;
				( cAliasSKG )->KG_FILIAL == xFilial( "SKG" ) .AND. ;
				( cAliasSKG )->KG_CODGRP == ( cAliasSKF )->KF_CODGRP .AND. ;
				( cAliasSKG )->KG_CODSUBG == ( cAliasSKF )->KF_CODSUBG
				
				aAdd( ::SubGroup:ListOfQuestion, WsClassNew( "QuestionView" ) )
				nLenQst := Len( ::SubGroup:ListOfQuestion )
				
				GetQuestion( @::SubGroup:ListOfQuestion[nLenQst], cAliasSKG, cAliasSKE, cAliasSKF )
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Multipla Escolha em Linhas ou Colunas³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA .AND. ;
					( ( cAliasSKG )->KG_TIPEXIB == LINHAS .OR. ;
					( cAliasSKG )->KG_TIPEXIB == COLUNAS )
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Linhas ou Colunas³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					( cAliasSKI )->( DbSetOrder( 1 ) )
					
					If ( cAliasSKI )->( MsSeek( xFilial( "SKI" ) + ;
						( cAliasSKG )->KG_CODQST ) )
						
						::SubGroup:ListOfQuestion[nLenQst]:ListOfOption := {}
						
						While !( cAliasSKI )->( EOF() ) .AND. ;
							( cAliasSKI )->KI_FILIAL == xFilial( "SKI" ) .AND. ;
							( cAliasSKI )->KI_CODQST == ( cAliasSKG )->KG_CODQST
							
							aAdd( ::SubGroup:ListOfQuestion[nLenQst]:ListOfOption, WsClassNew( "OptionView" ) )
							nLenOption := Len( ::SubGroup:ListOfQuestion[nLenQst]:ListOfOption )
							
							GetOption( @::SubGroup:ListOfQuestion[nLenQst]:ListOfOption[nLenOption], cAliasSKI )
							
							( cAliasSKI )->( DbSkip() )
						EndDo
					EndIf
				EndIf
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Unica Escolha e Multipla Escolha³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( cAliasSKG )->KG_TIPOQST == UNICA_ESCOLHA .OR. ;
					( cAliasSKG )->KG_TIPOQST == MULTIPLA_ESCOLHA
	
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Busca Alternativas³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					( cAliasSKH )->( DbSetOrder( 1 ) )
					
					If ( cAliasSKH )->( MsSeek( xFilial( "SKH" ) + ( cAliasSKG )->KG_CODQST ) )
						::SubGroup:ListOfQuestion[nLenQst]:ListOfAlternative := {}
						
						While !( cAliasSKH )->( EOF() ) .AND. ;
							( cAliasSKH )->KH_FILIAL == xFilial( "SKH" ) .AND. ;
							( cAliasSKH )->KH_CODQST == ( cAliasSKG )->KG_CODQST
							
							aAdd( ::SubGroup:ListOfQuestion[nLenQst]:ListOfAlternative, WsClassNew( "AlternativeView" ) )
							nLenAlt := Len( ::SubGroup:ListOfQuestion[nLenQst]:ListOfAlternative )
							
							GetAlternative( @::SubGroup:ListOfQuestion[nLenQst]:ListOfAlternative[nLenAlt], cAliasSKH )
							
							( cAliasSKH )->( DbSkip() )
						EndDo
					EndIf
				EndIf
	
				( cAliasSKG )->( DbSkip() )
			EndDo
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault( "GETSUBGROUP", "SubGrupo inválido." )
	EndIf
Else
	lRetorno := .F.
	SetSoapFault( "GETSUBGROUP", "UserCode invalido." )
EndIf

RestArea( aArea )

Return lRetorno