#INCLUDE "PROTHEUS.CH"
#INCLUDE "ERROR.CH"
#INCLUDE "XMLXFUN.CH"
#INCLUDE "WSPLSTISSO.ch"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define de tipos de WS de retorno										 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#DEFINE RESPOSTATISSSTRING "respostaTISSString"
#DEFINE RESPOSTAMENSAGEMTISSZIP "tissTransmiteMensagemZIP_OperationResponse"
#DEFINE PROTOCOLORECEBIMENTO "protocoloRecebimento"
#DEFINE RECIBOCANCELAGUIA "reciboCancelaGuiaWS"

#DEFINE SITUACAOAUTORIZACAO "situacaoAutorizacao"
#DEFINE SITUACAOAUTWS "situacaoAutorizacaoWS"

#DEFINE SITUACAOPROTOCOLO "situacaoProtocolo"
#DEFINE DEMONSTRATIVORETORNO "DemonstrativoRetorno"

#DEFINE RETORNOELEGIBILIDADE "retornoElegibilidade"
#DEFINE RESPOSTAELEGIBILIDADE "respostaElegibilidade"
#DEFINE RESPOSTAELEWS "respostaElegibilidadeWS"

#DEFINE AUTORIZACAOPROCEDIMENTO "autorizacaoProcedimento"
#DEFINE AUTORIZACAOPROCWS "autorizacaoProcedimentoWS"
#DEFINE AUTORIZACAOSERVICO "autorizacaoServico"

#DEFINE PROTOCOLORECEBIMENTORECURSO "recebimentoRecursoGlosa"
#DEFINE SITUACAOPROTOCOLORECURSO "situacaoProtocoloRecurso"

#DEFINE SOLICITACAODEMONSTRATIVORETORNOWS "solicitacaodemonstrativoretorno"
#DEFINE DEMONSTRATIVORETORNOWS "demonstrativoRetornoWS"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Staticas																 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
STATIC cSemaforo   	:= "PLSXML973ON.SMF"
STATIC cFileTmp	   	:= "XMLTMPON"
STATIC cTRecFault  	:= "PROTOCOLO_RECEBIMENTO"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³tissTransmiteMensagem³Autor  ³ Alexander Santos ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Transacao Web Service Tiss Transmite Mensagem e a Zip				³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function tissTransmiteMensagem()
	LOCAL cRet	 := ""
	LOCAL cRes	 := Iif( At("TISSTRANSMITEMENSAGEMZIP",Upper(httpHeadIn->MAIN) )>0,RESPOSTAMENSAGEMTISSZIP,RESPOSTATISSSTRING)

	cTRecFault 	 := "PROTOCOLO_RECEBIMENTO"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verfica se a versao suporta esta transacao								 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Upper(httpHeadIn->MAIN)=="TISSTRANSMITEMENSAGEMZIP" .And. TISVERS == "2.01.02"
		cRet := HttpSoapFault(cRes,"9903",STR0018+TISVERS+STR0008,"Verificar parametro de controle de versao MV_TISSVER")//"Transação não suportada nesta versão [ " //"]"
	Else
		cRet := ProcOnLine(cRes)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return StrTran( StrTran( StrTran( cRet ,Chr(13),""),Chr(10),""),Chr(9),"")


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³tissVerificaElegibilidade³Autor³ Alexander Santos ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Transacao Web Service Verifica Elegibilidade						  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function tissVerificaElegibilidade()
	LOCAL cRet := ""
	LOCAL cTissVer	:=  ""

	LOCAL cEnv := GetEnvServer()
	LOCAL cEmp := AllTrim(GETPVPROFSTRING(cEnv,"JEMP","",GetADV97()))
	LOCAL cFil := AllTrim(GETPVPROFSTRING(cEnv,"JFIL","",GetADV97()))
	LOCAL lLogTissOn := .F.

	If !Empty(cEmp) .And. !Empty(cFil)
		RpcSetEnv( cEmp,cFil,,,"PLS",,)
	Else
		cRet := "A ENVIRONMENT ["+cEnv+" ] não tem declarada as variaveis cEmp e cFil"
		LogOnl(cRet)
	EndIf

	lLogTissOn := Iif(GetNewPar("MV_PTLOGTO","0")=="1",.T.,.F.)
	If lLogTissOn
		PlsPtuLog("------------------------------------------------------------------", "tissonanalise.log")
		PlsPtuLog(PLSRetTime() + " - Iniciando tissVerificaElegibilidade (WSPLSTISSO)", "tissonanalise.log")
	EndIf

	cTissVer	:=  PGETTISVER()

	cTRecFault 	:= "SITUACAO_ELEGIBILIDADE"

	If cTissVer < '3'
		cRetWS := RESPOSTAELEGIBILIDADE
	Else
		cRetWS := RESPOSTAELEWS
	Endif

	cRet := ProcOnLine(RETORNOELEGIBILIDADE,cRetWS)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lLogTissOn
		PlsPtuLog(PLSRetTime() + " - Finalizando tissVerificaElegibilidade (WSPLSTISSO)", "tissonanalise.log")
	EndIf

Return StrTran( StrTran( StrTran( cRet ,Chr(13),""),Chr(10),""),Chr(9),"")
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³tissCancelaGuia          ³Autor³ Alexander Santos ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Transacao Web Service CancelaGuia									  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function tissCancelaGuia()
    LOCAL cRet  := ""
    LOCAL cEnv := GetEnvServer()
    LOCAL cEmp := AllTrim(GETPVPROFSTRING(cEnv,"JEMP","",GetADV97()))
    LOCAL cFil := AllTrim(GETPVPROFSTRING(cEnv,"JFIL","",GetADV97()))

    If !Empty(cEmp) .And. !Empty(cFil)
        RpcSetEnv( cEmp,cFil,,,"PLS",,)
    Else
        cRet := "A ENVIRONMENT ["+cEnv+" ] não tem declarada as variaveis cEmp e cFil"
        LogOnl(cRet)
    EndIf

    cRet := PLSCanceGui()
    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³ Fim da Funcao                                                            ³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return cRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³tissSolicitacaoProcedimento     ³Autor³ Alexander Santos ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Transacao Web Service Status Autorizacao							         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function tissSolicitacaoProcedimento()
	LOCAL cRet 		:= ""
	LOCAL cRes 		:= ""
	LOCAL cResAux  := ""
	LOCAL cTissVer	:=""
	LOCAL cEnv := GetEnvServer()
	LOCAL cEmp := AllTrim(GETPVPROFSTRING(cEnv,"JEMP","",GetADV97()))
	LOCAL cFil := AllTrim(GETPVPROFSTRING(cEnv,"JFIL","",GetADV97()))
	LOCAL lLogTissOn := .F.
	Local lDemons := .F.
	Local lSolis  := .F.
	Local lStRec := .F.

	If !Empty(cEmp) .And. !Empty(cFil)
		RpcSetEnv( cEmp,cFil,,,"PLS",,)
	Else
		cRet := "A ENVIRONMENT ["+cEnv+" ] não tem declarada as variaveis cEmp e cFil"
		LogOnl(cRet)
	EndIf

	lLogTissOn := Iif(GetNewPar("MV_PTLOGTO","0")=="1",.T.,.F.)
	If lLogTissOn
		PlsPtuLog("-----------------------------------------------", "tissonanalise.log")
		PlsPtuLog("Iniciando (WSPLSTISSO)", "tissonanalise.log")
		PlsPtuLog(Substr(Dtos(dDataBase),7,2)+"/"+Substr(Dtos(dDataBase),5,2)+"/"+Substr(Dtos(dDataBase),1,4) + " - " + Time(),"tissonanalise.log")
		PlsPtuLog("-----------------------------------------------","tissonanalise.log")
		PlsPtuLog("Soap Recebido:","tissonanalise.log")
		PlsPtuLog(httpOtherContent(),"tissonanalise.log")
	EndIf
	cTissVer	:=  PGETTISVER()

	cTRecFault 	:= "RESPOSTA_SOLICITACAO"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verfica qual tipo de solicitacao										 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Do Case
		Case "TISSSOLICITACAOPROCEDIMENTO" $ Upper(httpHeadIn->MAIN)
			If cTissVer < '3'
				cRes 	:= AUTORIZACAOPROCEDIMENTO
			Else
				cRes 	:= AUTORIZACAOPROCWS
			Endif
			cResAux := AUTORIZACAOSERVICO
		Case "TISSSOLICITACAODEMONSTRATIVORETORNO" $ Upper(httpHeadIn->MAIN)
			cRes := DEMONSTRATIVORETORNOWS
			lDemons := .T.
		Case "TISSSOLICITACAOSTATUSAUTORIZACAO" $ Upper(httpHeadIn->MAIN)
			If cTissVer > '3'
				cRes    := SITUACAOAUTWS
				cResAux := SITUACAOAUTORIZACAO
			Else
				cRes := SITUACAOAUTWS
			Endif
		Case "TISSSOLICITACAOSTATUSPROTOCOLO" $ Upper(httpHeadIn->MAIN)
			cRes := SITUACAOPROTOCOLO
			lSolis := aScan(Httpget->aGets,{|x| x == "WSDL" }) == 0
		Case "TISSSOLICITACAOSTATUSRECURSOGLOSA" $ Upper(httpHeadIn->MAIN)
			cRes := SITUACAOPROTOCOLORECURSO
			lStRec := .T.
		OtherWise
			If cTissVer < '3'
				cRes 	:= AUTORIZACAOPROCEDIMENTO
			Else
				cRes 	:= AUTORIZACAOPROCWS
			Endif
	EndCase

	If lDemons
		cRet := PLDempgOnl()
	elseIf lSolis
		cRet := PLStaProto()
	elseIf lStRec
		cRet := PLStaReGlo()
	else
		cRet := ProcOnLine(cRes,cResAux)
	endIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lLogTissOn
		PlsPtuLog("-----------------------------------------------","tissonanalise.log")
		PlsPtuLog("Finalizando (WSPLSTISSO)","tissonanalise.log")
		PlsPtuLog(Substr(Dtos(dDataBase),7,2)+"/"+Substr(Dtos(dDataBase),5,2)+"/"+Substr(Dtos(dDataBase),1,4) + " - " + Time(),"tissonanalise.log")
		PlsPtuLog("-----------------------------------------------","tissonanalise.log")
		PlsPtuLog("Soap Respondido:","tissonanalise.log")
		PlsPtuLog(cRet,"tissonanalise.log")
	EndIf

Return cRet


//-------------------------------------------------------------------
/*/{Protheus.doc} tissComunicacaoBeneficiario
Transacao Web Service Comunicacao Beneficiario

@author  sakai
@version P12
@since   04/06/19
/*/
//-------------------------------------------------------------------
Function tissComunicacaoBeneficiario(lAuto,cXmlAuto)

	Local cRet  := ""
	Local cEnv  := GetEnvServer()
	Local cEmp  := AllTrim(GETPVPROFSTRING(cEnv,"JEMP","",GetADV97()))
	Local cFil  := AllTrim(GETPVPROFSTRING(cEnv,"JFIL","",GetADV97()))
	Local lLogTissOn := .F.
	Local oComunBen

	Default lAuto   := .F.
	Default cXmlAuto := ""

	if !lAuto
		if !Empty(cEmp) .And. !Empty(cFil)
			RpcSetEnv( cEmp,cFil,,,"PLS",,)
		else
			cRet := "A ENVIRONMENT ["+cEnv+" ] não tem declarada as variaveis cEmp e cFil"
			LogOnl(cRet)
		endIf
	endIf

	lLogTissOn := Iif(GetNewPar("MV_PTLOGTO","0")=="1",.T.,.F.)
	if lLogTissOn
		PlsPtuLog("------------------------------------------------------------------", "tissonanalise.log")
		PlsPtuLog(PLSRetTime() + " - Iniciando tissComunicacaoBeneficiario (WSPLSTISSO)", "tissonanalise.log")
	endIf

	oComunBen := PComunBen():New()
	cRet := oComunBen:procXML(lAuto,cXmlAuto)

	FreeObj(oComunBen)
	oComunBen := Nil

	if lLogTissOn
		PlsPtuLog(PLSRetTime() + " - Finalizando tissSolicitacaoProcedimento (WSPLSTISSO)", "tissonanalise.log")
	endIf

Return cRet


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ProcOnLine³Autor  ³ Alexander Santos            ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Processa solicitacao enviada										   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ProcOnLine(cTagResposta,cTagResAux,lAuto,cSoapAuto,cAutHeadIn)
	LOCAL lEncod		:= .F.
	LOCAL nI			:= 0
	LOCAL nPosFB 		:= 0
	LOCAL nPosSB 		:= 0
	LOCAL cMsg			:= ""
	LOCAL cXml			:= ""
	LOCAL cErro			:= ""
	LOCAL cAviso		:= ""
	LOCAL cNumeSeq		:= ""
	LOCAL cSegArq		:= ""
	LOCAL cFileHas		:= ""
	LOCAL cNodPai		:= ""
	LOCAL cSoap			:= ""
	LOCAL cSoapObj		:= ""
	LOCAL cSoapAux		:= ""
	LOCAL cURLGet 		:= ""
	LOCAL PVirtual		:= ""
	LOCAL cCntType		:= ""
	LOCAL aCntType      := {"TEXT/XML","TEXT/XML; CHARSET=UTF-8","SOAP+XML","APPLICATION/SOAP+XML; CHARSET=UTF-8","TEXT/XML;CHARSET=UTF-8","TEXT/XML;CHARSET=ISO-8859-1","APPLICATION/XML"}
	LOCAL oXmlEnv		:= NIL
	LOCAL oNodFilho		:= NIL
	LOCAL oXml			:= NIL
	LOCAL aRet			:= {}
	LOCAL aMatDir   	:= {}//{DIRSUBRAI,DIRCAIEN,DIRCAISA,DIRPROCE,DIRNAPRO,DIRLOG}
	LOCAL cEnv 			:= GetEnvServer()
	LOCAL cEmp 			:= AllTrim(GETPVPROFSTRING(cEnv,"JEMP","",GetADV97()))
	LOCAL cFil 			:= AllTrim(GETPVPROFSTRING(cEnv,"JFIL","",GetADV97()))
	LOCAL cRdaOri		:= ""//PlsIntPad()
	LOCAL cPrefNS		:= ""//StrTran(NAMESPACE,":","")
	LOCAL cHeadIn       := ""
	Local nPosZ         := ""
	LOCAL cTissVerOn    := ""
	LOCAL lLogTissOn    := .F.
	LOCAL cHttpHost     := ""
	LOCAL cHttpHeadIn   := ""
	local lGrvCxTiss	:= .F.
	local cNameArqVl	:= ""

	PRIVATE __cTissWs   := ""
	PRIVATE __cTissTNS	:= ""
	PRIVATE __lTraZip	:= .F.

	DEFAULT cTagResAux  := ""
	DEFAULT lAuto       := .F.
	DEFAULT cSoapAuto   := ""
	DEFAULT cAutHeadIn  := ""

	//So for automacao nao precisamos inicializar o ambiente
	If !lAuto
		If !Empty(cEmp) .And. !Empty(cFil)
			RpcSetEnv( cEmp,cFil,,,"PLS",,)
		Else
			cRet := "A ENVIRONMENT ["+cEnv+" ] não tem declarada as variaveis cEmp e cFil"
			LogOnl(cRet)
		EndIf
	EndIf

	cHeadIn := iif(lAuto,cAutHeadIn,httpHeadIn->MAIN)
	
	nPosZ      := At( "V3" , cHeadIn ) + 1
	lGrvCxTiss	:= GetNewPar("MV_PTIONGS",'0') == '1'

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define de pastas	  													 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PRIVATE DIRRAIZ 	:= PLSMUDSIS( GetNewPar("MV_TISSDIR","\TISS\") )
	PRIVATE TISVERS	    := ""
	PRIVATE __ENCOD     := ""
	PRIVATE VERARQWSDL  := ""
	PRIVATE DIRONLINE 	:= PLSMUDSIS( "ONLINE\" )
	PRIVATE DIRSUBRAI 	:= DIRRAIZ+DIRONLINE
	PRIVATE DIRTEMP	  	:= PLSMUDSIS( DIRRAIZ+"TEMP\" )
	PRIVATE DIRCAIEN  	:= PLSMUDSIS( DIRSUBRAI+"CAIXAENTRADA\" )
	PRIVATE DIRCAISA  	:= PLSMUDSIS( DIRSUBRAI+"CAIXASAIDA\" )
	PRIVATE DIRPROCE  	:= PLSMUDSIS( DIRSUBRAI+"PROCESSADOS\" )
	PRIVATE DIRNAPRO  	:= PLSMUDSIS( DIRSUBRAI+"NAOPROCESSADOS\" )
	PRIVATE DIRLOG 		:= PLSMUDSIS( DIRSUBRAI+"LOG\" )
	PRIVATE DIRSCHEMA 	:= PLSMUDSIS( DIRRAIZ+"SCHEMAS\" )
	PRIVATE NAMESPACE  	:= ""
	PRIVATE TISS_DEBUG	:=IiF(GetNewPar("MV_PLTIDEB","1")=="1",.T.,.F.)
	PRIVATE LOG_CONOUT 	:= IiF(GetNewPar("MV_PLTILOG","1")=="1",.T.,.F.)
	PRIVATE cComplex 	:= ''
	PRIVATE cRdaCom 	:= ''

	cSoap := iif(lAuto,cSoapAuto,HttpOtherContent())
	cTissVerOn	:= Substr(cSoap,At("Padrao>", cSoap) + Len("Padrao>"),7)
	if empty(cTissVerOn)
		BA0->(DbSetOrder(1))
		if BA0->(DbSeek(xFilial("BA0")+PlsIntPad()))
			cTissVerOn := If( nPosZ > 1, STRTRAN( substr(cHeadIn, nPosZ, 7), "_", "."), Alltrim(BA0->BA0_TISVER) )
		else
			cTissVerOn := If( nPosZ > 1, STRTRAN( substr(cHeadIn, nPosZ, 7), "_", "."), "30000")
		endIf
	endIf

	//Verifica se tem a versão do TISS no endpoint do WS e substitui, ex:
	//TISSVERIFICAELEGIBILIDADEV4_00_01 -> TISSVERIFICAELEGIBILIDADE
	__cTissWs  := Iif( ( nPos := At( "V"+StrTran( cTissVerOn, ".", "_" ) , cHeadIn ) ) > 0, Left(cHeadIn,nPos-1), cHeadIn)
	__cTissTNS := "http://www.ans.gov.br/tiss/ws/tipos/"+__cTissWs
	__lTraZip  := ( Upper(__cTissWs) == "TISSTRANSMITEMENSAGEMZIP" )
	
	TISVERS    := cTissVerOn
	__ENCOD    :=  Iif(TISVERS != "2.02.01",'UTF-8','ISO-8859-1')
	VERARQWSDL := "V"+StrTran( TISVERS ,".","_" )+".WSDL"
	NAMESPACE  :=  Iif(TISVERS < "2.02.02" .OR. TISVERS >= "3" ,"ans:","ansTISS:")

	lLogTissOn := Iif(GetNewPar("MV_PTLOGTO","0")=="1",.T.,.F.)
	If lLogTissOn
		PlsPtuLog(PLSRetTime() + " - Iniciando ProcOnLine (WSPLSTISSO)", "tissonanalise.log")
	EndIf

	cRdaOri		:= PlsIntPad() 
	cPrefNS		:= StrTran(NAMESPACE,":","")
	aMatDir   	:= {DIRSUBRAI,DIRCAIEN,DIRCAISA,DIRPROCE,DIRNAPRO,DIRLOG}

	//foi feita a troca da tag de resposta aqui pois na funcao da elegibilidade a variavel da versao nao existe ainda
	If (TISVERS >= "3" .AND. cTagResposta == RETORNOELEGIBILIDADE)
		cTagResposta := RESPOSTAELEWS
	EndIf

	If (TISVERS >= "3")
		cTissVerOn := TISVERS
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se for transacao com objeto												 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lObj := ( !Upper(__cTissWs) $ "TISSTRANSMITEMENSAGEMZIP" )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define o tipo de pacote													 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	HttpCtType( "text/xml; charset="+__ENCOD )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se as pastas base estao criadas e cria							 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistDir(DIRRAIZ)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se as pastas base estao criadas e cria							 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nI:=1 To  Len(aMatDir)
			If !ExistDir(aMatDir[nI])
				If MakeDir( aMatDir[nI] ) <> 0
					LogOnl(STR0003+aMatDir[nI]+STR0004)//"Impossú“el criar diretorio ( "###" )"
					Return ""
				EndIf
			EndIf
		Next
	Else
		LogOnl(STR0005+DIRRAIZ+STR0006)//"Diretorio ( "###" ) não encontrado"
		Return ""
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Caso o debug esteja ligado vai logar na console e em arquivo			 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TISS_DEBUG
		LogOnl( Replicate("*",79) )
		LogOnl(STR0007+__cTissWs+STR0008)//"Recebido do WebService [ "###" ]"
		LogOnl("")
		LogOnl(HttpInfo())
		LogOnl("")
		LogOnl(Replicate("*",79) )
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega o WSDL para pegar o endereco de TNS								 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oXml := XmlParserFile(DIRSCHEMA+__cTissWs+VERARQWSDL,"_",@cErro,@cAviso)

	If Empty(cErro) .And. Empty(cAviso)
		If XmlNodeExist(oXml,"_WSDL_DEFINITIONS") .And. XmlNodeExist(oXml:_WSDL_DEFINITIONS,"_XMLNS_TNS")
			__cTissTNS := oXml:_WSDL_DEFINITIONS:_XMLNS_TNS:TEXT
		ElseIf XmlNodeExist(oXml,"_DEFINITIONS") .And. XmlNodeExist(oXml:_DEFINITIONS,"_XMLNS_TNS")
			__cTissTNS := oXml:_DEFINITIONS:_XMLNS_TNS:TEXT
		EndIf
	Else
		cMsg := Iif( !Empty(cErro) ,STR0009+cErro,"")//" Erro: "
		cMsg += Iif( !Empty(cAviso),STR0010+cAviso,"")//" Aviso: "
		LogOnl(cMsg)

		Return HttpSoapFault(cTagResposta,"9902",STR0012+__cTissWs+VERARQWSDL,"",,,,lObj)//"Transação não suportada : "
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se e uma solicitacao do WSDL									 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (!lAuto .And. aScan(Httpget->aGets,{|x| x == "WSDL" }) > 0 .And. Len(Httpget->aGets)==1) .Or. (lAuto .And. Empty(cSoapAuto))

		cHttpHost   := iif(lAuto,"localhost:2000"                 ,httpHeadIn->Host)
		cHttpHeadIn := iif(lAuto,"/portal/"+cAutHeadIn+".apw?wsdl",httpHeadIn->aHeaders[1])
		__cTissWs   := iif(lAuto,cAutHeadIn                       ,httpHeadIn->MAIN)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
		//³ Procurando pasta virtual na URL
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
		PVirtual := "/"
		If lAuto .Or. Len(httpHeadIn->aHeaders) >= 1
			nPosFB 	:= At("/",cHttpHeadIn)
			If nPosFB>0
				cURLGet := SubStr(cHttpHeadIn,nPosFB+1)
				nPosSB 	:= At("/",cURLGet)+1
				If nPosSB>0

					PVirtual := SubStr(cHttpHeadIn,nPosFB,nPosSB)
					If At(".APW",Upper(PVirtual) ) > 0 .Or. At("?WSDL",Upper(PVirtual) ) > 0
						PVirtual := "/"
					EndIf

				EndIf
			EndIf
		EndIf
		FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', "Pasta virtual: "+PVirtual , 0, 0, {})

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se e wsdl da versao 2 ou 3										 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If XmlNodeExist(oXml,"_WSDL_DEFINITIONS") .And. XmlNodeExist(oXml:_WSDL_DEFINITIONS,"_WSDL_SERVICE") .And.;
				XmlNodeExist(oXml:_WSDL_DEFINITIONS:_WSDL_SERVICE,"_WSDL_PORT") .And. XmlNodeExist(oXml:_WSDL_DEFINITIONS:_WSDL_SERVICE:_WSDL_PORT,"_SOAP_ADDRESS") .And.;
				XmlNodeExist(oXml:_WSDL_DEFINITIONS:_WSDL_SERVICE:_WSDL_PORT:_SOAP_ADDRESS,"_LOCATION")

			oXml:_WSDL_DEFINITIONS:_WSDL_SERVICE:_WSDL_PORT:_SOAP_ADDRESS:_LOCATION:TEXT := "http://"+cHttpHost+PVirtual+__cTissWs + "V" + STRTRAN(TISVERS, ".", "_")+".apw"


		ElseIf  XmlNodeExist(oXml,"_DEFINITIONS") .And. XmlNodeExist(oXml:_DEFINITIONS,"_SERVICE") .And.;
				XmlNodeExist(oXml:_DEFINITIONS:_SERVICE,"_PORT") .And. XmlNodeExist(oXml:_DEFINITIONS:_SERVICE:_PORT,"_SOAP_ADDRESS") //.And.;
			//XmlNodeExist(oXml:_DEFINITIONS:_SERVICE:_PORT:_SOAP_ADDRESS,"_LOCATION")
			//na versao 3.02.00 o atribudo location nao esta sendo carregado no protheus, entao eu criei ele forcadamente
			If !XmlNodeExist(oXml:_DEFINITIONS:_SERVICE:_PORT:_SOAP_ADDRESS,"_LOCATION")
				XmlNewNode(oXml:_DEFINITIONS:_SERVICE:_PORT:_SOAP_ADDRESS, "_LOCATION", "_LOCATION", "ATT" )
				oXml:_DEFINITIONS:_SERVICE:_PORT:_SOAP_ADDRESS:_LOCATION:REALNAME 	:=	"location"
			Endif
			If "V3" $ __cTissWs
				oXml:_DEFINITIONS:_SERVICE:_PORT:_SOAP_ADDRESS:_LOCATION:TEXT 			:=	"http://"+cHttpHost+PVirtual+__cTissWs +".apw"
			else
				oXml:_DEFINITIONS:_SERVICE:_PORT:_SOAP_ADDRESS:_LOCATION:TEXT 			:=	"http://"+cHttpHost+PVirtual+__cTissWs + "V" + STRTRAN(TISVERS, ".", "_")+".apw"
			endIf
		EndIf

		SAVE oXml XMLSTRING cXML
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Caso o debug esteja ligado vai logar na console e em arquivo			 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If TISS_DEBUG
			LogOnl( Replicate("*",79) )
			LogOnl(STR0011)//"Retornando WSDL ao Cliente"
			LogOnl("")
			LogOnl(cXML)
			LogOnl("")
			LogOnl(Replicate("*",79) )
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Retorna o WSDL como solicitado											 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Return cXML
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se e um pacote valido											 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lAuto
		cCntType := Upper(HttpRCTType())
		If aScan(aCntType,{|x| x == cCntType}) <= 0
			cDescErro := STR0012 + " - "
			cDescErro += cCntType
			Return HttpSoapFault(cTagResposta,"9902",cDescErro,"",,,,lObj)//"Transação não suportada : "
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Apos as validaçoes acima entende-se que e um pacote soap valido e iniciar³
	//³ o desmembramento do pacote para retirada do xml							 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSoap := iif(lAuto,cSoapAuto,HttpOtherContent())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se e transacao obj												 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lObj .Or. __lTraZip
		cSoapObj := SubStr(cSoap,At("BODY",Upper(cSoap))+5)
		cSoapObj := SubStr(cSoapObj,1,Rat("BODY",Upper(cSoapObj))-5)
		cSoapObj := SubStr(cSoapObj,1,Rat(">",Upper(cSoapObj)))
		If !__lTraZip
			If TISVERS != "2.02.01"
				cSoapAux := EncodeUtf8(cSoapObj)
				If ValType(cSoapAux) != 'U'
					cSoapObj := cSoapAux
					lEncod	 := .T.
				EndIf
			EndIf
		EndIf
	Else
		cSoapObj := cSoap
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Correcao do nome da transacao ate a ANS acertar.						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSoapObj := StrTran(cSoapObj,"solicitacaoStatusAutorizacao","solicitaStatusAutorizacao")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Parse do XML de String													 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lObj
		oXmlEnv	:= XmlParser( cSoapObj ,"_",@cErro,@cAviso )

		If Empty(cErro) .And. Empty(cAviso)
			cNodPai	:= "oXmlEnv"
			While .T.
				If XmlChildCount( Eval( &( "{||"+cNodPai+"}" ) ) ) > 0
					oNodFilho := XmlGetchild( Eval( &( "{||"+cNodPai+"}" ) ) , XmlChildCount( Eval( &( "{||"+cNodPai+"}" ) ) ))
					If oNodFilho:Type <> "NOD"
						Exit
					EndIf

					If "MENSAGEMTISSZIP" $ Upper(oNodFilho:REALNAME)
						oNodFilho := XmlGetchild( Eval( &( "{||"+cNodPai+"}" ) ) , XmlChildCount( Eval( &( "{||"+cNodPai+"}" ) ) ))
						cNodPai += ":_"+StrTran(StrTran(oNodFilho:REALNAME,":","_"),"-","_")
						cNodPai += ":_"+StrTran(StrTran(oNodFilho:REALNAME,":","_"),"-","_")
						Exit
					EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Monta NO pai															 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cNodPai += ":_"+StrTran(StrTran(oNodFilho:REALNAME,":","_"),"-","_")
				Else
					Exit
				EndIf
			EndDo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se existe algum conteudo no pacote								 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Empty( Eval( &("{||"+cNodPai+":TEXT }") ) )
				Return HttpSoapFault(cTagResposta,"9904",STR0013,"",,,,lObj) //"Pacote SOAP-XML Vazio"
			Endif
		Else
			cMsg := Iif( !Empty(cErro) ,STR0009 +cErro,"") //" Erro: "
			cMsg += Iif( !Empty(cAviso),STR0010+cAviso,"") //" Aviso: "

			LogOnl(cMsg)
			Return HttpSoapFault(cTagResposta,"9905",cMsg,"Erro ao tentar fazer o parse do arquivo xml",,,,lObj)
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tudo ok Carrego o conteudo do XML										 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lObj
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Parse para verificacao se o xml esta ok								 	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oXml := XmlParser( cSoapObj ,"_",@cErro,@cAviso )

		If Empty(cErro) .And. Empty(cAviso)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Correcao do nome da transacao ate a ANS acertar.						 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			__cTissWs := StrTran(__cTissWs,Upper("solicitacaoStatusAutorizacao"),Upper("solicitaStatusAutorizacao"))
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ajusto o Xml														     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cXml :=  TranXml( AllTrim(cSoapObj),"E")
		Else
			cMsg := Iif( !Empty(cErro) ,STR0009 +cErro,"") //" Erro: "
			cMsg += Iif( !Empty(cAviso),STR0010+cAviso,"") //" Aviso: "
			FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', cMsg , 0, 0, {})
			Return HttpSoapFault(cTagResposta,"9906",cMsg,"Erro ao tentar fazer o parse do arquivo xml",,,,lObj)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ajuste de encoding														 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lEncod
			If TISVERS != "2.02.01"
				cSoapAux := EncodeUtf8(cXml)
				If ValType(cSoapAux) != 'U'
					cXml := cSoapAux
				EndIf
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Parse do xml ajustado													 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oXml := XmlParser( cXml ,"_",@cErro,@cAviso )
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pega conteudo do obj													 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cXml := Eval( &("{||"+cNodPai+":TEXT }") )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Descompacta ZIP															 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If __lTraZip
			cXml := DECODE64(cXml)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Caso o debug esteja ligado vai logar na console e em arquivo			 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If TISS_DEBUG
			LogOnl( Replicate("*",79) )
			LogOnl(STR0014) //"XML descompactado"
			LogOnl("")
			LogOnl(cXML)
			LogOnl("")
			LogOnl(Replicate("*",79) )
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta OBJ																 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oXml := XmlParser( HtmlTags( cXml ) ,"_",@cErro,@cAviso )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se ok no parse													 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(cErro) .And. Empty(cAviso)
		SAVE oXml XMLSTRING cXML
	Else
		cMsg := Iif( !Empty(cErro) ,STR0009+cErro,"") //" Erro: "
		cMsg += Iif( !Empty(cAviso),STR0010+cAviso,"") //" Aviso: "
		FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', cMsg , 0, 0, {})
		Return HttpSoapFault(cTagResposta,"9907",cMsg,"Erro ao tentar fazer o parse do arquivo xml",,,,lObj)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Pega o sequencial do arquivo e o hash do arquivo						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If 	Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_IDENTIFICACAOTRANSACAO, '_" + cPrefNS + "_SEQUENCIALTRANSACAO') } " ) ) .And.;
			Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_EPILOGO, '_" + cPrefNS + "_HASH') } " ) )

		If TISVERS >= "3" .And. Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM, '_"  + cPrefNS + "_REGISTROANS' ) } " ) )
			cComplex := "_REGISTROANS, '_"
			cRdaCom := "_REGISTROANS"

		ElseIf TISVERS >= "3" .And. Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM, '_"  + cPrefNS + "_IDENTIFICACAOPRESTADOR' ) } " ) )
			cComplex := "_IDENTIFICACAOPRESTADOR, '_"
			cRdaCom := "_IDENTIFICACAOPRESTADOR:_"
		Else
			cComplex := "_CODIGOPRESTADORNAOPERADORA, '_"
			cRdaCom := "_CODIGOPRESTADORNAOPERADORA:_"
		EndIf

		If XmlNodeExist(oXml:_ans_MENSAGEMTISS:_ans_CABECALHO,"_ans_ORIGEM") //Verifica se foi informado a TAG ORIGEM
			If Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM, '_"  + cPrefNS + "_REGISTROANS' ) } " ) )

				cRdaOri  := AllTrim( Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cRdaCom + "  } " ) ):Text )

			ElseIf Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cComplex + cPrefNS + "_CODIGOPRESTADORNAOPERADORA' ) } " ) )

				cRdaOri  := AllTrim( Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cRdaCom + cPrefNS + "_CODIGOPRESTADORNAOPERADORA  } " ) ):Text )

			ElseIf Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cComplex + cPrefNS + "_CPF') } " ) )

				cRdaOri  := AllTrim( Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cRdaCom + cPrefNS + "_CPF  } " ) ):Text )

			ElseIf Eval( &( "{|| XmlNodeExist(oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cComplex + cPrefNS + "_CNPJ' ) } " ) )

				cRdaOri  := AllTrim( Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_ORIGEM:_" + cPrefNS + cRdaCom + cPrefNS + "_CNPJ  } " ) ):Text )

			EndIf
		EndIf

		cNumeSeq 	:= Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_IDENTIFICACAOTRANSACAO:_" + cPrefNS + "_SEQUENCIALTRANSACAO } " ) ):Text
		cSegArq  	:= StrZero(Val(cNumeSeq),20)
		cFileHas 	:= Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_EPILOGO:_" + cPrefNS + "_HASH } " ) ):Text
		cTRecFault	:= TranDePara( Upper( Eval( &( "{|| oXml:_" + cPrefNS + "_MENSAGEMTISS:_" + cPrefNS + "_CABECALHO:_" + cPrefNS + "_IDENTIFICACAOTRANSACAO:_" + cPrefNS + "_TIPOTRANSACAO } " ) ):Text ) )
	Else
		Return HttpSoapFault(cTagResposta,"9909",STR0015,"",,,,lObj) //"Estrutura recebida invalida."
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava o arquivo para processamento									   	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	if lGrvCxTiss
		cXML := '<?xml version="1.0" encoding="ISO-8859-1"?>'+Chr(10)+cXML
		cNameArqVl := cXML
	else
		nH := FCreate( DIRCAIEN+cSegArq+"_"+cFileHas+".XML",2)
		FWrite(nH,'<?xml version="1.0" encoding="ISO-8859-1"?>'+Chr(10)+ cXML)
		FClose(nH)
		cNameArqVl := DIRCAIEN+cSegArq+"_"+cFileHas+".XML"
	endif

	If (TISVERS >= "3")
		//faz a validação do XML gerado pelo TranXml com o XSD
		If ( (!lGrvCxTiss .and. !XmlFVldSch(cNameArqVl, DIRSCHEMA+"TISSV"+StrTran(TISVERS, ".", "_")+".XSD", @cErro,@cAviso)) .or.;
			(lGrvCxTiss .and. !XmlSVldSch(cNameArqVl, DIRSCHEMA+"TISSV"+StrTran(TISVERS, ".", "_")+".XSD", @cErro,@cAviso))	)
		cMsg := Iif( !Empty(cErro) ,STR0009 +cErro,"") //" Erro: "
		cMsg += Iif( !Empty(cAviso),STR0010+cAviso,"") //" Aviso: "
		FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', cMsg , 0, 0, {})
		Return HttpSoapFault(cTagResposta,"5002",cMsg,"XML recebido não está no formato correto.",,,,lObj)
	EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chama rotina do processamento de XML no plsa973							 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	if !lGrvCxTiss
		aRet := PLSA973PRO(cNameArqVl,DIRONLINE,.T.,nil,nil,.T., cTissVerOn)
	else
		aRet := P973SoPrFi(cXML,cTissVerOn,cFileHas)
	endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se nao teve mensagem de erro monta pacote								 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(aRet[1])
		cXml := iif( !lGrvCxTiss, MemoRead(DIRCAISA+aRet[2]), aRet[6] )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Compacta Retorno														 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If __lTraZip
			cXml := ENCODE64(cXml)
		EndIf
	Else
		Return HttpSoapFault(cTagResposta,Iif(len(aRet)>=5.and.!Empty(aRet[5]),aRet[5],"9911"),aRet[1],"Retorno referente ao processamento do arquivo",cNumeSeq,cRdaOri,If(!Empty(aRet[1]),'descricaoGlosa',cTagResAux),lObj,aRet[1])
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Troca XML																 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lObj
		cXml := TranXml( AllTrim(cXml),"R",cTagResposta,cTagResAux )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o pacote de retorno												 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cXml := HttpEnvSoap(cXml,cTagResposta)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Rotina															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lLogTissOn
		PlsPtuLog(PLSRetTime() + " - Finalizando ProcOnLine (WSPLSTISSO)", "tissonanalise.log")
	EndIf

Return cXml
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³HtmlTags    ³Autor  ³ Alexander Santos          ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Troca caracters por simbolos para montar as tags				    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function HtmlTags(cXml)

	cXml := StrTran(cXml,"&lt;","<")
	cXml := StrTran(cXml,"&gt;",">")
	cXml := StrTran(cXml,"&amp;","&")
	cXml := StrTran(cXml,"&quot;",'"')
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da rotina															³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return cXml
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³IsVldMacro  ³Autor  ³ Alexander Santos          ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Para auxiliar no DEBUG											³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function IsVldMacro(cString)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Vazio																	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty( cString := alltrim(cString) )
		Return .F.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Primeiro caracter nao aCRLFabetico										 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ElseIf !upper(left(cString,1))$"ABCDEFGHIJKLMNOPQRSTUVWXYZ_"
		Return .F.
	Else
		While !Empty(cString)
			If !Upper(left(cString,1))$"0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ_"
				Return .f.
			EndIf
			cString := SubStr(cString,2)
		Enddo
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da rotina															³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return .T.
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³VarExInfo   ³Autor  ³ Alexander Santos          ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Para auxiliar no DEBUG											³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function VarExInfo(cId,cMain,lHtml,lEcho)
	LOCAL nI
	LOCAL aInfo := &(cId+"->"+cMain)
	LOCAL cRet  := VarInfo(cId, aInfo ,,lHtml,lEcho )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta retorno															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType(aInfo)="A"
		For nI := 1 to len(aInfo)
			If Empty(aInfo[nI])
				cRet += VarInfo(aInfo[nI]+" (EMPTY)" , NIL ,,lHtml,lEcho)
			ElseIf !IsVldMacro(aInfo[nI])
				cRet += VarInfo(aInfo[nI]+" (INVALID)" , NIL ,,lHtml,lEcho)
			Else
				cRet += VarInfo(aInfo[nI] , &(cId+"->"+aInfo[nI]) ,,lHtml,lEcho)
			EndIf
		Next
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Rotina															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return cRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³HttpEnvSoap ³Autor  ³ Alexander Santos          ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta o pacote soap												³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function HttpEnvSoap(cXml,cTagResposta)

	LOCAL cSoap 	:= '<?xml version="1.0" encoding="'+__ENCOD+'"?>'+Chr(10)
	LOCAL cSubName  := "ans:"
	Local cMV_PLTOTNS := GetNewPar("MV_PLTOTNS","")//TISS Online TNS
	__cTissTNS := "http://www.ans.gov.br/padroes/tiss/schemas" //Ajustando a TAG pois o anterior não está mais disponível
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o tipo de retorno												 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lObj
		cSoap += '<soap:Envelope xmlns:soap="http://schemas.xmlsoap.org/soap/envelope/" xmlns:'+StrTran(NAMESPACE,":","")+'="http://www.ans.gov.br/padroes/tiss/schemas" >'+Chr(10)
		cSoap += '<soap:Body>'+Chr(10)

		If Empty(cMV_PLTOTNS)
			cSoap += '<'+cSubName+cTagResposta+' xmlns:'+"ans"+'="'+__cTissTNS+'">'+Chr(10)
		Else
			cSoap += '<'+cSubName+cTagResposta+' xmlns:'+"ans"+'="'+cMV_PLTOTNS+'" xmlns:ns2="http://www.w3.org/2000/09/xmldsig#">'+Chr(10)
		EndIf
		cSoap += cXml+Chr(10)
		cSoap += '</'+cSubName+cTagResposta+'>'+Chr(10)

		cSoap += '</soap:Body>'+Chr(10)
		cSoap += '</soap:Envelope>'+Chr(10)

	ElseIf __lTraZip

		cSoap += '<soap:Envelope xmlns:soap="http://schemas.xmlsoap.org/soap/envelope/">'+Chr(10)
		cSoap += '<soap:Body>'+Chr(10)
		cSoap += '<'+cSubName+cTagResposta+' xmlns:'+StrTran(cSubName,':','')+'="'+__cTissTNS+'">'+Chr(10)

		cSoap += '<'+cSubName+'tissTransmiteMensagemZIP_OperationResult>'+Chr(10)
		cSoap += '<'+cSubName+'RespostaMensagemTissZIP>'+cXml+'</'+cSubName+'RespostaMensagemTissZIP>'+Chr(10)
		cSoap += '<'+cSubName+'RespostaTipoMensagem>'+cTRecFault+'</'+cSubName+'RespostaTipoMensagem>'+Chr(10)
		cSoap += '</'+cSubName+'tissTransmiteMensagemZIP_OperationResult>'+Chr(10)

		cSoap += '</'+cSubName+cTagResposta+'>'+Chr(10)
		cSoap += '</soap:Body>'+Chr(10)
		cSoap += '</soap:Envelope>'+Chr(10)
	Else
		cSoap += '<soap:Envelope xmlns:soap="http://schemas.xmlsoap.org/soap/envelope/">'+Chr(10)
		cSoap += '<soap:Body>'+Chr(10)

		cSoap += '<'+cTagResposta+' xmlns="'+__cTissTNS+'">'+Chr(10)
		cSoap += HtmlNoTags(cXml)+Chr(10)
		cSoap += '</'+cTagResposta+'>'+Chr(10)

		cSoap += '</soap:Body>'+Chr(10)
		cSoap += '</soap:Envelope>'+Chr(10)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Caso o debug esteja ligado vai logar na console e em arquivo			 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TISS_DEBUG
		LogOnl( Replicate("*",79) )
		LogOnl("Retornando o pacote SOAP-XML")
		LogOnl("")
		LogOnl(cSoap)
		LogOnl("")
		LogOnl(Replicate("*",79) )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return(cSoap)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³HttpSoapFault³Autor  ³ Alexander Santos         ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta pacote soap fault											³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function HttpSoapFault(cTagResposta,cCodErro,cDesErro,cObsErro,cNumeSeq,cRdaOri,cTagResAux,lObj,cDescErro)
	LOCAL cXml	  	 	:= "Nao gerou arquivo na caixa de saida"
	LOCAL cArqRet 	 	:= ""
	DEFAULT cNumeSeq 	:= "1"
	DEFAULT cRdaOri	 	:= PlsIntPad()
	DEFAULT lObj	 	:= ( !Upper(__cTissWs) $ "TISSTRANSMITEMENSAGEMZIP" )
	DEFAULT cTagResAux 	:= ""
	DEFAULT cDesErro		:= ""

	If At("Transação não", cDesErro) > 0
		cDesErro := STRTRAN(cDesErro, "ã", "a")
		cDesErro := STRTRAN(cDesErro, "ç", "c")
	EndIF
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta retorno															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nH := PLSAbreSem(cSemaforo)
	FErase(DIRTEMP+cFileTmp)
	cArqRet := PLSPROCRES(	{ {} , cTRecFault+"="+cTagResposta ,cNumeSeq,'','','','','', , ,{}, {}} , NIL, .T., AllTrim(cCodErro), PLSXMLTACE( AllTrim(cDesErro) ), PLSXMLTACE(AllTrim(cObsErro)), cFileTmp, cRdaOri,DIRONLINE,.T.)
	PLSFechaSem(nH,cSemaforo)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o arquivo foi criado										 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cArqRet)
		cXml := MemoRead(DIRCAISA+cArqRet)
		If lObj
			cXml := TranXml( AllTrim(cXml),"R",cTagResposta,cTagResAux,cDesErro )
		EndIf
		cXml := HttpEnvSoap(cXml,cTagResposta)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return cXml
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³HttpInfo    ³Autor  ³ Alexander Santos          ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Log Debug do que esta chegando									³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function HttpInfo()
	LOCAL cHtml := ""

	cHtml +=   VarInfo("aHeaders          ",httpHeadIn->aHeaders,,.f.,.f.)
	cHtml += VarExInfo("httpCookies       ","aCookies",.f.,.f.)
	cHtml += VarExInfo("httpPost          ","aPost",.f.,.f.)
	cHtml += VarExInfo("httpGet           ","aGets",.f.,.f.)
	cHtml +=   VarInfo("HttpRCtType()     ",HttpRCtType(),,.f.,.f.)
	cHtml +=   VarInfo("HttpRCtLen()      ",HttpRCtLen(),,.f.,.f.)
	cHtml +=   VarInfo("HttpRCtDisp()     ",HttpRCtDisp(),,.f.,.f.)
	cHtml +=   VarInfo("SoapRAction()     ",SoapRAction(),,.f.,.f.)
	cHtml +=   VarInfo("HttpOtherContent()",HttpOtherContent(),,.f.,.f.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return(cHtml)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³LogOnl    ³Autor  ³ Alexander Santos            ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Gera log		 													³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LogOnl(cErro,lDebug)

	PRIVATE DIRRAIZ 	:= PLSMUDSIS( GetNewPar("MV_TISSDIR","\TISS\") )
	PRIVATE DIRONLINE 	:= PLSMUDSIS( "ONLINE\" )
	PRIVATE DIRSUBRAI 	:= DIRRAIZ+DIRONLINE
	PRIVATE DIRLOG 	:= 	PLSMUDSIS( DIRSUBRAI+"LOG\" )

	PLSLOGFIL( cErro,DIRLOG+"ONLINELOG.TXT" )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se tiver ligado vai enviar para console									 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If LOG_CONOUT
		FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', cErro , 0, 0, {})
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³TranXml   ³Autor  ³ Alexander Santos            ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Ajusta Xml	 													³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function TranXml(cXml,cTipo,cTagCorpo,cTagResAux,cDesErro)
	LOCAL nI
	LOCAL cTag	  	 	:= ""
	LOCAL cConteudo  	:= ""
	LOCAL aMatXml 	 	:= {}
	LOCAL cTagWs	 	:= ""
	LOCAL lTagCorpo		:= .T.
	LOCAL lHash			:= .F.
	DEFAULT cTagCorpo   := ""
	DEFAULT cTagResAux 	:= ""
	DEFAULT cDesErro		:= ""
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ajuste tag																 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTagWs := StrTran( Upper(__cTissWs) , "TISS" ,"")

	If (TISVERS >= "3")
		//na versão 3 da tiss o nome do webservide é diferente do nome da tag no XML
		If (cTagWs == "VERIFICAELEGIBILIDADE")
			cTagWs := "PEDIDOELEGIBILIDADE"
		EndIf
		If (cTagWs == "SOLICITACAOSTATUSAUTORIZACAO")
			cTagWs := "SOLICITASTATUSAUTORIZACAO"
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a troca pelo Aux pq a tag principal nao e igual a tag corpo			 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cTagResAux)
		cTagCorpo := cTagResAux
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Debug																	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TISS_DEBUG
		LogOnl( Replicate("*",79) )
		LogOnl(STR0016)// "Xml Original"
		LogOnl("")
		LogOnl(cXml)
		LogOnl("")
		LogOnl( Replicate("*",79) )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta matriz com as Tag													 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While At(">",cXml) > 1
		cTag := SubStr(cXml,At("<",cXml),At(">",SubStr(cXml,At("<",cXml))))

		cTag := StrTran( cTag ,"<types:","<" )
		cTag := StrTran( cTag ,"<TYPES:","<" )
		cTag := StrTran( cTag ,"<Types:","<" )

		If At("xmlns:",cTag) > 0
			cTag := AllTrim( Left( cTag, At("xmlns:",cTag)-1) ) + '>'
		EndIf

		If At("xsi:type",cTag) > 0
			cTag := AllTrim( Left( cTag, At("xsi:type",cTag)-1) ) + '>'
		EndIf

		If At("<",cTag) > 0 .And. At("</",cTag) == 0
			If At(":",cTag)>0 .And. At("xmlns:",Lower(cTag))==0
				cTag := "<" + NAMESPACE + SubStr( cTag,At(":",cTag)+1,Len(cTag) )
			Else
				cTag := "<" + NAMESPACE + SubStr( cTag,At("<",cTag)+1,Len(cTag) )
			EndIf
		EndIf

		If At("</",cTag) > 0 .And. !lHash
			If At(":",cTag)>0
				cTag := "</" + NAMESPACE + SubStr( cTag,At(":",cTag)+1,Len(cTag) )
			Else
				cTag := "</" + NAMESPACE + SubStr( cTag,At("<",cTag)+2,Len(cTag) )
			EndIf
		EndIf

		AaDd(aMatXml,cTag)

		cXml := SubStr(cXml,At(">",cXml)+1,Len(cXml))

		cConteudo := AllTrim(SubStr(cXml,1,At("<",cXml)-1))
		If !Empty(cConteudo)
			If TISVERS != "2.02.01"
				AaDd(aMatXml,DecodeUtf8(cConteudo) )//cConteudo
			Else
				AaDd(aMatXml,cConteudo )//cConteudo
			EndIf
		EndIf

	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envio																	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cTipo == "E"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ajustando																 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Len(aMatXml) > 0
			aMatXml[1] 				:= '<' + NAMESPACE + 'mensagemTISS xmlns="http://www.w3.org/2001/XMLSchema" xmlns:'+StrTran(NAMESPACE,':','')+'="http://www.ans.gov.br/padroes/tiss/schemas">'
			aMatXml[Len(aMatXml)] 	:= '</' + NAMESPACE + 'mensagemTISS>'
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ajusta as tag's															 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cXml := ""
		For nI:= 1 To Len(aMatXml)

			If TISS_DEBUG
				LogOnl( Upper(aMatXml[nI]) )
			Endif

			If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + cTagWs+">" .AND. At("<" + NAMESPACE + "prestadorParaOperadora>",cXml) == 0
				aMatXml[nI] := "<" + NAMESPACE + "prestadorParaOperadora>"+aMatXml[nI]
				If (TISVERS >= "3")
					//a tag que vem pelo webservide eh pedidoElegibilidade, porem o que esta no tissV3_00_01.xsd eh verificaElegibilidade
					aMatXml[nI] := StrTran(aMatXml[nI], "pedidoElegibilidade", "verificaElegibilidade")
				EndIf
			EndIf

			If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + cTagWs+">"
				aMatXml[nI] := aMatXml[nI]+"</" + NAMESPACE + "prestadorParaOperadora>"
				If (TISVERS >= "3")
					//a tag que vem pelo webservide eh pedidoElegibilidade, porem o que esta no tissV3_00_01.xsd eh verificaElegibilidade
					aMatXml[nI] := StrTran(aMatXml[nI], "pedidoElegibilidade", "verificaElegibilidade")
				EndIf
			EndIf

			If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + "HASH>" .AND. At("<" + NAMESPACE + "epilogo>",cXml) == 0
				aMatXml[nI] := "<" + NAMESPACE + "epilogo>"+aMatXml[nI]
			EndIf

			If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "HASH>"
				aMatXml[nI] := aMatXml[nI]+"</" + NAMESPACE + "epilogo>"
			EndIf
			If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "EPILOGO>" .AND. (At("</" + NAMESPACE + "epilogo>",cXml) <> 0 .OR. At("</" + NAMESPACE + "EPILOGO>",cXml) <> 0)
				aMatXml[nI] := ""
			EndIf
			If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "PRESTADORPARAOPERADORA>" .AND. (At("</" + NAMESPACE + "prestadorParaOperadora>",cXml) <> 0 .OR. At("</" + NAMESPACE + "PRESTADORPARAOPERADORA>",cXml) <> 0)
				aMatXml[nI] := ""
			EndIf
			cXml += AllTrim(aMatXml[nI])
		Next
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Resposta																 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ElseIf cTipo == "R"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ajustando																 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Len(aMatXml) > 0
			aMatXml[1] 				:= ""
			aMatXml[Len(aMatXml)] 	:= ""
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ajusta as tag's															 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cXml := ""
		For nI:= 1 To Len(aMatXml)

			If Empty(aMatXml[nI])
				Loop
			EndIf

			If At( "<" + Upper(NAMESPACE) + "?XML",Upper(aMatXml[nI]) ) > 0
				aMatXml[nI] := ""
			EndIf

			If At( "<" + Upper(NAMESPACE) + "MENSAGEMTISS",Upper(aMatXml[nI]) ) > 0
				aMatXml[nI] := ""
			EndIf

			//o retrno da situacaoAutorizacaoWs nao pode ter o Ws no corpo da mensagem versao3
			If UPPER("situacaoAutorizacaoWS") $ UPPER(aMatXml[nI])
				aMatXml[nI] := StrTran(aMatXml[nI], "situacaoAutorizacaoWS", "situacaoAutorizacao")
			Endif

			If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + "OPERADORAPARAPRESTADOR>"
				aMatXml[nI] := ""
			EndIf

			If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "OPERADORAPARAPRESTADOR>"
				aMatXml[nI] := ""
			EndIf

			If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + "EPILOGO>"
				aMatXml[nI] := ""
			EndIf

			If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "EPILOGO>"
				aMatXml[nI] := ""
			EndIf


			//na versao 3 esta tag estava tirando o ans: da tag servicoAutorizado
			If TISVERS < '3'
				If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + "CABECALHO>"
					aMatXml[nI] := "<cabecalho>"
				EndIf

				If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "CABECALHO>"
					aMatXml[nI] := "</cabecalho>"
				EndIf

				If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + Upper(cTagCorpo) + ">" .And. lTagCorpo
					aMatXml[nI] := "<"+cTagCorpo+">"
					lTagCorpo := .F.
				EndIf

				If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + Upper(cTagCorpo) + ">" .And. Len(aMatXml) <> nI

					If aScan(aMatXml,{|x| Upper(x) == "</" + Upper(NAMESPACE) + Upper(cTagCorpo) + ">" },(nI+1) ) == 0
						aMatXml[nI] := "</"+cTagCorpo+">"
					EndIf

				EndIf
			Else
				iF !Empty(cDesErro) // Exibe a descrição da glosa no retorno

					nPos := aScan(aMatXml,{|x| ValType(x) == "C" .AND. Upper(x) == "<" + Upper(NAMESPACE) + Upper("descricaoglosa") + ">" },(nI) )
					If nPos > 0
						aMatXml[nPos+1] := cDesErro
					EndIf

				Endif

			Endif

			//na versao 3 esta tag estava tirando o ans: da tag servicoAutorizado
			If TISVERS < '3'
				If Upper(aMatXml[nI]) == "<" + Upper(NAMESPACE) + "HASH>" .And. Len(aMatXml) <> nI
					aMatXml[nI] := "<hash>"
				EndIf

				If Upper(aMatXml[nI]) == "</" + Upper(NAMESPACE) + "HASH>" .And. Len(aMatXml) <> nI
					aMatXml[nI] := "</hash>"
				EndIf
			Endif
			cXml += AllTrim(aMatXml[nI])
		Next

	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Debug																	 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TISS_DEBUG
		LogOnl( Replicate("*",79) )
		LogOnl(STR0017)//"Xml Ajustado"
		LogOnl("")
		LogOnl(cXml)
		LogOnl("")
		LogOnl(Replicate("*",79) )
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return cXml
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³TranDePara³Autor  ³ Alexander Santos            ³ Data ³ 13.06.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna o nome da transacao de retorno							³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
STATIC Function TranDePara(cTran)

	Do Case
		Case cTran $ "ENVIO_LOTE_GUIAS,RE_APRESENTACAO_GUIA,AUTORIZACAO_ODONTOLOGIA"
			cTran := "PROTOCOLO_RECEBIMENTO"
		Case cTran == "VERIFICA_ELEGIBILIDADE"
			cTran := "SITUACAO_ELEGIBILIDADE"
		Case cTran == "SOLIC_DEMONSTRATIVO_RETORNO"
			cTran := "DEMONSTRATIVO_PAGAMENTO,DEMONSTRATIVO_ANALISE_CONTA,DEMONSTRATIVO_ODONTOLOGIA"
		Case cTran == "SOLIC_STATUS_PROTOCOLO"
			cTran := "SITUACAO_PROTOCOLO"
		Case cTran == "SOLICITACAO_PROCEDIMENTOS"
			cTran := "RESPOSTA_SOLICITACAO"
		Case cTran == "CANCELA_GUIA"
			cTran := "CANCELAMENTO_GUIA_RECIBO"
		Case cTran == "SOLICITA_STATUS_AUTORIZACAO"
			cTran := "STATUS_AUTORIZACAO"
		Case cTran == "SOLIC_STATUS_RECURSO_GLOSA"
			cTran := "RESPOSTA_RECURSO_GLOSA"
		Case cTran == "RECURSO_GLOSA"
			cTran := "RECEBIMENTO_RECURSO_GLOSA"
	EndCase
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return cTran


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao	 ³solicitademonst      ³Autor  ³ Renan Martins    ³ Data ³ 01/2016  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Transacao Web Service Solicitaçãod de Demonstrativo    			³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function tissSolDemonstrativoRetorno()
	//TODO: Revisar essa função e como será utilizada no futuro pois o nome anterior "TISSSOLICITACAODEMONSTRATIVORETORNO" conflita com a função de solicitação de procedimentos
	LOCAL cRet	:= ""

	cTRecFault 	:= "DEMONSTRATIVO_PAGAMENTO,DEMONSTRATIVO_ANALISE_CONTA,DEMONSTRATIVO_ODONTOLOGIA"

	If !Empty(cRet)
		cRet := HttpSoapFault(DEMONSTRATIVORETORNOWS,"9902",STR0012+cRet,"",,,,.T.)//"Transação não suportada : "
	Else
		cRet := ProcOnLine(DEMONSTRATIVORETORNOWS)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim da Funcao															 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Return StrTran( StrTran( StrTran( cRet ,Chr(13),""),Chr(10),""),Chr(9),"")


//----------------------------------------------------------------
/*/{Protheus.doc} PLSRetTime
Retorna o tempo do servidor com milisegundos

@type function
@author Renan Sakai
@since 29.08.18
@version 1.0
/*/
//----------------------------------------------------------------
Function PLSRetTime()
	Local nHH, nMM , nSS, nMS := seconds()

	nHH := int(nMS/3600)
	nMS -= (nHH*3600)
	nMM := int(nMS/60)
	nMS -= (nMM*60)
	nSS := int(nMS)
	nMS := (nMs - nSS)*1000

Return strzero(nHH,2)+":"+strzero(nMM,2)+":"+strzero(nSS,2)+"."+strzero(nMS,3)
