#INCLUDE "GEOA010.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³GEOA010   ³ Autor ³ Henry Fila            ³ Data ³ 16.12.03 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Geoprocessa o SM0 da empresa corrente                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GEOA010()

Local aCoor     := {}
Local aArray    := {}
Local aArrayScr := {}
Local aMap      := {}
Local cLat      := ""
Local cLong     := ""

SM0->(dbSetOrder(1))
If SM0->(MsSeek(cEmpAnt+cFilAnt))

	If !GeoExtEnt("  ","SM0",SM0->M0_CODIGO+SM0->M0_CODFIL,aCoor)     
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se utiliza o endereco de entrega ou de cobranca          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( SM0->M0_ENDENT ) .And. Empty( SM0->M0_CEPENT ) 
			aArray := CodGeoPrc(SM0->M0_ENDCOB,SM0->M0_CIDCOB,SM0->M0_ESTCOB,SM0->M0_CEPCOB,.T.)
		Else                                                                                    
			aArray := CodGeoPrc(SM0->M0_ENDENT,SM0->M0_CIDENT,SM0->M0_ESTENT,SM0->M0_CEPENT,.T.)
		EndIf 	

		If Len(aArray) > 0
			aArrayScr := GeoPrcScr(aArray)

			If Len(aArrayScr) > 0		
				cLat  := aArrayScr[1][6]
				cLong := aArrayScr[1][7]

				GeoSavCoor("  ","SM0",cEmpAnt+cFilAnt,aArrayScr[1][6],aArrayScr[1][7],.T.)
	
				If MsgYesNo(STR0001) //"Deseja visualizar posicao ?"
					Aadd(aMap,{aArrayScr[1][6],aArrayScr[1][7],"circle.gif"})
					GeoMap(aMap)
				Endif	
			Endif
		Endif	
	Else 
		Aadd(aMap,{aCoor[1],aCoor[2],"circle.gif"})
		GeoMap(aMap)
	Endif	

Endif

Return
