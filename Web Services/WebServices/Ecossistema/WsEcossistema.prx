#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "FILEIO.CH"
#INCLUDE "XMLXFUN.CH"

#DEFINE SITEDOECOSSISTEMA "http://ecossistema.microsiga.com.br"
#DEFINE WEBRASKA_URLPOSTPROD "http://www.webraska.com.br/microsiga.php" 
#DEFINE WEBRASKA_URLPOSTTEST "http://www.webraska.com.br/microsiga_teste.php"

#DEFINE ACSP_URLPOSTPROD "http://www.acsp.com.br/cgi-bin/db2www/netpo009.mbr/MicroSiga"//PRODUÇAO
#DEFINE ACSP_URLPOSTTEST "http://200.211.20.9/cgi-bin/db2www/netpo009.mbr/MicroSiga"//TESTES

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Servico   ³EcoPartner³ Autor ³Eduardo Riera          ³ Data ³15.12.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de integracao do Ecossistema - Web Services           ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Marcelo      ³12/10/05³      ³Inclusao das Consultas CHEQUE+,ICA        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSSTRUCT CompanyView
	WSDATA Code                         As String //Codigo da Empresa
	WSDATA Name                         As String //Nome da Empresa 
	WSDATA Branch                       As String //Nome da Filial
	WSDATA NickName                     As String //Nome Fantasia
	WSDATA FederalId                    As String OPTIONAL //CPF
	WSDATA StateId                      As String OPTIONAL //IE
	WSDATA Addresses                    As Array Of AddressView OPTIONAL //Enderecos
	WSDATA Phones                       As Array Of PhoneView OPTIONAL
ENDWSSTRUCT

WSSTRUCT CompanyUserView
	WSDATA UserID                    As String OPTIONAL //Codigo do usuario
	WSDATA Name                      As String  //Nome do usuario
	WSDATA FederalId                 As String OPTIONAL//CPF
	WSDATA Addresses                 As Array Of AddressView OPTIONAL//Enderecos
	WSDATA Phones                    As Array Of PhoneView OPTIONAL
	WSDATA Email                     As String OPTIONAL//Email
	WSDATA PositionDescription       As String OPTIONAL //Position	
	WSDATA GroupDescription          As String OPTIONAL //Grupo
	WSDATA DepartmentDescription     AS String OPTIONAL //Departamento
ENDWSSTRUCT

WSSTRUCT EcoPartnersView
	WSDATA Group                 As String
	WSDATA PartnerID             As String
	WSDATA Name                  As String
	WSDATA Note                  As String
	WSDATA Logo                  As Base64Binary OPTIONAL
	WSDATA URLLocation           As String
	WSDATA URLSoapAction         As String OPTIONAL
	WSDATA HomePage              As String	
	WSDATA UserID                As String OPTIONAL
	WSDATA PassWord              As String OPTIONAL
ENDWSSTRUCT

WSSTRUCT HandShakeView
	WSDATA UserID                As String
	WSDATA PassWord              As String
	WSDATA URL                   As String OPTIONAL
ENDWSSTRUCT

WSSTRUCT PartnerRecordView
	WSDATA PartnerID			 As String
	WSDATA ServiceID			 As String
	WSDATA FederalID			 As String
	WSDATA HardLock				 As String
	WSDATA UserID                As String
	WSDATA PassWord              As String
	WSDATA URL		             As String
ENDWSSTRUCT

WSSERVICE PartnerEcoSystem        DESCRIPTION "Serviço de exemplo do Web Service que deverá ser desenvolvido pelos parceiros do Ecossistema"
	WSDATA FederalID              As String
	WSDATA HardLock               As String	
	WSDATA UserID                 As String
	WSDATA PassWord               As String
	WSDATA ServiceID              As String
	WSDATA PartnerID              As String
	WSDATA HandShakeLogon         As HandShakeView OPTIONAL
	WSDATA Company                As CompanyView
	WSDATA Contact				  As CompanyUserView
	WSDATA DynamicHomePage        As String
	WSDATA BrwPartnerRecord		  As Array of PartnerRecordView
	WSDATA PartnerRecord		  As PartnerRecordView
	WSDATA PartnerRecordId		  As String
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Metodos executados no Parceiro                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WSMETHOD HandShake         DESCRIPTION "Método de exemplo que recebe os dados cadastrais do cliente e usuário e retorna a positivação ou negativação do HandShake. <br><br><i>Observação: O retorno deste método é utilizado para habilitar o serviço do parceiro após a contratação do mesmo. Por este motivo, o retorno deste método não é obrigatório, sendo que o sistema irá verificar apenas se houve erro no HandShake para confirmar a positivação ou negação do acesso ao serviço.</i>"
	WSMETHOD GetPartnerWeb     DESCRIPTION "Método de exemplo que recebe os dados cadastrais do cliente e usuário retornando uma url dinâmica para apresentação. O Objetivo deste método é possibilitar a contratação automática dos serviços do parceiro do ecossitema."
	WSMETHOD GetPartnerRecord
	WSMETHOD PutPartnerRecord
	WSMETHOD DelPartnerRecord
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³HandShake ³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de logon no Ecosistema                                ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpC2: Codigo da inscricao federal                           ³±±
±±³          ³ExpC3: Id Usuario do Sistema                                 ³±±
±±³          ³ExpC4: Senha do Sistema                                      ³±±
±±³          ³ExpC4: Id do Servico do Ecossistema                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo deve ser feito por cada parceiro do Ecosisstema. ³±±
±±³          ³Ele foi construido apenas como um exemplo e nao eh executado ³±±
±±³          ³pelo Protheus                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/                                                                                 	
WSMETHOD HandShake WSRECEIVE HardLock,FederalID,UserID,PassWord,ServiceID WSSEND HandShakeLogon WSSERVICE PartnerEcoSystem

Local lRetorno := .T.
Local cString := ""
Local cErro := ""
Local cUrlPostACSP := ""

DbSelectArea( "PA1" )
DbSetOrder( 5 )


cString += "HS  "
cString += "01"
cString += PadR( ::ServiceID, 15 )
cString += StrZero( Val( ::FederalID ), 14 )
cString += PadR( ::HardLock, 20 )
cString += PadR( ::UserID, 15 )
cString += PadR( ::Password, 15 )
cString += PadR( "", 15 )    


Do Case     
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisas ACSP³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ::ServiceID == "ACSP" .OR. ::ServiceID == "CIN" .OR. ::ServiceID == "SCPC" .OR. ;
		::ServiceID == "CHEQUE" .OR. ::ServiceID == "CEC" .OR. ::ServiceID == "SINTPJ" .OR. ;
		::ServiceID == "SCPCE" .OR. ::ServiceID == "PACE" .OR. ::ServiceID == "ACE" .OR. ;
		"RENA" $ ::ServiceID .OR. ::ServiceID == "USEFONE" .OR. ::ServiceID == "CE" .OR. ;
		::ServiceID == "CHEQUE+" .OR. ::ServiceID == "ICA" .OR. ::ServiceID == "CN"
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Alterna entre as URLS de POST de producao ou teste conforme a senha e usuario ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If PadR( Upper( ::Password ), 15 ) == PadR( "MICROS", 15 ) .And. PadR( ::userID, 15 ) == PAdR( "00313796", 15 )  
			cUrlPostACSP := ACSP_URLPOSTTEST
		Else
			cUrlPostACSP := ACSP_URLPOSTPROD
		Endif                                

		lRetorno := fRetHSGPW( "HS", HttpPost( cUrlPostACSP, , "lk_param=" + cString ), @cErro, @::HandShakeLogon:UserId, @::HandShakeLogon:PassWord )
		
		If !lRetorno
			SetSoapFault( "HANDSHAKE", cErro )
		EndIf

	Case ::ServiceID == "WEBRASKA" .OR. ::ServiceID == "REVGEO" .OR. ::ServiceID == "GEO" .OR. ;
		::ServiceID == "MAP" .OR. ::ServiceID == "MULTI" .OR. ::ServiceID == "RANK" .OR. ;
		::ServiceID == "ROUTE"
		::HandShakeLogon:UserId:= "114485"//114485                                //Usuario da Microsiga
		::HandShakeLogon:PassWord:= "920136"//920136                              //Senha da Microsiga
		::HandShakeLogon:Url:= "http://api.webraska.com.br:8080/api/gns?"  //URL da Webraska		

/*		If MsSeek( xFilial( "PA1" ) + PadR( ::ServiceID, 15 ) + PadR( ::HardLock, 20 ) + PadR( ::FederalID, 14 ) )
			::HandShakeLogon:UserID         := AllTrim( PA1->PA1_USER )
			::HandShakeLogon:PassWord       := AllTrim( PA1->PA1_PWD )
			::HandShakeLogon:URL            := AllTrim( PA1->PA1_URL )
		Else
			::HandShakeLogon:UserId   := ""
			::HandShakeLogon:PassWord := ""
			::HandShakeLogon:URL      := ""
		EndIf*/                                                                                                                                             
		
		//lRetorno := fRetHSGPW( "HS", HttpPost( WEBRASKA_URLPOSTTEST, , "lk_param=" + cString ), @cErro, @::HandShakeLogon:UserId, @::HandShakeLogon:PassWord )
		
		lRetorno := fRetHSGPW( "HS", HttpPost( WEBRASKA_URLPOSTPROD, , "lk_param=" + cString ), @cErro, @::HandShakeLogon:UserId, @::HandShakeLogon:PassWord )

	Case ::ServiceID == "EQUIFAX" .OR. ::ServiceID == "EQUIFAXP" .OR. ::ServiceID == "EQUIFAXE" .OR. ;
		::ServiceID == "EQUIFAXS"

		If MsSeek( xFilial( "PA1" ) + PadR( ::ServiceID, 15 ) + PadR( ::HardLock, 20 ) + PadR( ::FederalID, 14 ) )
			::HandShakeLogon:UserID         := AllTrim( PA1->PA1_USER )
			::HandShakeLogon:PassWord       := AllTrim( PA1->PA1_PWD )
			::HandShakeLogon:URL            := AllTrim( PA1->PA1_URL )
		Else
			::HandShakeLogon:UserId:= "07670581884"//Usuario fornecido pra Teste Microsiga
			::HandShakeLogon:PassWord:= "2262"// Senha pra teste microsiga
			::HandShakeLogon:URL      := ""
		EndIf		
EndCase

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPartner³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Obtem a URL do parceiro para divulgacao do Ecossitema        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpO2: Estrutura da Empresa                                  ³±±
±±³          ³ExpO3: Estrutura do contato                                  ³±±
±±³          ³ExpC4: Codigo de usuario da Microsiga - Fixo                 ³±±
±±³          ³ExpC5: Senha da Microsiga                                    ³±±
±±³          ³ExpC6: Servico que foi solicitado                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo deve ser feito por cada parceiro do Ecossistema. ³±±
±±³          ³Ele foi contruido apenas como um exemplo e nao eh executado  ³±±
±±³          ³pelo Protheus                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetPartnerWeb WSRECEIVE HardLock,Company,Contact,UserID,PassWord,ServiceID WSSEND DynamicHomePage WSSERVICE PartnerEcoSystem
Local cString      := ""
Local cErro        := ""
Local cCodErro     := ""
Local cVersionGPW  := "01"    

Local lRetorno     := .T.
Local lWebraska    := ( ::ServiceID == "WEBRASKA" .OR. ::ServiceID == "REVGEO" .OR. ::ServiceID == "GEO" .OR. ;
		::ServiceID == "MAP" .OR. ::ServiceID == "MULTI" .OR. ::ServiceID == "RANK" .OR. ;
		::ServiceID == "ROUTE" )   
		
Local nI           := 0		
		
If lWebraska 		
	cVersionGPW := "02"
EndIf 	

cString += "GPW "
cString += cVersionGPW  // Versao do LayOut
cString += PadR( ::ServiceID, 15 )
cString += PadR( ::UserID, 6 )
cString += PadR( ::Company:Code, 4 )
cString += PadR( ::Company:Branch, 15 )
cString += PadR( ::Company:Name, 40 )
cString += PadR( ::Company:NickName, 15 )
cString += PadR( ::Company:FederalID, 14 )
cString += PadR( ::Company:StateID, 14 ) 	

For nI := 1 To Len( ::Company:Addresses )
	If !Empty( ::Company:Addresses[nI]:Address ) .AND. ;
		!Empty( ::Company:Addresses[nI]:AddressNumber ) .AND. ;
		!Empty( ::Company:Addresses[nI]:Zone ) .AND. ;
		!Empty( ::Company:Addresses[nI]:District ) .AND. ;
		!Empty( ::Company:Addresses[nI]:State ) .AND. ;
		!Empty( ::Company:Addresses[nI]:ZipCode )

		cString += "E"
		cString += StrZero( Val( ::Company:Addresses[nI]:TypeOfAddress ), 2 )
		cString += PadR( ::Company:Addresses[nI]:Address, 30 )
		cString += PadR( ::Company:Addresses[nI]:AddressNumber, 10 )
		cString += PadR( ::Company:Addresses[nI]:Zone, 20 )
		cString += PadR( ::Company:Addresses[nI]:District, 20 )
		cString += PadR( ::Company:Addresses[nI]:State, 2 )
		cString += PadR( ::Company:Addresses[nI]:ZipCode, 8 )
	Else
		cString += PadR( "", 93 )
	EndIf
Next nI

For nI := 1 To Len( ::Company:Phones )
	If !Empty( ::Company:Phones[nI]:TypeOfPhone ) .AND. ;
		!Empty( ::Company:Phones[nI]:LocalAreaCode ) .AND. ;
		!Empty( ::Company:Phones[nI]:PhoneNumber )

		cString += "T"
		cString += StrZero( Val( ::Company:Phones[nI]:TypeOfPhone ), 2 )
		cString += PadR( StrZero( Val( ::Company:Phones[nI]:CountryAreaCode ), 3 ), 15 )
		cString += PadR( StrZero( Val( ::Company:Phones[nI]:LocalAreaCode ), 3 ), 15 )
		cString += PadR( StrZero( Val( ::Company:Phones[nI]:PhoneNumber ), 8 ), 15 )
		cString += PadR( "", 45 )
	Else
		cString += PadR( "", 93 )
	EndIf
Next nI

cString += PadR( ::Contact:UserID, 6 )
cString += PadR( ::Contact:Name, 40 )
cString += PadR( "", 14 )
cString += PadR( ::Contact:Email, 50 )
cString += PadR( ::Contact:PositionDescription, 4 )
cString += PadR( ::Contact:DepartmentDescription, 15 )
cString += PadR( "", 40 )
	
For nI := 1 To Len( ::Contact:Addresses )
	If !Empty( ::Contact:Addresses[nI]:Address ) .AND. ;
		!Empty( ::Contact:Addresses[nI]:AddressNumber ) .AND. ;
		!Empty( ::Contact:Addresses[nI]:Zone ) .AND. ;
		!Empty( ::Contact:Addresses[nI]:District ) .AND. ;
		!Empty( ::Contact:Addresses[nI]:State ) .AND. ;
		!Empty( ::Contact:Addresses[nI]:ZipCode )

		cString += "E"
		cString += StrZero( Val( ::Contact:Addresses[nI]:TypeOfAddress ), 2 )
		cString += PadR( ::Contact:Addresses[nI]:Address, 30 )
		cString += PadR( ::Contact:Addresses[nI]:AddressNumber, 10 )
		cString += PadR( ::Contact:Addresses[nI]:Zone, 20 )
		cString += PadR( ::Contact:Addresses[nI]:District, 20 )
		cString += PadR( ::Contact:Addresses[nI]:State, 2 )
		cString += PadR( ::Contact:Addresses[nI]:ZipCode, 8 )
	Else
		cString += PadR( "", 93 )
	EndIf
Next nI   

For nI := 1 To Len( ::Contact:Phones )     

	If !Empty( ::Contact:Phones[nI]:TypeOfPhone ) .AND. ;
		!Empty( ::Contact:Phones[nI]:LocalAreaCode ) .AND. ;
		!Empty( ::Contact:Phones[nI]:PhoneNumber )

		cString += "T"
		cString += StrZero( Val( ::Contact:Phones[nI]:TypeOfPhone ), 2 )
		cString += PadR( StrZero( Val( ::Contact:Phones[nI]:CountryAreaCode ), 3 ), 15 )
		cString += PadR( StrZero( Val( ::Contact:Phones[nI]:LocalAreaCode ), 3 ), 15 )
		cString += PadR( StrZero( Val( ::Contact:Phones[nI]:PhoneNumber ), 8 ), 15 )
		cString += PadR( "", 45 )
	Else
		cString += PadR( "", 93 )
	EndIf
Next nI   

cString += Space( 58 ) // Reservado 

If cVersionGPW == "02"
	cString += PadR( ::Hardlock, 20 )
EndIf

Do Case
	Case ::ServiceID == "ACSP" .OR. ::ServiceID == "CIN" .OR. ::ServiceID == "SCPC" .OR. ;
		::ServiceID == "CEC" .OR. ::ServiceID == "SINTPJ" .OR. ::ServiceID == "SCPCE" .OR. ;
		::ServiceID == "PACE" .OR. ::ServiceID == "ACE" .OR. ::ServiceID == "USEAUTO" .OR. ;
		::ServiceID == "USEFONE" .OR. ::ServiceID == "CHEQUE" .OR. ::ServiceID == "CE"  .OR. ;
		::ServiceID == "CHEQUE+" .OR. ::ServiceID == "ICA" .OR. ::ServiceID == "CN"

		lRetorno := fRetHSGPW( "GPW", HttpPost( ACSP_URLPOSTPROD, , "lk_param=" +cString ), @cErro, , , @::DynamicHomePage, @cCodErro )	

		If !lRetorno      

			SetSoapFault( "GETPARTNERWEB", cCodErro + "-" + cErro )
				
			If cCodErro == "99"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retorno 99 significa que existe alguma inconsistencia nos dados enviados      ³
				//³ pelo sistema Microsiga.                                                       ³
				//³ Ex: CNPJ invalido, dados obrigatorios nao enviados ( endereco, etc... )       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				AutoGRLog("GETPARTNERWEB -- Erro"+ " " +cCodErro+ "------" +cErro + "-" +Dtoc (dDataBase) +"---" +time())
			EndIf
			
		EndIf
	Case lWebraska 
		
		lRetorno := fRetHSGPW( "GPW", HttpPost( WEBRASKA_URLPOSTPROD, , "lk_param=" +cString ), @cErro, , , @::DynamicHomePage, @cCodErro )

		//lRetorno := fRetHSGPW( "GPW", HttpPost( WEBRASKA_URLPOSTTEST, , "lk_param=" +cString ), @cErro, , , @::DynamicHomePage, @cCodErro )	
		
EndCase

Return(lRetorno)        


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Servico   ³MsEcoSyste³ Autor ³Eduardo Riera          ³ Data ³15.12.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de integracao do Ecossistema com a Microsiga          ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSSERVICE MsEcoSystem             DESCRIPTION "Serviço de controle dos participantes do Ecossistema"
	WSDATA FederalID              As String
	WSDATA HardLock               As String	
	WSDATA ServiceID              As String	
	WSDATA Company                As CompanyView
	WSDATA ListOfCompany          As Array Of CompanyView
	WSDATA ListOfPartners         As Array Of EcoPartnersView
	WSDATA Contact				  As CompanyUserView
	WSDATA DynamicHomePage        As String
	WSDATA PartnerID              As String
	WSDATA Ok                     As String	
	WSDATA HandShake              As Boolean

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Metodos executados na Microsiga                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	WSMETHOD GetEcoWeb            DESCRIPTION "Método de retorno da URL do ecossitema"
	WSMETHOD GetPartners          DESCRIPTION "Método de consulta aos parceiros do Ecossistema. <br><br><i>Este método determina a UrlLocation dos Web Services do parceiro do Ecossitema.</i>"
	WSMETHOD PutPostage           DESCRIPTION "Método responsável pela bilhetagem do Ecossistema. <br><br><i>Entende-se por bilhatagem o volume de solicitaçãoes de serviço de cada cliente do Protheus.</i>"
	WSMETHOD PutMemberEcoSystem   DESCRIPTION "Método responsável pelo cadastramento dos clientes do Protheus no Ecossistema."
	WSMETHOD DelMemberEcoSystem   DESCRIPTION "Método responsável pelo descadastramento dos clientes do Protheus no Ecossistema."	
	WSMETHOD GetMemberEcoSystem   DESCRIPTION "Método de consulta ao cadastramento dos clientes do Protheus no Ecossistema."
	WSMETHOD HandShake            DESCRIPTION "Método verifica se o cliente esta habilitado a utilizar o ecossistema"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetEcoWeb ³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Obtem a URL da Microsiga de divulgacao do Ecossistema        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo deve ser feito por cada parceiro do Ecossistema. ³±±
±±³          ³Ele foi contruido apenas como um exemplo e nao eh executado  ³±±
±±³          ³pelo Protheus                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetEcoWeb WSRECEIVE HardLock,Company,Contact WSSEND DynamicHomePage WSSERVICE MsEcoSystem

Local lRetorno := .T.
Local aSite := {}
Local nI := 0, nJ := 0, nK := 0, nL := 0

If !File( "aSite" + ::HardLock + ::Company:FEDERALID )
	nHandle := FCreate( "aSite" + ::HardLock + ::Company:FEDERALID )
	
	FClose( nHandle )

	aSite := {}
EndIf

//OWSCOMPANY
aadd( aSite, {} )
aadd( aSite[Len( aSite )], ::Company:BRANCH )
aadd( aSite[Len( aSite )], ::Company:CODE )
aadd( aSite[Len( aSite )], ::Company:FEDERALID )
aadd( aSite[Len( aSite )], ::Company:NAME )
aadd( aSite[Len( aSite )], ::Company:NICKNAME )
aadd( aSite[Len( aSite )], ::Company:STATEID )

//OWSCOMPANY:OWSADDRESS
aadd( aSite[Len( aSite )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )

For nI := 1 To Len( ::Company:ADDRESSES )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:ADDRESS )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:ADDRESSNUMBER )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:DISTRICT )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:STATE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:TYPEOFADDRESS )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:ZIPCODE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:ADDRESSES[nI]:ZONE )
Next

//OWSCOMPANY:OWSPHONES
aadd( aSite[Len( aSite )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )
			
For nI := 1 To Len( ::Company:PHONES )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:PHONES[nI]:COUNTRYAREACODE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:PHONES[nI]:LOCALAREACODE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:PHONES[nI]:PHONENUMBER )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Company:PHONES[nI]:TYPEOFPHONE )
Next

//OWSCONTACT
aadd( aSite, {} )
aadd( aSite[Len( aSite )], ::Contact:USERID )
aadd( aSite[Len( aSite )], ::Contact:DEPARTMENTDESCRIPTION )
aadd( aSite[Len( aSite )], ::Contact:EMAIL )
aadd( aSite[Len( aSite )], ::Contact:FEDERALID )
aadd( aSite[Len( aSite )], ::Contact:GROUPDESCRIPTION )
aadd( aSite[Len( aSite )], ::Contact:NAME )
aadd( aSite[Len( aSite )], ::Contact:POSITIONDESCRIPTION )

//OWSCONTACT:OWSADDRESS
aadd( aSite[Len( aSite )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )

For nI := 1 To Len( ::Contact:ADDRESSES )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:ADDRESS )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:ADDRESSNUMBER )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:DISTRICT )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:STATE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:TYPEOFADDRESS )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:ZIPCODE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:ADDRESSES[nI]:ZONE )
Next

//OWSCONTACT:OWSPHONES
aadd( aSite[Len( aSite )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )
aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )], {} )
			
For nI := 1 To Len( ::Contact:PHONES )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:PHONES[nI]:COUNTRYAREACODE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:PHONES[nI]:LOCALAREACODE )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:PHONES[nI]:PHONENUMBER )
	aadd( aSite[Len( aSite )][Len( aSite[Len( aSite )] )][nI], ::Contact:PHONES[nI]:TYPEOFPHONE )
Next

If File( "aSite" + ::HardLock + ::Company:FEDERALID )
	nHandle := FOpen( "aSite" + ::HardLock + ::Company:FEDERALID, FO_WRITE )
	
	If nHandle <> -1
		For nI := 1 To Len( aSite )
			For nJ := 1 To Len( aSite[nI] )
				Do Case
					Case nJ == 7 .OR. nJ == 8 .OR. nJ == 9
						If ValType( aSite[nI][nJ] ) == "A"
							For nK := 1 To Len( aSite[nI][nJ] )
								For nL := 1 To Len( aSite[nI][nJ][nK] )
									If nL <> Len( aSite[nI][nJ][nK] )
										FWrite( nHandle, aSite[nI][nJ][nK][nL] + ";" )
									Else
										FWrite( nHandle, aSite[nI][nJ][nK][nL] + CRLF )
									EndIf
								Next nL
							Next nK
						Else
							FWrite( nHandle, aSite[nI][nJ] + CRLF )
						EndIf
					Otherwise
						FWrite( nHandle, aSite[nI][nJ] + CRLF )
				EndCase
			Next nJ
		Next nI
	EndIf
	
	FClose( nHandle )
EndIf

::DynamicHomePage := SITEDOECOSSISTEMA + "/w_siteecossistema.apw?cHardLock=" + ::HardLock + "&cFederalID=" + ::Company:FEDERALID

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutPostage³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de controle de bilhetagem do ecossitema          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpO2: Objeto dos dados da Empresa                           ³±±
±±³          ³ExpC3: Id do Parceiro do Ecossistema                         ³±±
±±³          ³ExpC4: Id do Servico do Ecossistema                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo eh utilizado para realizar a bilhetagem do Ecos- ³±±
±±³          ³sistema.                                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutPostage WSRECEIVE HardLock,Company,PartnerId,ServiceID WSSEND Ok WSSERVICE MsEcoSystem

Local lRetorno := .T.
Local cAlias := "PA2"

DbSelectArea( cAlias )
DbSetOrder( 1 )

If MsSeek( xFilial( cAlias ) + PadR( ::PartnerID, 15 ) + PadR( ::ServiceID, 15 ) + PadR( ::HardLock, 20 ) + PadR( ::FederalID, 14 ) )
	RecLock( cAlias, .F. )
Else
	RecLock( cAlias, .T. )
EndIf

( cAlias )->( FieldPut( FieldPos( cAlias + "_PARC" ), ::PartnerID ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_SERVIC" ), ::ServiceID ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_HARDL" ), ::HardLock ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_CNPJ" ), ::Company:FederalID ) )

MsUnLock()

Ok := ""

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutEcoSyst³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de cadastramento dos clientes da Microsiga no  - ³±±
±±³          ³ecossitema                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpO2: Objeto dos dados da Empresa                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo eh utilizado para cadastramento dos clientes da  ³±±
±±³          ³Microsiga no Ecossistema.                                    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutMemberEcoSystem WSRECEIVE HardLock,Company WSSEND Ok WSSERVICE MsEcoSystem

Local aArea    := GetArea()
Local cAlias   := "PA0"  //Ver com Marcos Lima
Local lRetorno := AllTrim(::HardLock)<>"-1"


If lRetorno
	dbSelectArea(cAlias)
	dbSetOrder(1)
	If MsSeek(xFilial(cAlias)+PadR(::HardLock,20)+PadR(::Company:FederalId,14))
		RecLock(cAlias,.F.)
	Else
		RecLock(cAlias,.T.)	
	EndIf
	(cAlias)->(FieldPut(FieldPos(cAlias+"_CODE"),::Company:Code))
	(cAlias)->(FieldPut(FieldPos(cAlias+"_NAME"),::Company:Name))
	(cAlias)->(FieldPut(FieldPos(cAlias+"_BRANCH"),::Company:Branch))
	(cAlias)->(FieldPut(FieldPos(cAlias+"_NICK"),::Company:NickName))
	(cAlias)->(FieldPut(FieldPos(cAlias+"_CNPJ"),::Company:FederalId))
	(cAlias)->(FieldPut(FieldPos(cAlias+"_IE"),::Company:StateId))
	(cAlias)->(FieldPut(FieldPos(cAlias+"_HARDL"),::HardLock))
	MsUnLock()
	::Ok := "OK"
Else
	SetSoapFault("PUTMEMBERECOSYSTEM","Numero de Hardlock invalido!")
EndIf


RestArea(aArea)
Return(lRetorno)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelEcoSyst³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de descadastramento dos clientes da Microsiga no ³±±
±±³          ³ecossitema                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpO2: Objeto dos dados da Empresa                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo eh utilizado para cadastramento dos clientes da  ³±±
±±³          ³Microsiga no Ecossistema.                                    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelMemberEcoSystem WSRECEIVE HardLock,Company WSSEND Ok WSSERVICE MsEcoSystem

Local aArea  := GetArea()
Local cAlias := "PA0"  //Ver com Marcos Lima

dbSelectArea(cAlias)
dbSetOrder(1)
If MsSeek(xFilial(cAlias)+PadR(::HardLock,20)+PadR(::Company:FederalId,14))
	RecLock(cAlias,.F.)
    dbDelete()
	MsUnLock()
EndIf
::Ok := "OK"
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetMemberE³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de cadastramento dos clientes da Microsiga no  - ³±±
±±³          ³ecossitema                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este WS verifica as empresas vinculadas em um HardLock que   ³±±
±±³          ³estao cadastradas no Ecossistema.                            ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetMemberEcoSystem WSRECEIVE HardLock WSSEND ListOfCompany WSSERVICE MsEcoSystem

Local aArea  := GetArea()
Local cAlias := "PA0"  //Ver com Marcos Lima
Local nX     := 0

DEFAULT ::ListOfCompany := {}
dbSelectArea(cAlias)
dbSetOrder(1)
MsSeek(xFilial(cAlias)+::HardLock)
While !Eof() .And. xFilial(cAlias) == (cAlias)->(FieldGet(FieldPos(cAlias+"_FILIAL"))) .And.;
	PadR(::HardLock,20) == (cAlias)->(FieldGet(FieldPos(cAlias+"_HARDL")))
	nX++
	aadd(::ListOfCompany,WsClassNew("CompanyView"))
	::ListOfCompany[nX]:Code     := (cAlias)->(FieldGet(FieldPos(cAlias+"_CODE")))
	::ListOfCompany[nX]:Name     := (cAlias)->(FieldGet(FieldPos(cAlias+"_NAME")))
	::ListOfCompany[nX]:Branch   := (cAlias)->(FieldGet(FieldPos(cAlias+"_BRANCH")))
	::ListOfCompany[nX]:NickName := (cAlias)->(FieldGet(FieldPos(cAlias+"_NICK")))
	::ListOfCompany[nX]:FederalId:= (cAlias)->(FieldGet(FieldPos(cAlias+"_CNPJ")))
	::ListOfCompany[nX]:StateId  := (cAlias)->(FieldGet(FieldPos(cAlias+"_IE")))
	dbSelectArea(cAlias)
	dbSkip()
EndDo
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPartner³Autor  ³ Eduardo Riera         ³ Data ³15.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de recuperacao dos parceiros do Ecossistema      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este WS verifica os parceiros que estao cadastrados no Ecos- ³±±
±±³          ³sistema.                                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetPartners WSRECEIVE PartnerID WSSEND ListOfPartners WSSERVICE MsEcoSystem

Local nX := 0

//Local cURLWebMicro := "http://localhost/ws" // Para teste local

Local cURLWebMicro := "http://webservices.microsiga.com.br" // URL dos WebServices hospedados na Microsiga  

::ListOfPartners := {}
If ::PartnerID == "E-FINANCIAL" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Financeiro"
	::ListOfPartners[nX]:PartnerID      := "E-FINANCIAL"
	::ListOfPartners[nX]:Name           := "e-Financial"
	::ListOfPartners[nX]:Note           := "Empresa líder na prestação de serviços e suporte às operações financeiras de vendas à distância. Nossos principais produtos, SmartCob e Smart Pag, são gateways de transações financeiras que reúnem o maior número de formas de pagamento de diversas instituições financeiras, todas num único ambiente, consolidadas e ligadas a um sistema de gestão operacional no modelo ASP. A E-Financial também proporciona soluções nos segmentos de seguradoras, processamento de cartões, instituições financeiras e cooperativas de crédito."
	::ListOfPartners[nX]:Logo           := Nil
	::ListOfPartners[nX]:URLLocation    := "http://www.smartcob.com.br/Partner/PartnerEcoSystem.asmx"
	::ListOfPartners[nX]:URLSoapAction  := "http://webservices.microsiga.com.br"
	::ListOfPartners[nX]:HomePage       := "http://www.e-financial.com.br/smart_cob.asp"
	::ListOfPartners[nX]:UserID         := "13D4B301E3B047EFF972D10BE2637C7A8FADFF7F12BC"
	::ListOfPartners[nX]:PassWord       := "mi354s6s"
EndIf
If ::PartnerID == "WEBRASKA" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Geoprocessamento"
	::ListOfPartners[nX]:PartnerID      := "WEBRASKA"
	::ListOfPartners[nX]:Name           := "Webraska"
	::ListOfPartners[nX]:Note           := "A Webraska do Brasil é bem conhecida no Brasil pelo seu site de mapas e rotas: Apontador.com.br o qual ganhou 4 i-best consecutivos desde o seu lançamento. Webraska e líder mundial em tecnologia de geoprocessamento no mercado de operadoras de telefonia celular. No Brasil Webraska goza da melhor base de mapas com informações de navegação, as quais são necessárias para todas as aplicações de roteirização. Por exemplo, foram 500 pessoas empregadas, somente em São Paulo, para efetuar o levantamento em campo de informações de mãos de direção e restrições de virada. Através das APIs, é fácil integrar as funcionalidades de mapas e rotas da Webraska em outras aplicações como as desenvolvidas em parceria com a Microsiga."
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := GetSrvProfString("EcoSistemaWebService",cURLWebMicro)+"/PARTNERECOSYSTEM.apw"//localhost/ws
	::ListOfPartners[nX]:URLSoapAction  := GetSrvProfString("EcoSistemaWebService",cURLWebMicro)//localhost/ws
	::ListOfPartners[nX]:HomePage       := "http://www.webraska.com"
	::ListOfPartners[nX]:UserID         := ""
	::ListOfPartners[nX]:PassWord       := ""
EndIf
If ::PartnerID == "FLYTOUR" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Turismo"
	::ListOfPartners[nX]:PartnerID      := "FLYTOUR"
	::ListOfPartners[nX]:Name           := "FlyTour"
	::ListOfPartners[nX]:Note           := "Uma empresa jovem, líder na emissão de bilhetes aéreos e detentora da maior rede de agências de viagem e turismo do país."
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := GetSrvProfString("EcoSistemaWebService",cURLWebMicro)+"/PARTNERECOSYSTEM.apw"
	::ListOfPartners[nX]:URLSoapAction  := "http://localhost/portugues/"
	::ListOfPartners[nX]:HomePage       := "http://www.flytour.com.br/"
	::ListOfPartners[nX]:UserID         := ""
	::ListOfPartners[nX]:PassWord       := ""
EndIf
If ::PartnerID == "ACSP" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Credito"
	::ListOfPartners[nX]:PartnerID      := "ACSP"
	::ListOfPartners[nX]:Name           := "Associacao Comercial do Estado de São Paulo"
	::ListOfPartners[nX]:Note           := "Associacao Comercial do Estado de São Paulo"
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := GetSrvProfString("EcoSistemaWebService",cURLWebMicro)+"/PARTNERECOSYSTEM.apw"//http://webservices.microsiga.com.br
	::ListOfPartners[nX]:URLSoapAction  := cURLWebMicro //http://webservices.microsiga.com.br
	::ListOfPartners[nX]:HomePage       := "http://www.acsp.com.br/"
	::ListOfPartners[nX]:UserID         := ""
	::ListOfPartners[nX]:PassWord       := ""
EndIf
If ::PartnerID == "EQUIFAX" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Credito"
	::ListOfPartners[nX]:PartnerID      := "Equifax"
	::ListOfPartners[nX]:Name           := "Equifax"
	::ListOfPartners[nX]:Note           := "Equifax"
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := GetSrvProfString("EcoSistemaWebService",cURLWebMicro)+"/PARTNERECOSYSTEM.apw"//localhost/ws
	::ListOfPartners[nX]:URLSoapAction  := cURLWebMicro//localhost/ws/
	::ListOfPartners[nX]:HomePage       := "http://www.equifax.com.br/"
	::ListOfPartners[nX]:UserID         := ""//07670581884
	::ListOfPartners[nX]:PassWord       := ""//2206
EndIf             

If ::PartnerID == "SERASA" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Credito"
	::ListOfPartners[nX]:PartnerID      := "Serasa"
	::ListOfPartners[nX]:Name           := "Serasa"
	::ListOfPartners[nX]:Note           := "Serasa"
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := GetSrvProfString("EcoSistemaWebService",cURLWebMicro)+"/PARTNERECOSYSTEM.apw"//localhost/ws	
	::ListOfPartners[nX]:URLSoapAction  := cURLWebMicro
	::ListOfPartners[nX]:HomePage       := "http://www.serasa.com.br/"
	::ListOfPartners[nX]:UserID         := ""
	::ListOfPartners[nX]:PassWord       := ""
EndIf

If ::PartnerID == "EVERSYSTEMS" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Credito"
	::ListOfPartners[nX]:PartnerID      := "Eversystems"
	::ListOfPartners[nX]:Name           := "Eversystems"
	::ListOfPartners[nX]:Note           := "A EverSystems é reconhecida e premiada internacionalmente por seu pioneirismo e inovação em soluções e-Finance, atendendo à importantes instituições financeiras no Brasil e no exterior com a mais completa gama de soluções e serviços de TI. Oferece máxima segurança em transações eletrônicas, independente do canal utilizado. Dentre as inovações tecnológicas, a EverSystems é pioneira em Tecnologia de Agregação, que permite a consolidação de dados de diversas fontes em tempo real. Conheça o ConsoliDATA!"
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := "https://www.organox.com.br/Microsiga/WebServices/services/PARTNERECOSYSTEMSOAP"
	::ListOfPartners[nX]:URLSoapAction  := "https://www.organox.com.br/Microsiga/WebServices/services/PARTNERECOSYSTEMSOAP?method="
	::ListOfPartners[nX]:HomePage       := "http://www.organox.com.br/"
	::ListOfPartners[nX]:UserID         := "MicrosigaProtheus"
	::ListOfPartners[nX]:PassWord       := "HjT#Y98>"
EndIf
/*If ::PartnerID == "NOVO" .Or. Empty(::PartnerID)
	aadd(::ListOfPartners,WsClassNew("EcoPartnersView"))
	nX++
	::ListOfPartners[nX]:Group          := "Diversos"
	::ListOfPartners[nX]:PartnerID      := "NOVO"
	::ListOfPartners[nX]:Name           := "Seja um parceiro do Ecossistema"
	::ListOfPartners[nX]:Note           := "Se sua empresa deseja participar do Ecossistema de negócios da Microsiga entre em contato com o nosso <b>Departamento de Novos Negócios</b>. Maiores informações podem ser obtidas através do seguinte e-mail: <br><a href='mailto:ecossistema@microsiga.com.br' class='linkSubmenu'>ecossistema@microsiga.com.br</a><br><br>Fone: 55 11 39816346<br> "
	::ListOfPartners[nX]:Logo           := Nil	
	::ListOfPartners[nX]:URLLocation    := GetSrvProfString("EcoSistemaWebService","http://webservices.microsiga.com.br")+"/PARTNERECOSYSTEM.apw"
	::ListOfPartners[nX]:HomePage       := "mailto:adriana@microsiga.com.br"	
	::ListOfPartners[nX]:UserID         := ""
	::ListOfPartners[nX]:PassWord       := ""
EndIf*/
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³HandShake ³Autor  ³ Eduardo Riera         ³ Data ³04.02.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de HandShake com a Microsiga                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpC2: CNPJ                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este WS verifica os parceiros que estao cadastrados no Ecos- ³±±
±±³          ³sistema.                                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD HandShake WSRECEIVE HardLock,FederalID WSSEND HandShake WSSERVICE MsEcoSystem

Local aArea  := GetArea()
Local cAlias := "PA0"  

dbSelectArea(cAlias)
dbSetOrder(1)
If MsSeek(xFilial(cAlias)+PadR(::HardLock,20)+PadR(::FederalId,14))
	::HandShake := .T.
Else
	::HandShake := .F.
EndIf

RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³HandShake ³Autor  ³ Eduardo Riera         ³ Data ³04.02.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de HandShake com a Microsiga                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpC2: CNPJ                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este WS verifica os parceiros que estao cadastrados no Ecos- ³±±
±±³          ³sistema.                                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetPartnerRecord WSRECEIVE PartnerID, ServiceID WSSEND BrwPartnerRecord WSSERVICE PartnerEcoSystem

Local lRetorno := .T.
Local aArea  := GetArea()
Local cAlias := "PA1"
Local nX     := 0

::BrwPartnerRecord := {}

DbSelectArea( cAlias )

If Empty( ::ServiceID )
	DbSetOrder( 2 )
	MsSeek( xFilial( cAlias ) + PadR( ::PartnerID, 15 ) )

	While !Eof() .And. xFilial(cAlias) == (cAlias)->( FieldGet( FieldPos( cAlias + "_FILIAL" ) ) ) .And.;
		PadR( ::PartnerID, 15 ) == (cAlias)->( FieldGet( FieldPos( cAlias + "_PARC" ) ) )
		aAdd( ::BrwPartnerRecord, WsClassNew( "PartnerRecordView" ) )
		nX++
	
		::BrwPartnerRecord[nX]:PartnerID 	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_PARC" ) ) )
		::BrwPartnerRecord[nX]:ServiceID  	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_SERVIC" ) ) )
		::BrwPartnerRecord[nX]:HardLock   	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_HARDL" ) ) )
		::BrwPartnerRecord[nX]:FederalID 	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_CNPJ" ) ) )
		::BrwPartnerRecord[nX]:UserID		:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_USER" ) ) )
		::BrwPartnerRecord[nX]:Password  	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_PWD" ) ) )
		::BrwPartnerRecord[nX]:URL		  	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_URL" ) ) )
	
		DbSelectArea( cAlias )
		DbSkip()
	EndDo
Else	
	DbSetOrder( 2 )
	MsSeek( xFilial( cAlias ) + PadR( ::PartnerID, 15 ) + PadR( ::ServiceID, 15 ) )

	While !Eof() .And. xFilial(cAlias) == (cAlias)->( FieldGet( FieldPos( cAlias + "_FILIAL" ) ) ) .And.;
		PadR( ::PartnerID, 15 ) == (cAlias)->( FieldGet( FieldPos( cAlias + "_PARC" ) ) ) .And.;
		PadR( ::ServiceID, 15 ) == (cAlias)->( FieldGet( FieldPos( cAlias + "_SERVIC" ) ) )
		aAdd( ::BrwPartnerRecord, WsClassNew( "PartnerRecordView" ) )
		nX++
	
		::BrwPartnerRecord[nX]:PartnerID 	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_PARC" ) ) )
		::BrwPartnerRecord[nX]:ServiceID  	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_SERVIC" ) ) )
		::BrwPartnerRecord[nX]:HardLock   	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_HARDL" ) ) )
		::BrwPartnerRecord[nX]:FederalID 	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_CNPJ" ) ) )
		::BrwPartnerRecord[nX]:UserID		:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_USER" ) ) )
		::BrwPartnerRecord[nX]:Password  	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_PWD" ) ) )
		::BrwPartnerRecord[nX]:URL		  	:= ( cAlias )->( FieldGet( FieldPos( cAlias + "_URL" ) ) )
	
		DbSelectArea( cAlias )
		DbSkip()
	EndDo
EndIf

RestArea( aArea )

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³HandShake ³Autor  ³ Eduardo Riera         ³ Data ³04.02.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de HandShake com a Microsiga                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpC2: CNPJ                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este WS verifica os parceiros que estao cadastrados no Ecos- ³±±
±±³          ³sistema.                                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutPartnerRecord WSRECEIVE PartnerRecord WSSEND PartnerRecordId WSSERVICE PartnerEcoSystem

Local lRetorno := .T.
Local cAlias := "PA1"

DbSelectArea( cAlias )
DbSetOrder( 2 )

If MsSeek( xFilial( cAlias ) + PadR( ::PartnerRecord:PartnerID, 15 ) + PadR( ::PartnerRecord:ServiceID, 15 ) + PadR( ::PartnerRecord:HardLock, 20 ) + PadR( ::PartnerRecord:FederalID, 14 ) )
	RecLock( cAlias, .F. )
Else
	RecLock( cAlias, .T. )
EndIf

( cAlias )->( FieldPut( FieldPos( cAlias + "_PARC" ), ::PartnerRecord:PartnerID ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_SERVIC" ), ::PartnerRecord:ServiceID ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_HARDL" ), ::PartnerRecord:HardLock ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_CNPJ" ), ::PartnerRecord:FederalID ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_USER" ), ::PartnerRecord:UserID ) )
( cAlias )->( FieldPut( FieldPos( cAlias + "_PWD" ), ::PartnerRecord:Password ) )

Do Case
	Case ::PartnerRecord:PartnerID == "WEBRASKA"
		( cAlias )->( FieldPut( FieldPos( cAlias + "_URL" ), "http://api.webraska.com.br:8080/api/gns" ) )
EndCase

MsUnLock()

PartnerRecordId := ""

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³HandShake ³Autor  ³ Eduardo Riera         ³ Data ³04.02.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Web Service de HandShake com a Microsiga                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do HardLock                                    ³±±
±±³          ³ExpC2: CNPJ                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este WS verifica os parceiros que estao cadastrados no Ecos- ³±±
±±³          ³sistema.                                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelPartnerRecord WSRECEIVE PartnerRecord WSSEND PartnerRecordId WSSERVICE PartnerEcoSystem

Local lRetorno := .T.

#IFDEF TOP
	cQuery := "DELETE "
	cQuery += "FROM " + RetSqlName( "PA1" )+ " PA1 "
	cQuery += "WHERE PA1.PA1_FILIAL='" + xFilial( "PA1" )+"' AND "
	cQuery += "PA1.PA1_PARC='" + ::PartnerRecord:PartnerID + "' AND "
	cQuery += "PA1.PA1_SERVIC='" + ::PartnerRecord:ServiceID + "' AND "
	cQuery += "PA1.PA1_CNPJ='" + ::PartnerRecord:FederalID + "' AND "
	cQuery += "PA1.PA1_HARDL='" + ::PartnerRecord:HardLock + "' AND "
	cQuery += "PA1.D_E_L_E_T_=' ' "

	TcSqlExec(cQuery)
#ELSE
	DbSelectArea( "PA1" )
	DbSetOrder( 2 )

	If MsSeek( xFilial( "PA1" ) + PadR( ::PartnerRecord:PartnerID, 15 ) + PadR( ::PartnerRecord:ServiceID, 15 ) + PadR( ::PartnerRecord:HardLock, 20 ) + PadR( ::PartnerRecord:FederalID, 14 ) )
		RecLock( "PA1" )
		DbDelete()
	EndIf
#ENDIF

PartnerRecordId := ""

Return lRetorno

/********************************************************************************************************/
Function fRetHSGPW( cConsulta, cResposta, cErro, cUsuario, cSenha, cURL, cCodErro )

Local lRetorno := .T.
Local nI := 0
Local nJ := 0
Local aTemp := {}
Local aTempLayout := {}
Local aResposta := {}
Default cCodErro := ""



//Campo, Tamanho, Tipo, Decimal
aAdd( aResposta, "RESPOSTA" )
aAdd( aResposta, {} )
aAdd( aResposta[2], { "TRANSACAO", 			4, "X", ""  } )
aAdd( aResposta[2], { "VERSAO", 			2, "X", ""  } )
aAdd( aResposta[2], { "CODIGO RETORNO", 	2, "X", ""  } )
aAdd( aResposta[2], { "MENSAGEM RETORNO", 	80, "X", ""  } )
aAdd( aResposta[2], { "USUARIO", 			15, "X", ""  } )
aAdd( aResposta[2], { "SENHA", 				15, "X", ""  } )
aAdd( aResposta[2], { "URL", 				80, "X", ""  } )
aAdd( aResposta[2], { "RESERVADO", 			2, "X", ""  } )



If !Empty( cResposta )
	If At( "<PRE>", Upper( cResposta ) ) > 0
		cResposta := Substr( cResposta, At( "<PRE>", cResposta ) + 6 )
		cResposta := Substr( cResposta, 1, At( "</PRE>", cResposta ) - 2 )
	EndIf
	
	nI := 1
	
	While nI <> 0
		nI := At( IIf( cConsulta == "HS", "HSR", "GPWR" ), cResposta )
		nJ := At( IIf( cConsulta == "HS", "HSR", "GPWR" ), SubStr( cResposta, nI + 1) )
	
		If nJ == 0
			nI := 0
			nJ := Len( cResposta )
		EndIf
	
		aAdd( aTemp, Substr( cResposta, nI, nJ ) )
		cResposta := Substr( cResposta, nJ + 1 )
	EndDo
	
	For nI := 1 To Len( aTemp )
		nPos := 1
		aAdd( aTempLayout, {} )
		aEval( aResposta[2], { |x| aAdd( aTempLayout[nI], { x[1], Substr( aTemp[nI], nPos, x[2] ) } ), nPos += x[2] } )
	Next nI
	
	For nI := 1 To Len( aTempLayout )
		nPos := aScan( aTempLayout[nI], { |x| x[1] == "CODIGO RETORNO" } )
		If nPos > 0
			If aTempLayout[nI][nPos][2] <> "00"
			    cCodErro := aTempLayout[nI][nPos][2]
				cErro := aTempLayout[nI][nPos + 1][2]
				cURL := aTempLayout[nI][nPos + 4][2]

				If Empty( cURL )
					lRetorno := .F.
				EndIf
			Else
				If cConsulta == "HS"
					cUsuario := AllTrim( aTempLayout[nI][nPos + 2][2] )
					cSenha := AllTrim( aTempLayout[nI][nPos + 3][2] )
				ElseIf cConsulta == "GPW"
					cURL := aTempLayout[nI][nPos + 4][2]
					If SubStr(cURL,1,16) == "http://webacspt/"
//						cURL := "http://200.211.20.9"+SubStr(cURL,16,(Len(cURL)))
						cURL := "http://www.acsp.com.br"+SubStr(cURL,16,(Len(cURL)))						
					EndIf				
				EndIf
			EndIf
		EndIf
	Next nI
Else
	cErro := "Falha de comunicacao ACSP"
	lRetorno := .F.
EndIf

Varinfo("", aTempLayout )


Return lRetorno

/********************************************************************************************************/
Function GetURLSoap( oObj, cWebService, cPartnerID )

Local cWsUrl := ""
Local aHandShake := {}
Local nPos := 0

GetGlbVars( "aEcoSystemHandShake", aHandShake )
nPos := aScan( aHandShake, { |x| x[1] == cPartnerID } )

If nPos > 0
	cWsUrl := aHandShake[nPos][9] + "/" + cWebService
	oObj:_URL := cWsUrl
EndIf

Return