#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'backoffice.fin.excel.controller.ch'

NAMESPACE totvs.protheus.backoffice.fin.excel
USING NAMESPACE totvs.protheus.backoffice.fin.apiutil

/*/{Protheus.doc} getVersion
	Retorna a versão da API
	@type  Function
	@author Vitor Duca
	@since 06/12/2022
	@version 1.0
	@return nVersion, Numeric, Versão atual da API
/*/
Function getVersion()
Return 1

/*/{Protheus.doc} getExcelTable
	Api para gerar excel com base nos registros de uma tabela
	@type  Function
	@author Vitor Duca
	@since 06/12/2022
/*/
@GET('/api/fin/v1/excel/table/:id')
Function getExcelTable()
	Local cId       := oRest:getPathParamsRequest()[ 'id' ] as Character
	Local jResponse := JsonObject():New() As Json
    Local oService  := NIL As Object
	Local oError    := ErrorBlock({|e| totvs.protheus.backoffice.fin.apiutil.internalError(e, oRest)}) As Object
    Local lRet      := .T. As Logical
    Local cFileXML  := "" As Character
    Local jQueryParams := NIL As Json
    Local cTitle := "Excel" As Character

	BEGIN SEQUENCE
		If !Empty(cId)
            jQueryParams := oRest:getQueryRequest()

            If jQueryParams:HasProperty("title")
                cTitle := Regex(jQueryParams["title"], {'/','|','@','*','$','#','!','%','(',')','\'})  
            Endif

            oService := FINDTEXCELREPORT():new(cTitle)
            lRet := oService:setTable(cId)
            oService:setQueryParams(jQueryParams)

            If lRet
                lRet := oService:execute()
            Endif

            If lRet 
                oRest:SetKeyHeaderResponse("Content-Disposition", "attachment; filename=" + oService:cNameArq + "")
                oRest:SetKeyHeaderResponse("content-type", "application/octet-stream")

                oFile := FwFileReader():New(oService:getDirectory())

                If (oFile:Open())
                    cFileXML := oFile:FullRead() 
                    cFileXML := Encode64(cFileXML)
                    jResponse['file'] := cFileXML
                    jResponse['fileName'] := oService:cNameArq
                    oRest:SetResponse(jResponse)
                Endif

                oFile:Close()
            Else
                jResponse := answerFormat(.F., 412, STR0001 , oService:getError()) //"Falha na geração do Excel"
                totvs.protheus.backoffice.fin.apiutil.AnswerRest(jResponse)
            Endif    
        EndIf
	END SEQUENCE
	ErrorBlock(oError)

Return
