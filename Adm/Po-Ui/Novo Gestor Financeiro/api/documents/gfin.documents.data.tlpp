#INCLUDE "tlpp-core.th"

NameSpace gfin.api.documents

Static __lF75FILORI := F75->(FieldPos("F75_FILORI")) > 0

//--------------------------------------------------------------------
/*/{Protheus.doc} apiDocumentsFields
campos defaults para retorno da API

@param  cTable As Character, SE1 ou SE2
@param  aApiFields As Array, lista de campos para adicionar

@return array, lsita dos campos

@author renato.ito
@since 01/03/2021
/*/
//--------------------------------------------------------------------
Function apiDocumentsFields(cTable as Character,  aApiFields as Array) as array
  Local cfields   := '' as Character
  Local nField    := 1  as Numeric
  Local cFilTable := '' as Character

  if 'SE1' $ cTable
    cFilTable := 'E1_FILIAL,'
    cFields   := 'E1_FILIAL,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_TIPO,E1_CLIENTE,E1_LOJA,E1_EMISSAO,E1_VENCREA,E1_NATUREZ,'
    cFields   += 'E1_VALOR,E1_SALDO,E1_SDACRES,E1_SDDECRE,E1_MOEDA,F75_TXMOED,F75_ABATIM,F75_VLVA,F75_MULTA,F75_JUROS,F75_SALDO,F75_VLTIT,F75_FLUXO'
  elseif 'SE2' $ cTable
    cFilTable := 'E2_FILIAL,'
    cFields   := 'E2_FILIAL,E2_PREFIXO,E2_NUM,E2_PARCELA,E2_TIPO,E2_FORNECE,E2_LOJA,E2_EMISSAO,E2_VENCREA,E2_NATUREZ,'
    cFields   += 'E2_VALOR,E2_SALDO,E2_SDACRES,E2_SDDECRE,E2_MOEDA,F75_TXMOED,F75_ABATIM,F75_VLVA,F75_MULTA,F75_JUROS,F75_SALDO,F75_VLTIT,F75_FLUXO'
  endif

  for nField := 1 to len(aApiFields)
    if !(aApiFields[nField] $ cfields)
      cfields += ',' + aApiFields[nField]
    endif
  next
  // retira o campo de filial se for compartilhado
  if FWModeAccess(cTable, 1) == 'C'
    strTran(cfields, cFilTable, '')
  endif
Return STRtoArray(cfields, ',')

//-------------------------------------------------------------------
/*/{Protheus.doc} apiDocumentsQuery
campos defaults para retorno da API

@param  table As Character, SE1 ou SE2

@return query As Character, query padrão para uso na api

@author renato.ito
@since 01/03/2021
/*/
//-------------------------------------------------------------------
Function apiDocumentsQuery(table as Character)
  Local query       as Character
  Local fieldNick   as Character
  Local cliforField as Character
  Local recpag      as Character

  If 'SE1' $ table
    fieldNick := table + '.E1_'
    cliforField := 'CLIENTE'
    recpag  := 'R'
  Else
    fieldNick := table + '.E2_'
    cliforField := 'FORNECE'
    recpag := 'P'
  endif

  query := " SELECT #QueryFields# "
  query += " FROM " + RetSqlName(table) + " " + table
  query += " LEFT JOIN " + RetSqlName('F75') + " F75"
  If __lF75FILORI
    query += " ON " + fieldNick + "FILIAL = F75.F75_FILORI"
  Else
    query += " ON " + fieldNick + "FILIAL = F75.F75_FILIAL"
  endif
  query += " AND " + fieldNick + "PREFIXO = F75.F75_PREFIX"
  query += " AND " + fieldNick + "NUM = F75.F75_NUM"
  query += " AND " + fieldNick + "PARCELA = F75.F75_PARCEL"
  query += " AND " + fieldNick + "TIPO = F75.F75_TIPO"
  query += " AND " + fieldNick + cliforField + " = F75.F75_CLIFOR"
  query += " AND " + fieldNick + "LOJA = F75.F75_LOJA"
  query += " AND F75.D_E_L_E_T_ =' '"
  query += " AND F75.F75_RECPAG ='" + recpag + "'"
  query += " WHERE "
  query += " #QueryWhere#"
Return query

//-------------------------------------------------------------------
/*/{Protheus.doc} apiDocumentsOrder
campos defaults para retorno da API

@param  table As Character, SE1 ou SE2

@return order As Character, string de order para query

@author renato.ito
@since 01/03/2021
/*/
//-------------------------------------------------------------------
Function apiDocumentsOrder(cTable as Character) as Character
  Local cQueryOrder := '' as Character
  Local cModeAccess := '' as Character
  Local cFields     := '' as Character

  cModeAccess := FWModeAccess(cTable, 01) // COMPARTILHAMENTO EMPRESA

  If 'SE1' $ cTable
    cFields     := IIF(cModeAccess == 'C', '', 'E1_FILIAL,')
    cQueryOrder := cFields + 'E1_PREFIXO,E1_NUM,E1_PARCELA,E1_TIPO,E1_CLIENTE,E1_LOJA,E1_VENCREA'
  ElseIf 'SE2' $ cTable
    cFields     := IIF(cModeAccess == 'C', '', 'E2_FILIAL,')
    cQueryOrder := cFields + 'E2_PREFIXO,E2_NUM,E2_PARCELA,E2_TIPO,E2_FORNECE,E2_LOJA,E2_VENCREA'
  endif
Return cQueryOrder
