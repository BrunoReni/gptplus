#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'gfin.validations.ch'

NameSpace gfin.api.validations
Using NameSpace gfin.util
//-------------------------------------------------------------------
/*/{Protheus.doc} validations
EndPoint para validar um comando, função ou expressão do protheus

@author daniel.muniz
@since 29/04/2020
/*/
//-------------------------------------------------------------------
@Post('/api/gfin/v1/Validate')
Function validateExpression()
  Local oBody     as Json
  Local oResponse as Json
  Local cError    as Character

  oBody := JsonObject():new()
  oBody:fromJson(oRest:GetBodyRequest())

  oResponse := JsonObject():new()
  oResponse['result'] := .T.
  oResponse['response'] := JsonObject():new()
  If !Empty(oBody['expression'])
    If !ChkAdvplSyntax(oBody['expression'], @cError, .F.)
      oResponse['response']['error'] := cError
    EndIf
  Else
    oResponse['response']['error'] := STR0001
  EndIf

  AnswerRest(oResponse)
  FreeObj(oResponse)
  FreeObj(oBody)
Return
