#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'

NameSpace gfin.api.currency
Using NameSpace gfin.util
//-------------------------------------------------------------------
/*/
{Protheus.doc} currencies
retorna as moedas do sistema

@author Renato Ito
@since 22/02/2021
/*/
//-------------------------------------------------------------------
@Get('/api/gfin/v1/currencies')
Function currencies()
	Local response  as Json

  	response := JsonObject():New()
	response["result"] := .T.
	response["response"] := JsonObject():New()
  	response["response"]["currencies"] := systemCurrency()

	AnswerRest(response)
	FreeObj(response)
	response := nil
return

/*/
{Protheus.doc} currencyRate
Retorna a taxa do dia de uma determinada moeda

@author Vitor Duca
@since 13/12/2021
/*/
@Get('/api/gfin/v1/currencies/rate')
Function currencyRate()
	Local oResponse  as Json
	Local oQueryRequest As Json
	Local dData := dDataBase As Date
	Local cMoeda := '1' As Character

	oQueryRequest := oRest:getQueryRequest()

	dData := Iif(Empty(oQueryRequest['date']), dDataBase, Stod(StrTran(oQueryRequest['date'],'-','')))
	cMoeda := Iif(Empty(oQueryRequest['currency']), '1', oQueryRequest['currency'])

  	oResponse := JsonObject():New()
	oResponse["result"] := .T.
	oResponse["response"] := JsonObject():New()
 	oResponse["response"]["quote"] := RecMoeda(dData,cMoeda,.T.)

	AnswerRest(oResponse)
	FreeObj(oResponse)
	FreeObj(oQueryRequest)
	oResponse := nil
return
