#Include "PROTHEUS.Ch"
#INCLUDE 'tlpp-core.th'

Static __oReport As Object //Objeto de referencia que sera usado na Static Function
Static __jSection As Json //Objeto JSON de referencia que sera usado na Static Function

NAMESPACE totvs.protheus.backoffice.ngf.treport

/*/{Protheus.doc} FINDTDYNAMICREPORT
    Classe responsavel por impressão via API, as informações serão impressas conforme JSON
    
    Layout de JSON que deve ser enviado para impressão
    {
        "title": "Titulo do relatorio"
        "description": "Descrição"
        "filename": "NomeArquivo"
        "sections": [
            {
                "title": "Titulo da seção 1",
                "struct": [
                    {
                        "field": "e1_num",
                        "size": 10,
                        "title": "Numero"
                    },
                    {
                        "field": "e1_prefixo",
                        "size": 3,
                        "title": "Prefixo"
                    }
                ],
                "items": [
                    {
                        "e1_num": "TST001",
                        "e1_prefixo": "FIN"
                    },
                    {
                        "e1_num": "TST002",
                        "e1_prefixo": "FIN"
                    }                
                ],
                "messages": [
                    {
                        "value": "Total em bancos: 252.007.278,48"
                    },
                    {
                        "value": "Total em caixa: 2.012.876,55"
                    },
                    {
                        "value": "Total disponivel: 254.020.155,03"
                    }
                ]
            }
        ]
    }
    
    @author Vitor
    @since 10/10/2022
    @version 1.0
/*/

Class FINDTDYNAMICREPORT FROM LongClassName
    Private Data oReport As Object // Objeto que contem a instancia do TReport
    Private Data jSection As Json // Json que deve ser impresso

    Public Method new(cNameReport, cTitle, cDescription) CONSTRUCTOR

    //Set's e Get's
    Public Method setSection(jSection)
    Public Method setNameFile(cNameFile)
    Public Method getFileName() 
    Public Method getDirectoryFile() 

    // Processamento de dados
    Private Method configureReport()
    Public Method execute()

End Class

/*/{Protheus.doc} new
    Construtor da classe
    @author Vitor Duca
    @since 07/10/2022
    @version 1.0
    @param cNameReport, Character, Nome do relatorio
    @param cTitle, Character, Titulo do relatorio
    @param cDescription, Character, Descrição do relatorio
    @return self, Object, Instancia da classe
/*/
Method new(cNameReport As Character, cTitle As Character, cDescription As Character) Class FINDTDYNAMICREPORT
    ::oReport := TReport():New(cNameReport ,cTitle, ,{|| ReportPrint()}, cDescription)
    ::jSection := JsonObject():new()
    __jSection := JsonObject():new()
    __oReport := NIL
Return self

/*/{Protheus.doc} setSection
    Seta o JSON que sera impresso atraves da classe
    @author Vitor Duca
    @since 10/10/2022
    @version 1.0
    @param jSection, Json, Objeto JSON que contem as informações que devem ser impressas
/*/
Method setSection(jSection As Json) Class FINDTDYNAMICREPORT
    ::jSection:FromJson(jSection:toJson())
Return 

/*/{Protheus.doc} setNameFile
    Seta o nome do arquivo que sera impresso
    @author Vitor Duca
    @since 07/10/2022
    @version 1.0
    @param cNameFile, Character, Nome do arquivo
/*/
Method setNameFile(cNameFile As Character) Class FINDTDYNAMICREPORT
    ::oReport:SetFile(Lower(cNameFile))
Return

/*/{Protheus.doc} getFileName
    Retorna o nome do arquivo que sera gerado
    @author Vitor Duca
    @since 10/10/2022
    @version 1.0
    @return self:oReport, Object, Instancia do TReport
/*/
Method getFileName() Class FINDTDYNAMICREPORT
Return AllTrim(::oReport:cFile + ".pdf")

/*/{Protheus.doc} getDirectoryFile
    Retorna o diretorio onde o arquivo foi salvo, a pasta deve estar contida
    na ROOTPATH
    @author Vitor Duca
    @since 10/10/2022
    @version 1.0
    @return self:oReport, Object, Instancia do TReport
/*/
Method getDirectoryFile() Class FINDTDYNAMICREPORT
Return Lower(Alltrim(SuperGetMv("MV_RELT",.F.,"")))


/*/{Protheus.doc} execute
    Realiza a impressão do relatorio
    @author Vitor Duca
    @since 07/10/2022
    @version 1.0
/*/
Method execute() Class FINDTDYNAMICREPORT
    ::configureReport()
    
    __oReport := ::oReport
    __jSection:FromJson(::jSection:toJson())

    ::oReport:Print(.F.)

Return 

/*/{Protheus.doc} configureReport
    Configura o objeto do TReport para a impressão
    @author Vitor Duca
    @since 10/10/2022
    @version 1.0
/*/
Method configureReport() Class FINDTDYNAMICREPORT
    Local oSection := NIL As Object
    Local nSections := 0 As Numeric
    Local nStruct := 0 As Numeric

    ::oReport:lViewPDF := .F.
    ::oReport:SetPortrait()

    ::oReport:SetUseGC(.F.)
    ::oReport:SetEnvironment(1) // Ambiente: 1-Server e 2-Client
    ::oReport:SetDevice(6) //Opções: 1-Arquivo,2-Impressora,3-Email,4-Planilha, 5-Html e 6-PDF 

    For nSections := 1 to Len(::jSection["sections"])
            oSection := TRSection():New( ::oReport , ::jSection["sections"][nSections]["title"] ,,,,,,,,, .T.,,,,, .T.,,,,,,) 
            For nStruct := 1 to Len(::jSection["sections"][nSections]["struct"])
                cField := ::jSection["sections"][nSections]["struct"][nStruct]["field"]
                cTitle := ::jSection["sections"][nSections]["struct"][nStruct]["title"]
                nSize := ::jSection["sections"][nSections]["struct"][nStruct]["size"]
                TRCell():New( oSection, cField,, cTitle,, nSize,,,,,,,, .T.,,,)
            Next nStruct
   
    Next nSections
Return 

/*/{Protheus.doc} ReportPrint
    Função responsavel por realizar a impressão do Objeto TReport
    @type  Static Function
    @author Vitor Duca
    @since 10/10/2022
    @version 1.0
/*/
Static Function ReportPrint()
    Local oSection	:= NIL As Object 
    Local nSections := 0 As Numeric
    Local nItems := 0 As Numeric
    Local nStruct := 0 As Numeric
    Local nPageWidth := __oReport:PageWidth() As Numeric
    Local nMessages := 0 As Numeric
    Local nSizeRow := sizeRowPage(__oReport) As Numeric

    For nSections := 1 to Len(__jSection["sections"])

        oSection := __oReport:Section(nSections)
        nItems := 0
        nStruct := 0

        For nItems := 1 to Len(__jSection["sections"][nSections]["items"])
            
            oSection:Init()

            For nStruct := 1 to Len(__jSection["sections"][nSections]["struct"])
                cField := __jSection["sections"][nSections]["struct"][nStruct]["field"]
                xValue := __jSection["sections"][nSections]["items"][nItems][cField]
                oSection:Cell(cField):SetValue(xValue)
            Next nStruct

            oSection:PrintLine()

        Next nItems 

        If __jSection["sections"][nSections]:hasProperty("messages") .and. len(__jSection["sections"][nSections]["messages"]) > 0
            __oReport:SkipLine()
            __oReport:Say(__oReport:Row(), 1, REPLICATE("-", nSizeRow),,nPageWidth)

            For nMessages := 1 to len(__jSection["sections"][nSections]["messages"])
                __oReport:SkipLine()
                __oReport:Say(__oReport:Row(), 1, __jSection["sections"][nSections]["messages"][nMessages]["value"],, nPageWidth)
            Next nMessages

            __oReport:SkipLine()
            __oReport:Say(__oReport:Row(), 1, REPLICATE("-", nSizeRow),,nPageWidth)

        Endif
        
    Next nSections
    
Return 

/*/{Protheus.doc} sizeRowPage
    Retorna a quantidade de caracteres que cabem na linha do relatorio
    @type  Static Function
    @author Vitor Duca
    @since 10/10/2022
    @version 1.0
    @param oReport, Object, Objeto do TReport
    @return nSizeRow, Numeric, Tamanho dos caracteres
/*/
Static Function sizeRowPage(oReport As Object) As Numeric
	Local nSizeRow    := 0 As Numeric
	Local nWidthPixel  := 0 As Numeric
	Local nLenFontPixel := 0 As Numeric

	nWidthPixel  := oReport:GetWidth()	// Tamanho da pagina em pixel
	nLenFontPixel := oReport:Char2Pix(1)	// Tamanho da fonte em pixel

	nSizeRow := Round( (nWidthPixel/nLenFontPixel) , 0)

Return nSizeRow
