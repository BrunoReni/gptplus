#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'backoffice.fin.banks.webhook.controller.ch'

NameSpace totvs.backoffice.fin.banks.webhook
/*/{Protheus.doc} postPixBB
    Webhook pix junto ao Banco do Brasil para notificar registros enviados para baixa.
    @type  Function
    @author Victor Azevedo
    @since 23/11/2022
    @version Protheus 12
/*/
@POST('/api/fin/v1/bb/pix/receivables/', STR0001) // "Serviço para recebimento de eventos WebHook Pix do Banco do Brasil"
Function postPixBB()
    Local cContent := DecodeUTF8(oRest:GetBodyRequest()) As Character
    Local jContent := JsonObject():New()                 As Json
    Local oTitulo                                        As Object


    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    If !Empty(jContent:fromJson(cContent))
        oRest:setStatusCode(403)
        oRest:setFault('{"' + STR0002 + '":"' + STR0003 + '"}') // "response" # "Conteúdo nulo ou indefinido"
        Return 
    EndIf
    
    oTitulo := Titulo():New()
    oTitulo:baixaTitul(jContent)
    oRest:setResponse(oTitulo:getRespons())

    If oTitulo <> Nil
        FreeObj(oTitulo)
    EndIf

    If jContent <> Nil
        FreeObj(jContent)
    EndIf
Return Nil

/*/{Protheus.doc} postBolBB
    Webhook bol junto ao Banco do Brasil para notificar registros enviados para baixa.
    @type  Function
    @author rafael.rondon
    @since 09/02/2023
    @version Protheus 12
/*/
@POST('/api/fin/v1/bb/bol/receivables') // "Serviço para recebimento de eventos WebHook de cobrança do Banco do Brasil"
Function postBolBB()
    Local cContent := DecodeUTF8(oRest:GetBodyRequest()) As Character
    Local jContent := JsonObject():New()                 As Json
    Local oWebHook                                       As Object

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    If !Empty(jContent:fromJson(cContent))
        oRest:setStatusCode(403)
        oRest:setFault('{"' + STR0002 + '":"' + STR0003 + '"}') // "response" # "Conteúdo nulo ou indefinido"
    Else
        oWebHook := BanksWebhookService():New()
        oWebHook:setBank('001')
        oWebHook:setPayLoad(jContent)
        oWebHook:postPayLoad()
        oRest:setResponse(oWebHook:getRespons())            
    EndIf

    If oWebHook <> Nil
        FreeObj(oWebHook)
    EndIf

    If jContent <> Nil
        FreeObj(jContent)
    EndIf

Return Nil
