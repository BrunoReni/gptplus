#INCLUDE 'tlpp-core.th'

NameSpace gfin.util

//-------------------------------------------------------------------
/*/{Protheus.doc } CurrencyLastQuote
busca a última cotação da moeda cadastrada na SM2

@param cCurrency as Character, moeda
@param cDate as Character, data
@return nQuote as Numeric , cotação da moeda

@author Renato Ito
@since 04/03/2021
/*/
//-------------------------------------------------------------------
Function currencyLastQuote(cCurrency as Character, cDate as Character) as Numeric
    Local cQuery      as Character
    Local nQuote      as Numeric
    Local cMoedaField as Character
    nQuote := 1
    If VAL(cCurrency) > 1
        cMoedaField := "M2_MOEDA" + cCurrency
        cQuery := "SELECT QUOTES.QUOTE AS VALUE FROM ("
        cQuery += " SELECT ROW_NUMBER() OVER (ORDER BY SM2.M2_DATA DESC) AS LINHA,"
        cQuery += " SM2." + cMoedaField + " AS QUOTE, SM2.M2_DATA FROM " + RetSqlName('SM2') + " SM2"
        cQuery += " WHERE SM2.M2_DATA <= '" + cDate + "'"
        cQuery += " AND SM2.D_E_L_E_T_ = ' ' "
        cQuery += " AND SM2." + cMoedaField + " > 0 "
        cQuery += " ) QUOTES "
        cQuery += " WHERE QUOTES.LINHA <= 1 "
        cQuery := ChangeQuery(cQuery)
        nQuote := MpSysExecScalar(cQuery, "VALUE")
    EndIf
Return nQuote


//-------------------------------------------------------------------
/*/{Protheus.doc } CurrencyValueQuote
busca a última cotação da moeda cadastrada na SM2 e cria array para uso em query

@param cCurrency as Character, moeda
@param cDate as Character, data
@return nQuote as Numeric , cotação da moeda

@author Francisco Oliveira
@since 13/09/2022
/*/
//-------------------------------------------------------------------
Function CurrencyValueQuote() As Array

    Local aCurrency := {}   As Array
    Local nQuote    := 0    As Numeric
    Local nSeqSM2   := 1    As Numeric
    Local lExist    := .T.  As Logical
    Local cCurrency := ""   As Character

    DbSelectArea('SM2')
    DbSetOrder(1)

    While lExist
        cCurrency   := cValToChar(nSeqSM2)
        If SM2->(FieldPos("M2_MOEDA" + cCurrency)) > 0
            nQuote := currencyLastQuote(cValToChar(nSeqSM2), Dtos(dDataBase))
            Aadd(aCurrency, {nSeqSM2, nQuote, CurrencySimb(cCurrency) })
            nSeqSM2++
        Else
            lExist := .F.
        Endif
    Enddo

Return aCurrency

//-------------------------------------------------------------------
/*/{Protheus.doc } CurrencySimb
busca a última cotação da moeda cadastrada na SM2 e cria array para uso em query

@param cCurrency as Character, moeda
@param cDate as Character, data
@return nQuote as Numeric , cotação da moeda

@author Francisco Oliveira
@since 13/09/2022
/*/
//-------------------------------------------------------------------
Static Function CurrencySimb(cCurrency As Character) As Character

Return Alltrim(SuperGetMV("MV_SIMB" + Alltrim(cCurrency),, " "))


