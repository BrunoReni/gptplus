#include 'tlpp-core.th'

NAMESPACE gfin.util.schedule

/*/{Protheus.doc} validBranchesSchedule
Valida se o schedule foi cadastrado com execução em mais de uma filial.
@type function
@version 12.1.33
@author rafael.rondon
@since 07/10/2021
@param cRotSch	, 		Código da rotina cadastrada no Schedule. Ex: FINA715('T1')
@return lRet - Booleano
/*/

Function validBranchesSchedule(cRotSch  As Character) As Logical

	Local lRet 			:= .T.	As Logical	
	Local cCodSch 		:= ''	As Character	
	Local cSchEmpFil	:= ''	As Character	
	Local aChsEmpFil	:= {}	As Array
	
	Default cRotSch := ""


	cCodSch := FwSchdByFunction(cRotSch)

	If Empty(cCodSch)
		cRotSch := StrTran(cRotSch,"'",'"')
	EndIf

	cCodSch := FwSchdByFunction(cRotSch)

	If !Empty(cCodSch)
		cSchEmpFil := FWSchdEmpFil(cCodSch)
		aChsEmpFil := StrTokArr(cSchEmpFil, ";")
		/*
			Não executar o job caso configurado mais de 1 filial. Configuração correta: "T1/D RJ 01;", Configuações erradas:
			"T1/D MG 01;T1/D MG 02;T1/D RJ 01;"  OU
			"T1" --> vai executar para cada filial uma a uma.
		*/
		If Len(aChsEmpFil) > 1 .OR. (Len(aChsEmpFil) == 1 .AND. aChsEmpFil[1] == cEmpAnt)
			lRet := .F.
		EndIf
	EndIf

Return lRet
