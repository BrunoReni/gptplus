#include 'tlpp-core.th'
#include 'totvs.ch'

NAMESPACE totvs.protheus.backoffice.ngf.util

/*/{Protheus.doc} GetError
    Retorna o ultimo erro encontrado quando o processo é feito por execauto
    @type  Function
    @author Vitor Duca
    @since 04/01/2022
    @version 1.0
    @return cErrorContent, Character, Mensagem de erro encontrada
/*/
Function GetError()
    Local aError := {} As Array
    Local nIndex := 0 As numeric
    Local cErrorContent := "" As Character
    Local cMemoError := "" As Character
    Local nLines := 0 As Numeric

    aError := GetAutoGRLog()
      
    IF !Empty(aError)
        For nIndex := 1 To Len(aError)
            cErrorContent += aError[nIndex] + CRLF
        Next nIndex
    Else
        cMemoError := MemoRead(NomeAutoLog())
        nLines := MLCount(cMemoError)

        For nIndex := 1 To nLines
            cErrorContent += RTrim(MemoLine(cMemoError,,nIndex)) + CRLF
        Next nIndex
    Endif

Return cErrorContent
