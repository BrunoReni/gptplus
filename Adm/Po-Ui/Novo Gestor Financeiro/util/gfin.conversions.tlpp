#include "tlpp-core.th"
NameSpace gfin.util
//-------------------------------------------------------------------
/*/{Protheus.doc } JsonToChar
Converte um variavel json para character

@param oJson as Json, variavel a ser convertida
@return cJson as Character , resultado da conversão

@author Daniel Muniz
@since 10/03/2021
/*/
//-------------------------------------------------------------------
Function JsonToChar(json as Json) as Character
  Local i     as Numeric
  Local text  as Character
  Local names as Array
  text := ''
  If (ValType(json) == 'J' .or. ValType(json) == 'O')
    names := json:GetNames()
    For i := 1 To LEN(names)
        If i > 1
            text += '&'
        EndIf
        text += names[i] + '=' + json[names[i]]
    Next i
  EndIf
Return text

//-------------------------------------------------------------------
/*/{Protheus.doc } ArrayToChar
Converte um variavel array para character

@param aArray as Array, variavel a ser convertida
@return cText as Character , resultado da conversão

@author Daniel Muniz
@since 19/04/2021
/*/
//-------------------------------------------------------------------
Function ArrayToChar(aArray as Array) as Character
  Local i     as Numeric
  Local cText  as Character
  cText := ''
  If ValType(aArray) == 'A' .and. !Empty(aArray)
    For i := 1 To LEN(aArray)
        If i > 1
            cText += ','
        EndIf
        cText += aArray[i]
    Next i
  EndIf
Return cText
