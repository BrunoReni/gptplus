#include 'tlpp-core.th'

namespace portal.cliente.util

//--------------------------------------------------------------------
/*/{Protheus.doc } getBillStatus
Retorna o status do título, sendo:

PENDING - Saldo em Aberto, pendente de pagamento por parte do cliente
OVERDUE - Vencido / Atrasado
PAID    - Pago pelo cliente

@param realDueDate, date   , Vencimento Real
@param paymentDate, date   , Data de Pagamento
@param balance    , numeric, Saldo do Título

@author Alison Lemes
@since sep|2020
/*/
//--------------------------------------------------------------------
function getBillStatus(realDueDate as date, paymentDate as date, balance as numeric) as character
	local status as character

	do case
		case balance > 0 .AND. dDataBase > realDueDate
			status := "OVERDUE"
		case balance == 0
			status := "PAID"
		case balance > 0
			status := "PENDING"
		otherwise
			status := "UNKNOWN"
	endcase

return status

//-------------------------------------------------------------------
/*/{Protheus.doc } getQueryStatus
Retorna o filtro da query de acordo com o status

PENDING - Saldo em Aberto, pendente de pagamento por parte do cliente
OVERDUE - Vencido / Atrasado
PAID    - Pago pelo cliente

@param status, array    , Status
@param type  , character, Tipo R=Receber ou P=Pagar

@author Alison Lemes
@since sep|2020
/*/
//-------------------------------------------------------------------
function getQueryStatus(status as array, type as character) as character
	local index       as numeric
	local content     as character
	local queryStatus as character
	local tablePrefix as character

	queryStatus := ""
	tablePrefix := if(type == "R", "E1", "E2")

	for index := 1 to len(status)
		content := lower(alltrim(status[index]))

		do case
			case content == "pending"
				queryStatus += "(" + tablePrefix + "_SALDO > 0 AND " + tablePrefix + "_VENCREA >= '" + DToS(dDataBase) + "')"
			case content == "overdue"
				queryStatus += "(" + tablePrefix + "_SALDO > 0 AND " + tablePrefix + "_VENCREA < '" + DToS(dDataBase) + "')"
			case content == "paid"
				queryStatus += "" + tablePrefix + "_SALDO = 0 "
			otherwise
				queryStatus += ""
		endcase

		queryStatus += " OR "

	next

	if ( !empty(queryStatus) )
		queryStatus := left(queryStatus, len(queryStatus) - 04)
		queryStatus := "( " + alltrim(queryStatus) + " )"
	endif

return queryStatus
