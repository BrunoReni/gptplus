#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace tr.store.client
using namespace tr.util


class StoreClientsController

	public method New()
	@GET('/api/v1/totvsRecibos/storeClients/:client')
	public method getStoreClient()
	@GET('/api/v1/totvsRecibos/storeClients')
	public method getStoreClients()
	@GET('/api/v1/totvsRecibos/storeClients/email/:store/:client')
	public method getEmailClient()
	@POST('/api/v1/totvsRecibos/storeClients/email')
	public method getEmailArrayClient()

endClass

Method new() class StoreClientsController

return

/*/{Protheus.doc} getStoreClient
EndPoint para obtener el los datos (Nombre,codigo y tienda) de un cliente especifico
@type function
@version  1
@author luis.aboytes
@since 10/02/2021
/*/

Method getStoreClient() Class storeClientsController
	Local oResponse As Object
	Local oStoreClientsService As Object

	oStoreClientsService := StoreClientsService():new()
	oResponse := oStoreClientsService:getStoreClientsService("SA1",oRest:getPathParamsRequest(),oRest:getQueryRequest())
	AnswerRest(oResponse)
Return Nil

/*/{Protheus.doc} getStoreClients
EndPoint para obtener el los datos de todos los clientes
@type function
@version  1
@author luis.aboytes
@since 10/02/2021
/*/

Method getStoreClients() Class storeClientsController
	Local oResponse As Object
	Local oStoreClientsService As Object

	oStoreClientsService := StoreClientsService():new()
	oResponse := oStoreClientsService:getStoreClientsService("SA1",oRest:getPathParamsRequest(),oRest:getQueryRequest())
	AnswerRest(oResponse)
Return Nil

/*/{Protheus.doc} getEmailClient
EndPoint que obtiene el correo electronico de un cliente en especifico
@type function
@version  1
@author luis.aboytes
@since 19/3/2021
/*/

Method getEmailClient() Class storeClientsController
	Local oResponse As Object
	Local oStoreClientsService As Object

	oStoreClientsService := StoreClientsService():new()
	oResponse := oStoreClientsService:getEmailClientService("SA1",oRest:getPathParamsRequest())
	AnswerRest(oResponse)
Return Nil

/*/{Protheus.doc} getEmailClient
EndPoint que obtiene el correo electronico de uno o varios clientes
@type function
@version  1
@author luis.aboytes
@since 13/7/2021
/*/
Method getEmailArrayClient() Class storeClientsController
	Local jBody 		As Character
	Local jResponse 	As Object

	jBody 			:= JsonObject():New()
	cBodyContent 	:= jBody:fromJson(oRest:GetBodyRequest())

	//Validamos si recibio un Json
	If ( cBodyContent <> nil )
		oRest:setStatusCode ( 400 )
		oRest:setResponse( "Objecto Json invalido" + cBodyContent )//STR0001 "Objeto Json invalido."
	Else
		oReceiptService := StoreClientsService():new()
		jResponse := oReceiptService:getEmailClientArrayService("SA1",jBody)
		AnswerRest(jResponse)
	EndIf
Return Nil
