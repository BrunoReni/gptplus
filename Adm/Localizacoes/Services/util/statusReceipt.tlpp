#include 'tlpp-core.th'
#include 'statusreceipt.ch'

namespace tr.util.receipt

/*/{Protheus.doc} getStatusReceipt
Metodo que obtiene los Status de los recibos dependiendo el pais
¡IMPORTANTE! Regla de negocio replicada de la "fina088.prw" funcion "Function FA088Leg()" 
@type function
@version  1
@author luis.aboytes
@since 7/4/2021
@return array, retorna los status dependiendo el pais
/*/
Function getStatusReceipt()
	Local aCores  As Array
	aCores := {}

	If cPaisLoc=="ARG"
		aAdd(aCores,{"BR_RED",STR0006}) //"Cancelado"
		aAdd(aCores,{"BR_BLACK",STR0007}) //"Version inactiva"
		aAdd(aCores,{"BR_GREEN",STR0008}) //"Version activa"
		aAdd(aCores,{"BR_PINK",STR0010}) //"Con Dif Cam"
	ElseIf cPaisLoc=="MEX"
		aAdd(aCores,{"BR_VERDE",STR0001}) //"Disponible"
		aAdd(aCores,{"BR_VERMELHO",STR0002}) //"Indisponible"
		aAdd(aCores,{"BR_AZUL",STR0005}) //"Timbrado"
		aAdd(aCores,{"BR_AMARELO",STR0009}) //"cancPend"
		aAdd(aCores,{"BR_PINK",STR0011}) //"Compensado" 
	Else
		aAdd(aCores,{"BR_VERDE",STR0001}) //"Disponible"
		aAdd(aCores,{"BR_VERMELHO",STR0002}) //"Indisponible"
		if cPaisLoc == "URU"
			aAdd(aCores,{"BR_PINK",STR0010}) //"Con Dif Cam"
		Endif
	Endif
Return aCores

/*/{Protheus.doc} setStatusColor
Funcion que recibe un color en especifico y retorna el estado del recibo
@type function
@version  1
@author luis.aboytes
@since 12/4/2021
@param cColor, character, color determinado por la regla de negocio
@return character, status del recibo
/*/
Function setStatusColor( cColor As Character) As Character
	Local aCores  As array
	Local nCont As Numeric
	Local cRet As Character
	aCores := getStatusReceipt()

	For  nCont := 1 to LEN(aCores)
		If aCores[nCont][1] == cColor
			cRet := aCores [nCont][2]
		Endif
	Next

Return cRet
