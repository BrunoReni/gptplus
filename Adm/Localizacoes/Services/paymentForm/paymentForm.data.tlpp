#include 'tlpp-core.th'

namespace tr.paymentForm 
 
/*/{Protheus.doc} paymentFormAdapter
Class paymentFormData
@type class
@version  1
@author José González
@since 04/03/2021
/*/
Class paymentFormAdapter From FWAdapterBaseV2
	Data tableNick As Character
	Public Method new()
	Public Method dataGetCGC()
EndClass

/*/{Protheus.doc} paymentFormAdapter::new
Constructor de la clase
@type method
@version  1
@author José González
@since 04/03/2021
/*/
Method new(resMethod As Character) Class paymentFormAdapter
	_Super:new(resMethod)
Return

/*/{Protheus.doc} paymentFormAdapter::dataGetCGC
Metodo que obtiene el CGC a traves de una consulta
@type method
@version  1	
@author luis.aboytes
@since 7/9/2022
/*/
Method dataGetCGC( jBody )  Class paymentFormAdapter
	Local cQueryFields 	:= "" As Character
	Local cQueryWhere   := "" As Character
	Local cQuery        := "" As Character
	Local cAlias		:= "" As Character
	Local cCGC			:= "" As Character
	Local cTienda		:= "" As character 
	Local cCliente		:= "" As character 

	cTienda  := Iif(jBody:hasproperty('loja'), jBody['loja'], jBody['tienda'])
	cCliente := Iif(jBody:hasproperty('client'), jBody['client'], jBody['cliente'])

	cAlias := GetNextAlias()

	cQueryWhere += " A1_FILIAL ='"+xFilial("SA1")+"' "
	cQueryWhere += " AND A1_COD ='"+cCliente+"' " 
	cQueryWhere += " AND A1_LOJA ='"+cTienda+"' " 
	cQueryWhere += " AND D_E_L_E_T_ = ' '"

	//Campo a obtener el valor dadas las condiciones where
	cQueryFields := " A1_CGC,A1_NOME "

	//Se genera la consulta SQL
	cQuery := "SELECT "+cQueryFields+" FROM "+ RetSqlName("SA1") +" WHERE "+cQueryWhere

	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery(cQuery, cAlias)

	cCGC :=  (cAlias)->A1_CGC

	(cAlias)->(DbCloseArea())

Return cCGC
