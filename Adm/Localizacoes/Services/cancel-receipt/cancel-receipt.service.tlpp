#include "tlpp-core.th"
#include "tlpp-rest.th"

#include "FWMVCDEF.CH"

namespace tr.cancelReceipt
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/cancelReceipt
Class cancelReceiptService

@author José González
@since 26/07/2022
/*/ 
//-------------------------------------------------------------------
Class cancelReceiptService
	Public Method new()
	Public Method getcancelReceiptService()
	Public Method getSE5Orig()
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/cancelReceipt
new
Constructor de la clase

@author José González
@since 26/07/2022
/*/
//-------------------------------------------------------------------
Method new() Class cancelReceiptService
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/cancelReceipt
@Return oResponse As Jobject, retorna la confirmación del Anulado
@author José González
@since 26/07/2022
/*/
//-------------------------------------------------------------------
 
Method getcancelReceiptService (jBody ) class  cancelReceiptService

local cTemp 	:= ""
local cRecibo	:= ""
local cSerie	:= ""
Local oResponse 	:= JsonObject():New()
Local lCancel	:= .F.
Local cVersao :=""

cRecibo := PadR( alltrim(jBody['receipt']),GetSx3Cache("EL_RECIBO","X3_TAMANHO"))
cSerie	:= PadR( alltrim(jBody['serie']),GetSx3Cache("EL_SERIE","X3_TAMANHO"))
cVersao	:= PadR( iif(!EMPTY( alltrim(jBody['version']) ),alltrim(jBody['version']),"00"),GetSx3Cache("FJT_VERSAO","X3_TAMANHO"))

oMdlTab := FwLoadModel("FINA887")
DbSelectArea("FJT")
FJT->(DbSetOrder(1))

If FJT->(MsSeek(xFilial("FJT")+cSerie+cRecibo+cVersao,.T.))
	oMdlTab:SetOperation(MODEL_OPERATION_UPDATE)

	oMdlTab:Activate()
	oMdlTab:SetValue('FJT_MASTER', "ASIENTO" 	, jBody["parameters"]["mv_par01"])
	oMdlTab:SetValue('FJT_MASTER', "AGRUPA" 	, jBody["parameters"]["mv_par02"])	
	oMdlTab:SetValue('FJT_MASTER', "ONLINE" 	, jBody["parameters"]["mv_par03"])	
	oMdlTab:SetValue('FJT_MASTER', "FJT_CANCEL" , "1")		

	If SEL->(ColumnPos("EL_TIPAGRO")) >0 
		oMdlTab:SetValue('SEL_DETAIL',"EL_TIPAGRO" ,jBody["cancelationReasson"])
	EndIf			
					
	If oMdlTab:VldData()
		If oMdlTab:CommitData()
			oResponse['receipt'] := cRecibo
			oResponse['success'] := .T.
			oResponse['message'] := ""
		EndIf
	Endif
	aError := oMdlTab:GetErrorMessage()
	If Len(aError)>0 .and. alltrim(aError[6]) <> "" 
		oResponse['receipt'] := cRecibo
		oResponse['success'] := .F.
		oResponse['message'] := aError[6]
	Endif
ELSE
	//Si se quiere anular un recibo generado con rutinas anteriores donde no guardaba FJT
	//Si no se encuentra en la FJT, generara un grabado de los datos en la tabla FJT para anular correctamente un recibo		
	DbSelectArea("SEL")
	SEL->(DbSetOrder(8)) //EL_FILIAL+EL_SERIE+EL_RECIBO+EL_TIPODOC+EL_PREFIXO+EL_NUMERO+EL_PARCELA+EL_TIPO  
	IF SEL->(MsSeek(xFilial("SEL")+cSerie+cRecibo))  
		If self:getSE5Orig(cRecibo, cSerie)
			RecLock("FJT",.T.)
			FJT->FJT_FILIAL := SEL->EL_FILIAL
			FJT->FJT_DTDIGI := SEL->EL_DTDIGIT
			FJT->FJT_SERIE	:= SEL->EL_SERIE
			FJT->FJT_RECIBO := SEL->EL_RECIBO
			FJT->FJT_VERSAO := "00"
			FJT->FJT_EMISSA := SEL->EL_EMISSAO
			FJT->FJT_NATURE := SEL->EL_NATUREZ
			FJT->FJT_CLIENT := SEL->EL_CLIORIG
			FJT->FJT_LOJA 	:= SEL->EL_LOJA
			FJT->FJT_VERATU := "1"
			FJT->(MsUnlock())
				
			DbSelectArea("FJT")
			FJT->(DbSetOrder(1)) //FJT_FILIAL+FJT_SERIE+FJT_RECIBO+FJT_VERSAO     
			FJT->(MsSeek(xFilial("FJT")+cSerie+cRecibo+cVersao,.T.))	
			oMdlTab:SetOperation(MODEL_OPERATION_UPDATE)	
			oMdlTab:Activate()
			oMdlTab:SetValue('FJT_MASTER', "ASIENTO" 	, jBody["parameters"]["mv_par01"])
			oMdlTab:SetValue('FJT_MASTER', "AGRUPA" 	, jBody["parameters"]["mv_par02"])
			oMdlTab:SetValue('FJT_MASTER', "ONLINE" 	, jBody["parameters"]["mv_par03"])		
			oMdlTab:SetValue('FJT_MASTER', "FJT_CANCEL" , "1")		

			If SEL->(ColumnPos("EL_TIPAGRO")) >0 
				oMdlTab:SetValue('SEL_DETAIL',"EL_TIPAGRO" ,jBody["cancelationReasson"])
			EndIf	                                             
			IF oMdlTab:VldData()
				IF oMdlTab:CommitData()
					lCancel := .T.
				ENDIF
			ENDIF
		ENDIF
		oResponse['receipt'] := cRecibo
		oResponse['success'] := lCancel
		oResponse['message'] := ""
		aError := oMdlTab:GetErrorMessage()
		IF Len(aError)>0 .and. alltrim(aError[6]) <> "" 
			oResponse['message'] := aError[6]
		ENDIF
	ENDIF
ENDIF

oMdlTab:DeActivate() 

Return oResponse

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/getSE5Orig
@Return lRet As logical, retorna si fue encontrado el registro con origen en la fina087a
@author Raúl Ortiz
@since 10/01/2023
@params 
		cRecibo as character - Número de recibo
		cSerie  as character - Serie del recibo
/*/
//-------------------------------------------------------------------
Method getSE5Orig (cRecibo As character, cSerie As character ) class  cancelReceiptService
Local cTemp		:= GetNextAlias()
Local lRet 		:= .F.
Local nCount	:= 0

Default cRecibo := ""
Default cSerie	:= ""

	//-- Filtra la información
	BEGINSQL ALIAS cTemp
		SELECT
			SE5.E5_FILIAL, SE5.E5_ORDREC, SE5.E5_SERREC, SE5.E5_ORIGEM
		FROM
			%Table:SE5% SE5
		WHERE
			SE5.%notdel%
			AND     SE5.E5_FILIAL  	= %xFilial:SE5%
			AND     SE5.E5_RECPAG  	= 'R'
			AND     SE5.E5_ORDREC 	= %Exp:cRecibo%
			AND     SE5.E5_SERREC   = %Exp:cSerie%
	ENDSQL

	count to nCount

	If nCount > 0
		lRet := .T.
	EndIf

	(cTemp)->(dbCloseArea())

Return lRet
