#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'systemprofilecontroller.ch'

namespace tr.systemProfile
using namespace tr.util
using namespace tr.systemProfile.Service

class systemprofilecontroller

	public method New()
	@GET('/api/v1/totvsRecibos/systemProfile/get/FIN998')
	public method getFinancialTitlesFIN998()
	@POST('/api/v1/totvsRecibos/systemProfile/post/FIN998')
	public method postFinancialTitlesFIN998()
	@POST('/api/v1/totvsRecibos/systemProfile/getParam/FIN998')
	public method getParamFinancialTitlesFIN998()

endClass

Method new() class systemprofilecontroller
     
return 

/*/{Protheus.doc} financialTitles
EndPoitn donde retornara los parametros registrados de la SX1 de la FINA87A
@type function
@version  1
@author luis.aboytes
@since 15/4/2021
/*/
Method getFinancialTitlesFIN998() Class systemprofilecontroller
	Local oSystemProfileService as Object
	Local oResponse as Object

	oSystemProfileService := systemProfileService():new()
	oResponse := oSystemProfileService:getParametersFIN998()
	AnswerRest(oResponse)
Return


/*/{Protheus.doc} postFinancialTitlesFIN998
Endpoint donde retornara una respuesta de que si fue actualizado o insertado el valor en la tabla MP_SYSTEM_PROFILE
@type function
@version  1
@author luis.aboytes
@since 15/4/2021
/*/
Method postFinancialTitlesFIN998() Class systemprofilecontroller
	Local oSystemProfileService 
	Local jBody 
	Local cBodyContent
	Local oResponse 

	jBody := JsonObject():New()
	cBodyContent := jBody:fromJson(oRest:GetBodyRequest())
	//Validamos si recibio un Json
	If ( cBodyContent <> nil )
		oRest:setStatusCode ( 400 )
		oRest:setResponse(  STR0001 + cBodyContent )//STR0001 "Objeto Json invalido."
	Else
		oSystemProfileService := systemProfileService():new()
		oResponse := oSystemProfileService:postFinancialTitlesFIN998(jBody)
		AnswerRest(oResponse)
	EndIf
Return


/*/{Protheus.doc} getFinancialTitlesFIN998
Endpoint que obtendra las configuraciones almacenadas en la tabla de system-profile de los parametros de la rutina FINA87A
@type function
@version  1
@author luis.aboytes
@since 16/4/2021
/*/
Method getParamFinancialTitlesFIN998() Class systemprofilecontroller
	Local oSystemProfileService 
	Local jBody 
	Local cBodyContent 
	Local oResponse  

	jBody := JsonObject():New()
	cBodyContent := jBody:fromJson(oRest:GetBodyRequest())
	//Validamos si recibio un Json
	If ( cBodyContent <> nil )
		oRest:setStatusCode ( 400 )
		oRest:setResponse( STR0001 + cBodyContent )//STR0001 "Objeto Json invalido."
	Else
		oSystemProfileService := systemProfileService():new()
		oResponse := oSystemProfileService:getFinancialTitlesFIN998(jBody)
		AnswerRest(oResponse)
	EndIf
Return
