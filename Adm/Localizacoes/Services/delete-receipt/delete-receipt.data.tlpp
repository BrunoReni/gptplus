#include 'tlpp-core.th'

namespace tr.deleteReceipt
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/deleteReceipt
Class deleteReceiptData

@author José González
@since 13/05/2021
/*/
//-------------------------------------------------------------------
Class deleteReceiptData
	Data tableNick As Character

	Public Method new()
	Public Method getdeleteReceiptAdapter()
	
	
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/deleteReceipt
	new
	Constructor de la clase
	
@author José González
@since 13/05/2021
/*/
//-------------------------------------------------------------------

Method new() class deleteReceiptData
     
return 
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/deleteReceipt

	Verifica y retorna los datos para comprobar la eliminacion del recibo

	@Input 	filial, serie, cliente, tienda

@author José González
@since 13/05/2021
/*/
//-------------------------------------------------------------------
method getdeleteReceiptAdapter(cBranch,cSerie,cRecibo,cCliente,cLoja) class deleteReceiptData
	
	Local cQueryFields As Character
	Local cQueryWhere As Character
	Local cQuery As  Character
	Local cAlias As  Character
	Local oValues := JsonObject():new()
	Local aSEL 		:= {}
	Local aSELitem 	:= {}
	Local lCfd 		:= .F.
	Default cBranch 	:= ""
	Default cSerie 		:= ""
	Default cRecibo 	:= ""	
	Default cCliente 	:= ""	
	Default cLoja	:= "" 	


	cAlias := GetNextAlias()

	cQueryFields := "EL_PREFIXO,EL_NUMERO ,EL_PARCELA,EL_TIPO,EL_CLIENTE,EL_LOJA,EL_BANCO,EL_AGENCIA,EL_CONTA "
	IF (SEL->(ColumnPos("EL_CANCEL")) > 0 ) .and. (SEL->(ColumnPos("EL_UUID")) > 0 )
		cQueryFields += " ,EL_CANCEL,EL_UUID "
		lCfd := .T.
	EndIf
	cQueryWhere := " EL_FILIAL ='"		+  xFilial("SEL",cBranch) +"' "
	cQueryWhere += " AND EL_SERIE ='"	+ cSerie 		+"' "
	cQueryWhere += " AND EL_RECIBO ='"	+ cRecibo 		+"' "
	cQueryWhere += " AND EL_CLIENTE ='"	+ cCliente 		+"' "
	cQueryWhere += " AND EL_LOJA ='"	+ cLoja 		+"' "
	cQueryWhere += " AND D_E_L_E_T_ = ' ' "

	cQuery := "SELECT "+ cQueryFields +" FROM "+ RetSqlName("SEL") +" WHERE "+ cQueryWhere

	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery(cQuery, cAlias)
	iF lCfd
		oValues['cancel'] 		:= Alltrim((cAlias)->EL_CANCEL )
		oValues['uuid'] 		:= Alltrim((cAlias)->EL_UUID )
	EndIf
	WHILE (cAlias)->(!EOF())
		aSELitem 	:= {}
		AADD(aSELitem,	Alltrim((cAlias)->EL_PREFIXO	) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_NUMERO		) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_PARCELA	) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_TIPO		) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_CLIENTE	) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_LOJA		) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_BANCO		) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_AGENCIA	) )
		AADD(aSELitem,	Alltrim((cAlias)->EL_CONTA 		) )
		AADD(aSEL,aSELitem)

		(cAlias)->(DbSkip())
	EndDo
	oValues['aRegs'] := aSEL
  
	(cAlias)->(DbCloseArea()) 

Return oValues

