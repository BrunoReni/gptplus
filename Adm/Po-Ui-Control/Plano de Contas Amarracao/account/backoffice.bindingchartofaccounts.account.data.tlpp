#include 'tlpp-core.th'
#include 'tlpp-rest.th'

#Define DEFAULT_VERB 'GET'

namespace totvs.protheus.backoffice.bindingchartofaccounts.account.data
Using namespace totvs.protheus.backoffice.bindingchartofaccounts.account.service

Class bindingchartofaccountsAccountData From FWAdapterBaseV2
    
    Public Method New()
    public Method GetPage()

EndClass

Method New() Class bindingchartofaccountsAccountData
    _Super:New( DEFAULT_VERB, .T. )
Return Self

Method GetPage() Class bindingchartofaccountsAccountData

    Local jQuery
    Local cFilter

    jQuery      := oRest:GetQueryRequest() 

	cFilter   :=	'%' + IIF(jQuery['filter'] <> Nil,jQuery['filter'], '' ) + '%'

    AddMapFields( self )
    //Informa a Query a ser utilizada pela API
    ::SetQuery( GetQuery() )
    //Informa a clausula Where da Query
    cWhere := " CT1_FILIAL = '"+ FWxFilial('CT1') +"' AND CT1.D_E_L_E_T_ = ' ' AND CT1_CLASSE ='2'"
    If cFilter <> "%%"
         cWhere += " AND CT1_CONTA LIKE '"+ cFilter +"' 
    EndIf
    ::SetWhere( cWhere )
    //Informa a ordenação padrão a ser Utilizada pela Query
    ::SetOrder( "CT1_CONTA" )
    //Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
    If ::Execute()
        // Gera o arquivo Json com o retorno da Query
        ::FillGetResponse()
    EndIf

Return

Static Function AddMapFields( oSelf )  
    oSelf:AddMapFields( 'id'        , 'R_E_C_N_O_' , .T., .F., { 'R_E_C_N_O_', 'N'  , 999999999                 , 0 } )
    oSelf:AddMapFields( 'filial'    , 'CT1_FILIAL' , .T., .F., { 'CT1_FILIAL', 'C'  , TamSX3( 'CT1_FILIAL')[1]  , 0 } )
    oSelf:AddMapFields( 'conta'     , 'CT1_CONTA'  , .T., .F., { 'CT1_CONTA' , 'C'  , TamSX3( 'CT1_CONTA' )[1]  , 0 } )    
    oSelf:AddMapFields( 'descricao' , 'CT1_DESC01' , .T., .F., { 'CT1_DESC01', 'C'  , TamSX3( 'CT1_DESC01')[1]  , 0 } )    
    oSelf:AddMapFields( 'classe'    , 'CT1_CLASSE' , .T., .F., { 'CT1_CLASSE', 'C'  , TamSX3( 'CT1_CLASSE')[1]  , 0 } )    
    oSelf:AddMapFields( 'normal'    , 'CT1_NORMAL' , .T., .F., { 'CT1_NORMAL', 'C'  , TamSX3( 'CT1_NORMAL')[1]  , 0 } )  
    oSelf:AddMapFields( 'ntsped'    , 'CT1_NTSPED' , .T., .F., { 'CT1_NTSPED', 'C'  , TamSX3( 'CT1_NTSPED')[1]  , 0 } )  
    oSelf:AddMapFields( 'dtexist'   , 'CT1_DTEXIS' , .T., .F., { 'CT1_DTEXIS', 'D'  , TamSX3( 'CT1_DTEXIS')[1]  , 0 } )
Return


Static Function GetQuery()
    Local cQuery 
    cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'CT1' ) + " CT1 "
    cQuery += " WHERE #QueryWhere#"
Return cQuery
