#include 'tlpp-core.th'
#include 'tlpp-rest.th'

#Define DEFAULT_VERB 'GET'

namespace totvs.protheus.backoffice.bindingchartofaccounts.costcenter.data

Class bindingchartofaccountsCostcenterData From FWAdapterBaseV2
    Public Method New()
    public Method GetPage()
EndClass

Method New() Class bindingchartofaccountsCostcenterData
    _Super:New( DEFAULT_VERB, .T. )
Return Self

Method GetPage() Class bindingchartofaccountsCostcenterData

    AddMapFields( self )
    //Informa a Query a ser utilizada pela API
    ::SetQuery( GetQuery() )
    //Informa a clausula Where da Query
    cWhere := " CTT_FILIAL = '"+ FWxFilial('CTT') +"' AND CTT.D_E_L_E_T_ = ' '"
    ::SetWhere( cWhere )
    //Informa a ordenação padrão a ser Utilizada pela Query
    ::SetOrder( "CTT_CUSTO" )
    //Executa a consulta, se retornar .T. tudo ocorreu conforme esperado
    If ::Execute()
        // Gera o arquivo Json com o retorno da Query
        ::FillGetResponse()
    EndIf

Return    

Static Function AddMapFields( oSelf )  
    oSelf:AddMapFields( 'id', 'CTT_CUSTO' , .T., .F., { 'CTT_CUSTO', 'C', TamSX3( 'CTT_CUSTO' )[1], 0 } )
    oSelf:AddMapFields( 'ccusto', 'CTT_CUSTO' , .T., .F., { 'CTT_CUSTO', 'C', TamSX3( 'CTT_CUSTO' )[1], 0 } )
    oSelf:AddMapFields( 'descricao', 'CTT_DESC01' , .T., .F., { 'CTT_DESC01', 'C', TamSX3( 'CTT_DESC01' )[1], 0 } )    
Return

Static Function GetQuery()
    Local cQuery 
    cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + RetSqlName( 'CTT' ) + " CTT "
    cQuery += " WHERE #QueryWhere#"
Return cQuery
