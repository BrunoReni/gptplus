#include "tlpp-core.th"
#include "backoffice.reconciliation.reconciliationprogress.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.reconciliationprogress
using namespace totvs.protheus.backoffice.reconciliation.userprofile
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} Reconciliationprogress
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class ReconciliationProgress
    public method new()

    @Post("/api/tc/reconciliationprogress/")
    public method getAllReconciliationProgress()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} Reconciliationprogress
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class ReconciliationProgress
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllReconciliationprogresss
Metodo que inicia a chamada das configuracoes de Reconciliation do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllReconciliationProgress() class ReconciliationProgress
    Local oService := ReconciliationProgressService():getInstance() As Json
    Local cBody     := oRest:getBodyRequest() as object      
    Local aResponse := {} as Array
    Local jBody     := JsonObject():new() as Json
    Local uRet     
    Local lPadrao   := .F. As Logical
    Local lResponse := .F. As Logical
    Local cChaveRes := "message" As Character
    Local cResponse := STR0001 As Character //"Corpo inválido."
    Local oUserService := UserProfileService():getInstance() as Object    

     Private _oCposChv as object

    If ValType(cBody) == "C"
        uRet := jBody:fromJson( cBody )
        If ValType(uRet) == "U"
            //Posiciona na filial passada pelo tenantId
            tcGetHeaders() 
            aResponse := oService:getAllReconciliationProgress(jBody)

            //Tratamento do retorno
            If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                lResponse := aResponse[1]                                                 
                cResponse := aResponse[2]  
                lPadrao   := .T. 
                oUserService:postUserProfileByBody(jBody) //Adiciona profile dos filtros                
            EndIf                                    
        Else
            cResponse := STR0002 //"Conteúdo do corpo inválido."
        EndIf    
    EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)    
    FwFreeArray(aResponse)
    FreeObj(oService)    

return
