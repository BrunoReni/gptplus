#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.reconciliation.matchnext.controller.ch"
 
namespace totvs.protheus.backoffice.reconciliation.matchprocessing
using namespace totvs.protheus.backoffice.reconciliation.userprofile
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchProcessing
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class MatchProcessing
    public method new()

    @Post("/api/tc/matchprocessing/")
    public method matchProcessingByBody()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchProcessing
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class MatchProcessing
return
//-------------------------------------------------------------------
/*/{Protheus.doc} matchProcessingById
Metodo que inicia a chamada dos processamentos de acordo com as
filiais informadas

@author Totvs
/*/
//-------------------------------------------------------------------
method matchProcessingByBody() class MatchProcessing
    Local oService  := matchProcessingService():getInstance() as object
    Local cBody     := oRest:getBodyRequest() as object    
    Local aResponse := {} as Array
    Local jBody     := JsonObject():new() as Json
    Local uRet     
    Local lPadrao   := .F.
    Local lResponse := .F. As Logical
    Local cChaveRes := "message" As Character
    Local cResponse := STR0001 As Character //"Conteúdo do corpo inválido."
    Local oUserService  := UserProfileService():getInstance() as Object    

    Private _oCposChv as object

    If ValType(cBody) == "C"
        uRet := jBody:fromJson( cBody )
        If ValType(uRet) == "U"
            //Posiciona na filial passada pelo tenantId
            tcGetHeaders() 
            aResponse := oService:matchProcessingByBody(jBody)

            //Tratamento do retorno
            If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                If aResponse[1]
                    lResponse := aResponse[1]                                                 
                    cResponse := aResponse[2]  
                    lPadrao   := .T.
                    oUserService:postUserProfileByBody(jBody) //Adiciona profile dos filtros
                Else
                    cResponse := aResponse[2]  
                EndIf                 
            EndIf                                    
        Else
            cResponse := STR0001 //"Conteúdo do corpo inválido."
        EndIf    
    EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)    
    FwFreeArray(aResponse)
    FreeObj(oService)    
Return .T.
