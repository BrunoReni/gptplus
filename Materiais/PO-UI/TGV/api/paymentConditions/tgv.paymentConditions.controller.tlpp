#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace tgv.paymentconditions
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPaymentConditions
	API para consulta de Condições de Pagamentos.

	@author		Danilo Salve
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvPaymentConditions
	Data jResposta  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method New() as Object

	@Get("/api/tgv/paymentconditions/")
	Public Method GetAll() as Logical

	@Get("/api/tgv/paymentconditions/diff/:dateSync")
	Public Method GetDiff() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPaymentConditions
	Cria uma nova instancia do objeto.

	@sample		tgvPaymentConditions:New()
	@author		Danilo Salve
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvPaymentConditions
	::nPage     := 1
	::nPageSize := 10
	::oService  := PaymentConditionsService():GetInstance()
	::jResposta := JsonObject():New()
Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAll
	Efetua o GET de todos os registros

	@sample		GET /api/tgv/paymentconditions/
	@author		Danilo Salve
	@since		18/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetAll() as Logical Class tgvPaymentConditions
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:GetPaymentConditions(::nPage, ::nPageSize, /*cDate*/))
	AnswerRest(::jResposta)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetDiff
	Efetua o GET dos regitros alterados

	@sample		GET /api/tgv/paymentconditions/getdiff/:data
	@author		Danilo Salve
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetDiff() as Logical Class tgvPaymentConditions
	GetPageAndPageSize(@::nPage, @::nPageSize)
	::JResposta:FromJson(::oService:GetPaymentConditions(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResposta)
Return .T.
