#include 'tlpp-core.th'

namespace tgv.paymentconditions
using namespace tgv.util

Static __lPGVPCQRY := ExistBlock("GVPCQRY")

//------------------------------------------------------------------------------
/*/{Protheus.doc} PaymentConditionsProtheusData
	Efetua a consulta de Condições de Pagamentos no Protheus.
	@type		class
	@sample		PaymentConditionsProtheusData:New()
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
CLASS PaymentConditionsProtheusData FROM FWAdapterBaseV2
	Public Data aCustomFields		as Array
	Public Data cCustomFields		as Character
	Public Data cSource				as Character
	Public Data isAdmin				as Logical
	Public Data lcanObfuscateField	as Logical
	Public Data oHashFieldsPD		as Object
	Public Data nFields				as Numeric

	Public Method New() as Object
	Public Method Get()
	Public Method RowToJson()
	Public Method AddCustomFields()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} PaymentConditionsProtheusData::New() as Object
	Obtem uma nova instancia da classe PaymentConditionsProtheusData
	@type		method
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		29/09/2020
	@version	12.1.33
	@return		object, nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method New() as Object CLASS PaymentConditionsProtheusData
	_Super:New( "GET", .T. )
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GPaymentConditionsProtheusData::Getet
	Efetua a consulta de condições de pagamento conforme a data de atualização.
	@type		method
	@version	12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since		21/01/2021
	@param		nPage		, numeric	, Número da página que será retornada
	@param		nPageSize	, numeric	, Quantidade de registros por pagina
	@param		cData		, character , Data de sincronismo
/*/
//------------------------------------------------------------------------------
Method Get(nPage as Numeric, nPageSize as Numeric, cDate as Character) CLASS PaymentConditionsProtheusData
	Local aArea 	as Array
	Local cWhere	as Character

	Default nPage   	:= 1
	Default nPageSize 	:= 10
	Default cDate       := ""

	aArea := GetArea()
	AddMapFields( self )

    ::AddCustomFields()
	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( getQueryTGV("SE4") )

	cWhere := " SE4.E4_FILIAL = '" + FwXFilial("SE4") + "'"

	If __lPGVPCQRY
		cWhere += ExecBlock("GVPCQRY", .F., .F.)
	Endif

	If !Empty(cDate) .And. SubStr(cDate,1,1) != '-'
		cWhere += " AND SE4.S_T_A_M_P_ > " + ConvertTimeStamp( cDate, "value", "timestamp" )
	Else
		cWhere += " AND SE4.D_E_L_E_T_ = ' ' "
	EndIf
  
	::SetWhere( cWhere )
	::SetOrder( "E4_FILIAL, E4_CODIGO" )

	If ::Execute()
		::FillGetResponse()
	EndIf

	RestArea( aArea )
	aSize( aArea, 0 )
    aSize(::aCustomFields, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} PaymentConditionsProtheusData::RowToJson
	Converte o retorno da consulta SQL em JSON
	@type		method
	@version	12.1.33
	@author 	Danilo Salve / Squad CRM & Faturamento
	@since 		22/12/2020
	@param		cAlias	, character	, Alias da Consulta
	@param		aFields	, array		, Campos (MapFields)
	@param		lEmpty	, logical	, Vazio.
/*/
//------------------------------------------------------------------------------
METHOD RowToJson( cAlias as Character, aFields as Array , lEmpty as Logical) CLASS PaymentConditionsProtheusData
	Default lEmpty := .F.
	ConvertResponseValues(aFields, cAlias, lEmpty, @Self)

	if (cAlias)->(!Eof()) .And. (cAlias)->(!Bof())
		Self:oJsonObj:setProp( "internalid"	, ((cAlias)->E4_FILIAL + (cAlias)->E4_CODIGO) , .F. )
	endIf
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API
	@type		function
	@version	12.1.33
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		29/09/2020
	@param		oSelf	, Object	, Objeto com herança da classe FWAdapterBaseV2
/*/
//------------------------------------------------------------------------------
Static Function AddMapFields( oSelf as Object )
	oSelf:AddMapFields( "branchid"          , "E4_FILIAL"	, .T., .F., { "E4_FILIAL"	, 'C', GetFieldLength("E4_FILIAL")  , 0 } )
	oSelf:AddMapFields( "paymentcode"       , "E4_CODIGO"	, .T., .F., { "E4_CODIGO"	, 'C', GetFieldLength("E4_CODIGO")  , 0 } )
	oSelf:AddMapFields( "paymentdescription", "E4_DESCRI"	, .T., .F., { "E4_DESCRI"	, 'C', GetFieldLength("E4_DESCRI")  , 0 } )
	if SE4->(ColumnPos("E4_MSBLQL")) > 0
		oSelf:AddMapFields( "status"			, "E4_MSBLQL"	, .T., .F.,	{ "E4_MSBLQL"	, 'C', GetFieldLength("E4_MSBLQL")	, 0 })
	endIf
	oSelf:AddMapFields( "deleted"           , "deleted"     , .T., .F., { "deleted"     , 'C', 1, 0 }, "SE4.D_E_L_E_T_" )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} PaymentConditionsProtheusData::AddCustomFields
	Metodo Auxiliar, deve carregar os Atributos utilizados na conversão dos valores.
	@type		method
	@version	12.1.33
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		04/06/2021
/*/
//------------------------------------------------------------------------------
METHOD AddCustomFields() CLASS PaymentConditionsProtheusData
	Self:cSource:= "TGVPAYMENTCONDITIONS"
	Self:isAdmin := FwIsAdmin()
	Self:aCustomFields := {}
	Self:nFields := Len(Self:oJsonObj:aMapFields)
	Self:cCustomFields := ""
	Self:lcanObfuscateField := CanAccessProtectedData(Self:isAdmin)
Return Nil
