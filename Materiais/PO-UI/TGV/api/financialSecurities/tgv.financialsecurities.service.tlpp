#include 'tlpp-core.th'

namespace tgv.financialsecurities

static __instance as Object

//------------------------------------------------------------------------------
/*/{Protheus.doc} FinancialSecuritiesService
    Disponibiliza um serviço para consultar os títulos a receber.

    @sample		FinancialSecuritiesService
    @author		Danilo Salve
    @since		18/09/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Class FinancialSecuritiesService
	Data jResponse as Json

	Public Method New() as Object
	Public Method GetInstance() as Object
	Public Method Get() as Json
	Public Method GetDefault() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
    Disponibiliza um serviço para consultar os titulos a receber

    @sample		FinancialSecuritiesService:New()
    @author		Danilo Salve
    @since		18/09/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class FinancialSecuritiesService
	::jResponse := JsonObject():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
    Retorna uma instancia do serviço

    @sample		FinancialSecuritiesService:GetInstance()
    @author		Danilo Salve
    @since		18/09/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetInstance() as Object class FinancialSecuritiesService
	If (__instance == Nil)
		__instance := FinancialSecuritiesService():New()
	Endif
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAll
    Retorna todos os titulos a receber

    @sample		FinancialSecuritiesService:GetAll(1, 10, aQueryString, '2020-09-18')
    @param      nPage,      Numerico    , Numero da Página
    @param      nPageSize,  Numerico    , Tamanho da página
    @param      aQueryString, Array     , Filtro utilizado para filtrar os registros
    @author		Danilo Salve
    @since		21/01/2021
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method Get(nPage as Numeric, nPageSize as Numeric, aQueryString as Array) as Json Class FinancialSecuritiesService
	Local oData := FinancialsecuritiesDataFactory():GetData()

	Default nPage           := 1
	Default nPageSize       := 10
	Default aQueryString    := {}

	oData:Get(nPage, nPageSize, aQueryString)

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	Else
		::jResponse := SetRestFault(oData:GetCode(), oData:GetMessage())
	EndIf

	oData:DeActivate()
	FreeObj(oData)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetDefault
    Consulta a inadimplencia de um cliente especifico

    @sample		FinancialSecuritiesService:GetDefault('000001', '01')
    @param      cCode   ,  Char, Codigo do Cliente
    @param      cStore  ,  Char, Loja do Cliente
    @author		Danilo Salve
    @since		25/09/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetDefault(cCode as Character, cStore as Character) as Json Class FinancialSecuritiesService
	Local oData := FinancialsecuritiesDataFactory():GetData()

	::jResponse := oData:GetDefault(cCode, cStore)

	FreeObj(oData)
return ::jResponse
