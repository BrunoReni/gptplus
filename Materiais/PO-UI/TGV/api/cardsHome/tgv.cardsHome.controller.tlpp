#include "tlpp-core.th"

namespace tgv.cardsHome
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvCardsHome
  API para consulta dos Cards (Indicadores de Venda)

  @author		Squad CRM & Faturamento
  @since		06/01/2021
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvCardsHome
  Public Method New() as Object

	@Get("/api/tgv/cardsHome/")
	Public Method GetCards() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
  Cria um novo objeto de Cards (Indicadores de Venda)

  @sample		tgvCardsHome:New()
  @author		Squad CRM & Faturamento
  @since		06/01/2021
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvCardsHome
return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetCards - /api/tgv/cardsHome/
  Efetua o GET de todos os Cards

  @sample   tgvCardsHome:GetCards()
  @author		Squad CRM & Faturamento
  @since		06/01/2021
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetCards() as Logical Class tgvCardsHome
	Local dInitialDate  := Date()
	Local dFinalDate    := Date()
	Local jResposta     := JsonObject():new()
	Local oService      := CardsHomeService():getInstance()

	If (oRest:getQueryRequest():GetJsonText('initialDate') != 'null')
		dInitialDate := STOD(oRest:getQueryRequest():GetJsonText('initialDate'))
	EndIf

	If (oRest:getQueryRequest():GetJsonText('finalDate') != 'null')
	  dFinalDate := STOD(oRest:getQueryRequest():GetJsonText('finalDate'))
	EndIf

	jResposta:fromJson(oService:getCards(dInitialDate, dFinalDate))
	AnswerRest(jResposta)
return .T.
