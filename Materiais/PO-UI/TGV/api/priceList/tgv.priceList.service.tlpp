#include "tlpp-core.th"

namespace tgv.priceList

static __instance as object

//------------------------------------------------------------------------------
/*/{Protheus.doc} PriceListService
	Disponibiliza um serviço para consultar as tabelas de preço

	@author		Squad CRM/Faturamento
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class PriceListService
	Public Method New() as Object
	Public Method getInstance() as Object
	Public Method GetPriceList() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Disponibiliza um serviço para consultar as tabelas de preço

	@author		Squad CRM/Faturamento
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class PriceListService
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} getInstance
	Retorna uma instancia do serviço

	@author		Squad CRM/Faturamento
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object Class PriceListService
	If (__instance == NIL)
		__instance := PriceListService():New()
	EndIf
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetPriceList
	Retorna todas as tabelas de preço (apenas cabeçalho - DA0)

	@param		nPage		, Numeric	, Numero da Página
	@param		nPageSize	, Numeric	, Tamanho da página
	@param		cDate		, Character	, Data de sincronismo
	@param		lItem		, Logical	, Obtem lista de Itens ou Cabeçalhos
	@author		Squad CRM/Faturamento
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetPriceList(nPage as Numeric, nPageSize as Numeric, cDate as Character, lItem as Logical) as Json Class PriceListService
	Local oPriceLists   := PriceListDataFactory():getData()
	Local jResponse     := JsonObject():New()

	Default nPage       := 1
	Default nPageSize   := 10
	Default cDate       := ""
	Default lItem       := .F.

	If lItem
		oPriceLists:GetItem(nPage, nPageSize, cDate)
	Else
		oPriceLists:GetHeader(nPage, nPageSize, cDate)
	Endif

	If oPriceLists:lOk
		jResponse := oPriceLists:getJSONResponse()
	Else
		jResponse := SetRestFault(oPriceLists:GetCode(), oPriceLists:GetMessage())
	EndIf

	oPriceLists:DeActivate()
	FreeObj(oPriceLists)
Return jResponse
