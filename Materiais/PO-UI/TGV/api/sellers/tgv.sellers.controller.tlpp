#include 'tlpp-core.th'

namespace tgv.sellers
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvSellers
	API para integração dos Vendedores

	@author		Danilo Salve
	@since		06/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvSellers
	Public Data nPage      as Numeric
	Public Data nPageSize  as Numeric
	Public Data jResposta  as Json
	Public Data oService   as Object

	Public Method New() as Object

	@Get("/api/tgv/sellers/codeuser")
	Public Method GetSeller() as Logical

EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvSellers
	Cria uma nova instacia do objeto tgvSellers

	@sample		tgvSellers:New()
	@author		Danilo Salve
	@since		06/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvSellers
	::nPage		:= 1
	::nPageSize	:= 10
	::jResposta	:= JsonObject():New()
	::oService	:= SellersService():GetInstance()
Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetSeller
	Retorna os dados do Vendedor logado.

	@sample		GET /api/tgv/sellers/codeuser/
	@author		Danilo Salve
	@since		06/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetSeller() as Logical Class tgvSellers
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:GetSeller(::nPage, ::nPageSize, __cUserId))
	AnswerRest(::jResposta)
Return .T.
