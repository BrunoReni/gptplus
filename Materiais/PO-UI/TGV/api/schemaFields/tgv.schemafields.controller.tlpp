#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace tgv.schemafields
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvSchemaFields
	API para integração de Campos do Schema - Utilizada no TOTVS Gestão de Vendas

	@author		Danilo Salve
	@since		31/03/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvSchemaFields
	Public Data jResposta  as Json
	Public Data nPage      as Numeric
	Public Data nPageSize  as Numeric
	Public Data oService   as Object

	Public Method New() as Object

	@Get("/api/tgv/schemafields/")
	Public Method Get() as Logical

	@Get("/api/tgv/schemafields/diff/:dateSync")
	Public Method GetDiff() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvSchemaFields
	Cria uma nova instacia do objeto tgvSchemaFields

	@sample		tgvSchemaFields:New()
	@author		Danilo Salve
	@since		31/03/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvSchemaFields
	::nPage         := 1
	::nPageSize     := 10
	::oService      := SchemaFieldsService():getInstance()
	::jResposta     := JsonObject():New()
Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} Get
	Retorna todos Campos

	@sample		GET /api/tgv/prospects/
	@author		Danilo Salve
	@since		31/03/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method Get() as Logical Class tgvSchemaFields
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:getSchemaFields(::nPage, ::nPageSize, /*cDateSync*/))
	AnswerRest(::jResposta)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetDiff
	Retorna somente os Campos alterados, conforme data informado no parametro DateSync

	@sample		GET /api/tgv/prospects/diff/:dateSync
	@author		Danilo Salve
	@since		31/03/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetDiff() as Logical Class tgvSchemaFields
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:getSchemaFields(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResposta)
Return .T.
