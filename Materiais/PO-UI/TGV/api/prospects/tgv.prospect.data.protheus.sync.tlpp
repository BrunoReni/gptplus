#include 'tlpp-core.th'

namespace totvs.protheus.backoffice.tgvProspects
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProspectProtheusDataSync
	Efetua a consulta dos Prospects no Protheus.
	@type class
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 19/01/2022
/*/
//------------------------------------------------------------------------------
class ProspectProtheusDataSync from FWAdapterBaseV2
	public data aCustomFields		as array
	public data cCustomFields		as character
	public data cSource				as character
	public data isAdmin				as logical
	public data lcanObfuscateField	as logical
	public data lItems 				as logical
	public data oHashFieldsPD		as object
	public data nFields				as numeric

	public method new() as object
	public method get()
	public method RowToJson()
	public method AddCustomFields()
endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProspectProtheusDataSync::new() as Object
	obtem uma nova instancia da classe ProspectProtheusDataSync
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 19/01/2022
	@return object, nova intancia da classe ProspectProtheusDataSync
/*/
//------------------------------------------------------------------------------
method new() as object class ProspectProtheusDataSync
	_Super:New( "GET", .T. )
	::oHashFieldsPD := tHashMap():New()
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProspectProtheusDataSync::get
	Obtem uma Lista de Prospects
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 19/01/2022
	@param nPage, numeric, número da página
	@param nPageSize, numeric, Quantidade de registros por página
	@param cDataSync, character, data de sincronismo/modificação (S_T_A_M_P_)
/*/
//------------------------------------------------------------------------------
method get(nPage as numeric, nPageSize as numeric, cDateSync as character) class ProspectProtheusDataSync
	
	Local aArea	:= GetArea() as array
	Local cInnerJoin As Character
	Local cConcate	:= IIF(AllTrim( Upper( TcGetDb() ) ) == "MSSQL","+","||") As Character

	AddMapFieldsSUS( self, .T. )

	cInnerJoin+=" INNER JOIN "+RetSqlName('AQ5')+" AQ5"
	cInnerJoin+=" ON"
	cInnerJoin+=" SUS.US_COD " + cConcate + " SUS.US_LOJA = AQ5.AQ5_CODENT"
	cInnerJoin+=" INNER JOIN " + RetSqlName('AQ4') + " AQ4"
	cInnerJoin+=" ON"
	cInnerJoin+=" AQ5.AQ5_UUID = AQ4.AQ4_UUID" 
	cInnerJoin+=" AND AQ4.AQ4_FILIAL = AQ5.AQ5_FILIAL"
	cInnerJoin+=" AND AQ5.D_E_L_E_T_ = AQ4.D_E_L_E_T_"

	::AddCustomFields()
	::setPage(nPage)
	::setPageSize(nPageSize)
	::SetQuery( getQueryTGV("SUS", cInnerJoin) )

	::SetWhere( getProspectFilter(::isAdmin, cDateSync,/*cCode*/, /*cStore*/, /*cSeller*/, .F.,.T.) )
	::SetOrder( "SUS.US_FILIAL, SUS.US_COD, SUS.US_LOJA" )
	
	If ::Execute()
		::FillGetResponse()
	EndIf

	FTPDUnload()
	::oHashFieldsPD:Clean()

	RestArea( aArea )
	aSize( aArea, 0 )
	aSize(::aCustomFields, 0)
	FreeObj(::oHashFieldsPD)
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProspectProtheusDataSync::RowToJson
	Efetua a conversão da consulta SQL em um objeto JSON
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 19/01/2022
	@param cAlias, character, Retorno da consulta
	@param aFields, array, Lista de Campos utilizados no retorno
	@param lEmpty, logical, Parametro reservado
/*/
//------------------------------------------------------------------------------
method RowToJson( cAlias As character, aFields as array, lEmpty as logical) class ProspectProtheusDataSync
	Default lEmpty := .F.
	ConvertResponseValues(aFields, cAlias, lEmpty, @Self )
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProspectProtheusDataSync::AddCustomFields
	Metodo Auxiliar, deve carregar os Atributos utilizados na conversão dos valores.
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 04/06/2021
/*/
//------------------------------------------------------------------------------
method AddCustomFields() class ProspectProtheusDataSync
	Self:cSource:= "TGVPROSPECTS"
	Self:isAdmin := FwIsAdmin()
	Self:aCustomFields := {}
	Self:nFields := Len(Self:oJsonObj:aMapFields)

	aAdd(Self:aCustomFields, {"internalid", {||GetInternalIdQry((cAlias)->US_FILIAL + (cAlias)->US_COD + (cAlias)->US_LOJA)}})

	Self:cCustomFields := "internalid"

	If !Self:isAdmin
		Self:oHashFieldsPD := setProtectedFields(Self:oJsonObj:aMapFields, Self:cSource)
	Endif

	Self:lcanObfuscateField := CanAccessProtectedData(Self:isAdmin)
return nil
