#INCLUDE 'tlpp-core.th'
#INCLUDE 'FWMVCDEF.CH'

namespace tgv.integrationQueue

static __instance as Object

//------------------------------------------------------------------------------
/*/{Protheus.doc} IntegrationQueueService
	Disponibiliza um serviço para Consulta e Manutenção dos registros da fila
	de processamento (A1T).

	@sample		IntegrationQueueService
	@author		Danilo Salve
	@since		08/12/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class IntegrationQueueService
	Data jResponse  as Json

	Public Method New() as Object
	Public Method GetInstance() as Object
	Public Method GetQueue() as Json
	Public Method WriteOnQueue() as Json
Endclass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Disponibiliza uma nova intancia do serviço Fila de Processamento dos Registros.

	@sample		IntegrationQueueService:New()
	@author		Danilo Salve
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class IntegrationQueueService
	::jResponse := JsonObject():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
	Retorna uma instancia do serviço

	@sample		IntegrationQueueService:GetInstance()
	@author		Danilo Salve
	@since		08/12/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetInstance() as Object class IntegrationQueueService
	If (__instance == Nil)
		__instance := IntegrationQueueService():New()
	Endif
Return __instance
//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQueue
	Retorna os registros que foram incluídos na fila de processamento

	@sample		IntegrationQueueService:GetQueue(1, 10, '2020-09-30')
	@param		nPage		, Numeric	, Numero da Página
	@param		nPageSize	, Numeric	, Tamanho da página
	@param		cData		, Character	, Data de sincronismo
	@return		jResponse	, Json		, Resposta da Requisição
	@author		Danilo Salve
	@since		08/12/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetQueue(nPage as Numeric, nPageSize as Numeric, cData as Character ) as Json Class IntegrationQueueService
	Local oData := IntegrationQueueDataFactory():GetData("GET")

	Default nPage       := 1
	Default nPageSize   := 10

	oData:GetQueue(nPage, nPageSize, cData)

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	Else
		::jResponse := SetRestFault(oData:GetCode(), oData:GetMessage())
	EndIf

	oData:DeActivate()
	FreeObj(oData)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} PostQueue
	Inclui um registro na fila de processamento.

	@sample		IntegrationQueueService:PostQueue(jBody,)
	@param		jBody       , Json		, Objecto com o corpo da requisição
	@param		cOperation  , Character	, Tipo de Operação
	@param		cExternalId , Character	, Identificador externo da entidade
	@param		cSource     , Character	, Recurso ( Pedido de Venda ou Orçamento )
	@return		jResponse   , Json		, Resposta da Requisição
	@author		Danilo Salve
	@since		08/12/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method WriteOnQueue(jBody as Json, cOperation as Character, cExternalId as Character, cSource as Character) as Json class IntegrationQueueService
	Local oData := IntegrationQueueDataFactory():GetData("POST")

	Default cOperation  := "3"
	Default cExternalId := ""

	oData:WriteOnQueue(jBody, IIF( Empty(cExternalId), "3", cOperation), cExternalID, /*cSource*/)

	oData:DeActivate()
	FreeObj(oData)
Return ::jResponse
