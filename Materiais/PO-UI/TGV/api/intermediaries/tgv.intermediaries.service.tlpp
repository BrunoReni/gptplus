#include "tlpp-core.th"

namespace tgv.intermediaries

static __oInstance as object

//------------------------------------------------------------------------------
/*/{Protheus.doc} IntermediariesService
	Disponibiliza um serviço de Intermediadores.

	@sample		IntermediariesService():New()
	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class IntermediariesService
	Public Method new() as Object
	Public Method getInstance() as Object
	Public Method getIntermediaries() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Disponibiliza uma nova instancia do serviço Intermediadores.

	@sample		IntermediariesService():New()
	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method new() as Object Class IntermediariesService
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
	Obtem uma nova instanacia do servido de Intermediadores

	@sample		IntermediariesService():getInstance()
	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object Class IntermediariesService
	If (__oInstance == Nil)
		__oInstance := IntermediariesService():New()
	EndIf
Return __oInstance

//------------------------------------------------------------------------------
/*/{Protheus.doc} getIntermediaries
	Obtem um Json com os dados dos intermediadores.

	@sample		IntermediariesService():getIntermediaries(1, 10, '2021-06-23')
	@param		nPage		, Numeric	, Numero da Pagina
	@param		nPageSize	, Numeric	, Tamanho da Pagina
	@param		cDateSync	, Character	, Data de Sincronismo/alteracao
	@return		JsonObject	, Reposta da Consulta/Requisição
	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getIntermediaries(nPage as Numeric, nPageSize as Numeric, cDateSync as Character) as Json class IntermediariesService
	Local jResponse := JsonObject():New()
	Local oData := IntermediariesDataFactory():getData()

	Default nPage       := 1
	Default nPageSize   := 10
	Default cDateSync	:= ""

	oData:getIntermediaries(nPage, nPageSize, cDateSync)

	If oData:lOk
		jResponse := oData:getJSONResponse()
	Else
		jResponse := SetRestFault(oData:GetCode(), oData:GetMessage())
	EndIf

	oData:DeActivate()
	FreeObj(oData)
Return jResponse
