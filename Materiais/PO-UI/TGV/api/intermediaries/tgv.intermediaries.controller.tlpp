#include "tlpp-core.th"

namespace tgv.intermediaries
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvIntermediaries
	API para consulta de Intermediadores do TOTVS Gestão de Vendas

	@author		Danilo SAlve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvIntermediaries
	Data jResposta  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method new() as Object

	@Get("/api/tgv/intermediaries/")
	Public Method getIntermediaries() as Logical

	@Get("/api/tgv/intermediaries/diff/:dateSync")
	Public Method getIntermediariesDiff() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} new
	Método para criar uma nova instância do objeto

	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method new() as Object Class tgvIntermediaries
	::nPage     := 1
	::nPageSize := 10
	::oService  := IntermediariesService():getInstance()
	::jResposta := JsonObject():New()
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} getIntermediaries
	Obtem uma lista de todos os intermediarios

	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getIntermediaries() as Logical Class tgvIntermediaries
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:getIntermediaries(::nPage, ::nPageSize, /*cData*/))
	AnswerRest(::jResposta)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} getIntermediariesDiff
	Obtem uma lista de todos os intermediarios modificados conforme parametro Diff.

	@author		Danilo Salve
	@since		23/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getIntermediariesDiff() as Logical Class tgvIntermediaries
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:getIntermediaries(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResposta)
return .T.
