#include "tlpp-core.th"
#include "msobject.ch"

namespace tgv.productBalance
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProductBalance
	API para consulta de saldo de produtos do TOTVS Gestão de Vendas.

	@author	Squad CRM/Faturamento
	@since	04/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvProductBalance
	Data jResposta  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method New() as Object

	@Get("/api/tgv/productBalance/")
	Public Method GetAllProductBalance() as Logical

	@Get("/api/tgv/productBalance/diff/:dateSync")
	Public Method GetAllProductBalanceDiff() as Logical

	@Get("/api/tgv/productBalance/:product")
	Public Method GetProductBalanceById() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProductBalance
	Método para nova instância do objeto.

	@author	Squad CRM/Faturamento
	@since	04/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvProductBalance
	::nPage     := 1
	::nPageSize := 10
	::oService  := ProductBalanceService():getInstance()
	::jResposta := JsonObject():New()
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAllProductBalance - /api/tgv/productBalance/
	Efetua o GET do saldo de todos os produtos.

	@author	Squad CRM/Faturamento
	@since	04/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetAllProductBalance() as Logical Class tgvProductBalance
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:getProductBalance(::nPage, ::nPageSize, /*cCodeProduct*/,/*cDate*/))
	AnswerRest(::jResposta)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAllProductBalanceDiff - /api/tgv/productBalance/:data
	Efetua o GET do saldo de todos os produtos alterados/incluídos e não sincronizados.

	@author	Squad CRM/Faturamento
	@since	04/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetAllProductBalanceDiff() as Logical Class tgvProductBalance
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:getProductBalance(::nPage, ::nPageSize, /*cCodeProduct*/, GetSyncDate()))
	AnswerRest(::jResposta)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAllProductBalanceById - /api/tgv/productBalance/:id
	Efetua o GET do saldo de um produto específico.

	@author		Squad CRM/Faturamento
	@since		04/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetProductBalanceById() as Logical Class tgvProductBalance
	Local jPath	:= oRest:getPathParamsRequest()	
	
	::jResposta:fromJson(::oService:getProductBalance(/*nPage*/, /*nPageSize*/, jPath["product"], /*cDate*/))
	AnswerRest(::jResposta)	

	FreeObj(jPath)
return .T.
