#include "tlpp-core.th"

namespace tgv.productBalance

static __oInstance as object

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProductBalanceService
    Disponibiliza um serviço de Saldo de Produtos

    @sample		ProductBalanceService
    @author		Squad CRM & FAturamento
    @since		04/11/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Class ProductBalanceService
	Public Method New() as Object
	Public Method getInstance() as Object
	Public Method getProductBalance() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
    Disponibiliza uma nova intancia do serviço.

    @sample		ProductBalanceService:New()
    @author		Squad CRM & FAturamento
    @since		04/11/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class ProductBalanceService
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
    Retorna uma instancia do serviço

    @sample		ProductBalanceService:getInstance()
    @author		Squad CRM & FAturamento
    @since		04/11/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object class ProductBalanceService
	If (__oInstance == NIL)
		__oInstance := ProductBalanceService():New()
	EndIf
Return __oInstance

//------------------------------------------------------------------------------
/*/{Protheus.doc} getProductBalance
    Efetua a consulta de Saldo de produtos

    @sample		ProductBalanceService:getProductBalance()
    @param      nPage       , Numerico  , Pagina
    @param      nPageSize   , Numerico  , Tamanho da Pagina
    @param      cCodeProduct,  Caractere , Codigo do Produto
    @param      cDate       ,  Caractere , Data de Sincronismo/alteracao
    @return     JsonObject  , Reposta da Consulta/Requisição
    @author		Danilo Salve
    @since		22/01/2021
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getProductBalance(nPage as Numeric, nPageSize as Numeric, cCodeProduct as Character, cDate as Character) as Json Class ProductBalanceService
	Local oProducts := ProductBalanceDataFactory():getData()
	Local jResponse := JsonObject():New()

	Default nPage           := 1
	Default nPageSize       := 10
	Default cCodeProduct 	:= ""
	Default cDate 			:= ""

	oProducts:Get(nPage, nPageSize, cCodeProduct, cDate)

	If oProducts:lOk
		jResponse := oProducts:getJSONResponse()
	Else
		jResponse := SetRestFault(oProducts:GetCode(), oProducts:GetMessage())
	EndIf

	oProducts:DeActivate()
	FreeObj(oProducts)
Return jResponse
