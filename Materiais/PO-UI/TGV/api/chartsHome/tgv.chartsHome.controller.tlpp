#include 'tlpp-core.th'

namespace tgv.chartsHome
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvChartsHome
	API para consulta dos Graficos de Indicadores de Venda.

	@author		Squad CRM & Faturamento
	@since		07/01/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvChartsHome
	Public Method New() as Object

	@Get('/api/tgv/chartsHome/')
	Public Method GetCharts() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Cria um novo objeto de Graficos de Indicadores de Venda.

	@sample		tgvChartsHome:New()
	@author		Squad CRM & Faturamento
	@since		07/01/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvChartsHome
Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetCards - /api/tgv/chartsHome/
	Efetua o GET de todos os Graficos de Indicadores de Venda

	@sample		tgvChartsHome:GetCards()
	@author		Squad CRM & Faturamento
	@since		07/01/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetCharts() as Logical Class tgvChartsHome
	Local cType         as Character
	Local dInitialDate  := Date()
	Local dFinalDate    := Date()
	Local jResposta     := JsonObject():New()
	Local oService      := ChartsHomeService():GetInstance()

	If (oRest:getQueryRequest():GetJsonText('type') != 'null')
		cType := oRest:getQueryRequest():GetJsonText('type')
	EndIf

	If (oRest:getQueryRequest():GetJsonText('initialDate') != 'null')
		dInitialDate := STOD(oRest:getQueryRequest():GetJsonText('initialDate'))
	EndIf

	If (oRest:getQueryRequest():GetJsonText('finalDate') != 'null')
		dFinalDate := STOD(oRest:getQueryRequest():GetJsonText('finalDate'))
	EndIf

	jResposta:fromJson(oService:getCharts(cType, dInitialDate, dFinalDate))
	AnswerRest(jResposta)
Return .T.
