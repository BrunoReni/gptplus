#include "tlpp-core.th"

namespace tgv.carriers

static __instance as object

//------------------------------------------------------------------------------
/*/{Protheus.doc} CarriersService
    Disponibiliza um serviço de Transportadoras.

    @sample		CarriersService
    @author		Danilo Salve
    @since		01/10/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Class CarriersService
	Public Method New() as Object
	Public Method getInstance() as Object
	Public Method GetCarriers() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
    Disponibiliza uma nova intancia do serviço Trasportadoras.

    @sample		CarriersService:New()
    @author		Danilo Salve
    @since		01/10/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class CarriersService
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
    Retorna uma instancia do serviço

    @sample		CarriersService:GetInstance()
    @author		Danilo Salve
    @since		01/10/2020
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object class CarriersService
	If (__instance == Nil)
		__instance := CarriersService():New()
	EndIf
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetCarriers
    Efetua a consulta de Transportadoras

    @sample		CarriersService:GetCarriers()
    @param      nPage       , Numerico  , Pagina
    @param      nPageSize   , Numerico  , Tamanho da Pagina
    @param      cDate       ,  Caractere , Data de Sincronismo/alteracao
    @return     JsonObject  , Reposta da Consulta/Requisição
    @author		Danilo Salve
    @since		19/01/2021
    @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetCarriers(nPage as Numeric, nPageSize as Numeric, cDate as Character) as Json class CarriersService
	Local jResponse := JsonObject():New()
	Local oCarriers := CarriersDataFactory():getData()

	Default nPage       := 1
	Default nPageSize   := 10
	Default cDate       := ""

	oCarriers:Get(nPage, nPageSize, cDate)

	If oCarriers:lOk
		jResponse := oCarriers:getJSONResponse()
	Else
		jResponse := SetRestFault(oCarriers:GetCode(), oCarriers:GetMessage())
	EndIf

	oCarriers:DeActivate()
	FreeObj(oCarriers)
Return jResponse
