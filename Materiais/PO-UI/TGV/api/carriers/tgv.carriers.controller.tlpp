#include "tlpp-core.th"

namespace tgv.carriers
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvCarriers
	API para consulta de transportadoras do TOTVS Gestão de Vendas

	@author		Squad CRM/Faturamento
	@since		07/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class tgvCarriers
	Data jResposta  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method New() as Object

	@Get("/api/tgv/carriers/")
	Public Method GetAllCarriers() as Logical

	@Get("/api/tgv/carriers/diff/:dateSync")
	Public Method GetAllCarriersDiff() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
  Método para nova instância do objeto

  @author	Squad CRM/Faturamento
  @since	07/10/2020
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class tgvCarriers
	::nPage     := 1
	::nPageSize := 10
	::oService  := CarriersService():getInstance()
	::jResposta := JsonObject():New()
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAllCarriers - /api/tgv/carriers/
	Efetua o GET de todas as transportadoras

	@author		Squad CRM/Faturamento
	@since		07/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetAllCarriers() as Logical Class tgvCarriers
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:GetCarriers(::nPage, ::nPageSize, /*cData*/))
	AnswerRest(::jResposta)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAllCarriersDiff - /api/tgv/carriers/:data
	Efetua o GET de todas as transportadoras alteradas/incluídas e não sincronizadas

	@author		Squad CRM/Faturamento
	@since		07/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetAllCarriersDiff() as Logical Class tgvCarriers
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResposta:fromJson(::oService:GetCarriers(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResposta)
return .T.
