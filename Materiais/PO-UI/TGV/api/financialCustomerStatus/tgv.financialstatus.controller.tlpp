#include 'tlpp-core.th'
#include "tgv.customer.controller.ch"

namespace tgv.financialstatus
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvFinancialStatus
	API para consulta do status financeido do cliente.
	@type		class
	@version	12.1.33
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		27/08/2021
/*/
//------------------------------------------------------------------------------
Class tgvFinancialStatus
	Data jResponse  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method new() as Object

	@Get("/api/tgv/financialstatus/sync")
	Public Method getAllSync() as Logical

	@Get("/api/tgv/financialstatus/sync/diff/:dateSync")
	Public Method getDiffSync() as Logical

	@Get("/api/tgv/financialstatus")
	Public Method getAll() as Logical

	@Get("/api/tgv/financialstatus/:id/:store")
	Public Method getById() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvFinancialStatus::new() as Object
	Cria uma nova instacia da classe tgvFinancialStatus
	@type		method
	@version	12.1.27
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		27/08/2021
	@return		object, nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method new() as Object Class tgvFinancialStatus
	::nPage     := 1
	::nPageSize := 10
	::oService  := FinancialStatusService():getInstance()
	::jResponse := JsonObject():new()
Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvFinancialStatus::getAllSync() as Logical
	Deve obter uma lista de status financeiro de clientes, utilizando o padrão
	de campos do PO-SYNC.
	@type		method
	@version	12.1.27
	@author		Squad CRM & Faturamento
	@since		24/09/2020
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getAllSync() as Logical Class tgvFinancialStatus
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getAllSync(::nPage, ::nPageSize, /*cDate*/))
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvFinancialStatus::getDiffSync() as Logical
	Efetua o GET dos regitros alterados, filtrando pelo campo S_T_A_M_P_.
	@type		method
	@version	Squad CRM & Faturamento
	@author		Squad CRM & Faturamento
	@since		24/09/2020
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getDiffSync() as Logical Class tgvFinancialStatus
	GetPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:FromJson(::oService:getAllSync(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvFinancialStatus::getAll() as Logical
	Obtem uma Lista de Status Financeiro dos clientes, possibilitando filtrar
	(QueryParam - FILTER) os clientes e permitindo retornar todos os campos ou
	somente os campos especificados no QueryParam FIELDS.
	Observação: Ao utilizar o GETOnline o campo DELETED não será retornado. 
	@type		method
	@version	12.1.33
	@author		Squad CRM & Faturamento
	@since		27/07/2021
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getAll() as Logical Class tgvFinancialStatus
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields 		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character

	GetPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:FromJson(::oService:getAll(::nPage, ::nPageSize, aURLFilter, cFields, /*customerID*/, /*cCustomerStore*/ cSort))
	AnswerRest(::jResponse)
	
	aSize(aURLFilter, 0)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc}  tgvFinancialStatus::getById() as Logical
	Deve obter o status financeiro de um cliente filtrando pelo Código e Loja.
	Obtem o status financeiro do cliente filtrando pelo campo Filial e Codigo e Loja,
	considerando a filial atual e o parametro cCustomerId da requisição,
	permitindo retornar todos os campos ou somente os campos especificados no
	QueryParam FIELDS.
	Observação: Ao utilizar o GETOnline o campo DELETED não será retornado.
	@type		method
	@author		Squad CRM & Faturamento
	@since		27/07/2021
	@version	12.1.33
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getById() as Logical Class tgvFinancialStatus
	Local cFields	:= getQueryParam('FIELDS') as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')

	::jResponse:FromJson(::oService:getAll(1, 10, {}, cFields, GetPathParams("id"), GetPathParams("store")))

	If Empty(::jResponse["id"])
		SetRestFault(404, STR0001) // Cliente não encontrado
	Else
		oRest:setResponse(::jResponse)
	EndIf
Return .T.
