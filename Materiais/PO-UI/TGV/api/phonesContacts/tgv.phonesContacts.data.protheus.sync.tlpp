#include "tlpp-core.th"

namespace tgv.phoneContacts
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} PhoneContactsProtheusDataSync
  Efetua a consulta dos Telefones dos Contatos Protheus.

  @author	Squad CRM & Faturamento
  @since	17/09/2020
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
CLASS PhoneContactsProtheusDataSync FROM FWAdapterBaseV2
	Public Data aCustomFields		as Array
	Public Data cCustomFields		as Character
	Public Data cSource				as Character
	Public Data isAdmin				as Logical
	Public Data lcanObfuscateField	as Logical
	Public Data oHashFieldsPD		as Object
	Public Data nFields				as Numeric

	Public Method New() as Object
	Public Method GetAllSync()
	Public Method RowToJson()
	Public Method AddCustomFields()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Cria uma nova instancia do Objeto PhoneContactsProtheusDataSync.

	@sample		PhoneContactsProtheusDataSync():New("GET", .T.)
	@param		cVerbo	, Character	, Verbo que será utilizado na requisição
	@param		lList	, Logical	, Se irá listar o Json
	@author		Squad CRM/Faturamento
	@since		17/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New(cVerbo as Character, lList as Logical) as Object CLASS PhoneContactsProtheusDataSync
	Default cVerbo  := "GET"
	Default lList   := .T.

	_Super:New( cVerbo, lList )
	::oHashFieldsPD := tHashMap():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} Get
	Efetua a consulta de Telefones.

	@param		nPage		, Numerico	, Número da página que será retornada
	@param		nPageSize	, Numerico	, Quantidade de registros por página
	@param		cDate		, Character	, Data de Sincronismo/Alteracao
	@author		Danilo Salve
	@since		21/01/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetAllSync(nPage as Numeric, nPageSize as Numeric, cDate as Character) CLASS PhoneContactsProtheusDataSync
	Local aArea 	as Array
	Local cWhere	as Character

	Default nPage   	:= 1
	Default nPageSize 	:= 10
	Default cDate		:= ""

	aArea   := GetArea()
	AddFields( self, .F. )

	::AddCustomFields()
	::setPage(nPage)
	::setPageSize(nPageSize)
	::SetQuery( GetQueryTGV("AGB") )

	cWhere := " AGB.AGB_FILIAL = '" + FwXFilial("AGB") + "'"

	If !Empty(cDate) .And. SubStr(cDate,1,1) != '-'
		cWhere += " AND AGB.S_T_A_M_P_ > " + ConvertTimeStamp( cDate, "value", "timestamp" )
	Else
		cWhere += " AND AGB.D_E_L_E_T_ = ' ' "
	EndIf

	::SetWhere( cWhere )
	::SetOrder( "AGB_FILIAL,AGB_ENTIDA,AGB_CODENT,AGB_TIPO,AGB_PADRAO" )

	If ::Execute()
		::FillGetResponse()
	EndIf

	FTPDUnload()
	::oHashFieldsPD:Clean()

	RestArea( aArea )
	aSize( aArea, 0 )
	aSize(::aCustomFields, 0)
	FreeObj(::oHashFieldsPD)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} RowToJson
	Converte o retorno da consulta SQL em JSON

	@sample		PhoneContactsProtheusDataSync:RowToJson( cAlias, aFields, .F.)
	@param		cAlias	, Character	, Alias da Consulta
	@param		aFields	, Array		, Campos (MapFields)
	@param		lEmpty	, Logical	, Vazio.
	@author		Danilo Salve
	@since		22/12/2020
	@version	12.1.27 ou Superior
/*/
//------------------------------------------------------------------------------
METHOD RowToJson( cAlias as Character, aFields as Array , lEmpty as Logical) CLASS PhoneContactsProtheusDataSync
	DEFAULT lEmpty := .F.
	ConvertResponseValues(aFields, cAlias, lEmpty, @Self )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} AddCustomFields
  Metodo Auxiliar, deve carregar os Atributos utilizados na conversão dos valores.

  @author	Squad CRM & Faturamento
  @since	02/06/2021
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
METHOD AddCustomFields() CLASS PhoneContactsProtheusDataSync
	Self:cSource := "TGVPHONESCONTACTS"
	Self:isAdmin := FwIsAdmin()
	Self:aCustomFields := {}
	Self:nFields := Len(Self:oJsonObj:aMapFields)

	aAdd(Self:aCustomFields, {"internalid"			, {|| GetInternalIdQry((cAlias)->AGB_FILIAL + (cAlias)->AGB_CODIGO + (cAlias)->AGB_CODENT)}})
	aAdd(Self:aCustomFields, {"contactinternalid"	, {|| GetInternalIdQry((cAlias)->AGB_FILIAL + (cAlias)->AGB_CODENT)}})

	Self:cCustomFields := "internalid#contactinternalid"

	If !Self:isAdmin
		Self:oHashFieldsPD := setProtectedFields(Self:oJsonObj:aMapFields, Self:cSource)
	Endif

	Self:lcanObfuscateField := CanAccessProtectedData(Self:isAdmin)
Return Nil
