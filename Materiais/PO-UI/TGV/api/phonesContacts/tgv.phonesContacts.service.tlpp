#include 'tlpp-core.th'

namespace tgv.phoneContacts
static __instance as object

//------------------------------------------------------------------------------
/*/{Protheus.doc} PhoneContactsService
  Disponibiliza um serviço de Telefones.

  @sample		PhoneContactsService
  @author		Danilo Salve
  @since		01/10/2020
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
class PhoneContactsService
  Data jResponse as Json

	Public Method New() as Object
	Public Method getAllSync() as Json
  Public Method getAll() as Json
	Public Method getInstance() as Object
endclass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
  Disponibiliza uma nova instancia do serviço Telefones.

  @sample		PhoneContactsService:New()
  @author		Danilo Salve
  @since		01/10/2020
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object class PhoneContactsService
 ::jResponse := JsonObject():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
  Retorna uma instancia do serviço.

  @sample		PhoneContactsService:GetInstance()
  @author		Danilo Salve
  @since		01/10/2020
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object class PhoneContactsService
	If (__instance == Nil)
		__instance := PhoneContactsService():New()
	EndIf
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} getAllSync
  Efetua a consulta de Telefones.

  @sample		PhoneContactsService:getAllSync(1, 10, '2020-01-01')
  @param      nPage       , Numerico      , Pagina
  @param      nPageSize   , Numerico      , Tamanho da Pagina
  @param      cDate       ,  Caractere    , Data de Sincronismo/alteracao
  @return     JsonObject  , Reposta da Consulta/Requisição
  @author		Danilo Salve
  @since		21/01/2021
  @version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getAllSync(nPage as Numeric, nPageSize as Numeric, cDate as Character) as Json class PhoneContactsService
	Local oPhoneContacts  := PhoneContactsDataFactory():GetDataSync()

	Default nPage       := 1
	Default nPageSize   := 10
	Default cDate       := ""

	oPhoneContacts:GetAllSync(nPage, nPageSize, cDate)

	If oPhoneContacts:lOk
		::jResponse := oPhoneContacts:getJSONResponse()
	Else
		::jResponse := SetRestFault(oPhoneContacts:GetCode(), oPhoneContacts:GetMessage())
	EndIf

	oPhoneContacts:DeActivate()
	FreeObj(oPhoneContacts)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} getAll
  Efetua a consulta de Telefones - Utilizado quando o TOTVS Gestão de Vendas
  estiver operando Online.

  @sample		oPhoneContacts:getAll(1, 10, {}, 'code, store', Nil)
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cCustomerId	, Character	, Codigo do Cliente (Codigo e Loja)
	@param		cSort		, Character	, Ordernação do Response
  @return     JsonObject  , Reposta da Consulta/Requisição
  @author		Squad CRM/FAT
  @since		28/07/2021
  @version	12.1.33
/*/
//------------------------------------------------------------------------------
Method getAll(nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character, cContactId as Character,;
  cSort as Character) as Json class PhoneContactsService
	Local oPhoneContacts  := PhoneContactsDataFactory():GetData()

	Default nPage       := 1
	Default nPageSize   := 10

	oPhoneContacts:GetAll(nPage, nPageSize, aURLFilter, cFields, cContactId, cSort)

	If oPhoneContacts:lOk
		::jResponse := oPhoneContacts:getJSONResponse()
	Else
		::jResponse := SetRestFault(oPhoneContacts:GetCode(), oPhoneContacts:GetMessage())
	EndIf

	oPhoneContacts:DeActivate()
	FreeObj(oPhoneContacts)
Return ::jResponse
