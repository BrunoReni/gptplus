#include 'tlpp-core.th'
#include 'tgv.phonescontacts.controller.ch'

namespace tgv.phoneContacts
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPhoneContacts
	API para consulta de telefones relacionados a um Contato do TOTVS Gestão de
	Vendas.
	@type		class
	@version	12.1.27
	@author		Squad CRM & Faturamento
	@since		27/08/2021
/*/
//------------------------------------------------------------------------------
Class tgvPhoneContacts
	Data jResponse  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method new() as Object

	@Get("/api/tgv/phoneContacts/sync")
	Public Method getAllSync() as Logical

	@Get("/api/tgv/phoneContacts/sync/diff/:dateSync")
	Public Method getAllDiff() as Logical

	@Get("/api/tgv/phoneContacts/")
	Public Method getAll() as Logical

	@Get("/api/tgv/phoneContacts/:contactId")
	Public Method getById() as Logical
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPhoneContacts::new() as Object
	Obtem uma nova instacia da classe tgvPhoneContacts
	@type		method
	@version	12.1.27
	@author		Squad CRM & Faturamento
	@since		17/09/2020
	@return		object, retorna uma nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method new() as Object Class tgvPhoneContacts
	::nPage     := 1
	::nPageSize := 10
	::oService	:= PhoneContactsService():getInstance()
	::jResponse := JsonObject():new()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPhoneContacts::getAllSync() as Logical
	Obtém um lista de telefones de contatos, utilizando o padrão de campos do
	PO-SYNC. 
	@type		method
	@version	12.1.27
	@author 	Squad CRM & Faturamento
	@since		17/09/2020
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getAllSync() as Logical Class tgvPhoneContacts
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getAllSync(::nPage, ::nPageSize, /*cDate*/))
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPhoneContacts::getAllDiff() as Logical
	Efetua o GET dos registros alterados, filtrando pelo campo S_T_A_M_P_.
	@type		method
	@version	12.1.27
	@author		Squad CRM & Faturamento
	@since		17/09/2020
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getAllDiff() as Logical Class tgvPhoneContacts
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getAllSync(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPhoneContacts::getAll() as Logical
	Obtem uma Lista de Telefones de contatos, possibilitando filtrar
	(QueryParam - FILTER) os telefones e permitindo retornar todos os campos ou
	somente os campos especificados no QueryParam FIELDS.
	Observação: Ao utilizar o GETOnline o campo DELETED não será retornado. 
	@type		method
	@version	12.1.33
	@author		Squad CRM & Faturamento
	@since		28/07/2021
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getAll() as Logical Class tgvPhoneContacts
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields 		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character

	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getAll(::nPage, ::nPageSize, aURLFilter, cFields, /*contactId*/, cSort))
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvPhoneContacts::getById() as Logical
	Deve obter o telefone de um contato, filtrando pelo seu código,
	permitindo retornar todos os campos ou somente os campos especificados no
	QueryParam FIELDS.
	Observação: Ao utilizar o GETOnline o campo DELETED não será retornado.	
	@type		method
	@author		Squad CRM/FAT
	@since		28/07/2021
	@version	12.1.33
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getById() as Logical Class tgvPhoneContacts
	Local cFields 		:= getQueryParam('FIELDS') as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')

	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getAll(::nPage, ::nPageSize, {}, cFields, GetPathParams("contactId")))

	If Empty(::jResponse["items"])
		SetRestFault(404, STR0001) // Contato não encontrado.
	Else
		AnswerRest(::jResponse)
	EndIf
Return .T.
