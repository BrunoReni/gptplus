#include 'tlpp-core.th'

namespace tgv.typesOfOperation
using namespace tgv.util

#DEFINE KEY 3
#DEFINE DESCRIPTION 4

//------------------------------------------------------------------------------
/*/{Protheus.doc} TypesOfOperationProtheusData
	Efetua a consulta dos Tipos de Operações no Protheus.

	@sample		TypesOfOperationProtheusData:New()
	@author		Danilo Salve
	@since		26/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
CLASS TypesOfOperationProtheusData
	Public Method New() as Object
	Public Method Get() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Cria um nova instancia.

	@sample		TypesOfOperationProtheusData:New()
	@author		Danilo Salve
	@since		26/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object CLASS TypesOfOperationProtheusData
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} Get
	Retorna todos os Tipos de Operações (SX5 - DJ)

	@sample		TypesOfOperationProtheusData:Get()
	@author		Danilo Salve
	@since		26/11/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method Get() as Json CLASS TypesOfOperationProtheusData
	Local aTypeOperation 	:= {}
	Local aSX5DJ			as Array
	Local jResponse 		:= JsonObject():New()
	Local jTypeOperation	as Json
	Local nI				as Numeric

	aSX5DJ := FWGetSX5( "DJ" )

	For nI := 1 To Len(aSX5DJ)
		jTypeOperation := JsonObject():New()
		jTypeOperation['operationtypeid'] := AllTrim(aSX5DJ[nI, KEY])
		jTypeOperation['description'] := AllTrim(aSX5DJ[nI, DESCRIPTION])

		aAdd(aTypeOperation, jTypeOperation)
		FreeObj(jTypeOperation)
	Next

	jResponse["items"] := aTypeOperation

	aSize(aSX5DJ, 0)
Return jResponse
