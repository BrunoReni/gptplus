#Include "PROTHEUS.CH"
#include "Mata180.ch"
#INCLUDE 'FWMVCDEF.CH'

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA180  ³ Autor ³ Eveli Morasco         ³ Data ³ 28/10/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao dos dados complementares do produto³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ DATA   ³ BOPS ³Prograd.³ALTERACAO							          ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³09.05.01³XXXXXX³Ricardo ³Inclusao de Rotina Automatica 		          ³±±
±±³14.08.01³XXXXXX³Ricardo ³Inclusao de Pesquisa para o Oficina           ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³ Responsavel              ³ Data            BOPS            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³                          ³                                 ³±±
±±³      02  ³ Nereu Humberto Junior    ³ 21/09/2006      00000107189     ³±±
±±³      03  ³                          ³                                 ³±±
±±³      04  ³                          ³                                 ³±±
±±³      05  ³ Nereu Humberto Junior    ³ 21/09/2006      00000107189     ³±±
±±³      06  ³                          ³                                 ³±±
±±³      07  ³                          ³                                 ³±±
±±³      08  ³                          ³                                 ³±±
±±³      09  ³                          ³                                 ³±±
±±³      10  ³                          ³                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA180(xRotAuto,nOpcAuto)
Static lMvcMata180 := NIL
STATIC lHistFiscal := HistFiscal()

// Atualiza a static lMvcMata180, se necessario 
A180MVC()

If lMvcMata180
	MATA180M(xRotAuto,nOpcAuto) // tratamento com MVC
Else
	MATA180Old(xRotAuto,nOpcAuto) // Tratamento sem MVC(modelo antigo)
EndIf

Return NIL

/*/{Protheus.doc} MATA180Old
//TODO Cadastro em ADVPL sem MVC, Modelo antigo
@author reynaldo
@since 30/04/2018
@version 1.0
@return ${return}, ${return_description}
@param xRotAuto, , descricao
@param nOpcAuto, numeric, descricao
@type function
/*/
Static Function MATA180Old(xRotAuto,nOpcAuto)
Local aMemUser 	    := {}
Local oMBrowse 	    := Nil

/*BEGINDOC
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso seja necessario chamar a rotina automatica para a exclusao³
//³do complemento de produto (SB5), não esqueça que, caso         ³
//³seja chamada a rotina automatica para a exclusão do produto    ³
//³(SB1) o próprio Mata010 já faz a exclusão do SB5, não havendo  ³
//³então a necessidade de chamar a rotina automática de exclusão  ³
//³do SB5 pelo Mata180.                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ENDDOC*/

// Variavel para a Rotina Automatica - assume inclusao como padrao se nao for informado
nOpcAuto := If (nOpcAuto == Nil,3,nOpcAuto)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// variavel para rotina automatica
Private lM180Auto := (ValType(xRotAuto) == "A")
Private aRotAuto  := If(lM180Auto,aClone(xRotAuto),NIL)
Private aRotina   := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao de variavel para atualizacao de campos MEMO do tipo virtual          ³
//³ Deve ser iniciada com os campos do tipo MEMO ja existentes no dicionario (SB5) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private aMemos    := {}

PRIVATE aCpoAltSB5 := {} //Vetor usado na gravacao do historico de alteracoes

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para Tratamento de Campos Memo - Virtuais   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MT180MEM" )
	aMemUser := ExecBlock( "MT180MEM", .F., .F. )
	If ValType( aMemUser ) == "A"
		AEval( aMemUser, { |x| AAdd( aMemos, x ) } )
	EndIf
EndIf

If lM180Auto
  aRotina := { { STR0001,"AxPesqui"  , 0 , 1, 0, .F.},; //"Pesquisar"
  			 { STR0002,"AxVisual"  , 0 , 2, 0, .F.},;    //"Visualizar"
             { STR0003,"Am180Inc"  , 0 , 3, 0, nil},;	//"Incluir"
             { STR0004,"Am180Inc"  , 0 , 4, 0, nil},;	//"Alterar"
             { STR0005,"A180Deleta", 0 , 5, 1, nil} }	//"Excluir"
Endif

	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define o cabecalho da tela de atualizacoes                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE cCadastro := OemtoAnsi(STR0006)		//"Dados complementares"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lM180Auto
	MsRotAuto(nOpcAuto,aRotAuto,"SB5")
Else
	oMBrowse := FWMBrowse():New()
	oMBrowse:SetAlias("SB5")			
	oMBrowse:SetDescription( cCadastro )
	oMBrowse:SetAttach( .T. )
	//Se não for SIGACRM inibe a exibição do gráfico
	If nModulo <> 73
		oMBrowse:SetOpenChart( .F. )
	EndIf
	oMBrowse:SetTotalDefault('B5_FILIAL','COUNT',STR0026) //'Total de Registros'
	oMBrowse:Activate()		
EndIf

Return NIL

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A180Deleta³ Autor ³ Eveli Morasco         ³ Data ³ 28/10/92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de exclusao dos dados complementares do produto   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A180Deleta(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA180                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A180Deleta(cAlias,nReg,nOpc)
Local nOpcA
Local aArea 	:= {}
Local bCampoSB5 := {}
Local aCmps     := {}
Local aParam	:= {}

// Atualiza a static lMvcMata180, se necessario 
A180MVC()

If lMvcMata180
	FWExecView(STR0005,"MATA180",MODEL_OPERATION_DELETE) // Excluir
Else
	aArea := GetArea()
	bCampoSB5 := { |x| SB5->(Field(x)) }
	aParam := {{||.T.},{|| A180ECVDel(cAlias,nReg,nOpc)},{|| .T.},{|| .T.}}
	If lHistFiscal
	aCmps :=  RetCmps("SB5",bCampoSB5)
	EndIf

	nOpcA := AxDeleta(cAlias,nReg,nOpc,"Ma180GrvPE(" + Alltrim(Str(nOpc)) + ")",,,aParam,aRotAuto)

	If lHistFiscal .And. Len(aCmps)>0 .And. nOpcA == 2
	GrvHistFis("SB5", "SS5", aCmps)
	aCmps := {}
	EndIf
	
	RestArea(aArea)
EndIf

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AM180Inc  ¦ Autor ³Ricardo Farinelli   º Data ³  05/08/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Faz a inclusao atraves de rotina automatica                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Mata180()                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AM180INC(cAlias,nReg,nOpc)
Local nOpcA
// rotina automatica

lM180Auto := (ValType(aRotAuto) == "A")
If ( lM180Auto )
	Begin Transaction
		nOpcA := AxIncluiAuto(cAlias,,,nOpc,nReg)
		// Integração OMS x CPL
		If nOpcA == 1 .And. FindFunction("OMSXCPLINT")
			OMSXCPLINT("SB5")
		EndIf
	End Transaction
EndIf
Return

/*/{Protheus.doc} PesqOfi180
//TODO Descrição auto-gerada.
@author (desconhecido)
@since (desconhecido)
@version 1.0
@return ${return}, ${return_description}

@type function
/*/
Function PesqOfi180()
Local oDlgPesq
Local nOpcao
Local aArea		:= {}
Local aAreaSB1	:= {}
Local aAreaSB5	:= {}

Private oCodGrp
Private oCodIte
Private cCodGrp := Criavar("B5_CODGRP",.F.)
Private cCodIte := Criavar("B5_CODITE",.F.)

DEFINE MSDIALOG oDlgPesq TITLE STR0011 From 3,0 to 7,50 of oMainWnd //"Pesquisa Por Grupo e Item - Oficina"

	@ 003,001 TO 031,167 LABEL '' OF oDlgPesq PIXEL
	@ 013.5,005 SAY Oemtoansi(STR0012) Of oDlgPesq Pixel Size 38,9 //"Grupo do Item:"
	@ 013,050 MSGET oCodGrp Var cCodGrp Pict PesqPict("SB5","B5_CODGRP") F3 CpoRetF3("B5_CODGRP") ;
	When VisualSx3("B5_CODGRP") Of oDlgPesq Pixel Size 15,9
	@ 013.5,080 SAY Oemtoansi(STR0013) Of oDlgPesq Pixel Size 15,9 //"Item:"
	@ 013,105 MSGET oCodIte Var cCodIte Pict RtPict() ;
	When VisualSx3("B5_CODITE") Of oDlgPesq Pixel Size 60,9
   DEFINE SBUTTON FROM 005,169 TYPE 1 ACTION (nOpcao := 1,oDlgPesq:End()) ENABLE OF oDlgPesq
   DEFINE SBUTTON FROM 018,169 TYPE 2 ACTION (nOpcao := 2,oDlgPesq:End()) ENABLE OF oDlgPesq
   oCodGrp:BLOSTFOCUS := { || oCodIte:PictVar := RtPict(),oCodIte:Refresh() }

ACTIVATE MSDIALOG oDlgPesq CENTER

If nOpcao == 1
	aArea := GetArea()
	aAreaSB1 := SB1->(GetArea())
   SB1->(DbSetOrder(7))
   If SB1->(DBseek(xFilial("SB1")+cCodGrp+cCodIte))
		aAreaSB5 := SB5->(GetArea())
      SB5->(DbsetOrder(1))
      If !SB5->(Dbseek(xFilial("SB5")+SB1->B1_COD))
         Help(1,,"REGNOIS")
      Endif
		RestArea(aAreaSB5)
   Else
      Help(1,,"REGNOIS")
   Endif
	RestArea(aAreaSB1)
	RestArea(aArea)
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFuncao    ³RtPict    ³Autor  ³Valdir              ³ Data ³ 10/08/2000  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Retorna a Picture do CODITE de acordo c/ o GRUPO            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Veiculos                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function RtPict()
Local cPict := Trim(POSICIONE('SBM',1,xFilial('SBM')+cCodGrp,'BM_PICPAD'))
cPict += Iif(!Empty(cPict),'%C',"@!%C")
Return RTrim(cPict)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³06/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Local lVeiculo  	:= If (Type("lVeiculo") == "U",.F.,lVeiculo)
Local aRotAdic  	:= {}

Private aRotina	:= {}

Aadd(aRotina,{ STR0001,"AxPesqui"  , 0 , 1, 0, .F.})	//"Pesquisar"
Aadd(aRotina,{ STR0002,"AxVisual"  , 0 , 2, 0, NIL})	//"Visualizar"
Aadd(aRotina,{ STR0003,"Ma180Inc"  , 0 , 3, 0, NIL})	//"Incluir"
Aadd(aRotina,{ STR0004,"Ma180Alt"  , 0 , 4, 2, NIL})	//"Alterar"
Aadd(aRotina,{ STR0005,"A180Deleta", 0 , 5, 1, NIL})	//"Excluir"

If (nModulo == 42) .AND. SuperGetMV("MV_WMSNEW", .F., .F.)
	Aadd(aRotina,{"Estrutura Armazenagem","WMSA335()", 0 , 2, 0, NIL})	//"Excluir"
EndIf

If lVeiculo
	Aadd(aRotina,{STR0010,"PesqOfi180",0,1,0,nil}) //"Pesq.Oficina"
Endif
// P.E. utilizado para adicionar items no Menu da mBrowse
If ExistBlock("MT180ROT")
	aRotAdic := ExecBlock("MT180ROT",.F.,.F.)
	If ValType(aRotAdic) == "A"
		AEval(aRotAdic,{|x| AAdd(aRotina,x)})
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA180MNU")
	ExecBlock("MTA180MNU",.F.,.F.)
EndIf

//-------------------------------------------------------------------
// Entrada para a ativação de Históricos Fiscais
// É necessário que seja abilitado o parâmetro: 
// MV_HISTFIS
//-------------------------------------------------------------------
If lHistFiscal
	Aadd(aRotina,{STR0029, "A180Hist()", 0, 0, 0}) //Histórico
EndIf

Return aRotina

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ma180Inc  ºAutor  ³Raphael Zampieri    º Data ³  09/15/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Chamada da funcao AxInclui								  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma180Inc(cAlias,nReg,nOpc)

// Atualiza a static lMvcMata180, se necessario 
A180MVC()

If lMvcMata180
	FWExecView(STR0003,"MATA180",MODEL_OPERATION_INSERT) //Incluir
Else
	AxInclui(cAlias,nReg,nOpc,,,,"Ma180Valid()",,"Ma180GrvPE(" + Alltrim(Str(nOpc)) + ")")
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ma180ValidºAutor  ³Raphael Zampieri    º Data ³  09/15/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Funcao para executar chamada do ponto de entrada "MT180INC"º±±
±±º          ³ que serve para pegar as informacoes que estao sendo gravadas±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma180Valid()
Local lRet      := .T.
Local lMA180TOK := ExistBlock("MA180TOK")
Local nX        := 0
Local bCampoSB5 := { |x| SB5->(Field(x)) }
Local lECommerce := SuperGetMV("MV_LJECOMM",.F.,.F.)

If ExistBlock("MT180INC")
	ExecBlock("MT180INC",.F.,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de entrada para customizar a validação da rotina.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMA180TOK
	lRet := ExecBlock("MA180TOK",.F.,.F.)
	If ValType(lRet) # "L"
		lRet := .T.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Geracao do log de produto.	³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. ALTERA
	A013GrvLog("SB5",M->B5_COD)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se algum campo foi alterado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lHistFiscal .And. Altera .And. lRet
	aCpoAltSB5 := {}
	For nX := 1 to SB5->(FCount())
		If !(M->&( eVal( bCampoSB5, nX)) == SB5->&( eVal( bCampoSB5, nX)))
			aAdd( aCpoAltSB5, { eVal( bCampoSB5, nX), SB5->&( eVal( bCampoSB5, nX) ) } )
		EndIf
	Next nX

	If  Len(aCpoAltSB5)>0 .And. lHistFiscal
		M->B5_IDHIST := IdHistFis()
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica Alguns campos Obrigatorios para o E-Commerce.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. lECommerce
	lRet := Ma180EComm()
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ma180Alt  ºAutor  ³Aline Sebrian       º Data ³  29/10/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Chamada da funcao AxAltera								  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Mata180                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma180Alt(cAlias,nReg,nOpc)
Local aCmps      := {}
Local bCampoSB5	:= {}

// Atualiza a static lMvcMata180, se necessario 
A180MVC()

If lMvcMata180
	FWExecView(STR0004,"MATA180",MODEL_OPERATION_UPDATE) // Alterar
Else
	bCampoSB5 := { |x| SB5->(Field(x)) }
	If Type("aCpoAltSA1") == "U"
	Private aCpoAltSA1 := {}
	EndIf

	If lHistFiscal
	aCmps :=  RetCmps("SB5",bCampoSB5)
	EndIf

	AxAltera(cAlias,nReg,nOpc,,,,,'Ma180Valid()',"Ma180GrvPE(" + Alltrim(Str(nOpc)) + ")",,,,,.T.)

	If lHistFiscal .And. Altera .And. Len(aCpoAltSB5)>0
		GrvHistFis("SB5", "SS5", aCmps)
		aCpoAltSB5 := {}
		aCmps      := {}
	EndIf
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ma180GrvPEºAutor  ³Aline Sebrian       º Data ³  04/11/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Chamada do ponto de entrada MT180GRV		    			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Mata180                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma180GrvPE(nOpc)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento para e-Commerce      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lECommerce := SuperGetMV("MV_LJECOMM",.F.,.F.)
Local cProduto   := SB5->B5_COD   //Codigo para posicionar na SB1
Local cCodigo    := ""            //Codigo raiz para obter todos os produtos da grade dos produtos filhos ou o codigo do produto simples.
Local nRegOld    := 0             //Salvar o registro atual da tabela SMO  
Local aMascRaiz  := &("{"+SuperGetMV("MV_MASCGRD",,"11,2,2")+"}")  //Cria vetor com os dados da Mascara da grade        
Local nTamProd   := SB1->(TamSx3("B1_COD")[1]) // Tamanho do código do Produto
Local lECCia		:= SuperGetMV("MV_LJECOMO",,.F.) //E-commerce ciashop
Local aAreaSB1	:= {} //WorkArea SB1 //produto-filho

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada executado após a gravação do Complemento do Produto    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistBlock("MT180GRV"))
	ExecBlock("MT180GRV",.F.,.F.,{nOpc})
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Limpa o campo para exportacao do e-commerce.    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lECommerce
	If nOpc # 5	//-- Inclusao e alteracao
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica os campos obrigatorios para o e-commerce.     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(M->B5_ECFLAG) .And. Ma180EComm()
			M->B5_ECDTEX := " "
			SB5->B5_ECDTEX := " "

			If lECCia
				M->B5_ECDTEX2 := ""
				SB5->B5_ECDTEX2 := ""				 
			EndIf
 
			SB1->(dbSetOrder(1)) //B1_FILIAL+B1_COD
			SB1->(dbSeek(xFilial("SB1")+B5_COD))

			If ( nOpc == 3  )  .And.  ;
			    !Empty(SB1->B1_PRODPAI) .AND.;
				MsgNoYes(STR0027 + RTrim(RetTitle("B5_ECFLAG")) +  STR0028 )  //"Deseja atualizar com o conteúdo do campo "#" para seus produtos-filhos que não possuam essa informação?"
				
				aAreaSB1 := SB1->(GetArea())
				
				cCodigo := SB5->(Left(B5_COD,aMascRaiz[1])) //Obtem a raiz do produto sem a variacao da grade.
				nTamProd := aMascRaiz[1]

				SB0->(dbSetOrder(1)) //B0_FILIAL+B0_COD				
				SB1->(dbSeek(xFilial("SB1")+cCodigo))
						
				
				While SB1->(!EOF() .And. B1_FILIAL == xFilial("SB1")) .AND. SB1->B1_GRADE == "S" .AND. AllTrim(SB1->B1_PRODPAI) == AllTrim(SB5->B5_COD) 
					SB0->(dbSeek(xFilial("SB0")+SB1->B1_COD))

					
					If SB0->(Found())  
						RecLock( "SB0", .F.)
					Else
						RecLock( "SB0", .T.)
						SB0->B0_FILIAL := xFilial("SB0")
						SB0->B0_COD := SB1->B1_COD
					EndIf
					If Empty(SB0->B0_ECFLAG)
						SB0->B0_ECFLAG := SB5->B5_ECFLAG						
					EndIf
					SB0->(MsUnLock())
					SB1->(DbSkip(1))
				End
				
				RestArea(aAreaSB1)

			EndIf

		EndIf
	Else		//-- Exclusao
    	SB5->B5_ECDTEX := " "   //Registro vem travado.
    	If lECCia
			SB5->B5_ECDTEX2 := ""				 
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ e-Commerce: exclusao dos produtos pai e filhos. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(SB5->B5_ECFLAG) .And. MsgNoYes(STR0016)  //"Deseja excluir o produto e-commerce do cadastro de produto também?"
		   	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Re-posiciona os registros para não haver problemas de gravacao do SLH ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SB1->(dbSetOrder(1)) //B1_FILIAL+B1_COD
			SB1->(dbSeek(xFilial("SB1")+cProduto))
			If Empty(SB1->B1_PRODPAI)
			    cCodigo := SB1->B1_COD
			Else
				cCodigo := SB1->(Left(B1_PRODPAI,aMascRaiz[1])) //Obtem a raiz do produto sem a variacao da grade.
				nTamProd := aMascRaiz[1]
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclusao dos produtos pai e filhos relacionado ao codigo                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SB1->(dbSeek(xFilial("SB1")+cCodigo))
			While SB1->(!EOF() .And. B1_FILIAL == xFilial("SB1") .And. Left(B1_COD,nTamProd) == cCodigo) 
			   
			   If SB1->B1_GRADE == "S" .AND.  AllTrim(SB1->B1_PRODPAI) == AllTrim(SB5->B5_COD) //FABIANA
			   
				    cProduto := SB1->B1_COD
	
					SA5->(dbSetOrder(2))
					SA5->(dbSeek(xFilial("SA5")+cProduto))
					While SA5->(!EOF() .And. SA5->A5_PRODUTO == cProduto)
						RecLock("SA5",.F.,.T.)
						SA5->(dbDelete())
						SA5->(MsUnLock())
	
						SA5->(dbSkip())
					End
	
					//-- Quando for SB1 exclusivo
					If FWModeAccess("SB1",3) == "E"
						SB2->(dbSetOrder(1))
						SB2->(dbSeek(xFilial("SB2")+cProduto))
						While SB2->(!EOF() .And. B2_FILIAL+B2_COD == xFilial("SB2")+cProduto)
							RecLock('SB2',.F.,.T.)
							SB2->(dbDelete())
							SB2->(MsUnLock())
	
							SB2->(dbSkip())
						End
	
						SB0->(dbSetOrder(1))
						SB0->(dbSeek(xFilial("SB0")+cProduto))
						While SB0->(!EOF() .And. B0_FILIAL+B0_COD == xFilial("SB0")+cProduto)
							RecLock('SB0',.F.,.T.)
							SB0->(dbDelete())
							SB0->(MsUnLock())
	
							SB0->(dbSkip())
						End
					Else
						nRegOld := SM0->(Recno())
	
						SM0->(dbGoTop())
						While !SM0->(EOF())
							SB2->(dbSetOrder(1))
							SB2->(dbSeek(xFilial("SB2",FWCodFil())+cProduto))
							While SB2->(!EOF() .And. B2_FILIAL+B2_COD == xFilial("SB2",FWCodFil())+cProduto)
								RecLock('SB2',.F.,.T.)
								SB2->(dbDelete())
								SB2->(MsUnLock())
	
								SB2->(dbSkip())
							End
	
							SB0->(dbSetOrder(1))
							SB0->(dbSeek(xFilial("SB0",FWCodFil())+cProduto))
							While SB0->(!EOF() .And. (B0_FILIAL+B0_COD == xFilial("SB0",FWCodFil())+cProduto))
								RecLock('SB0',.F.,.T.)
								SB0->(dbDelete())
								SB0->(MsUnLock())
	
								SB0->(dbSkip())
							End
	
							SM0->(dbSkip())
						End
						SM0->(dbGoTo(nRegOld))
					EndIf
	
			        // Adequado para Integridade referencial
					RecLock("SB1",.F.,.T.)
					SB1->(dbDelete())
					SB1->(MsUnLock())
				EndIf 

				SB1->( DbSkip() )
			End
		EndIf
	EndIf
EndIf

// Integração OMS x CPL
If nOpc <> 5 .And. FindFunction("OMSXCPLINT")
	OMSXCPLINT("SB5")
EndIf

FwIntegDef("MATA010",,,,"MATA010")

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ A080Hist     ºAutor  ³Wemerson Randolfo     º Data ³ 04/08/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Visualizacao do historico das alteracoes                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ Nao ha                                                        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ .T. ou .F.                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAplicacao ³ Funcao chamada pelo menu                                      º±±
±±º          ³                                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A180Hist
Local lRet := .T.

lRet := HistOperFis("SS5", SB5->B5_COD, SB5->B5_CEME, "S5_COD")

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ma180ECommºAutor  ³Antonio C Ferreira  º Data ³  18/02/2013 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validacao do e-commerce.  								  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Mata180                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma180EComm()
Local nA        := 0
Local cCmpObrig := ""
Local aCampos   := {}
Local lEcCia 	:= SuperGetMv("MV_LJECOMO",, .F.)
Local lRet   	:= .T.

If !lEcCia
	aadd( aCampos, { "B5_PESO"   , {|| (M->B5_PESO > 0)    } } )
	
		aadd( aCampos, { "B5_ECFLAG ", {|| !( Empty(M->B5_ECFLAG)  ) } } )
		aadd( aCampos, { "B5_ECPESOE", {|| (M->B5_ECPESOE > 0) } } )
	If SB5->(ColumnPos("B5_ECQTMAX")) > 0
		aadd( aCampos, { "B5_ECQTMAX", {|| (M->B5_ECQTMAX > 0) } } )
	EndIf
	If SB5->(ColumnPos("B5_ECTIPOP")) > 0	
		aadd( aCampos, { "B5_ECTIPOP", {|| !( Empty(M->B5_ECTIPOP) ) } } )
	EndIf
	If SB5->(ColumnPos("B5_ECPRESE")) > 0
		aadd( aCampos, { "B5_ECPRESE", {|| !( Empty(M->B5_ECPRESE) ) } } )
	EndIf	
ElseIf M->B5_ECFLAG == "1" 
	aadd( aCampos, { "B5_PESO"   , {|| (M->B5_PESO > 0)    } } )
		aadd( aCampos, { "B5_ECDESCR", {|| !( Empty(M->B5_ECDESCR) ) } } )
		aadd( aCampos, { "B5_ECTITU", {|| !( Empty(M->B5_ECTITU) ) } } )
		aadd( aCampos, { "B5_ECLARGU", {|| !( M->B5_ECLARGU = 0  .AND. M->B5_ECCOMP+M->B5_ECPROFU > 0  ) } } )	
		aadd( aCampos, { "B5_ECCOMP", {|| !( M->B5_ECCOMP = 0 .AND. M->B5_ECLARGU+M->B5_ECPROFU > 0 ) } } )	
		aadd( aCampos, { "B5_ECPROFU", {|| !( M->B5_ECPROFU = 0 .AND. M->B5_ECCOMP+M->B5_ECLARGU > 0 ) } } )
EndIf

For nA := 1 to Len(aCampos)
	If !Eval(aCampos[nA,2])
		If !Empty(cCmpObrig)
			cCmpObrig += ","
		EndIf
		cCmpObrig += "[" + Alltrim(RetTitle(aCampos[nA,1])) + "]"  //Obtem o titulo de tela do campo para facilitar para o usuario
	EndIf
Next nA

If !Empty(cCmpObrig)	
	Aviso("OBRIGAT",STR0024 + CRLF + cCmpObrig,{"OK"})  //"Os seguintes campos devem ser preenchidos para o funcionamento do e-commerce: "
EndIf

If !Empty(cCmpObrig) .And. lEcCia
	lRet := .F.
Endif

Return (lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A180ECDel ³ Autor ³ Antonio C Ferreira    ³ Data ³ 24/02/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da exclusao do complemento para e-commerce.      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A180ECDel()                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA180                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A180ECVDel(cAlias,nReg,nOpc)	
Local nA        := 0
Local lRet      := .T.
Local aArea     := GetArea()
Local aLocais   := {}
Local aProdutos := {}
Local cCod      := SB5->B5_COD
Local nRegOld   := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento para e-Commerce      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lECommerce := SuperGetMV("MV_LJECOMM",.F.,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso nao seja e-commerce, nao ira validar.                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lECommerce .And. !Empty(SB5->B5_ECFLAG)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Obtem os Armazens do e-commerce para verificar estoque.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MF6->(dbSetOrder(1)) //MF6_FILIAL+MF6_CEPDE+MF6_CEPATE
	MF6->(dbGoTop())
	While MF6->( !Eof() )
		MF6->(aAdd(aLocais,{MF6_XFILIA,MF6_LOCAL}))
		MF6->(DbSkip())
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Obtem o codigo raiz do produto de grade ou codigo do produto normal.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SB1->(dbSetOrder(1) ) //B1_FILIAL+B1_COD
	SB1->(dbSeek(xFilial("SB1")+SB5->B5_COD))
	If Empty(SB1->B1_PRODPAI)
	    cCodigo := SB1->B1_COD
	Else
		cCodigo := SB1->(Left(B1_PRODPAI,Len(Alltrim(B1_PRODPAI))-4)) //Obtem a raiz do produto sem a variacao da grade.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o estoque e-commerce do Produto Pai e dos produtos filhos        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SB1->(dbSeek(xFilial("SB1")+cCodigo))
	While SB1->(!EOF() .And. B1_FILIAL == xFilial("SB1") .And. Left(B1_COD,Len(cCodigo)) == cCodigo)
	   If SB1->B1_GRADE == "S" .AND.  AllTrim(SB1->B1_PRODPAI) == AllTrim(SB5->B5_COD)
	   
		    For nA := 1 to Len(aLocais)
				SB2->(dbSetOrder(1)) //B2_FILIAL+B2_COD+B2_LOCAL
				SB2->(dbSeek(aLocais[nA,1]+SB1->B1_COD+aLocais[nA,2]))
				If SB2->B2_QATU > 0
					SB2->(aAdd(aProdutos,{B2_COD,B2_FILIAL,B2_LOCAL}))
				EndIf
			Next nA
		EndIf

		SB1->(dbSkip())
	End

	If Len(aProdutos) > 0
		lRet := .F.

		cMensagem := STR0015+CRLF   //"Abaixo produtos e armazens que contem estoque e-commerce, invalidando "
		cMensagem += STR0016+CRLF   //"a exclusao deste complemento:"
		For nA := 1 to Len(aProdutos)
			cMensagem += Space(3) +STR0017 +aProdutos[nA,1] +STR0018 +aProdutos[nA,2] +STR0019 +aProdutos[nA,3]+CRLF  //"Produto: "##" Filial: "##" Armazém: "
		Next nA

		Alert(cMensagem)
	EndIf

	If lRet
		SB1->(dbSeek(xFilial("SB1")+cCodigo))
		While SB1->(!EOF() .And. B1_FILIAL == xFilial("SB1") .And. Left(B1_COD,Len(cCodigo)) == cCodigo)
		
			If SB1->B1_GRADE == "S" .AND.  AllTrim(SB1->B1_PRODPAI) == AllTrim(SB5->B5_COD)
				//Quando for SB1 exclusivo
				If FWModeAccess("SB1",3) == "E"
					SB2->(dbSetOrder(1))
					SB2->(dbSeek(xFilial("SB2")+SB1->B1_COD))
					While SB2->(!EOF() .And. B2_FILIAL+B2_COD == xFilial("SB2")+SB1->B1_COD)
						If SB2->B2_QATU <> 0
							Alert(STR0017 +SB2->B2_COD +STR0020)   //"Produto: "##" tem saldo!"
							lRet := .F.
							Exit
						EndIf
						If Lj110SeekSD(xFilial("SB2")+SB2->B2_COD)
							MsgStop(STR0021 +SB2->B2_COD +STR0022) //"Este produto "##" não poderá ser excluído, verifique as notas de entradas, notas de saída e movimentações."
							lRet := .F.
							Exit
						EndIf
						SB2->(dbSkip())
					End
	
					SL2->(dbSetOrder(2))
					If SL2->(dbSeek(xFilial("SL2")+SB1->B1_COD))
						SL2->(dbSetOrder(1))
						Help(" ",1,"MA010_06")
						lRet := .F.
						Exit
					EndIf
					SL2->(dbSetOrder(1))
				Else
					nRegOld := SM0->(Recno())
	
					SM0->(dbGoTop())
					While !SM0->(EOF())
						SB2->(dbSetOrder(1))
						SB2->(dbSeek(xFilial("SB2",FWCodFil())+SB1->B1_COD))
						While SB2->(!EOF() .And. B2_FILIAL+B2_COD == xFilial("SB2",FWCodFil())+SB1->B1_COD)
							If SB2->B2_QATU <> 0
								Alert(STR0017 +SB2->B2_COD +STR0020)   //"Produto: "##" tem saldo!"
								lRet := .F.
								Exit
							EndIf
							If Lj110SeekSD(xFilial("SB2",FWCodFil())+SB2->B2_COD)
								MsgStop(STR0021 +SB2->B2_COD +STR0022) //"Este produto "##" não poderá ser excluído, verifique as notas de entradas, notas de saída e movimentações."
								lRet := .F.
								Exit
							EndIf
							SB2->(dbSkip())
						End
						If !lRet
							Exit
						Endif
						SM0->(dbSkip())
					End
					SM0->(dbGoTo(nRegOld))
				Endif
			EndIf
			SB1->(dbSkip())
		End
	EndIf
EndIf

If lRet .And. IntDl(cCod)
	lRet := WmsVlDelB5(cCod)
EndIf


FwIntegDef("MATA010",,,,"MATA010")

RestArea(aArea)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A180VldPrtºAutor  ³Leonardo Quintania  º Data ³  28/06/2013 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida alteracao do campo B5_PROTOTI caso o produto esteja º±±
±±º			 ³ sendo usado como prototipo.								  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA180                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A180VldPrt()
Local lRet		 := .T.

//-- Valida se pode retirar de prototipo
DG3->(dbSetOrder(3))
If SB5->B5_PROTOTI .And. !M->B5_PROTOTI .And. DG3->(dbSeek(xFilial('DG3')+M->B5_COD))
	Help(" ",1,"PRODPROTOT")
	lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} VldSvcWMS
//TODO Validar se o tipo de serviço WMS pode ser utilizado no campo em questão.
		Esta função é chamada na validacao (X3_VALID) dos campos B5_SERVENT,B5_SERVINT,B5_SERVSAI,B5_SERVDEV e B5_SERVREQ
		chamadas por StaticCall, a qual foram avaliadas na release 12.1.17 janeiro/2018
@author (desconhecido)
@since (desconhecido)
@version 1.0
@return ${return}, ${return_description}
@param cTipoServ, caracter, descricao
@type function
/*/
Function VldSvcWMS(cTipoServ)
Local lRet := .T.
Local cServico := &(ReadVar())

	If !Empty(cServico)
		DC5->(DbSetOrder(1)) // DC5_FILIAL+DC5_SERVIC+DC5_ORDEM
		lRet := ExistCPO('DC5')
		If lRet .And. DC5->(MsSeek(xFilial('DC5')+cServico)) .And. DC5->DC5_TIPO != cTipoServ
			Help(,,'SIGAWMS',, STR0025, 1, 0 ) //-- "O tipo de serviço informado no cadastro de 'Serviços x Atividades' do WMS não é compatível com o objetivo do campo."
			lRet := .F.
		EndIf
	EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A180VldPrtºAutor  ³Filipe Gonçalves    º Data ³  17/08/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validação do campo B5_TPISERV para fazer os bloqueios dos  º±±
±±º			 ³ campos de MI, MC e LE e setar o valor como Não             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA180                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A180TPSERV(cCampo)
Local lRet		 := .T.

If M->B5_TPISERV == "4" .OR. M->B5_TPISERV == "6"
	If cCampo == "B5_GSMI"
		M->B5_GSMI := "2"
		lRet := .F.
	ElseIf cCampo == "B5_GSMC"
		M->B5_GSMC := "2"
		lRet := .F.
	ElseIf cCampo == "B5_GSLE"
		M->B5_GSLE := "2"
		lRet := .F.
	EndIf
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A180CtrWms
Objetivo: Validar se o tipo de serviço WMS pode ser utilizado no campo em questão

@author Alexsander Burigo Correa
@since 18/04/2017
@version P12.1.17
/*/
//-------------------------------------------------------------------
Function A180CtrWms()
Local lRet := .T.
	If !Empty(M->B5_COD)
		If (Empty(SB5->B5_CTRWMS) .And. M->B5_CTRWMS == '1') .Or. (!Empty(SB5->B5_CTRWMS) .And. !(SB5->B5_CTRWMS == M->B5_CTRWMS))
			lRet := WmsSldAmz(,M->B5_COD)
		EndIf
	EndIf
Return lRet

/*/{Protheus.doc} ModelDef
//TODO Definicao da Model
@author reynaldo
@since 30/04/2018
@version 1.0
@return ${return}, ${return_description}

@type function
/*/
Static Function ModelDef()
Local oModel	:= MPFormModel():New("MATA180")
Local oStruSB5	:= FWFormStruct(1,"SB5")
Local aMemosUser	
Local nX
		
	// Ponto de Entrada para Tratamento de Campos Memo - Virtuais 
	If ExistBlock( "MT180MEM" )
		aMemosUser := ExecBlock( "MT180MEM", .F., .F. )
		If ValType( aMemosUser ) == "A"
			For nX := 1 to Len(aMemosUser)
				FWMemoVirtual(oStruSB5, aMemosUser)
			Next nX		
		EndIf
	EndIf
	
	oModel:AddFields("SB5MASTER",,oStruSB5)	
	
	oModel:InstallEvent("PAD.",,MATA180EVDEF():New())
	oModel:InstallEvent("PE.",,MATA180EVPE():New())

	If FindFunction("WMSCLS0066")
		oModel:InstallEvent("INTOMS",, MATA180EVOMS():New())
	EndIf
	
	If SuperGetMV("MV_LJECOMM",.F.,.F.) //TODO remover comentario
		oModel:InstallEvent("INT.ECOMMERCE",,MATA180ECOMMERCE():New())
	EndIf
	
	If FindFunction("MATA180PCP") .And. FindFunction("RodaNewPCP") .And. RodaNewPCP()
		oModel:InstallEvent("MATA180API",, MATA180API():New(oModel))
	EndIf

Return oModel

/*/{Protheus.doc} ViewDef
//TODO Definicao da View para o MVC
@author reynaldo
@since 30/04/2018
@version 1.0
@return ${return}, ${return_description}

@type function
/*/
Static Function ViewDef()
Local oView
Local oModel	:= FWLoadModel("MATA180")
Local oStruSB5	:= FWFormStruct(2,"SB5")

	oStruSB5:RemoveField('B5_IDHIST')

	oView := FWFormView():New()
	oView:SetModel(oModel)

	oView:AddField('FORMSB5' , oStruSB5 , "SB5MASTER") 
	oView:CreateHorizontalBox( 'BOXFORMSB5', 100)
	oView:SetOwnerView('FORMSB5','BOXFORMSB5')
	
Return oView

/*/{Protheus.doc} A180MVC
//TODO Verifica se deve utilizar o cadastro em MVC ou não de acordo com a existencia da tabela G3Q.
Criada função para centralizar o tratamento.
@author reynaldo
@since 30/04/2018
@version 1.0
@return ${return}, ${return_description}

@type function
/*/
Static Function A180MVC()
//alteração realizada para o TFS realizar o merge (Teste Sistêmico)
lMvcMata180 := IIF(ValType(lMvcMata180) == "L",lMvcMata180, TableInDic( "G3Q", .F. ))
Return
