#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'protheus.ch'
#INCLUDE "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"

namespace ac.acStockClosing.Repository

Class acStockClosingRep
    public Method New()
    public Method eventStockClosing280()
    public Method logErrorProc()
    public Method version()
EndClass

Method new() class acStockClosingRep
   
return Self

/*/{Protheus.doc} eventStockClosing280
    Metodo responsavel por iniciar processamento do MATA280 por job
    @type  Metodo
    @author Samuel Macedo
    @since  Fevereiro 05,2021
    @version 12.1.27
/*/
Method eventStockClosing280(cDate, aListaFil, aParAuto, cCompany, cBranch, cId, cIdChild,cUserNameJ, cAcUserId) Class acStockClosingRep

local oJourney :=  acJourneyLog():New()
Local cProcess       := 'MATA280'
Local oError := ErrorBlock({|e| ::logErrorProc(e:Description, e:ErrorStack, oJourney, cDate)})

Private lMsErroAuto     := .F.
Private lAutoErrNoFile  := .T.

IniEnviron(cCompany,cBranch, cProcess)
IniVarEnAc(cAcUserId, cUserNameJ)

oJourney:cIdParent:= cId 
oJourney:cIdChild:= cIdChild 

oJourney:envValid(.T.)
oJourney:idMovD3X()
oJourney:setThreadId( Strzero(ThreadID(),15))

LockByName(cProcess + '_' +  cIdChild, .F. , .F.)

oJourney:insertTable(cId, cIdChild, cProcess, 'SETUP', cUserNamej)
Begin Sequence
    MATA280(.T.,cDate,,,,,,,,aListaFil, aParAuto, oJourney, .T., cUserNamej)
End Sequence

UnLockByName(cProcess + '_' +  cIdChild, .F., .F.)

ErrorBlock(oError)

return

/*/{Protheus.doc} logErrorProc
    Metodo responsavel por gravar o log de erro
    @type  Metodo
    @author Samuel Macedo
    @since  Fevereiro 05,2021
    @version 12.1.27
/*/
method logErrorProc(errorMessage, errorStack,JourneyLog, cDate) class acStockClosingRep

Local cProcess := 'MATA280'

If Empty(JourneyLog:cProcess)
    JourneyLog:cProcess := cProcess
Endif

JourneyLog:LogProAtu('ERRO', errorMessage, errorMessage + errorStack, cFilAnt, 'ER', cDate, cFilAnt, .T.)
UnLockByName(cProcess + '_' +  JourneyLog:cIdChild, .F., .F.)
userException(errorMessage)

return 

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Metodo
    @author pedro.missaglia 
    @since  Marco 01, 2023
    @version 12.1.22.10
/*/
method version() class acStockClosingRep

return 100

