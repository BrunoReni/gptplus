#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'protheus.ch'
#INCLUDE "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"

namespace ac.acBalanceClosing.Repository

Class acBalanceClosing
    public Method New()
    public Method eventRec350()
    public method logErrorProc()
    public method version()
EndClass

Method new() class acBalanceClosing
   
return Self

/*/{Protheus.doc} eventRec350
    Metodo responsavel por iniciar processamento do MATA350 por job
    @type  Metodo
    @author André Maximo 
    @since  Novembro 26,2020
    @version 12.1.27
/*/
Method eventRec350(aListaFil,cId, cIdChild,cCompany,cBranch,cUserNamej, cAcUserId) Class acBalanceClosing

local objJourneycEmp :=  acJourneyLog():New()
Local cProcess       := 'MATA350'
Local oError := ErrorBlock({|e| ::logErrorProc(e:Description, e:ErrorStack, objJourneycEmp)})

IniEnviron(cCompany,cBranch, cProcess)
IniVarEnAc(cAcUserId, cUserNameJ)

objJourneycEmp:cIdParent:= cId 
objJourneycEmp:cIdChild:= cIdChild 

LockByName(cProcess + '_' +  cIdChild, .F. , .F.)

objJourneycEmp:insertTable(cId, cIdChild, cProcess, 'SETUP', cUserNamej)
Begin Sequence
    MATA350(.T.,aListaFil,.T., objJourneycEmp, cUserNamej)
End Sequence

UnLockByName(cProcess + '_' +  cIdChild, .F., .F.)

ErrorBlock(oError)

return

/*/{Protheus.doc} logErrorProc
    Metodo responsavel por indicar a versao da classe
    @type  Metodo
    @author André Maximo 
    @since  Novembro 26,2020
    @version 12.1.27
/*/

method logErrorProc(errorMessage, errorStack,JourneyLog) class acBalanceClosing

Local cProcess := 'MATA350'

If Empty(JourneyLog:cProcess)
    JourneyLog:cProcess := cProcess
Endif


JourneyLog:LogProAtu('ERRO', errorMessage, errorMessage + errorStack, cFilAnt, 'ER', dDatabase, cFilAnt, .T.)
UnLockByName(cProcess + '_' +  JourneyLog:cIdChild, .F., .F.)
userException(errorMessage)

return 

/*/{Protheus.doc} version
    Metodo responsavel por indicar a versao da classe
    @type  Metodo
    @author pedro.missaglia 
    @since  Marco 01, 2023
    @version 12.22.1.10
/*/

method version() class acBalanceClosing

return 100
