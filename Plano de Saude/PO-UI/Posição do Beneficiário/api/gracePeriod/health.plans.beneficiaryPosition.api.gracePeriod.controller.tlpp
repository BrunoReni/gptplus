#Include "tlpp-core.th"
#include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.beneficiaryPosition.api.gracePeriod

/*/{Protheus.doc} GracePeriodBeneficiaryController
Classe responsavel por controlar as APIs de Carência do Beneficiário

@type class
@author Vinicius Queiros Teixeira
@since 08/12/2022
@version Protheus 12
/*/
Class GracePeriodBeneficiaryController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/gracePeriodGroup")
    Public Method getGracePeriodGroup()

    @GET("/totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/gracePeriodClass")
    Public Method getGracePeriodClass()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 08/12/2022
@version Protheus 12
/*/
Method new() Class GracePeriodBeneficiaryController
Return Self

/*/{Protheus.doc} getGracePeriodGroup
Método responsavel pela API que irá retornar os grupos de carência do
beneficiário
Url: /totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/gracePeriodGroup
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 08/12/2022
@version Protheus 12
/*/
Method getGracePeriodGroup() Class GracePeriodBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := GracePeriodBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:gracePeriodGroupPage(jPath["subscriberId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return

/*/{Protheus.doc} getGracePeriodClass
Método responsavel pela API que irá retornar as classes carência do
beneficiário
Url: /totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/gracePeriodClass
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 08/12/2022
@version Protheus 12
/*/
Method getGracePeriodClass() Class GracePeriodBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := GracePeriodBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:gracePeriodClassPage(jPath["subscriberId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return
