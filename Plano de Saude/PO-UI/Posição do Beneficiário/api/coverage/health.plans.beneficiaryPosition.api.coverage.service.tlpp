#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.beneficiaryPosition.api.coverage

Using Namespace totvs.protheus.health.plans.beneficiaryPosition.api.util

/*/{Protheus.doc} CoverageBeneficiaryService
Classe responsavel por processar a busca das coberturas do Beneficiário

@type class
@author Vinicius Queiros Teixeira
@since 09/12/2022
@version Protheus 12
/*/
Class CoverageBeneficiaryService From BaseService

  	Public Method new() Constructor
    Public Method coverageGroupPage(cSubscriberId As Character) As Logical

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 09/12/2022
@version Protheus 12
/*/
Method new() Class CoverageBeneficiaryService
    _Super:new()
Return Self

/*/{Protheus.doc} coverageGroupPage
Método responsavel por retornar a pagina de grupo de cobertura do beneficiário

@type method
@author Vinicius Queiros Teixeira
@since 09/12/2022
@version Protheus 12
/*/
Method coverageGroupPage(cSubscriberId As Character) As Logical Class CoverageBeneficiaryService

    Local aAreaBA1 := BA1->(FWGetArea()) As Array
    Local lSucess := .F. As Logical
    Local aGroup := {} As Array
    Local aRetorno := {} As Array
    Local nX As Numeric

    BA1->(DbSetOrder(2))
    If BA1->(MsSeek(FWXFilial("BA1")+Padr(cSubscriberId, TamSX3("BA1_CODINT")[1]+TamSX3("BA1_CODEMP")[1]+TamSX3("BA1_MATRIC")[1]+TamSX3("BA1_TIPREG")[1]+TamSX3("BA1_DIGITO")[1])))
        aRetorno := PLSGCOBUSR(cSubscriberId)

        If aRetorno[1]
            For nX := 1 To Len(aRetorno[2])
                aAdd(aGroup, {"groupCode" : aRetorno[2][nX][1],;
                              "coverageGroup" : Alltrim(aRetorno[2][nX][2]),;
                              "level" : aRetorno[2][nX][3]})
            Next nX
        EndIf
 
        lSucess := .T.
        Self:nCodeStatus := 200 // Ok
        Self:pageArrayItems(Self:jParam, aGroup)
    Else
        lSucess := .F.
        Self:SetError("E001", "Beneficiário não encontrado.",;
                              "Não foi encontrado nenhum Beneficiário com a matricula "+cSubscriberId,;
                              404) // Not Found
    EndIf

    FWRestArea(aAreaBA1)
    
Return lSucess
