#Include "tlpp-core.th"
#include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.beneficiaryPosition.api.contract

/*/{Protheus.doc} ContractBeneficiaryController
Classe responsavel por controlar as APIs de contrato dos beneficiários

@type class
@author Vinicius Queiros Teixeira
@since 03/10/2022
@version Protheus 12
/*/
Class ContractBeneficiaryController

    Private Data oService As Object

    Public Method new() Constructor

    @Get("/totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/contract")
    Public Method getBeneficiaryContract()

    @Get("/totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/familyBeneficiaries")
    Public Method getFamilyBeneficiaries()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 03/10/2022
@version Protheus 12
/*/
Method new() Class ContractBeneficiaryController
Return Self

/*/{Protheus.doc} getBeneficiaryContract
Método responsavel pela API que irá retornar os dados de contrato do beneficiário
Url: /totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/contract
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 03/10/2022
@version Protheus 12
/*/
Method getBeneficiaryContract() Class ContractBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := ContractBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:contractBeneficiary(jPath["subscriberId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return

/*/{Protheus.doc} getFamilyBeneficiaries
Método responsavel pela API que irá retornar os beneficiários da familia 
Url: /totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/familyBeneficiaries
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 10/10/2022
@version Protheus 12
/*/
Method getFamilyBeneficiaries() Class ContractBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := ContractBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:familyBeneficiaries(jPath["subscriberId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return
