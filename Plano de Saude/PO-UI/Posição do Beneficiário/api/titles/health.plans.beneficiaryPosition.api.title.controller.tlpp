#Include "tlpp-core.th"
#include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.beneficiaryPosition.api.title

/*/{Protheus.doc} TitlesBeneficiaryController
Classe responsavel por controlar as APIs de Títulos do beneficiário

@type class
@author Vinicius Queiros Teixeira
@since 04/10/2022
@version Protheus 12
/*/
Class TitlesBeneficiaryController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/titles")
    Public Method getTitles()

    @GET("/totvsHealthPlans/v1/beneficiaryPosition/titles/:titleId/compositions")
    Public Method getTitleCompositions()

    @GET("/totvsHealthPlans/v1/beneficiaryPosition/titles/:titleId/pdf")
    Public Method getTitlePdf()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 04/10/2022
@version Protheus 12
/*/
Method new() Class TitlesBeneficiaryController
Return Self

/*/{Protheus.doc} getTitles
Método responsavel pela API que irá retornar os títulos de cobrança do beneficiário
Url: /totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/titles
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 04/10/2022
@version Protheus 12
/*/
Method getTitles() Class TitlesBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := TitlesBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:allTitlesPage(jPath["subscriberId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return

/*/{Protheus.doc} getTitleCompositions
Método responsavel pela API que irá retornar a composição do título
Url: /totvsHealthPlans/v1/beneficiaryPosition/titles/:titleId/compositions
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 04/10/2022
@version Protheus 12
/*/
Method getTitleCompositions() Class TitlesBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := TitlesBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:titleCompositions(jPath["titleId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return

/*/{Protheus.doc} getTitlePdf
Método responsavel pela API que irá retornar o pdf em base64 do título
Url: /totvsHealthPlans/v1/beneficiaryPosition/titles/:titleId/pdf
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 04/10/2022
@version Protheus 12
/*/
Method getTitlePdf() Class TitlesBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := TitlesBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:titlePdf(jPath["titleId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return
