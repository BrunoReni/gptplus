#Include "tlpp-core.th"
#include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.beneficiaryPosition.api.billing

/*/{Protheus.doc} BillingBeneficiaryController
Classe responsavel por controlar as APIs de cobrança dos beneficiários

@type class
@author Vinicius Queiros Teixeira
@since 30/11/2022
@version Protheus 12
/*/
Class BillingBeneficiaryController

    Private Data oService As Object

    Public Method new() Constructor

    @Get("/totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/billing")
    Public Method getBilling()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 30/11/2022
@version Protheus 12
/*/
Method new() Class BillingBeneficiaryController
Return Self

/*/{Protheus.doc} getBilling
Método responsavel pela API que irá retornar o valor de cobrança do beneficiário
Url: /totvsHealthPlans/v1/beneficiaryPosition/:subscriberId/billing
Verbo: GET

@type method
@author Vinicius Queiros Teixeira
@since 30/11/2022
@version Protheus 12
/*/
Method getBilling() Class BillingBeneficiaryController

    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := BillingBeneficiaryService():New()
    Self:oService:setQueryParam(oRest:getQueryRequest())

    If Self:oService:billingBeneficiary(jPath["subscriberId"])
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getCodeStatus())
    EndIf

Return
