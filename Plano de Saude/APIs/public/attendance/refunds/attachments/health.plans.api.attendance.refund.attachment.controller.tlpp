#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plans.api.attendance.refund.attachment

/*/{Protheus.doc} AttachmentController
Classe controladora das APIs de anexos do protocolo de reembolso

@type class
@version Protheus 12.1.2310
@author vinicius.queiros
@since 04/05/2023
/*/
class AttachmentController

    private data oService as object

    public method new() constructor

    @GET("/totvsHealthPlans/attendance/v1/refunds/:protocol/attachments")
    public method apiGetAttachments()

    @POST("/totvsHealthPlans/attendance/v1/refunds/attachments")
    public method apiPostAttachments()

    @DELETE("/totvsHealthPlans/attendance/v1/refunds/:protocol/attachments/:code")
    public method apiDeleteAttachments()

endclass

/*/{Protheus.doc} new
Contrutor da classe

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 04/05/2023
@return object, objecto da classe AttachmentController
/*/
method new() class AttachmentController
return self

/*/{Protheus.doc} apiGetAttachments
API responsavél por retornar os anexos do protocolo de reembolso

@type method
@version Protheus 12.1.2310 
@author vinicius.queiros
@since 04/05/2023
/*/
method apiGetAttachments() class AttachmentController

    local jQueryParams := oRest:getQueryRequest() as json
    local jPathParams := oRest:getPathParamsRequest() as json

    self:oService := AttachmentService():new()
    self:oService:setQueryParams(jQueryParams)
    self:oService:setPathParams(jPathParams)

    if self:oService:getAttachments()
        oRest:setResponse(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    else
        oRest:setFault(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")
    
return

/*/{Protheus.doc} apiPostAttachments
API responsavél por adicionar anexo no protocolo de reembolso

@type method
@version Protheus 12.1.2310 
@author vinicius.queiros
@since 05/05/2023
/*/
method apiPostAttachments() class AttachmentController

    local cBody := oRest:getBodyRequest() as character

    self:oService := AttachmentService():new()

    if self:oService:setBodyString(cBody, "E001")
        if self:oService:postAttachments()
            oRest:setResponse(Self:oService:getJsonResult())
            oRest:setStatusCode(Self:oService:getStatusCode())
        else
            oRest:setFault(Self:oService:getJsonResult())
            oRest:setStatusCode(Self:oService:getStatusCode())
        endif
    else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

return

/*/{Protheus.doc} apiDeleteAttachments
API responsavél por deletar os anexos do protocolo de reembolso

@type method
@version Protheus 12.1.2310 
@author vinicius.queiros
@since 05/05/2023
/*/
method apiDeleteAttachments() class AttachmentController

    local jPathParams := oRest:getPathParamsRequest() as json
    
    self:oService := AttachmentService():new()
    self:oService:setPathParams(jPathParams)

    if self:oService:deleteAttachments()
        oRest:setStatusCode(self:oService:getStatusCode())
    else
        oRest:setFault(self:oService:getJsonResult())
        oRest:setStatusCode(self:oService:getStatusCode())

        oRest:setKeyHeaderResponse("Content-Type", "application/json")
    endif

return
