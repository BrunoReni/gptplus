#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.refundSetup.continuedMedication

/*/{Protheus.doc} ContinuedMedicationController
Classe controladora das APIs de Medicamentos de Uso Continuo

@type class
@author Vinicius Queiros Teixeira
@since 17/03/2023
@version Protheus 12
/*/
Class ContinuedMedicationController

    Private Data oService As Object

    Public Method new() Constructor

    @POST("/totvsHealthPlans/refundSetup/v1/continuousMedications")
    Public Method apiPostContinuousMedications()

    @GET("/totvsHealthPlans/refundSetup/v1/continuousMedications")
    Public Method apiGetContinuousMedications()
   
    @GET("/totvsHealthPlans/refundSetup/v1/continuousMedications/refunds")
    Public Method apiGetRefunds()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 17/03/2023
@version Protheus 12
/*/
Method new() Class ContinuedMedicationController
Return Self

/*/{Protheus.doc} apiPostContinuousMedications
Método responsável por cadastrar medicamentos de uso contínuo
Url: /totvsHealthPlans/refundSetup/v1/continuousMedications
Verbo: POST

@type Method
@author Vinicius Queiros Teixeira
@since 17/03/2023
@version Protheus 12
/*/
Method apiPostContinuousMedications() Class ContinuedMedicationController

    Local cBody := oRest:getBodyRequest() As Character

    Self:oService := ContinuedMedicationService():new()

    If Self:oService:setBodyString(cBody, "E001")
        If Self:oService:postContinuousMedications()
            oRest:setResponse(Self:oService:getJsonResult())
            oRest:setStatusCode(Self:oService:getStatusCode())
        Else
            oRest:setFault(Self:oService:getJsonResult())
            oRest:setStatusCode(Self:oService:getStatusCode())
        EndIf
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json") 

Return

/*/{Protheus.doc} apiGetContinuousMedications
Método responsável por pela a consulta  dos medicamentos de uso continuo do
beneficiário

Url: /totvsHealthPlans/refundSetup/v1/continuousMedications
Verbo: GET

@type method
@author Giovanna Charlo
@since 11/04/2023
@version Protheus 12
/*/
Method apiGetContinuousMedications() Class  ContinuedMedicationController

    Local jQueryParams := oRest:getQueryRequest() As Json
    
    Self:oService := ContinuedMedicationService():new()
    Self:oService:setQueryParams(jQueryParams)
   
    If Self:oService:getContinuousMedications()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json") 
Return

/*/{Protheus.doc} apiGetRefunds
Método responsável por pela a consulta dos medicamentos de uso continuo 
dos Protocolos de Reembolso 

Url: /totvsHealthPlans/refundSetup/v1/continuousMedications/Refunds
Verbo: GET

@type method
@author Giovanna Charlo
@since 11/04/2023
@version Protheus 12
/*/
Method apiGetRefunds() Class  ContinuedMedicationController

    Local jQueryParams := oRest:getQueryRequest() As Json
    
    Self:oService := ContinuedMedicationService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getRefunds()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json") 
Return