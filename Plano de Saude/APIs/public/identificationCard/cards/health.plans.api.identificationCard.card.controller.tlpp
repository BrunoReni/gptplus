#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.identificationCard.card

/*/{Protheus.doc} cardController
Classe controladora das APIs de Elegibilidade dos Beneficiarios

@type class
@author José Paulo
@since 01/03/2023
@version Protheus 12
/*/
Class CardController

    Private Data oService As Object

    Public Method new() Constructor

    @POST("/totvsHealthPlans/identificationCard/v1/cards")
    Public Method apicard()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author José Paulo
@since 01/03/2023
@version Protheus 12
/*/
Method new() Class CardController
Return Self

/*/{Protheus.doc} apicard
Método responsável pela api que retorna a Elegibilidade dos beneficiarios
Url: /totvsHealthPlans/identificationCard/v1/beneficiaries/:subscriberId/card
Verbo: GET

@type Method
@author José Paulo
@since 01/03/2023
@version Protheus 12
/*/
Method apicard() Class CardController

    Local jPathParams  := oRest:getPathParamsRequest() As Json
    Local jQueryParams := oRest:getQueryRequest() As Json
    Local jBody        := JsonObject():new() As Json

    jBody:fromjson(oRest:getBodyRequest())

    Self:oService := CardService():new()
    Self:oService:setPathParams(jPathParams)
    Self:oService:setQueryParams(jQueryParams)
    Self:oService:setBody(jBody)

    If Self:oService:getcard()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return


