#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.procedures.classProcedure

/*/{Protheus.doc} ClassController
Classe controladora das APIs de Classes de Procedimentos

@type class
@author Vinicius Queiros Teixeira
@since 05/04/2023
@version Protheus 12
/*/
Class ClassController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/procedure/v1/classes")
    Public Method apiGetClasses()

    @GET("/totvsHealthPlans/procedure/v1/classes/:classId/procedures")
    Public Method apiGetProcedures()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 05/04/2023
@version Protheus 12
/*/
Method new() Class ClassController
Return Self

/*/{Protheus.doc} apiGetClasses
Método responsável por retornar as classes de procedimentos
Url: /totvsHealthPlans/procedure/v1/classes
Verbo: GET

@type Method
@author Vinicius Queiros Teixeira
@since 05/04/2023
@version Protheus 12
/*/
Method apiGetClasses() Class ClassController

    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := ClassService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getClasses()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return

/*/{Protheus.doc} apiGetProcedures
Método responsável por retornar os procedimentos da classe
Url: /totvsHealthPlans/procedure/v1/classes/:classId/procedures
Verbo: GET

@type Method
@author Vinicius Queiros Teixeira
@since 06/04/2023
@version Protheus 12
/*/
Method apiGetProcedures() Class ClassController

    Local jQueryParams := oRest:getQueryRequest() As Json
    Local jPath := oRest:getPathParamsRequest() As Json

    Self:oService := ClassService():new()
    Self:oService:setQueryParams(jQueryParams)
    Self:oService:setPathParams(jPath)

    If Self:oService:getProcedures()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return
