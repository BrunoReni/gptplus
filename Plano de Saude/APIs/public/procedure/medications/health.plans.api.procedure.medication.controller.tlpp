#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.procedures.medication

/*/{Protheus.doc} MedicationController
Classe controladora das APIs de Medicamentos da Tabela Padrão

@type class
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Class MedicationController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/procedure/v1/medications")
    Public Method apiGetMedications()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method new() Class MedicationController
Return Self

/*/{Protheus.doc} apiGetMedications
Método responsável pela api que retorna os medicamentos da tabela padrão
Url: /totvsHealthPlans/procedure/v1/medications
Verbo: GET

@type Method
@author Vinicius Queiros Teixeira
@since 15/03/2023
@version Protheus 12
/*/
Method apiGetMedications() Class MedicationController

    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := MedicationService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getMedications()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return
