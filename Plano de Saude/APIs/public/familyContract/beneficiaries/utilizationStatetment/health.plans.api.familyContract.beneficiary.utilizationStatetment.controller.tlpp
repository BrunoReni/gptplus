#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.familyContract.beneficiary.utilizationStatetment

/*/{Protheus.doc} UtilizationStatetmentController
Classe controladora da API de Extrato de Utilização

@type class
@author Cesar Almeida
@since 27/04/2023
@version Protheus 12
/*/
Class UtilizationStatetmentController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/utilizationStatetment/base64")
    Public Method apiUtilizationStatetmentBase64()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Cesar Almeida
@since 27/04/2023
@version Protheus 12
/*/
Method new() Class UtilizationStatetmentController
Return Self

/*/{Protheus.doc} apiUtilizationStatetmentBase64
Método responsável por retornar o Extatro de Utilização em base64
Url: /totvsHealthPlans/familyContract/v1/beneficiaries/:subscriberId/utilizationStatetment
Verbo: GET

@type method
@author Cesar Almeida
@since 27/04/2023
@version Protheus 12
/*/
Method apiUtilizationStatetmentBase64() Class UtilizationStatetmentController

    Local jPath := oRest:getPathParamsRequest() As Json
    Local jQueryParams := oRest:getQueryRequest() As Json    

    Self:oService := UtilizationStatetmentService():new()
    Self:oService:setPathParams(jPath)
    Self:oService:setQueryParams(jQueryParams)    

    If Self:oService:getUtilizationStatetmentBase64()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())

        oRest:SetKeyHeaderResponse("content-type", "application/octet-stream")
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())

        oRest:setKeyHeaderResponse("Content-Type", "application/json")
    EndIf

Return
