#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.familyContract.company.contract

/*/{Protheus.doc} ContractController
Classe controladora das APIs de Contratos da Empresa

@type class
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Class ContractController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/familyContract/v1/companies/:companyCode/contracts")
    Public Method apiContracts()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Method new() Class ContractController
Return Self

/*/{Protheus.doc} apiContracts
Método responsável pela api que retorna os contratos da empresa
Url: /totvsHealthPlans/v1/companies/{companyCode}/contracts
Verbo: GET

@type Method
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Method apiContracts() Class ContractController

    Local jQueryParams := oRest:getQueryRequest() As Json
    Local jPathParams := oRest:getPathParamsRequest() As Json

    Self:oService := ContractService():new()
    Self:oService:setQueryParams(jQueryParams)
    Self:oService:setPathParams(jPathParams)

    If Self:oService:getContracts()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return
