#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.familyContract.company.contract.subcontract

/*/{Protheus.doc} SubcontractController
Classe controladora das APIs de subContratos da Empresa

@type class
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Class SubcontractController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/familyContract/v1/companies/:companyCode/contracts/:contractCode/subcontracts")
    Public Method apiSubcontracts()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Method new() Class SubcontractController
Return Self

/*/{Protheus.doc} apiSubcontracts
Método responsável pela api que retorna os subContratos da empresa
Url: /totvsHealthPlans/v1/companies/:companyCode/contracts/:contractCode/subcontracts
Verbo: GET

@type Method
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Method apiSubcontracts() Class SubcontractController

    Local jQueryParams := oRest:getQueryRequest() As Json
    Local jPathParams := oRest:getPathParamsRequest() As Json

    Self:oService := SubcontractService():new()
    Self:oService:setQueryParams(jQueryParams)
    Self:oService:setPathParams(jPathParams)

    If Self:oService:getSubcontracts()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    Else
        oRest:setFault(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return
