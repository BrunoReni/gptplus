#Include "tlpp-core.th"
#Include "tlpp-rest.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.regionalCouncil

/*/{Protheus.doc} RegionalCouncilController
Classe controladora das APIs de Conselho Regional

@type class
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Class RegionalCouncilController

    Private Data oService As Object

    Public Method new() Constructor

    @GET("/totvsHealthPlans/attendanceNetwork/v1/regionalCouncils")
    Public Method apiGetRegionalCouncils()

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method new() Class RegionalCouncilController
Return Self

/*/{Protheus.doc} apiGetRegionalCouncils
Método responsável pela api que retorna os conselhos regionais
Url: /totvsHealthPlans/attendanceNetwork/v1/regionalCouncils
Verbo: GET

@type Method
@author Vinicius Queiros Teixeira
@since 16/03/2023
@version Protheus 12
/*/
Method apiGetRegionalCouncils() Class RegionalCouncilController

    Local jQueryParams := oRest:getQueryRequest() As Json

    Self:oService := RegionalCouncilService():new()
    Self:oService:setQueryParams(jQueryParams)

    If Self:oService:getRegionalCouncils()
        oRest:setResponse(Self:oService:getJsonResult())
        oRest:setStatusCode(Self:oService:getStatusCode())
    EndIf

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

Return
