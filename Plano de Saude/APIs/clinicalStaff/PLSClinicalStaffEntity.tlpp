#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgIte 'items'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSClinicalStaffEntity

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSClinicalStaffEntity from PLSEntAux

    Data cPageSize as Character
    public method new()
    public method setPageSize(cPageSize)
    //Metodos de montagem do json
    public method montaJson(oDao)

endClass

method new() class PLSClinicalStaffEntity
    _Super:New()
    self:cPageSize := "10"
Return self

method setPageSize(cPageSize) class PLSClinicalStaffEntity
    self:cPageSize := cPageSize
return

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos de montagem do json

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method montaJson(oDao) class PLSClinicalStaffEntity

    Local cAli := oDao:getAliasTemp()
    Local nX   := 1

    self:oResult[_tgIte] := {}

    while !oDao:isEof() .And. nX <= Val(self:cPageSize)

        aAdd(self:oResult[_tgIte], JsonObject():new())
        self:oResult[_tgIte][nx]["idOnHealthInsurer"]   := self:maskField( (cAli)->BC1_CODRDA )
        self:oResult[_tgIte][nx]["professionalCouncil"] := self:maskField( (cAli)->BC1_SIGLCR )
        self:oResult[_tgIte][nx]["professionalCouncilNumber"] := self:maskField( (cAli)->BC1_NUMCR )
        self:oResult[_tgIte][nx]["stateAbbreviation"] := self:maskField( (cAli)->BC1_ESTCR )
        self:oResult[_tgIte][nx]["name"] := self:maskField( (cAli)->BC1_NOMPRF )
        self:oResult[_tgIte][nx]["id"] := self:maskField( (cAli)->RECNO )

        oDao:dbSkip()
        nX ++
    endDo

    self:oResult["hasNext"] := oDao:hasNext()

Return