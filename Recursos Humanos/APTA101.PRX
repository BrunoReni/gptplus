#INCLUDE "PROTHEUS.CH"
#INCLUDE "APTA101.CH"

Function APTA101()

	Local aSays		   	:= {}
	Local aButtons	   	:= {}

	aAdd(aSays,OemToAnsi(STR0002)) //"Atenção, a rotina APTA101 foi desabilitada não deve ser utilizada."
	aAdd(aSays,"") 
	aAdd(aSays,OemToAnsi(STR0003)) //"Para mais detalhes e orientações, clique em Visualizar e consulte a documentacao no TDN."
	aAdd(aSays,"") 

	aAdd(aButtons, { 15 ,.T.,{||  ShellExecute("open","https://tdn.totvs.com/pages/releaseview.action?pageId=731213540","","",1) } } )
	aAdd(aButtons, { 2 ,.T.,{|o| FechaBatch() }} )
	FormBatch( "APTA101", aSays, aButtons )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} JsToAdvpl
@type			function
@description	Bloco de código que receberá as chamadas JavaScript.
@author			martins.marcio
@since			02/12/2022
@version		1.0
/*/
//---------------------------------------------------------------------
Static Function JsToAdvpl( oWebChannel, cType, cContent )

	Local cContext     as character
	Local cJsonCompany as character
	Local cJsonContext as character
	Local cIsTAFFull   as character
	Local cJsonCodUser as character
	Local cJsonTAFFull as character
	Local cTafTsi      as Character

	Default cType := "" 

	cContext     := ""
	cJsonCompany := ""
	cJsonContext := ""
	cIsTAFFull   := "true"
	cJsonCodUser := ""
	cJsonTAFFull := ""
	cTafTsi		 := "noTsi"

	Private aSX9Rel    as array

	If FWIsInCallStack( "APTA101" )
		cContext := "labor-process"
		cIsTAFFull := "false"		
	EndIf

	Do Case
		Case cType == "preLoad"

			cJsonCompany := '{ "company_code" : "' + FWGrpCompany() + '", "branch_code" : "' + FWCodFil() + '" }'
			cJsonContext := '{ "context" : "' + cContext + '" }'
			cJsonCodUser := '{ "codUser" : "' + RetCodUsr() + '" }'
			cJsonTAFFull := '{ "tafFull" : "' + cIsTAFFull + '" }'
			cJsonTafTsi  := '{ "tafTsi" : "' + cTafTsi + '" }'

			oWebChannel:AdvPLToJS( "setCompany", cJsonCompany )
			oWebChannel:AdvPLToJS( "setContext", cJsonContext )
			oWebChannel:AdvPLToJS( "setCodUser", cJsonCodUser )
			oWebChannel:AdvPLToJS( "setlIsTafFull", cJsonTAFFull )
			oWebChannel:AdvPLToJS( "setTafTsi", cJsonTafTsi )

	EndCase

Return()

//---------------------------------------------------------------------
/*/{Protheus.doc} GPEAliasOK
@type			function
@description	Função que verifica se está tudo certo com as tabelas utilizadas no Processo.
@author			martins.marcio
@since			05/12/2022
@version		1.0
/*/
//---------------------------------------------------------------------

Function GPEAliasOk(aAliasList)

	Local lRet := .T.
	lOCAL lHelp	:= .F.
	Local nI

	Default aAliasList := {}
	
	For nI := 1 To Len(aAliasList)
		cAlias := aAliasList[nI]
		lRet := TAFAlsInDic(cAlias,lHelp)
		If !lRet
			Help("",1,"Help","Help",STR0001,1,0) // 'Dicionário desatualizado, verifique as atualizações do Processo Trabalhista'
			EXIT
		EndIf
	Next nI

Return lRet
