<%
// ######################################################################################
// Projeto: DATA WAREHOUSE
// Modulo : Actions
// Fonte  : dwUsers - Manutenção de Usuários
// ---------+-------------------+--------------------------------------------------------
// Data     | Autor             | Descricao
// ---------+-------------------+--------------------------------------------------------
// 28.11.05 |2481-Paulo R Vieira| Versão 3
// --------------------------------------------------------------------------------------

#include "dwincs.ch"
#include "dwUsers.ch"

Local oUser, oGroups	:= InitTable(TAB_USER)
Local aFields	:= {}, aButtons := {}
Local nID 		:= DwVal(HttpGet->ID)
Local cLogin, cNome, nGrupo, cEmail, cCargo, lAdmin, lAtivo, lUsSiga, cPainel
Local aGroups := {}, aUsers := {}
Local lOperOK	:= .F.

// carga dos grupos
oGroups:Seek(2, { "G" })
while !oGroups:Eof() .and. (oGroups:value("tipo") == "G")
	aAdd(aGroups, { oGroups:value("nome"), oGroups:value("id") })
	oGroups:_Next()
enddo

// carga dos usuários
oGroups:Seek(2, { "U" })
aAdd(aUsers, { STR0013 , 0 }) //"ninguem"
while !oGroups:Eof() .and. (oGroups:value("tipo") == "U")
  if !(oGroups:value("id") == nID) .and. !oGroups:value("Admin")
		aAdd(aUsers, { oGroups:value("nome"), oGroups:value("id") })
	endif
	oGroups:_Next()
enddo

if HttpGet->Action == AC_REC_MANUT
	nID := DwVal(HttpGet->ID)
	
	oUser := initTable(TAB_USER)
	
	HttpGet->Oper := DwVal(HttpGet->Oper)
	if HttpGet->Oper == OP_SUBMIT
		nID := DwVal(HttpPost->edID)
		
		if HttpSession->subOper == OP_REC_EDIT .and. oUser:seek(1, { nID })
			if !empty(HttpPost->edSenha)
				HttpPost->edSenha := dwCripto(pswencript(HttpPost->edSenha), PASSWORD_SIZE, 0)
			else
				if oUser:seek(1, { nID })
					HttpPost->edSenha := oUser:value("senha")
        endif
			endif
			lOperOK 	:= oUser:update(oUser:fromPost(HttpPost->APost))
		elseif HttpSession->subOper == OP_REC_DEL .and. oUser:seek(1, { nID })
			lOperOK	:= oUser:delete()
		else
			HttpPost->edID := NIL 
			HttpPost->edSenha := dwCripto(pswencript(HttpPost->edSenha), PASSWORD_SIZE, 0)
			if lOperOK := oUser:append(oUser:fromPost(HttpPost->APost))
				nID 		:= oUser:value("id")
			endif
		endif

		if !lOperOK
			DwRaise(ERR_002, SOL_000, oUser:Msg(.T.))
		else
			HttpPost->edCopiarPriv := dwVal(HttpPost->edCopiarPriv)
			if HttpPost->edCopiarPriv <> 0
				oUserDW:copyPriv(HttpPost->edCopiarPriv, nID)
			endif
		endif
	else
		HttpSession->subOper := HttpGet->Oper
		
	endif
	
	if oUser:seek(1, { nID })
		cLogin 	:= oUser:value("login")
		cNome   := oUser:value("nome") 
		nGrupo	:= oUser:value("id_grupo")
		cEmail	:= oUser:value("email")
		cCargo	:= oUser:value("cargo")
		lAdmin	:= oUser:value("admin")
		lAtivo	:= oUser:value("ativo")
		lUsSiga	:= oUser:value("us_siga")
		cPainel := oUser:value("usePanel")
		if empty(cPainel)
			cPainel := oSigaDW:UsePanels()
		endif
	else
		cLogin	:= ''
		cNome		:= ''
		nGrupo	:= 0
		cEmail	:= ''
		cCargo	:= ''
		lAdmin	:= .F.
		lAtivo	:= .F.
		lUsSiga	:= .F.
		cPainel := oSigaDW:UsePanels()
	endif
	
	makeHidden(aFields,		'edID',			nID)
	makeKeyField(aFields,	'edLogin',		STR0001, .T., 'C', 10, 0, , cLogin) //'Login'
	makePassword(aFields,	'edSenha', 		STR0002, empty(cLogin), 'C', 20, 0, , '') //'Senha'
	makeField(aFields, 		'edNome', 		STR0003, .T., 'C', 30, 0, , cNome) //'Nome'
	makeHidden(aFields,		'edTipo',		"U")
	makeCombo(aFields,		'edId_grupo',	STR0004,	.T., aGroups, 	, nGrupo) //'Grupo'
	makeField(aFields,	 	'edEmail', 		STR0005,	.T., 'C', 60, 0, , cEmail) //'Email'
	makeField(aFields,	 	'edCargo', 		STR0006,	.T., 'C', 30, 0, , cCargo) //'Cargo'
	makeField(aFields, 		'edAdmin', 		STR0007, .T., 'L',  1, 0, , lAdmin) //'Admin'
	makeField(aFields, 		'edAtivo',		STR0008, .T., 'L',  1, 0, , lAtivo) //'Ativo'
	makeField(aFields, 		'edUs_siga',	STR0009, .T., 'L',  1, 0, , lUsSiga) //'Usuário Siga'
	makeCombo(aFields,		'edCopiarPriv',	STR0012,	.F., aUsers, , 0) //"Copiar privilégios"
	makeCombo(aFields,  	"edUsePanel"  , STR0014, .f., ; //"Usar painel"
					{ { STR0015, "0" }, ; //"Simples"
					  { STR0016, "1" } }, , cPainel) //"Duplo"
	%>
	<%= buildForm("frmUsers", STR0010, HttpGet->Action, OP_SUBMIT, , aFields) %> <%= /*"Usuário"*/ %>
	
	<% if HttpGet->Oper == OP_SUBMIT %>
  		<%=buildAutoClose()%>
	<% endif %>
	
<% else
	makeItemToolbar(aButtons, STR0017, STR0018, "ic_import.gif", ;
		"js:doLoad("+ makeAction(AC_USER_IMPORT)+", '_window', null, 'winDWImp', " + DwStr(TARGET_75_WINDOW) + "," + DwStr(TARGET_75_WINDOW)+")", "right") /*"Importar"###"Importa os usuários do SigaAdv"*/
%>
  <%= buildBrowse(STR0011, .T., TAB_USER, , , ,          , { "TIPO='U'" }, , , , , , , , , aButtons) %> <%/*"Manutenção de Usuários"*/ %>
<% endif %>
<%=tagJs()%>
function doImportUsr()
{
	
	
}
</script>