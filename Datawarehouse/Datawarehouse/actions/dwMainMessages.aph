<%
// ######################################################################################
// Projeto: DATA WAREHOUSE
// Modulo : Actions
// Fonte  : dwMainMessages - Manutenção de mensagens
// ---------+-------------------+--------------------------------------------------------
// Data     | Autor             | Descricao
// ---------+-------------------+--------------------------------------------------------
// 29.11.05 |2481-Paulo R Vieira| Versão 3
// --------------------------------------------------------------------------------------

#include "dwincs.ch"
#include "dwMainMessages.ch"

Local oMsgTbl 		:= InitTable(TAB_MSG)
Local oMsgUserTbl	:= InitTable(TAB_MSG_USER)
Local oUsers		:= InitTable(TAB_USER)
Local aFields 		:= {}
Local cDtPubIni, cHrPubIni, cDtPubFin, cHrPubFin, cDtIncl, cMsg, cAutor, lExibir
Local lOperSucess
Local aMsgUser		:= {}
Local aGrupos		:= {}
Local aData			:= {}
Local aTemp

Local nID			:= DwVal(HttpGet->ID)

Local nInd, aUsers, aValues
Local lChoosed 		:= .F.
Local lAdmin		:= iif (oUserDW:UserIsAdm(), .T., .F.)

if HttpGet->Action == AC_REC_MANUT
	HttpGet->Oper := DwVal(HttpGet->Oper)
	if HttpGet->Oper == OP_SUBMIT
		nID := DwVal(HttpPost->edID)
		
		if HttpSession->subOper == OP_REC_DEL .and. oMsgTbl:seek(1, { nID })
			lOperSucess := oMsgTbl:Delete()
		else
			aValues := oMsgTbl:fromPost(HttpPost->APost)
			if valType(HttpPost->edUsers) == "C"
				aUsers 	:= DwToken(HttpPost->edUsers, ",")
			else
				aUsers := {}
				oUsers:Seek(2, { "U" })
				while !oUsers:EoF() .and. oUsers:value("tipo") == "U"
					if oUsers:value("ativo")
						aAdd(aUsers, oUsers:value("id"))
					endif
					oUsers:_Next()
				enddo
			endif

			for nInd := 1 to len(aUsers)
				aAdd(aValues, { "ID_USER", aUsers[nInd] })
			next
			
			if HttpSession->subOper == OP_REC_EDIT .and. oMsgTbl:seek(1, { nID })
				lOperSucess := oMsgTbl:Update(aValues)
			else
				HttpPost->edID := NIL
				if lOperSucess := oMsgTbl:append(aValues)
					nID := oMsgTbl:value("id")
				endif
			endif
		endif
		
		if !lOperSucess
			DwRaise(ERR_002, SOL_000, oMsgTbl:Msg(.T.))
		endif
	else
		HttpSession->subOper := HttpGet->Oper
	endif
	
	if oMsgTbl:Seek(1, { nID })
		cDtPubIni	:= oMsgTbl:value("dt_pub_ini")
		cHrPubIni	:= oMsgTbl:value("hr_pub_ini")
		cDtPubFin	:= oMsgTbl:value("dt_pub_fin")
		cHrPubFin	:= oMsgTbl:value("hr_pub_fin")
		cDtIncl 	:= oMsgTbl:value("data_incl")
		cMsg		:= oMsgTbl:value("mensagem")
		cAutor		:= oMsgTbl:value("autor")
		lExibir 	:= oMsgTbl:value("exibir")
		
		oMsgUserTbl:Seek(2, { nID })
		while !oMsgUserTbl:Eof() .and. oMsgUserTbl:value("ID_MSG") == nID
			aAdd(aMsgUser, oMsgUserTbl:value("ID_USER"))
			oMsgUserTbl:_Next()
		enddo
	else
		cDtPubIni	:= date()
		cHrPubIni	:= ''
		cDtPubFin	:= date()
		cHrPubFin	:= ''
		cDtIncl		:= date()
		cMsg		:= ''
		cAutor		:= oUserDW:LoginName()
		lExibir		:= .T.
	endif
	
	makeHidden(aFields,		"edID",			nID)
	makeTitle(aFields, 		STR0001) //"Data de Publicação:" 
	makeDateField(aFields, 	"edDt_pub_ini", STR0002		, , .T., , cDtPubIni) //"Data Inicial:"
	makeField(aFields, 		"edHr_pub_ini", STR0003		, .F., "H", 10, 1, , cHrPubIni) //"Horário:"
	makeDateField(aFields, 	"edDt_pub_fin", STR0004		, , .F., , cDtPubFin) //"Data Final:"
	makeField(aFields, 		"edHr_pub_fin", STR0003		, .F., "H", 10, 1, , cHrPubFin) //"Horário:"
	makeHidden(aFields, 	"edData_incl", 	cDtIncl)
	makeTextArea(aFields, 	"edMensagem", 	"|"+STR0005		, .T.,  50, 8,  ,   cMsg) //"Mensagem"
	makeHidden(aFields, 	"edAutor", 		cAutor)
	makeCheckField(aFields, "edExibir", 	STR0006		, .T.,    , lExibir) //"Exibir"

	makeAttention(aFields, STR0009+"<br>"+STR0011)

	oUsers:Seek(2, { "G" })
	while !oUsers:EoF() .and. oUsers:value("tipo") == "G"
		if oUsers:value("ativo")
			aAdd(aGrupos, { oUsers:value("id"), oUsers:value("login") + " - " + oUsers:value("nome") })
		endif
		oUsers:_Next()
	enddo
	
	for nInd := 1 to len(aGrupos)
		aAdd(aData, STR0007 + aGrupos[nInd][2]) //"Grupo: "
		
		// procura pelo id_grupo
		aTemp := {}
		oUsers:Seek(4, { aGrupos[nInd][1] })
		while !oUsers:EoF() .and. oUsers:value("id_grupo") == aGrupos[nInd][1]
			if oUsers:value("ativo")
				lChoosed := .F.
				if aScan(aMsgUser, oUsers:value("id")) > 0
					lChoosed := .T.
				endif
				aAdd(aTemp, makeHCheckField(NIL, "edUsers", oUsers:value("login") + " - " + oUsers:value("nome"), .F., .T., , DwStr(oUsers:value("id")), lChoosed) )
			endif
			oUsers:_Next()
		enddo
		if len(aTemp) > 0
			aAdd(aData, DwConcatWSep("<br>", aTemp))
		else
			aAdd(aData, "-")
		endif
	next
	aAdd(aData, makeAttention(NIL, STR0009+"<br>"+STR0011))
	makeText(aFields, displayData(aData, 1, { STR0008 })) //"Atribuir mensagem aos Grupos/Usuários:"
	%>
	
	<% if !(HttpSession->subOper == OP_REC_DEL) %>
		<%= tagJS() %>
			function frmMessages_valid(oSender) {
				// verifica a seleção de pelo menos um usuário
				var lRet = true;
				var lSelectedUser = false;
				var oElements = getElement("edUsers");
				if (oElements && oElements.length == 1) {
					lSelectedUser = ((oElements.type == "checkbox") && (oElements.checked));
				} else {
					for (var nInd = 0; nInd < oElements.length; nInd++) {
						if ((oElements[nInd].type == "checkbox") && oElements[nInd].checked) {
							lSelectedUser = true;
						}
					}
				}
				
				if (!lSelectedUser) {
					lRet = confirm("<%=STR0009%>\n<%=STR0011%>"); //###"ATENÇÃO: Caso não seja selecionado nenhum usuário,"##"a mensagem será direcionada à todos os usuários do sistema"
				}
				
				return lRet;
			}
		</script>
	<% endif %>
	
	<%= buildForm("frmMessages", STR0010, AC_REC_MANUT, OP_SUBMIT, {}, aFields, lAdmin, , , , , 1) %> <%= /*"Mensagens"*/ %>
	<% if HttpGet->Oper == OP_SUBMIT %>
		<%= buildAutoClose() %>
	<% endif %>
	
<% else %>
	<%= buildBrowse(STR0010, lAdmin, oMsgTbl:TableName()) %> <%=/*"Mensagens"*/ %>
	
<% endif %>