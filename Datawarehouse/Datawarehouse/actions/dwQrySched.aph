<%
// ######################################################################################
// Projeto: DATA WAREHOUSE
// Modulo : Actions
// Fonte  : dwQrySched - Listagem dos agendamentos de exportações
// ---------+-------------------+--------------------------------------------------------
// Data     | Autor             
// ---------+-------------------+--------------------------------------------------------
// 01.06.07 | 6362-Renato Ceadareanu
// --------------------------------------------------------------------------------------

#include "dwincs.ch"            
#include "dwQrySched.ch"

local nID := dwval(HTTPGET->Id)
local nExp := dwval(HTTPGET->id_exp)
local oSXM := InitTable(TAB_SXM)
local oCons := InitTable(TAB_CONSULTAS)
local oExp := InitTable(TAB_EXPORT)
local oTabObj, aFields := {}
local aButtons := {}, aOperButtons := {}
local cTitle := "", lOperSubmit := .f.
local cAgTipo, cXM_Tipo, lXM_Ativo, dXM_DtIni, cXM_HrIni, dXM_DtFim
local cXM_HrFim, cXM_Interv, cXM_SemanaExt, cXM_MensalExt, cXM_ProxExec, cXM_TaskCode
                                           
if oCons:seek(1, { nID } )
	cTitle := STR0001 + " [ " + oCons:value("nome") + "-" + oCons:value("descricao") + " ]" //###"Consulta"
	httpSession->idQuery := nExp
else
	DWRaise(ERR_002, SOL_000, oCons:Msg(.t.))
endif %>

<%=buildBrowse(STR0002 + " <small>" + cTitle + "</small>", .t., oSXM:Tablename(), , , ,{ |aaData| prepRow(aaData, oSXM)} , { "AGTIPO = " + dwStr(AG_EXP) + " and ID_DSN =" + dwStr(nexp) }, aButtons, , AC_EDTQRY_SCHED, AC_EDTQRY_SCHED,,,,,,,, "&id_exp=" + dwstr(nExp) ) /*###"Agendamento"*/%>

<% return __Response 

static function prepRow(aaData, aoSXM)
	local aPerio := dwComboOptions(PERIODICIDADE_SCHD)
	local nPos, cAux:="", oTempDsn, oTempDF, oTempDW

	if !aoSXM:isOpen()
		aoSXM:Open()
	endif
	aoSXM:seek(1, { aaData[1] })
	oTempDsn := initTable(TAB_DSN)
	if oTempDsn:seek(1, {aoSXM:value("id_dsn")} )
		if oTempDsn:value("TIPO") == "D"
		     oTempDF := initTable(TAB_DIMENSAO)
		else
			oTempDF := initTable(TAB_CUBESLIST)
		endif
	    if oTempDF:seek(1, { oTempDsn:value("ID_TABLE") } )    
	    	oTempDW := initTable(TAB_DW)
	    	if oTempDW:seek(1, { oTempDF:value("ID_DW") } )
				cAux := dwStr(aoSXM:value("dsn") + " - " + oTempDW:value("nome"))		     		     
			else
				cAux := dwStr(aoSXM:value("dsn") + " - ...")		     			
			endif
		else
			cAux := dwStr(aoSXM:value("dsn") + " - ...")
		endif
	endif

	aaData[2] := "<span style='float:left'>" + substr(cAux, at(":", cAux)+1) + "</span>"
	
	if aaData[3] == AG_IMP .or. aaData[3] == AG_DSN
		aaData[3] := STR0003 + ": " + substr(cAux, 1, at(":", cAux)-1) //###"Importação"
	else
		aaData[3] := STR0004 + ": " + STR0001 //###"Exportação"###"Consulta"
	endif           

	aaData[3] := "<span style='float:left'>" + aaData[3]+"</span>"

	cAux := aoSXM:value("XM_PROXEXEC")
	if left(cAux, 2) == "**"
		cAux := ""
	else
		cAux := " <small>(" + cAux + ")</small>"
	endif
	aaData[4] := dwStr(aaData[4])		
	nPos := ascan(aPerio, { |x| x[2] == aaData[4]})
	if nPos > 0
	aaData[4] := "<span style='float:left'>"+aPerio[nPos,1]	+ cAux + "</span>"
	endif
		
return .t.         

%>