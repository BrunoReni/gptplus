<%
// ######################################################################################
// Projeto: DATA WAREHOUSE
// Modulo : Actions
// Fonte  : dwApoioCalend - Página de calendário de apoio
// ---------+-------------------+--------------------------------------------------------
// Data     | Autor             | Descricao
// ---------+-------------------+--------------------------------------------------------
// 06.10.05 | 0548-Alan Candido | Versão 3
// --------------------------------------------------------------------------------------

#include "dwincs.ch"
#include "dwApoioPeriodo.ch"

local oDS := initTable(TAB_PERIODO)
local lOperSubmit := .f.
local aFields, aButtons, aValues
local cDescricao, dInicio, dFinal
local nID := dwVal(HttpGet->ID), lOk

if HttpGet->Action == AC_REC_MANUT     
	aFields := {}
	aButtons := {}
	HttpGet->Oper := DwVal(HttpGet->Oper)
	if HttpGet->Oper == OP_SUBMIT
		lOperSubmit := .t.
		aValues := oDS:fromPost(HttpPost->APost)                               
		nID := dwVal(HttpPost->edID)
		if HttpSession->subOper == OP_REC_EDIT .and. oDS:seek(1, { dwVal(nID) }) 
			lOk := oDS:update(aValues)
		elseif HttpSession->subOper == OP_REC_DEL .and. oDS:seek(1, { dwVal(nID) }) 
			lOk := oDS:delete()                     
		else
			if (lOk := oDS:append(aValues))
				nID := oDS:value("id")
			endif
		endif
		if !lOk
			DWRaise(ERR_002, SOL_000, oDS:Msg(.t.))
		endif
	else                           
		HttpSession->subOper := HttpGet->Oper
	endif          

	if oDS:seek(1, { dwVal(nID) }) 
		cDescricao := oDS:value("descricao")
		dInicio    := oDS:value("dt_inicio")
		dFinal     := oDS:value("dt_final")
	endif
	
	makeHidden(aFields, 'edID'        , nID)
	makeField(aFields, 'edDt_Inicio'  , STR0001  , .T., 'D', 10, 0,,dInicio) //'Data inicial'
	makeField(aFields, 'edDt_Final'   , STR0002    , .T., 'D', 10, 0,,dFinal) //'Data final'
	makeField(aFields, 'edDescricao'  , STR0003     , .T., 'C', 50, 0,,cDescricao) //'Descrição'
	%>
	<%=buildForm('frmPeriodo', STR0004, HttpGet->Action, OP_SUBMIT, aButtons, aFields)%> <%= /*'Períodos'*/ %>
	<%if lOperSubmit%>
	<%=buildAutoClose()%>
	<%endif%>
<%else%>
<%=buildBrowse(STR0005, .T., oDS:Tablename())%> <%= /*"Período"*/ %>
<%endif%>