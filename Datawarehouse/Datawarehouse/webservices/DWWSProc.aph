<%
#include "dwincs.ch"
#INCLUDE "WSDW.ch"

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºProgram   ³DWWSPROC     ºAuthor ³Alexandre Silva     º Date ³  20/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Biblioteca de funcoes para uso dos web Services do DW.         º±±
±±º          ³                                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUse       ³ AP                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
local lRet	:=	.t.

do case
	case lower(HttpGet->cmd) == "ogetxmlconsultas"
		return oGetXMLConsultas()
	case lower(HttpGet->cmd) == "ogetxmlcubos"
		return oGetXMLCubos()
	case lower(HttpGet->cmd) == "ogetxmlcubo"
		return oGetCubo(val(HttpPost->IDCUBE))
	case lower(HttpGet->cmd) == "ogetconsultastruct"
		return oGetConStruct(val(HttpPost->IDCONSULTA),lower(HttpPost->listvalues) == "true")
	otherwise
		return STR0037 //###"Requisicao nao localizada."
endcase					

return lRet
%>

<%
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºProgram   ³oGetXMLConsultas    ºAuthor ³Alexandre Silva     º Date ³  20/07/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a lista de Consultas que o usuario possui acesso.      		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUse       ³ AP                                                            		º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
static function oGetXMLConsultas()	
	local oTblConsulta	:= 	initTable(TAB_CONSULTAS) //oSigaDW:Consultas()
	local nUser			:= 	oSigaDW:Admin():UserID()	
    local oXMLConsulta	:=	TBIXMLNode():New("CONSULTAS")
	local oXmlNode		:=	nil	   
	local aTmpReg		:=	{}
	local nReg			:=	0

	oTblConsulta:open()
	oTblConsulta:goTop()
		
	while ! oTblConsulta:eof()
		if oSigaDW:Admin():HaveAccessCons(oTblConsulta,nUser) .and. (oTblConsulta:value("ID") # 0)
			oXmlNode:=	oXMLConsulta:oAddChild(TBIXMLNode():New("CONSULTA"))
			aTmpReg	:=	oTblConsulta:record(1,{"SOGRUPO","PUBLIC","ID_CONS","ID_CUBE","VALIDA","VALGRA","ID_USER","ID_GRUPO","PUBURL","EXCEL","F_L_A_G_W"})
			for nReg := 1 to len(aTmpReg)
				oXmlNode:oAddChild(TBIXMLNode():New(aTmpReg[nReg,1],aTmpReg[nReg,2]))
			next nReg
		endif
		oTblConsulta:_Next()
	end
		
return oXMLConsulta:cXMLString(.t., "ISO-8859-1")      

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºProgram   ³oGetConStru         ºAuthor ³Alexandre Silva     º Date ³  21/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a estrutura da consulta atraves de um ID passado.      		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUse       ³ AP                                                            		º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
static function oGetConStruct(anID, alListValues)	
	local oTblConsulta	:= 	initTable(TAB_CONSULTAS)
	local nUser			:= 	oSigaDW:Admin():UserID()	
    local oXMLConsulta	:=	TBIXMLNode():New("CONSULTAS")
    local oXMLFldY		:=	TBIXMLNode():New("DIMENSIONS_Y")
    local oXMLFldX		:=	TBIXMLNode():New("DIMENSIONS_X")
    local oXMLIndexs	:=	TBIXMLNode():New("INDICADORES")    
	local aDimX			:=	{}
	local aDimY			:=	{}	
	local aInds			:=	{}
	local oXmlNode		:=	nil
	private oConsulta	:=	TConsulta():New(anID,1)	
	
	if oTblConsulta:seek(1,{anID}).and. oSigaDW:Admin():HaveAccessCons(oTblConsulta,nUser)
		oXmlNode:=	oXMLConsulta:oAddChild(TBIXMLNode():New("CONSULTA"))
		oXmlNode:oAddChild(TBIXMLNode():New("NAME",oTblConsulta:FCALIAS))
		oXmlNode:oAddChild(TBIXMLNode():New("ALIAS",oTblConsulta:FCDESCRICAO))
		oXmlNode:oAddChild(TBIXMLNode():New("DESCRIPTION",oTblConsulta:FCDESCRICAO))
		oXmlNode:oAddChild(TBIXMLNode():New("ID",anID))

		//Adicionado estrutura do eixo Y.
		aDimY	:=	oConsulta:DimFieldsY()		
		addFieldsProp(oXMLFldY,aDimY ,"DIMENSION_Y",alListValues)
		oXmlNode:oAddChild(oXMLFldY)
		
		//Adicionado estrutura do eixo X.
		aDimX	:=	oConsulta:DimFieldsX()		
		addFieldsProp(oXMLFldX,aDimX ,"DIMENSION_X",alListValues)
		oXmlNode:oAddChild(oXMLFldX)
		
		//Adicionado estrutura os indicadores.
		aInds := oConsulta:Indicadores(.t.)
		addFieldsProp(oXMLIndexs,aInds ,"INDICADOR",alListValues)
		oXmlNode:oAddChild(oXMLIndexs)

	end
		
return oXMLConsulta:cXMLString(.t., "ISO-8859-1")      

/*
*Adiciona a propriedade dos campos.
*/
static function addFieldsProp(oXml,aFieldStru,cType,lAddValues)
	local oConDados	:= oConsulta:getDSForExport({})
	local oXmlField	:=	nil
	local nItem		:=	0
		
	for nItem := 1 to len(aFieldStru)
		oXmlField	:=	oXml:oAddChild(TBIXMLNode():New(cType))
		oXmlField:oAddChild(TBIXMLNode():New("NAME"		,aFieldStru[nItem]:Name()	))
		oXmlField:oAddChild(TBIXMLNode():New("DESC"		,aFieldStru[nItem]:Desc()	))
		oXmlField:oAddChild(TBIXMLNode():New("TYPE"		,aFieldStru[nItem]:Tipo()	))
		oXmlField:oAddChild(TBIXMLNode():New("SIZE"		,aFieldStru[nItem]:Tam()	))
		oXmlField:oAddChild(TBIXMLNode():New("DECIMAL"	,aFieldStru[nItem]:nDec()	))
		oXmlField:oAddChild(TBIXMLNode():New("ID"		,aFieldStru[nItem]:id()		))
		oXmlField:oAddChild(TBIXMLNode():New("TEMPORAL"	,aFieldStru[nItem]:Temporal()))

		//Adicionando os valores para os indicadores;
		if(lAddValues)
			cCmpAlias	:=	aFieldStru[nItem]:Alias() 
			oXmlField	:=	oXmlField:oAddChild(TBIXMLNode():New("VALUES"))
			oConDados:goTop()
			n := 1
			while ! oConDados:eof()

				if(aFieldStru[nItem]:Tipo() == "D" .and.  aFieldStru[nItem]:Temporal() == 0)
					oXmlField:oAddChild(TBIXMLNode():New("VALUE",dtos(cTod(oConDados:value(cCmpAlias)))))
				else
					oXmlField:oAddChild(TBIXMLNode():New("VALUE",oConDados:value(cCmpAlias)))
				endif				
				n++			
				oConDados:_Next()
			end
		endif			
	next nItem

return .t.


/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºProgram   ³oGetXMLCubos        ºAuthor ³Alexandre Silva     º Date ³  14/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a lista de Cubos     que o usuario possui acesso.      		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUse       ³ AP                                                            		º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
static function oGetXMLCubos()	
	local oTblCubes		:= initTable(TAB_CUBESLIST)
	local nUser			:= 	oSigaDW:Admin():UserID()	
    local oXMLCubos		:=	TBIXMLNode():New("CUBESLIST")
	local oXmlNode		:=	nil	   
	local aTmpReg		:=	{}
	local nReg			:=	0
	local nCubAccess	:=	0

	oTblCubes:open()
	oTblCubes:goTop()
		
	while ! oTblCubes:eof()
		nCubAccess := oSigaDW:Admin():AccessCub(nUser,,oTblCubes:value("ID"))
		//Verificacao de acesso a um cubo.
		if  nCubAccess != 0  .and. nCubAccess != 2	.and. oTblCubes:value("ID") # 0
			oXmlNode:=	oXMLCubos:oAddChild(TBIXMLNode():New("CUBELIST"))
			aTmpReg	:=	oTblCubes:record(1,{"DT_CREATE","HR_CREATE","DT_PROCESS","HR_PROCESS","IMPORTADO","NOTIFICAR","F_L_A_G_W"})
			for nReg := 1 to len(aTmpReg)
				oXmlNode:oAddChild(TBIXMLNode():New(aTmpReg[nReg,1],aTmpReg[nReg,2]))
			next nReg
		endif
		oTblCubes:_Next()
	end
	
return oXMLCubos:cXMLString(.t., "ISO-8859-1")

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºProgram   ³oGetCubo            ºAuthor ³Alexandre Silva     º Date ³  15/08/06   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna os dados de um determinado cubo.                       		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUse       ³ AP                                                            		º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
static function oGetCubo(anCubeID)	   
	local oCube := oSigaDW:OpenCube(anCubeID, .t.)

	oCube:Close()

return .t.
%>     