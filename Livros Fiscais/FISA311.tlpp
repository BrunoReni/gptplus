#include "protheus.ch"
#include "fwmbrowse.ch"
#include 'tlpp-core.th'
#include "rwmake.ch"
#include "Protheus.ch"
#include "TBICONN.CH"
#include "FISA311.CH"

Function FISA311()

    Wizard()
	
Return

Static Function Wizard()

	Local aPerg   	:= {}
	Local aParam  	:= {Space(7),Space(250),Space(250)}
	Local cParamKey := "IssSorocaba"+"FISA311"
	Local lObrigat  := .F.
	Local oProcess
	Local lEnd      := .F.
	Local cCompetencia  := ""	
	Local cDestArq := ""
	Local cNomeArq := ""

	aadd(aPerg,{1,STR0001,aParam[01],"99/9999",".T.","",".T.",30,lObrigat}) //Competência (MM/AAAA)
	aadd(aPerg,{6,STR0002,aParam[02],"","DirectoryExists(MV_PAR02)","!empty(MV_PAR02)",80,lObrigat,"Arquivos TXT |*.TXT","",GETF_RETDIRECTORY+GETF_LOCALHARD,.T.}) //Salvar em
	aadd(aPerg,{1,STR0003,aParam[03],"",".T.","",".T.",100,lObrigat}) //"Nome do arquivo"

	aParam[01] := ParamLoad(cParamKey,aPerg,1,aParam[01])
	aParam[02] := ParamLoad(cParamKey,aPerg,2,aParam[02])
	aParam[03] := ParamLoad(cParamKey,aPerg,3,aParam[03])

	If ParamBox(aPerg,STR0004,@aParam,,,,,,,cParamKey,.T.,.T.) //"ISS Tomador - Sorocaba"

		cCompetencia  := Alltrim(MV_PAR01)
		cDestArq := Alltrim(MV_PAR02)
		cNomeArq := Alltrim(MV_PAR03)

		oProcess:= FISProgress():New({|lEnd| lEnd := CreateFile(@lEnd, oProcess, cCompetencia, cDestArq, cNomeArq) }, STR0005) //"Gerando arquivo"
		oProcess:Activate()

	EndIf

Return

Function DirectoryExists(cDestino)

	Local lOk := .T.

	If !Empty(cDestino)
		lOk := ExistDir( Alltrim(cDestino), , .f.)
	EndIf

	If !lOk
		Help(NIL, NIL, "Help", NIL, STR0006,; //"Caminho para destino do arquivo não existe!"
		 1, 0, NIL, NIL, NIL, NIL, NIL,{STR0007})  //"Informe um caminho válido"
	EndIf

Return lOk

Static Function CreateFile(lEnd, oProcess, cCompetencia, cDestArq, cNomeArq)

	Local cFilePath := cDestArq + cNomeArq
	Local oWriter 	:= FWFileWriter():New(cFilePath, .T., 30000)
	Local oNFSESorocabaData := backoffice.fiscal.arquivos.municipal.nfsesorocaba.data.NFSESorocabaData():new(cCompetencia)
	Local oNFSESorocabaFile := backoffice.fiscal.arquivos.municipal.nfsesorocaba.domain.NFSESorocabaFile():new(oNFSESorocabaData)
	Local nCountLines := oNFSESorocabaFile:countLines() as Integer
	Local oFile 	:= Nil
	Local cFileName	:= ""
	Local TimeFim   := Time()
	Local TimeIni   := Time()
	Local cSucessArq:= ""
	Local nI as Integer

	If oWriter:Exists()
		oWriter:Erase()
	EndIf

	oProcess:SetDetProgress(STR0008,TimeIni,; //"Início do processamento" 
	STR0009, "",; //"Fim do processamento" 
	STR0010, "","","") //"Tempo de processamento"

	If !oWriter:Create()
		oProcess:Inc1Progress(STR0011) //"Não foi possível gerar o arquivo"
		oProcess:Inc2Progress(oWriter:Error():Message)

		TimeFim := Time()
		oProcess:SetDetProgress(STR0008,TimeIni,; //"Início do processamento" 
		STR0009, TimeFim,; //"Fim do processamento" 
		STR0010, ELAPTIME(TimeIni,TimeFim),"","") //"Tempo de processamento"

		Return .F.
	Endif

    oProcess:Inc2Progress(STR0005) //"Gerando arquivo"

	For nI := 1 To nCountLines
		If nI < nCountLines
			oWriter:writeLine(oNFSESorocabaFile:getNextLine())
		Else
			oWriter:write(oNFSESorocabaFile:getNextLine())
		EndIf
	Next nI
	
	If oWriter:Exists()

		oProcess:Inc2Progress(STR0012) //"Analizando arquivo"

		cFileName := oWriter:cFilename

		oWriter:Close()

		oFile := FWFileReader():New(cFileName)

		If oFile:Open() .AND. oFile:getFileSize() > 0
			cSucessArq := " - " + STR0013 + cFileName //"Arquivo criado no diretório "
			oFile:Close()
		EndIf

	EndIf

	oProcess:Inc2Progress(STR0014 + cSucessArq) //"Concluído "
	TimeFim := Time()
	oProcess:SetDetProgress(STR0008,TimeIni,; //"Início do processamento" 
		STR0009, TimeFim,; //"Fim do processamento" 
		STR0010, ELAPTIME(TimeIni,TimeFim),"","") //"Tempo de processamento"

	FreeObj(oNFSESorocabaData)
	FreeObj(oNFSESorocabaFile)
	FreeObj(oWriter)
	FreeObj(oFile)

	oNFSESorocabaData := Nil
	oNFSESorocabaFile := Nil
	oWriter := Nil
	oFile := Nil

Return .T.




